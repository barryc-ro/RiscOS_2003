; File:    semaphore.s
; Purpose: Test binary semaphores in IOMD2
; Author:  Timothy Roddis
; History: 20-Feb-1998: TGR: created
; 

                            ;
SEMAPH_TESTSTART_MESS
        DCB     "TUBE:Semaphore testing beginning",&0
SEMAPH_TESTEND_MESS
        DCB     "TUBE:Semaphore testing completed",&0
SEMAPH_TESTPASS_MESS
        DCB     "TUBE:Semaphore PASSED test",0
SEMAPH_RESETFAIL_MESS
        DCB     "TUBE:Semaphore FAILED to clear on reset",4,0
SEMAPH_SETFAIL_MESS
        DCB     "TUBE:Semaphore FAILED to set on read",4,0
SEMAPH_CLEARFAIL_MESS
        DCB     "TUBE:Semaphore FAILED to clear on write",4,0
SEMAPH_SET2FAIL_MESS
        DCB     "TUBE:Semaphore FAILED to set on 2nd read",4,0
        
        ALIGN

;; Start of the actual semaphore test code
SEMAPH_TEST ROUT

        ;; Send test starting message
        LDR     R0,=SEMAPH_TESTSTART_MESS
        LDR     R10,=fasttube
        STR     R0,[R10]

; Start testing sequence

        LDR     r5, =semaph_0
        LDR     r6, =semaph_63
10        
        LDR     r0,[r5]
        TEQ     r0,#0
        LDRNE   r12,=SEMAPH_RESETFAIL_MESS
        BNE     SEMAPH_TESTEND
        
        LDR     r0,[r5]
        TEQ     r0,#1
        LDRNE   r12,=SEMAPH_SETFAIL_MESS
        BNE     SEMAPH_TESTEND
        
        STR     r0,[r5] ; doesn't matter what we write in, should still clear it
        LDR     r0,[r5]
        TEQ     r0,#0
        LDRNE   r12,=SEMAPH_CLEARFAIL_MESS
        BNE     SEMAPH_TESTEND
        
        LDR     r0,[r5]
        TEQ     r0,#1
        LDRNE   r12,=SEMAPH_SET2FAIL_MESS
        BNE     SEMAPH_TESTEND
        
        STR     r0,[r5] ; might as well leave it clear
        
        ADD     r5, r5, #4 ; done here to aid clarity and allow easy modification of above
        CMP     r5,r6
        BLE     %BT10 ; unsigned comparison
        LDR     r12,=SEMAPH_TESTPASS_MESS

SEMAPH_TESTEND
        LDR     R10,=fasttube
        STR     R12,[R10]

        ;; Send test complete message
        LDR     R0,=SEMAPH_TESTEND_MESS
        LDR     R10,=fasttube
        STR     R0,[R10]

        ; Unstack return address
        MOV     PC,LR

	LTORG

        END
        
