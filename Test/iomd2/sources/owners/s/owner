; File:    owner.s
; Purpose: Test possible owner regs in IOMD2
; Author:  Timothy Roddis
; History: 20-Feb-1998: TGR: created
; 
                            ;
OWNER_TESTSTART_MESS
        DCB     "TUBE:Possible owner testing beginning",&0
OWNER_TESTEND_MESS
        DCB     "TUBE:Possible owner testing completed",&0
OWNER_TESTPASS_MESS
        DCB     "TUBE:Possible owner test PASSED",0
OWNER_RESETFAIL_MESS
        DCB     "TUBE:Possible owner FAILED to clear on reset",4,0        
OWNER_SETFAIL_MESS
        DCB     "TUBE:Possible owner FAILED to set on write",4,0        
OWNER_DENYFAIL_MESS
        DCB     "TUBE:Possible owner FAILED to deny on write",4,0        
OWNER_CLEARFAIL_MESS        
        DCB     "TUBE:Possible owner FAILED to clear on write",4,0        
OWNER_SET2FAIL_MESS
        DCB     "TUBE:Possible owner FAILED to set on 2nd write",4,0        
OWNER_DENY2FAIL_MESS
        DCB     "TUBE:Possible owner FAILED to deny on 2nd write",4,0     
        
        ALIGN

;; Start of the actual semaphore test code
OWNER_TEST ROUT

        ;; Send test starting message
        LDR     R0,=OWNER_TESTSTART_MESS
        LDR     R10,=fasttube
        STR     R0,[R10]

; Start testing sequence

        LDR     r5, =owner_0
        LDR     r6, =owner_31
        ADR     r4, OWNER_TEST_DATA
        LDR     r0,[r4]     ; r0 = first process id 
        LDR     r1,[r4,#4]  ; r1 = second process id 

10
        LDR     r2,[r5]
        TEQ     r2,#0
        LDRNE   r12,=OWNER_RESETFAIL_MESS
        BNE     OWNER_TESTEND
        
        STR     r0,[r5]
        LDR     r2,[r5]
        TEQ     r2,r0
        LDRNE   r12,=OWNER_SETFAIL_MESS
        BNE     OWNER_TESTEND
        
        STR     r1,[r5]
        LDR     r2,[r5]
        TEQ     r2,r0
        LDRNE   r12,=OWNER_DENYFAIL_MESS
        BNE     OWNER_TESTEND
        
        MOV     r2,#0
        STR     r2,[r5]
        LDR     r2,[r5]
        TEQ     r2,#0
        LDRNE   r12,=OWNER_CLEARFAIL_MESS
        BNE     OWNER_TESTEND
        
        STR     r1,[r5]
        LDR     r2,[r5]
        TEQ     r2,r1
        LDRNE   r12,=OWNER_SET2FAIL_MESS
        BNE     OWNER_TESTEND
        
        STR     r0,[r5]
        LDR     r2,[r5]
        TEQ     r2,r1
        LDRNE   r12,=OWNER_DENY2FAIL_MESS
        BNE     OWNER_TESTEND
        
        MOV     r2,#0    ; might as well clear it
        STR     r2,[r5]  ;
        
        ADD     r5, r5, #4
        CMP     r5,r6
        BLE     %BT10
        LDR     r12,=OWNER_TESTPASS_MESS

OWNER_TESTEND
        LDR     R10,=fasttube
        STR     R12,[R10]

        ;; Send test complete message
        LDR     R0,=OWNER_TESTEND_MESS
        LDR     R10,=fasttube
        STR     R0,[R10]

        ; Unstack return address
        MOV     PC,LR

OWNER_TEST_DATA

        DCD     &15
        DCD     &2a

	LTORG

        END
