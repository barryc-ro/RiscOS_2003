; File:    iocr.s
; Purpose: I/O control register test
; Author:  Timothy Roddis
; History: 20-Feb-1998: TGR: created
;

IOCR_TESTSTART_MESS
        DCB     "TUBE:IOCR testing beginning",&0
IOCR_TESTEND_MESS
        DCB     "TUBE:IOCR testing completed",&0
IOCR_TESTPASSED_MESS
        DCB     "TUBE:IOCR PASSED test",0
IOCR_GIOOPFAIL_MESS
        DCB     "TUBE:IOCR FAILED to output value", 4, 0
IOCR_GIOIPFAIL_MESS
        DCB     "TUBE:IOCR FAILED to input value", 4, 0
IOCR_NINDEXFAIL_MESS
        DCB     "TUBE:IOCR FAILED nindex connectivity test", 4, 0
        ALIGN

;; Start of the actual semaphore test code
IOCR_TEST ROUT

        ;; Send test starting message
        LDR     R0,=IOCR_TESTSTART_MESS
        LDR     R10,=fasttube
        STR     R0,[R10]

; Start testing sequence
10
        LDR     r0, =giostat
        LDR     r1, =iocr
        MOV     r3, #0

        EOR     r5, r3, #&ff

        STR     r3, [r1] ; gio output
        STR     r5, [r0] ; gio input external
        LDR     r4, [r0] ; gio output external
        LDR     r6, [r1] ; gio input

        TEQ     r3, r4
        LDRNE   r12,=IOCR_GIOOPFAIL_MESS

        TEQEQ   r5, r6
        LDRNE   r12,=IOCR_GIOIPFAIL_MESS
        BNE     IOCR_TESTEND

        ADD     r3, r3, #1
        CMP     r3, #&3f
        BLE     %BT10
        
        LDR     r0,=extintcfg00
        MOV     r3, #3
        STR     r3, [r0, #20] ; interrupt no. 5
        MOV     r3, #1 :SHL: 5
        STR     r3, [r0, #&40] ; cause interrupt etc.
        LDR     r3, [r1]
        TST     r3, #1:SHL:6
        LDREQ   r12,=IOCR_NINDEXFAIL_MESS
        BEQ     IOCR_TESTEND
        
        MOV     r3, #0         ; blitz modified locations
        STR     r3, [r0, #20]  ; interrupt no. 5
        STR     r3, [r0, #&40] ; uncause interrupt etc.

        LDR     r12,=IOCR_TESTPASSED_MESS

IOCR_TESTEND

        ;; Send test complete message
        LDR     R0,=IOCR_TESTEND_MESS
        LDR     R10,=fasttube
        STR     R0,[R10]

        ; Unstack return address
        MOV     PC,LR

	LTORG

        END

