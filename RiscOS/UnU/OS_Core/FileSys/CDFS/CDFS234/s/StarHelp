
 [ RISC_OS > 2

;-----------------------------------------------------------------------------------------------
;       This retrieves messages from the 'Messages' file for *Help, ie/ *Help CDDevices
;-----------------------------------------------------------------------------------------------

;-----------------------------------------------------------------------------------------------
; on entry:
;          r0 -> buffer to place string in
;          r1  = length of buffer
; on exit:
;          r0 -> a string to be XOS_PrettyPrinted
;          r1 - r6 and r12 can be corrupted
;-----------------------------------------------------------------------------------------------

bye_help
         MOV        r4, #HELP_BYE
         B          %FT01

cddevices_help
         MOV        r4, #HELP_CDDEVICES
         B          %FT01

cdfs_help
         MOV        r4, #HELP_CDFS
         B          %FT01

cdrombuffers_help
         MOV        r4, #HELP_CDROMBUFFERS
         B          %FT01

cdromdrives_help
         MOV        r4, #HELP_CDROMDRIVES
         B          %FT01

cdspeed_help
         MOV        r4, #HELP_CDSPEED
         B          %FT01

drive_help
         MOV        r4, #HELP_DRIVE
         B          %FT01

eject_help
         MOV        r4, #HELP_EJECT
         B          %FT01

lock_help
         MOV        r4, #HELP_LOCK
         B          %FT01

play_help
         MOV        r4, #HELP_PLAY
         B          %FT01

playlist_help
         MOV        r4, #HELP_PLAYLIST
         B          %FT01

playmsf_help
         MOV        r4, #HELP_PLAYMSF
         B          %FT01

stop_help
         MOV        r4, #HELP_STOP
         B          %FT01

supported_help
         MOV        r4, #HELP_SUPPORTED
         B          %FT01

unlock_help
         MOV        r4, #HELP_UNLOCK
         B          %FT01

whichdisc_help
         MOV        r4, #HELP_WHICHDISC
         B          %FT01


;-----------------------------------------------------------------------------------------------
; Get the workspace pointer and reserved the return address
;-----------------------------------------------------------------------------------------------
01

         LDR        r12, [ r12 ]

         MOV        r6, r14

;-----------------------------------------------------------------------------------------------
; Build up the message tag for the *Help
; r4 = message number
;-----------------------------------------------------------------------------------------------

         ADR        r1, TempArea            ; "ha"<null>
         ADD        r4, r4, #"h"
         STR        r4, [ r1 ]

;-----------------------------------------------------------------------------------------------
; Look the message up in the 'Messages' file
;-----------------------------------------------------------------------------------------------

         ADR        r0, message_block

         ADR        r2, TempArea + 4

         MOV        r3, #128

         SWI        XMessageTrans_GSLookup

         ADRVC      r2, TempArea + 4
         ADDVC      r2, r2, r3

;-----------------------------------------------------------------------------------------------
; Build up the message tag for the Syntax help
;-----------------------------------------------------------------------------------------------

         MOVVC      r14, #"s"

         STRVCB     r14, TempArea + 0

;-----------------------------------------------------------------------------------------------
; Look the message up in the 'Messages' file
;-----------------------------------------------------------------------------------------------

         ADRVC      r0, message_block

         ADRVC      r1, TempArea            ; "sa"<null>

         MOVVC      r3, #128

         SWIVC      XMessageTrans_GSLookup

;-----------------------------------------------------------------------------------------------
; Point r0 at the message
;-----------------------------------------------------------------------------------------------

         ADRVC      r0, TempArea + 4

         ADDVS      r0, r0, #4

;-----------------------------------------------------------------------------------------------
; Exit
;-----------------------------------------------------------------------------------------------

         BICS       pc, r6, #Overflow_Flag

;-----------------------------------------------------------------------------------------------


 ]

 END

