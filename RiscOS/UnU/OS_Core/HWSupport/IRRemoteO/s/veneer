; s.veneer
;
;       Veneer for events and vectors.
;

        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:ModHand

 [ :LNOT: :DEF: DEBUG
	GBLL	DEBUG
DEBUG		SETL	{FALSE}
 |
	GET	Hdr:Debug
 ]
 [ :LNOT: :DEF: OLD_SERIAL
	GBLL	OLD_SERIAL
OLD_SERIAL	SETL	{FALSE}
 ]
 [ :LNOT: :DEF: KEYBOARD
	GBLL	OLD_SERIAL
KEYBOARD	SETL	{FALSE}
 ]

	AREA	|veneer_code|,REL,CODE,READONLY

        IMPORT	|_Lib$Reloc$Off$DP|
        IMPORT	airmouse_pointerv
 [ KEYBOARD
        EXPORT  veneer_key_table
 ]
        EXPORT	veneer_pointerv

PointerVHandler DCD     airmouse_pointerv

veneer_pointerv
        Push    "r10-r12,lr"
        LDR     r11, PointerVHandler
;       Drop through to...

CallC
; In:   r11->routine to call
;       r12->private word
;       registers {r10-r12,lr,pc(claim)} stacked
;
        MOV     r10, pc                 ; Ensure SVC mode.
        ORR     lr, r10, #3
        TEQP    lr, #0
        MOV     r0, r0

        Push    "r0-r10,lr"             ; Save regs r0-r9 (for call) + old mode in r10 + SVC_lr
        MOV     r10, r13, LSR #20
        MOV     r10, r10, ASL #20
        LDMIA   r10, {r4,r5}            ; Save old reloc modifiers.
        MOV     r9, r11
        LDR     r12, [r12]
        LDMIB   r12, {r11,r12}
        STMIA   r10, {r11,r12}
        ; This is equivalent of 'ADD r10, r10, #0' + |_Lib$Reloc$Off$DP|
        DCD     |_Lib$Reloc$Off$DP| + &E28AA000
        MOV     r11, #0                 ; Halt C backtrace here!

        MOV     r0, sp                  ; Parameter is _kernel_swi_regs *
        MOV     lr, pc                  ; Branch to the code.
        MOV     pc, r9

        ; This is equivalent of 'SUB r10, r10, #0' + |_Lib$Reloc$Off$DP|
        DCD     |_Lib$Reloc$Off$DP| + &E24AA000
        STMIA   r10, {r4,r5}            ; Restore old reloc modifiers.
        MOV     r12, r0                 ; Save return value from C call.
        Pull    "r0-r10,lr"

        TEQP    r10, #0                 ; Restore old processor mode.
        MOV     r0, r0
        TEQ     r12, #0                 ; If C call returned 0 then
        Pull    "r10-r12,pc",EQ,^       ;   pass on call
        Pull    "r10-r12,lr,pc",,^      ; else claim it.


 [ KEYBOARD

veneer_key_table
        DCB     &FF, &FF, &FF, &FF, &3B, &FF, &FF, &FF  ; FF-07
        DCB     &05, &5D, &4F, &FF, &01, &12, &3D, &28  ; 08-0F
        DCB     &FF, &03, &50, &04, &02, &13, &3E, &29  ; 10-17
        DCB     &FF, &26, &4E, &00, &10, &11, &3C, &27  ; 18-1F
        DCB     &52, &2B, &51, &40, &15, &14, &3F, &2A  ; 20-27
        DCB     &60, &FF, &FF, &5E, &FF, &0D, &FF, &0E  ; 28-2F
        DCB     &FF, &4C, &58, &FF, &FF, &FF, &FF, &FF  ; 30-37
        DCB     &5F, &1E, &47, &06, &09, &0A, &33, &FF  ; 38-3F
        DCB     &FF, &FF, &FF, &FF, &FF, &FF, &FF, &FF  ; 40-47
        DCB     &62, &FF, &0F, &59, &20, &FF, &21, &FF  ; 48-4F
        DCB     &63, &FF, &22, &FF, &34, &0B, &36, &2F  ; 50-57
        DCB     &64, &FF, &FF, &35, &1F, &0C, &FF, &30  ; 58-5F
        DCB     &FF, &07, &56, &FF, &08, &19, &44, &FF  ; 60-67
        DCB     &57, &31, &FF, &46, &1B, &1A, &45, &FF  ; 68-6F
        DCB     &FF, &32, &55, &FF, &1C, &18, &43, &2E  ; 70-77
        DCB     &53, &2C, &54, &41, &16, &17, &42, &2D  ; 78-7F

 ]

 [ DEBUG
   	InsertDebugRoutines
 ]
        END
