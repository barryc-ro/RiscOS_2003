        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:PublicWS
        GET     Hdr:Sound

        GBLL    CheckRiscPC
CheckRiscPC     SETL    {FALSE}

        GBLL    CheckNoVRAM
CheckNoVRAM     SETL    {FALSE}

        GBLL    DoResources
DoResources     SETL    {FALSE}

        GBLL    RingBell
RingBell        SETL    {FALSE}

; Define workspace.
;
                ^       0,r12
numchan         #       4               ; Number of sound channels
bufflen         #       4               ; Buffer length.
period          #       4               ; Sample period.
chandler        #       4               ; Channel handler.
scheduler       #       4               ; Scheduler.
spositions      #       4*8             ; Stereo positions.

; Entry point.
;
        Push    "lr"

        SWI     XOS_EnterOS
        BVS     exit

 [ CheckRiscPC
        MOV     r0, #2
        SWI     XOS_ReadSysInfo
        BVS     exit
        AND     r0, r0, #&FF00
        TEQ     r0, #&0100
        ANDEQS  r2, r2, #&FF00
        BNE     exit
 ]

 [ CheckNoVRAM
        MOV     r0, #&208
        SWI     XOS_Memory
        BVS     exit
        TEQ     r1, #0
        BNE     exit
 ]

        MOV     r0, #0
        MOV     r1, #0
        MOV     r2, #0
        MOV     r3, #0
        MOV     r4, #0
        SWI     XSound_Configure
        BVS     exit

        ADR     r5, numchan             ; Store configure parameters.
        STMIA   r5!, {r0-r4}            ; r5 now points to spositions.

10
        MOV     r1, #-128               ; Read and store stereo positions of all channels.
        SWI     XSound_Stereo
        BVS     exit
        SUBS    r0, r0, #1
        STR     r1, [r5, r0]
        BNE     %BT10

 [ DoResources
        ADR     r0, load_resources      ; Load our modules.
        SWI     XOS_CLI
 ]
        ADRVC   r0, load_sounddma
        SWIVC   XOS_CLI
        BVS     exit

        ADR     r5, numchan             ; Restore sound configuration.
        LDMIA   r5!, {r0-r4}            ; r5 now points to spositions again.
        SWI     XSound_Configure
        BVS     exit

        SUB     r5, r5, #4              ; Adjust because channels numbered from 1.
20
        LDR     r1, [r5, r0]            ; Restore stereo positions.
        SWI     XSound_Stereo
        BVS     exit
        SUBS    r0, r0, #1
        BNE     %BT20

 [ RingBell
        SWI     XOS_WriteI+7            ; Ring the bell.
 ]

exit
        MOV     lr, pc                  ; Exit in USER mode but preserve flags.
        BIC     lr, lr, #3
        TEQP    lr, #0
        MOV     r0, r0
        Pull    "pc"

 [ DoResources
load_resources
        DCB     "*RMLoad <Obey$Dir>.SoundRes",0
 ]
load_sounddma
        DCB     "*RMLoad <Obey$Dir>.SoundDMA",0

        DCB     "NewSound 1.01 31-May-95 © Acorn Computers Ltd 1995", 0
 [ CheckRiscPC
        DCB     " (Risc PC only)",0
 ]
 [ CheckNoVRAM
        DCB     " (No VRAM only)",0
 ]
        ALIGN
        END
