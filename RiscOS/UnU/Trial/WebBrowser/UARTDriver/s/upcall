; > upcall
;
; This code should handle all the requests from the upcall vector

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; call: handle_upcall
;
; in:  r0 - reason code
;      r1 - buffer handle
;      r2 - 0
;
; Handle trapping the UpCall vector as required.
;
handle_upcall   ENTRY	"r0, r2"

		LDR	lr, InputBufferHandle			; check against input buffer handle
		TEQ	r1, lr
		TEQNE	r0, #UpCall_BufferFilling 		; 8, INSV has passed threshold
		TEQNE	r0, #UpCall_BufferEmptying		; 9, REMV has increased free space behind threshold

                EXITS   NE

                LDR     r11, UARTBaseAddress                 		; r11 -> UART base
                TEQP    PC, #SVC_mode + I_bit           		; disable IRQs
                LDR     r2, UARTDeviceFlags				; r2 = UART device flags

		TEQ	r0, #UpCall_BufferEmptying			; moved below threshold
		BEQ	%10						;  deal with it below

;upcall threshold halt
                ORR     r2, r2, #1:SHL:SF_Thresholded

                TST     r2, #1:SHL:SF_XONXOFFOn         		; if doing XON/XOFF
                ORRNE   r2, r2, #1:SHL:SF_IXOFFedHim    		; then indicate we're XOFFing him
                STR     r2, UARTDeviceFlags				; store UART device flags

                MOVNE   lr, #XOFFChar
                STRNEB  lr, UARTXONXOFFChar           			; try to send an XOFF

;		BEQ	%5
;		Push	"lr"
;		SWI	OS_WriteS
;		= "Send Xoff",10,13,0
;		Pull	"lr"
;5
		B	%20
10
;upcall threshold resume
                BIC     r2, r2, #1:SHL:SF_Thresholded

                TST     r2, #1:SHL:SF_XONXOFFOn
                BICNE   r2, r2, #1:SHL:SF_IXOFFedHim    		; clear my XOFF bit
                STR     r2, UARTDeviceFlags				; store UART device flags
                BEQ     %20

                TST     r2, #1:SHL:SF_UserXOFFedHim     		; if user not XOFFing him
                MOVEQ   lr, #XONChar                   	 		; then send an XON
                STREQB  lr, UARTXONXOFFChar				; store UART device flags

;		BNE	%15
;		Push	"lr"
;		SWI	OS_WriteS
;		= "Send Xon",10,13,0
;		Pull	"lr"
;15
20
                BL      update_DTRRTSTXI              			; update RTS, TXI

		PullEnv
		Pull	pc,,^						; claim the vector - Ta Tim.

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

		END
