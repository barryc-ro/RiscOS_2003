; FX screen transition and half-intensity box routines

r0              RN      0
r1              RN      1
r2              RN      2
r3              RN      3
r4              RN      4
r5              RN      5
r6              RN      6
r7              RN      7
r8              RN      8
r9              RN      9
r10             RN      10
r11             RN      11
r12             RN      12
r13             RN      13
r14             RN      14
r15             RN      15
lr              RN      14
pc              RN      15

        AREA    |C$$code|, CODE, READONLY

; fx_halfbox - plot a half-intensity box
; in -  r0 -> top left of box
;       r1 = width of box (pixels)
;       r2 = height of box (pixels)
;       r3 = linelen

        EXPORT  fx_halfbox

fx_halfbox
        STMFD   r13!, {r4-r6,lr}
        LDR     r4, =&3DEF3DEF
        SUB     r3, r3, r1, LSL #1
fxhalfboxline
        MOV     r5, r1
        TST     r0, #2
        BEQ     fxhalfboxword

        LDR     r6, [r0]
        MOV     r6, r6, LSR #1
        AND     r6, r6, r4
        STRB    r6, [r0], #1
        MOV     r6, r6, LSR #8
        STRB    r6, [r0], #1
        SUBS    r5, r5, #1
        BEQ     fxhalfbox_endofline

fxhalfboxword
        SUBS    r5, r5, #2
        LDRCS   r6, [r0]
        MOVCS   r6, r6, LSR #1
        ANDCS   r6, r6, r4
        STRCS   r6, [r0], #4
        BCS     fxhalfboxword

        TST     r5, #1
        BEQ     fxhalfbox_endofline

        LDR     r6, [r0]
        MOV     r6, r6, LSR #1
        AND     r6, r6, r4
        STRB    r6, [r0], #1
        MOV     r6, r6, LSR #8
        STRB    r6, [r0], #1
fxhalfbox_endofline
        ADD     r0, r0, r3
        SUBS    r2, r2, #1
        BNE     fxhalfboxline

        LDMFD   r13!, {r4-r6,pc}

        LTORG

        END
