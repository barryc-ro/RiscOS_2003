| > !STBWeb.!Run
|
| World Wide Web browsing software
| (c) ANT Limited, 1995
|
IconSprites <Obey$Dir>.!Sprites
|
Set STBWeb$Dir <Obey$Dir>
SetMacro STBWeb$Path <STBWeb$Dir>.
|
| -----------------------------------------------------------------------
|
| You must edit the following lines to remove the | at the front and
| change the IP addresses to be those of your DNS resolvers
| change the domain name to your domain
|
IF "<Inet$Resolvers>" = "" Then Set Inet$resolvers 136.170.131.2 136.170.131.3 
IF "<Inet$LocalDomain>" = "" Then Set Inet$LocalDomain acorn.co.uk
|
| -----------------------------------------------------------------------
|
IF "<Inet$Resolvers>" = "" Then Error You must configure !STBWeb.!Run with the name of at least one DNS resolver
IF "<Inet$LocalDomain>" = "" Then Error You must configure !STBWeb.!Run with your domain name
|
If "<Inet$MimeMappings>" = "" Then SetMacro Inet$MimeMappings <STBWeb$Dir>.MimeMap
|
| -----------------------------------------------------------------------
| Operating system variables
|
SetEval STBWeb$NeedChoices "<STBWeb$ChoicesDir>" = ""
If STBWeb$NeedChoices AND "<Choices$Write>" <> "" Then SetMacro STBWeb$ChoicesDir <Choices$Write>.STBWeb
If STBWeb$NeedChoices AND "<Choices$Write>" =  "" Then SetMacro STBWeb$ChoicesDir <Wimp$ScrapDir>.STBWebC
Unset STBWeb$NeedChoices
|
If "<STBWeb$Errors>" = "" Then Set STBWeb$Errors 2> null:
If "<STBWeb$Config>" = "" Then SetMacro STBWeb$Config <STBWeb$Dir>.Config
If "<STBWeb$Cookies>" = "" Then SetMacro STBWeb$Cookies <STBWeb$ChoicesDir>.Cookies
If "<STBWeb$Help>" = "" Then SetMacro STBWeb$Help <STBWeb$Dir>.Help
If "<STBWeb$Home>" = "" Then SetMacro STBWeb$Home <STBWeb$Dir>.Welcome
If "<STBWeb$Hotlist>" = "" Then SetMacro STBWeb$Hotlist <STBWeb$ChoicesDir>.Hotlist
If "<STBWeb$Users>" = "" Then SetMacro STBWeb$Users <STBWeb$ChoicesDir>.Users
If "<STBWeb$Welcome>" = "" Then SetMacro STBWeb$Welcome <STBWeb$Dir>.Welcome
|
| -----------------------------------------------------------------------
| Set UseTB to 0 or 1 here or set to something before calling
|
If "<STBWeb$UseTB>" = "" Then SetEval STBWeb$UseTB 1
|
| -----------------------------------------------------------------------
| Boot file stuff
|
Set File$Type_695 GIF
Set File$Type_B28 URL
Set File$Type_B60 PNG
Set File$Type_B61 XBitMap
Set File$Type_B62 Gopher
Set File$Type_C85 JPEG
Set File$Type_FAF HTML
|
Set Alias$URLOpen_HTTP   /STBWeb:!Run -URL %%*0
Set Alias$URLOpen_GOPHER /STBWeb:!Run -URL %%*0
Set Alias$URLOpen_FILE   /STBWeb:!Run -URL %%*0
Set Alias$URLOpen_FTP    /STBWeb:!Run -URL %%*0
|
| -----------------------------------------------------------------------
| Network modules
|
RMEnsure Resolver       0.39 RMLoad <STBWeb$Dir>.resolver
RMEnsure Resolver       0.39 Error You need Resolver 0.39 or later
|
RMEnsure HTTP 		0.52 RMLoad <STBWeb$Dir>.httpmod
RMEnsure HTTP 		0.52 Error You need HTTPMod 0.52 or later
RMEnsure WebGopher 	0.08 RMLoad <STBWeb$Dir>.WebGopher
RMEnsure WebGopher 	0.08 Error You need WebGopher 0.08 or later
RMEnsure WebFTP 	0.13 RMLoad <STBWeb$Dir>.WebFTP
RMEnsure WebFTP 	0.13 Error You need WebFTP 0.13 or later
|
| -----------------------------------------------------------------------
|
RMEnsure NHTwitter 	0.07 RMLoad <STBWeb$Dir>.NHTwitter
RMEnsure NHTwitter 	0.07 Error You need NHTwitter 0.07 or later
|
| -----------------------------------------------------------------------
| Toolbox modules
|
If STBWeb$UseTB Then Set STBWeb$TBOptions -t Else Set STBWeb$TBOptions ""
|
If STBWeb$UseTB Then RMEnsure Toolbox 1.36 RMLoad System:Modules.Toolbox.Toolbox
If STBWeb$UseTB Then RMEnsure Toolbox 1.36 Error You need Toolbox 1.36 or later
If STBWeb$UseTB Then RMEnsure Window  1.31 RMLoad System:Modules.Toolbox.Window
If STBWeb$UseTB Then RMEnsure Window  1.31 Error You need Window 1.30 or later
If STBWeb$UseTB Then RMEnsure Menu    0.24 RMLoad System:Modules.Toolbox.Menu
If STBWeb$UseTB Then RMEnsure Menu    0.24 Error You need Menu 0.23 or later
If STBWeb$UseTB Then RMEnsure ToolAction 0.12 RMLoad <STBWeb$Dir>.ToolAction
If STBWeb$UseTB Then RMEnsure ToolAction 0.12 Error You need ToolAction 0.12 or 
|
| -----------------------------------------------------------------------
| Ensure we have a scrap directory
|
If "<Wimp$ScrapDir>" = "" Then /STBWeb:SetScrap
If "<Wimp$Scrap>" = "" Then SetMacro Wimp$Scrap <Wimp$ScrapDir>.ScrapFile
CDir <Wimp$ScrapDir>
|
| Create choices directory here in case it is in the scrap directory
CDir <STBWeb$ChoicesDir>
|
| -----------------------------------------------------------------------
| Set Config Cache variables depending on cache location
|
SetEval STBWeb$ScrapFS Wimp$ScrapDir LEFT 1
If "<STBWeb$ScrapFS>" = "a" Then Set STBWeb$ScrapFS "A"
If "<STBWeb$ScrapFS>" = "c" Then Set STBWeb$ScrapFS "C"
|
If "<STBWeb$ScrapFS>" = "C" Then RMEnsure FileWatch 0.02 RMLoad <STBWeb$Dir>.FileWatch
|
UnSet STBWeb$ConfigCacheKeep
UnSet STBWeb$ConfigCacheKeepUpToDate
If "<STBWeb$ScrapFS>" = "A" Then Set STBWeb$ConfigCacheKeep Yes
If "<STBWeb$ScrapFS>" = "A" Then Set STBWeb$ConfigCacheKeepUpToDate Yes
|
| -----------------------------------------------------------------------
|
UnSet STBWeb$ConfigCookieUpToDate
If "<STBWeb$ScrapFS>" = "A" Then Set STBWeb$ConfigCookieUpToDate Yes
|
UnSet STBWeb$ConfigPasswordsUpToDate
If "<STBWeb$ScrapFS>" = "A" Then Set STBWeb$ConfigPasswordsUpToDate Yes
|
| -----------------------------------------------------------------------
| Set Config Display variables depending on TV$Type
|
Unset STBWeb$ConfigDisplayMarginLeft
Unset STBWeb$ConfigDisplayMarginRight
Unset STBWeb$ConfigDisplayMarginTop
Unset STBWeb$ConfigDisplayMarginBottom
If "<TV$Type>" = "PAL" Then Set STBWeb$ConfigDisplayMarginLeft 8
If "<TV$Type>" = "PAL" Then Set STBWeb$ConfigDisplayMarginRight 8
If "<TV$Type>" = "PAL" Then Set STBWeb$ConfigDisplayMarginTop 8
If "<TV$Type>" = "PAL" Then Set STBWeb$ConfigDisplayMarginBottom 8
If "<TV$Type>" = "NTSC" Then Set STBWeb$ConfigDisplayMarginLeft 2
If "<TV$Type>" = "NTSC" Then Set STBWeb$ConfigDisplayMarginRight 2
If "<TV$Type>" = "NTSC" Then Set STBWeb$ConfigDisplayMarginTop 6
If "<TV$Type>" = "NTSC" Then Set STBWeb$ConfigDisplayMarginBottom 6
|
Unset STBWeb$ConfigDisplayScale
If "<TV$Type>" = "PAL" OR "<TV$Type>" = "NTSC" Then Set STBWeb$ConfigDisplayScale 125
|
| -----------------------------------------------------------------------
|
WimpSlot -min 512k -max 512K
|
| -----------------------------------------------------------------------
| Choose which RunImage to use (StrongARM or standard)
|
If "<STBWeb$RunImage>" = "" Then /<STBWeb$Dir>.CheckProc
|
| -----------------------------------------------------------------------
|
Set Alias$STBWebRun /<STBWeb$Dir>.<STBWeb$RunImage> <STBWeb$Errors> <STBWeb$TBOptions> %*0 <STBWeb$Welcome>
STBWebRun
