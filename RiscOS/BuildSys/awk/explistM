
# MODIFIED VERSION FOR ETHERNET ROM BUILD ("M" = Modification field support)
# ---------------------------------------
#
# Based on explist.  Modified by J. Farrell to allow Components files to specify
# the modification field.  This is required by all the different EtherI podules
# as STB3 uses Modification "4", original Acorn NCs use "0"
#
#
# genextlist: generate a list of modules to be included in the ROM Image
#             The generated list is used as input for "MakeExpROM"
#
# Notes:
#   The Component's Source directory (field 3) is unused.
#   Modified for automatic release numbering.
#
{
    if (index($0, "#") == 0)			# Only process non-comment lines
    {
        if (index($0, "Description") == 1)	# Description line
        {
            Prod = $2
            Ver  = $3
            Mod = $4				# *** FOR ETHERNET
            Desc = $5				# *** FOR ETHERNET
            
            split(Ver,Rel,".")                  # split 0.01 etc
            for (i=6; i<=NF; i++)	# each word of description *** FOR ETHERNET
                Desc = Desc " " $i
	}
	else if (NF >= 5)			# only lines with "ROM modules" entries
        {
            Name    = $1
            Type    = $2
            Install = $4

            # Print input to Make8bit
            if (index(Name, "ROMImage") == 1)
            {
              for (i=5; i<=NF; i++)		# list each module on the line
              {
                printf("<install$dir>.%s.%.6s/%s%s\n", Install, $i,Rel[1],Rel[2])
              }
              printf("%d\n", Type)		# Size in K
              printf("%s\n", Prod)		# Product code
              printf("%s (release %s)\n", Desc,Ver)	# description & version
              printf("\n")			# Serial (none)
              printf("%s\n", Mod)			# Mod Status (none)
              printf("\n")			# Pace of manufacture (none)
              printf("\n")			# Part number (none)
              printf("\n")			# Date (today's date)
            }
            else
            {
              for (i=5; i<=NF; i++)		# list each module on the line
              {
                printf("<install$dir>.%s.%s\n", Install, $i)
              }
            }
        }
    }
}

END {
    printf("\n")
}
