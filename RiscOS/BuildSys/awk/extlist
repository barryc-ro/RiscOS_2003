# genextlist: generate a list of modules to be included in the ROM Image
#             The generated list is used as input for "Make8bit"
#
# Notes:
#   The Component's Source directory (field 3) is unused.
#   Modified for automatic release numbering.
#
{
    if (index($0, "#") == 0)			# Only process non-comment lines
    {
        if (index($0, "Description") == 1)	# Description line
        {
            Prod = $2
            Ver  = $3
            split(Ver,Rel,".")                  # split 0.01 etc
            Desc = $4
            for (i=5; i<=NF; i++)		# each word of description
                Desc = Desc " " $i
	}
        else if (index($0, "SharedCLibrary") == 1)
            CLibDir = $4
            
	else if (NF >= 5)			# only lines with "ROM modules" entries
        {
            Name    = $1
            Type    = $2
            Install = $4

            # Print input to Make8bit
            if (index(Name, "ROMImage") == 1)
            {
                printf("<install$dir>.%s.%.6s/%s%s\n", Install, $5 ,Rel[1],Rel[2])
                printf("%d\n", Type)		# Size in K
                printf("\n")			# Manufacturer (Acorn)
                printf("\n")			# Manufacturer country (UK)
                printf("\n")			# Serial (none)
                printf("TODAY\n")		# Date (today's date)
                printf("\n")			# Mod Status (none)
                printf("\n")			# Place of manufacture (none)
                printf("%s (release %s)\n", Desc,Ver)	# description & version
                printf("\n")			# Part number (none)
            }
            else
            {
                for (i=5; i<=NF; i++)		# list each module on the line
                {
                    Module = $i
                    if (index(Type, "C") == 1)
                        printf("<install$dir>.%s.linked.%s\n", CLibDir, Module)
                    else
                        printf("<install$dir>.%s.%s\n", Install, Module)
                }
            }
        }
    }
}

END {
    printf("\n")
}
