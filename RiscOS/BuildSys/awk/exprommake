# exprommake: generate a Makefile to control the final expansion ROM Image
#             construction stage
# Notes:
#   Modified for automatic release numbering.
#
BEGIN {
    i = 0
}

{
    if (index($0, "#") == 0)                    # Only process non-comment lines
    {
        if  (index($1, "ROMImage") == 1)
        {
            ROMInst = $4
            ROMname = $5
        }
        else if (index($0, "Description") == 1)	# Description line
        {
            split($3,Rel,".")                   # split 0.01 etc
	}
        else
        {
          # only lines with "ROM modules" entries are listed
          if (NF >= 5)
          {
            for (j=5; j<=NF; j++)
            {
                Name[i]    = $1
                Type[i]    = $2
                Source[i]  = $3
                Install[i] = $4
                Module[i]  = $j
                if (index(Name[i], "SharedCLibrary") == 1)
                    CLibDir = Install[i]
                i++
            }
          }
        }
    }
}

END {
    printf("# Makefile for ROMImage\n\n")

    printf("CD      = dir\n")
    printf("LD      = link\n")
    printf("MKDIR   = cdir\n")
    printf("MKIMAGE = MakeExpROM\n\n")

    printf("SRCDIR  = <src$dir>\n")
    printf("INSTDIR = <install$dir>\n")
    printf("CLIBDIR = <install$dir>.%s\n", CLibDir)
    printf("ABSSYM  = RISC_OSLib:o.AbsSym\n")
    printf("\n")

    printf("MODULES =")

    ncomp = i-1
    for (i=ncomp; i>=0; i--)
        printf(" \\\n %s", Module[i])
    printf("\n\n")

    printf("#\n")
    printf("# Build rules:\n")
    printf("#\n")

    printf("install: ${MODULES}\n")
    printf("        ${MKIMAGE} { < ForBigSplt }\n")
    printf("        settype <install$dir>.%s.%.6s/%s%s FE5\n\n",ROMInst,ROMname,Rel[1],Rel[2])

    for (i=0; i<=ncomp; i++)
    {
        printf("%s:\n", Module[i])

        if (i==0)
            printf("        @seteval PositionInRom &03C00000\n")

        printf("        @setolength File$Length ${INSTDIR}.%s.%s\n", Install[i], Module[i])
        printf("        @echo %-25s  <PositionInRom>  <File$Length>\n", Module[i])
        printf("        @seteval PositionInRom PositionInRom + ((File$Length + 3) AND NOT 3) + 4\n\n")
    }
}
