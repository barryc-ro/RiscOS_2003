        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System

        GET     Hdr:OSEntries
        GET     Hdr:HALEntries

        GET     hdr.StaticWS
        GET     hdr.Hardware

        AREA    |Asm$$Code|, CODE, READONLY, PIC

        EXPORT  IIC_Init
        EXPORT  HAL_IICBuses
        EXPORT  HAL_IICType
        EXPORT  HAL_IICSetLines
        EXPORT  HAL_IICReadLines

IIC_Init
        LDR     a4, HW_Address
        ADD     a4, a4, #I2C_BASE - PERIPHERAL_BASE
        LDR     a1, =&103F      ; Disable engine, SLAx push-pull, D+C open-drain, all outputs
        STR     a1, [a4, #CFGI]
        MOV     a1, #2_1100
        STR     a1, [a4, #DATAI]
        STR     a1, IIC_DATAI_SoftCopy
        MOV     pc, lr

HAL_IICBuses
        MOV     a1, #1
        MOV     pc, lr

HAL_IICType
        MOV     a1, #IICFlag_LowLevel
        MOV     pc, lr

HAL_IICSetLines
        LDR     a4, HW_Address
        ORR     a1, a3, a2, LSL #1
        MRS     ip, CPSR
        ADD     a4, a4, #I2C_BASE - PERIPHERAL_BASE
        ORR     a2, ip, #I32_bit
        MSR     CPSR_c, a2
        LDR     a3, IIC_DATAI_SoftCopy
        BIC     a3, a3, #2_11
        ORR     a1, a1, a3
        STR     a1, [a4, #DATAI]
        STR     a1, IIC_DATAI_SoftCopy
        MSR     CPSR_c, ip
        B       HAL_IICReadLines2

HAL_IICReadLines
        LDR     a4, HW_Address
        ADD     a4, a4, #I2C_BASE - PERIPHERAL_BASE
HAL_IICReadLines2
        LDR     a3, [a4, #DATAI]
        MOV     ip, #1
        AND     a1, ip, a3, LSR #1
        AND     a2, ip, a3
        MOV     pc, lr

        END
