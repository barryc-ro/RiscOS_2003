        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:Machine.<Machine>
        GET     Hdr:ImageSize.<ImageSize>

        GET     Hdr:OSEntries

        GET     hdr.StaticWS
        GET     hdr.Hardware
        GET     hdr.Video

        AREA    |Asm$$Code|, CODE, READONLY, PIC

        EXPORT  Interrupt_Init
        EXPORT  HAL_IRQEnable
        EXPORT  HAL_IRQDisable
        EXPORT  HAL_IRQClear
        EXPORT  HAL_IRQSource
        IMPORT  HAL_IRQClear_Video
        EXPORT  HAL_FIQDisableCode

Interrupt_Init
        LDR     a3, HW_Address
        MVN     a1, #0
        ADD     a3, a3, #INT_BASE-PERIPHERAL_BASE
        STR     a1, [a3, #CLRIRQMSKA]
        STR     a1, [a3, #CLRIRQMSKB]
        STR     a1, [a3, #CLRFIQMSKA]
        STR     a1, [a3, #CLRFIQMSKB]
        MOV     a1, #1:SHL:(38-32) + 1:SHL:(39-32)
        STR     a1, [a3, #IRQTRIGGER]           ; interrupts 38+39 (vsync) are edge triggered
        MOV     pc, lr

HAL_IRQEnable
        LDR     a3, HW_Address
        MOV     ip, #1
        CMP     a1, #32
        SUBHS   a1, a1, #32
        MOV     a1, ip, LSL a1
        ADD     a3, a3, #INT_BASE-PERIPHERAL_BASE
        STRLO   a1, [a3, #IRQMASKA]
        STRHS   a1, [a3, #IRQMASKB]
        MOV     pc, lr

HAL_IRQDisable
        LDR     a3, HW_Address
        MOV     ip, #1
        CMP     a1, #32
        SUBHS   a1, a1, #32
        MOV     a1, ip, LSL a1
        ADD     a3, a3, #INT_BASE-PERIPHERAL_BASE
        STRLO   a1, [a3, #CLRIRQMSKA]
        STRHS   a1, [a3, #CLRIRQMSKB]
        MOV     pc, lr

        IMPORT  HAL_IRQClear_Timer

HAL_IRQClear
        LDR     a3, HW_Address

        CMP     a1, #4
        RSBHSS  ip, a1, #8
        BHS     HAL_IRQClear_Timer

        TEQ     a1, #4
        TEQNE   a1, #5
        BEQ     HAL_IRQClear_Timer
        TEQ     a1, #20
        BEQ     HAL_IRQClear_Video

        SUBS    a1, a1, #32
        MOVLO   pc, lr

        MOV     ip, #1
        MOV     a1, ip, LSL a1
        ADD     a3, a3, #INT_BASE-PERIPHERAL_BASE
        STR     a1, [a3, #RAWIRQSTB]
        MOV     pc, lr

 [ {TRUE}
HAL_IRQSource
        LDR     a3, HW_Address
        ADD     a3, a3, #INT_BASE-PERIPHERAL_BASE
        MOV     a1, #0
        LDR     ip, [a3, #MASKIRQSTA]
05      MOVS    ip, ip, LSR #1
        MOVCS   pc, lr
        BEQ     %FT30
        ADD     a1, a1, #1
        B       %BT05
30      MOV     a1, #32
        LDR     ip, [a3, #MASKIRQSTB]
10      MOVS    ip, ip, LSR #1
        MOVCS   pc, lr
        ADD     a1, a1, #1
        BNE     %BT10
        MOV     a1, #64
        MOV     pc, lr
 |
HAL_IRQSource
        LDR     a3, INT_BASE_Address
        LDR     a1, [a3, #IRQLStatus]
                                                ; ARM7  ARM7M ARM9    SA1
        LDR     ip, =&07DCD629                  ; 3     3     1       1
        ORR     a1, a1, a1, LSR #1              ; 1     1     1       1
        ORR     a1, a1, a1, LSR #2              ; 1     1     1       1
        ORR     a1, a1, a1, LSR #4              ; 1     1     1       1
        ORR     a1, a1, a1, LSR #8              ; 1     1     1       1
        ORRS    a1, a1, a1, LSR #16             ; 1     1     1       1
        MLANE   a1, ip, a1, ip                  ; 2-17  3-7   3-7     1-3
        MOVEQ   a1, #32                         ; 1     1     1       1
        LDRNEB  a1, [pc, a1, LSR #27]           ; 3     3     1       1
                                                ; ----- ----- -----   ----
                                                ; 14-29 15-19 11-15   9-11
        MOV     pc, lr

irq_table
        =       31, 0,22, 1,28,23,13, 2,29,26,24,17,19,14, 9, 3
        =       30,21,27,12,25,16,18, 8,20,11,15, 7,10, 6, 5, 4
 ]

HAL_FIQDisableCode
        ADR     a2, FIQOff_Code
        LDMIA   a2!, {a3,a4,ip}
        STMIA   a1!, {a3,a4,ip}
        LDR     ip, HW_Address
        LDMIA   a2!, {a3,a4}
        ADD     ip, ip, #INT_BASE-PERIPHERAL_BASE
        STMIA   a1!, {a3,a4,ip}
        MOV     pc, lr

FIQOff_Code
        LDR     r8, FIQOff_INT_BASE
        MVN     r9, #0
        STR     r9, [r8, #CLRFIQMSKA]
        STR     r9, [r8, #CLRFIQMSKB]
        SUBS    pc, lr, #4
FIQOff_INT_BASE
FIQOff_Code_End

        END
