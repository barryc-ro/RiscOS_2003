        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:Machine.<Machine>
        GET     Hdr:ImageSize.<ImageSize>

        GET     Hdr:OSEntries
        GET     Hdr:HALEntries

        GET     hdr.StaticWS
        GET     hdr.Hardware

        AREA    |Asm$$Code|, CODE, READONLY, PIC

        EXPORT  Timer_Init

        EXPORT  HAL_Timers
        EXPORT  HAL_TimerDevice
        EXPORT  HAL_TimerGranularity
        EXPORT  HAL_TimerMaxPeriod
        EXPORT  HAL_TimerSetPeriod
        EXPORT  HAL_TimerPeriod
        EXPORT  HAL_TimerReadCountdown

        EXPORT  HAL_CounterRate
        EXPORT  HAL_CounterPeriod
        EXPORT  HAL_CounterRead
        EXPORT  HAL_CounterDelay

Timer_Init
        LDR     a4, HW_Address
        ADD     a4, a4, #TIMER_BASE - PERIPHERAL_BASE
        MOV     a1, #31                 ; /32 = 2MHz (if 64MHz clock)
        STR     a1, [a4, #GLOBAL_PRESCALE]
        MOV     a1, #&40
        STR     a1, [a4, #TIMER1CONTROL]
        STR     a1, [a4, #TIMER2CONTROL]
        MOV     pc, lr

HAL_Timers
        MOV     a1, #2                  ; 2 timers
        MOV     pc, lr

HAL_TimerDevice
        ADD     a1, a1, #4              ; devices 4 and 5
        MOV     pc, lr

HAL_CounterRate
HAL_TimerGranularity
        LDR     a1, =TIMERRATE
        MOV     pc, lr

HAL_TimerMaxPeriod
        MOV     a1, #&10000             ; 16-bit counter
        MOV     pc, lr

HAL_TimerSetPeriod
        LDR     a4, HW_Address
        ADD     a4, a4, #TIMER_BASE - PERIPHERAL_BASE

        ADR     a3, TimerPeriods
        STR     a2, [a3, a1, LSL #2]

        SUBS    a2, a2, #1
        ASSERT  TIMER2LOAD - TIMER1LOAD = 32
        ADD     a4, a4, a1, LSL #5
        BMI     %FT50

        STR     a2, [a4, #TIMER1LOAD]
        LDR     a1, [a4, #TIMER1CONTROL]
        ORR     a1, a1, #&80           ; Enable, periodic
        STR     a1, [a4, #TIMER1CONTROL]
        MOV     pc, lr

50      LDR     a1, [a4, #TIMER1CONTROL]
        BIC     a1, a1, #&80            ; Disable
        STR     a1, [a4, #TIMER1CONTROL]
        MOV     pc, lr

HAL_TimerPeriod
        ADR     a4, TimerPeriods
        LDR     a1, [a4, a1, LSL #2]
        MOV     pc, lr

HAL_CounterRead
        MOV     a1, #0
        ; Fall through

HAL_TimerReadCountdown
        LDR     a4, HW_Address
        ADD     a4, a4, #TIMER_BASE - PERIPHERAL_BASE

        ASSERT  TIMER2LOAD - TIMER1LOAD = 32
        ADD     a4, a4, a1, LSL #5

        LDRH    a1, [a4, #TIMER1VALUE]

        MOV     pc, lr

        MACRO
        Div5    $r1, $r2, $r3
; takes argument in $r1
; returns quotient in $r1, remainder in $r2
; $r3 corrupted
; cycles could be saved if only divide or remainder is required
        SUB     $r2, $r1, #5
        SUB     $r1, $r1, $r1, LSR #2
        ADD     $r1, $r1, $r1, LSR #4
        ADD     $r1, $r1, $r1, LSR #8
        ADD     $r1, $r1, $r1, LSR #16
        MOV     $r1, $r1, LSR #2
        ADD     $r3, $r1, $r1, LSL #2
        SUBS    $r2, $r2, $r3, LSL #0
        ADDPL   $r1, $r1, #1
        ADDMI   $r2, $r2, #5
        MEND

HAL_CounterPeriod
        LDR     a1, TimerPeriods + 0
        MOV     pc, lr

; If they want n ticks, wait until we've seen n+1 transitions of the clock.
HAL_CounterDelay
        LDR     a4, HW_Address
 [ TIMERRATE = 2000000
        MOV     a1, a1, LSL #1          ; convert to 2MHz ticks
 |
  [ TIMERRATE = 1600000
        MOV     a1, a1, LSL #3          ; *8
        Div5    a1, a2, a3              ; a1 := a1 / 5; a2 := a1 % 5; a3 temp
  |
   [ TIMERRATE = 1000000
        ; Do nowt
   |
        ! 1, "Can't cope with this timer rate"
   ]
  ]
 ]
        ADD     a4, a4, #TIMER_BASE - PERIPHERAL_BASE

        LDRH    a2, [a4, #TIMER1VALUE]

10      LDRH    a3, [a4, #TIMER1VALUE]
        SUBS    ip, a2, a3              ; ip = difference in ticks
        ADDLO   ip, a2, #1              ; if wrapped, must have had at least old latch+1 ticks
        SUBS    a1, a1, ip
        MOVLO   pc, lr
        LDRH    a2, [a4, #TIMER1VALUE]
        SUBS    ip, a3, a2              ; ip = difference in ticks
        ADDLO   ip, a3, #1              ; if wrapped, must have had at least old latch+1 ticks
        SUBS    a1, a1, ip
        BHS     %BT10
        MOV     pc, lr

        END
