;**************************************************************************
;
;  File:        s.veneer
;
;  Description: Header for the h263 ARMovie codec
;
;  Version:     0.0 16 Sep 1997
;
;  Author:      Alex Burr (aburr@acorn.com)
;		modified by PHBG
;               modified by Pete Goodliffe (pgoodliffe@acorn.com)
;		further 'improved' by RJW
;
;  Modification History:
;
;    Date         Name                  Description
;    ----------------------------------------------------------------------
;    Prehistory   PHBG			Made cope with nulls
;    16 Sep 1997  Pete Goodliffe        Coalesced veneer, entries, dosetxy
;                                       and nullcopy
;    29 Sep 1997  RJW			Merged the 'D' cruft in here. Now
;					just predefine "dtype", and include
;					this file.
;
;**************************************************************************

        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     HDR:APCS.<APCS>

; EXPORT gubbins here

	EXPORT	|_CodeStart|		; the start of the codec
	EXPORT	nullcopy ;(to, from)	; the nullcopy function
	EXPORT	dosetxy

; And IMPORTs here

	IMPORT	DecompEntry		; C function implementation
	IMPORT	|__RelocCode|
	IMPORT	InitEntry
	IMPORT	ShutEntry


;**************************************************************************
;* the entry points
;**************************************************************************

|_CodeStart|
	DCD	PatchTable - |_CodeStart|	; patch table offset
	B	|_InitEntry|		; entry point 4: init
	B	DecompEntry		; entry point 8: decompress


;**************************************************************************
;* the code implementing entry point 4: init
;**************************************************************************


shut	DCB	"SHUT"

|_InitEntry|
	stmfd	r13!,{lr}
	ldr	r4,shut
	cmp	r4,r2
	beq	%ft10		; not init call
	stmfd	r13!,{r0-r12}
	bl	|__RelocCode|
	mov	r0,#0
	swi	&2006e		; synchronise code areas
	ldmfd	r13!,{r0-r12}
	bl	InitEntry
	cmp	r0,#0
	bne	%ft05
	adds	r0,r0,#0	; clears V,C; Z undefined, N undefined
	ldmfd	r13!,{pc}

05
	mov	r4,#&80000000
	adds	r4,r4,r4	; sets V
	ldmfd	r13!,{pc}

10
	bl	ShutEntry
	cmp	r0,#0
	bne	%ft15
	adds	r0,r0,#0
	ldmfd	r13!,{pc}	; clears V,C; Z undefined, N undefined

15
	mov	r4,#&80000000
	adds	r4,r4,r4	; sets V
	ldmfd	r13!,{pc}


;**************************************************************************
;* dosetxy
;**************************************************************************

xpix	DCD	0			; image x size
ypix	DCD	0			; image y size

dosetxy					; called during initialisation
	str	r0,xpix
	str	r1,ypix
        Return  , LinkNotStacked

;**************************************************************************
;* Now we set some variables based on the which value has been assigned to
;* dtype...
;**************************************************************************
;   decsize  - bpp output size
;   ppl      - whether to use plook
;   mash     - 3 for dec21, 0 fir dec24, 1 for else
;   torgb    - whether to convert from YUV to RGB
;   out16    - whether to use tryout or tryoutd16 == 16 bit output

;**************************************************************************
; The file format descriptions

 [ dtype="Decompressor"
decsize	*	4
ppl	*	1
mash	*	1
torgb	*	0
out16	*	1
fastdec	*	0
 ]

;**************************************************************************

 [ dtype="DecB"
decsize	*	1
ppl	*	1
mash	*	1
torgb	*	0
out16	*	0
fastdec	*	0
 ]

;**************************************************************************

 [ dtype="DecH"
decsize	*	2
ppl	*	1
mash	*	1
torgb	*	0
out16	*	1
fastdec	*	0
 ]

;**************************************************************************

 [ dtype="Dec8"
decsize	*	1
ppl	*	0
mash	*	1
out16	*	0
fastdec	*	0
 ]

;**************************************************************************

 [ dtype="Dec16"
decsize	*	2
ppl	*	0
mash	*	1
torgb	*	0
out16	*	1
fastdec	*	0
 ]

;**************************************************************************

 [ dtype="Dec21"
decsize	*	4
ppl	*	0
mash	*	3
torgb	*	0
out16	*	0
fastdec	*	0
 ]

;**************************************************************************

 [ dtype="Dec216Y5UV"
decsize	*	4
ppl	*	0
mash	*	3
torgb	*	1
out16	*	0
fastdec	*	0
 ]

;**************************************************************************

 [ dtype="FastDec21"
decsize	*	4
ppl	*	0
mash	*	0
torgb	*	1
out16	*	0
fastdec	*	1
 ]

;**************************************************************************

 [ dtype="Dec24"
decsize	*	4
ppl	*	0
mash	*	0
torgb	*	0
out16	*	0
fastdec	*	0
 ]

;**************************************************************************

 [ dtype="Dec246Y5UV"
decsize	*	4
ppl	*	0
mash	*	0
torgb	*	1
out16	*	0
fastdec	*	0
 ]

;**************************************************************************

 [ dtype="FastDec24"
decsize	*	4
ppl	*	0
mash	*	0
torgb	*	1
out16	*	0
fastdec	*	1
 ]

;**************************************************************************

 [ dtype="FastDecYUV"
decsize	*	2
ppl	*	0
mash	*	0
torgb	*	0
out16	*	0
fastdec	*	1
 ]

;**************************************************************************
;* GET tryout, according to vars above
;**************************************************************************

 [ fastdec=0
  [ out16=1
	GET	s.tryoutd16
  |
	GET	s.tryout
  ]
 |
  [ torgb=1
	GET	s.tryoutFast
  |
	GET	s.tryoutFYUV
  ]
 ]

;**************************************************************************
;* nullcopy: PHBG
;**************************************************************************

nullcopy
        FunctionEntry "r4-r6"
	LDR	r2, xpix
	LDR	r3, ypix
	MUL	r2, r3, r2
 [ decsize = 2
	MOV	r2, r2, LSL#1
 ]
 [ decsize = 4
	MOV	r2, r2, LSL#2
 ]
nullcopynext
	LDMIA	r1!, {r3-r6}
	STMIA	r0!, {r3-r6}
	SUBS	r2, r2, #16
	BHI	nullcopynext
        Return  "r4-r6"

;**************************************************************************
;* the end
;**************************************************************************

	END
