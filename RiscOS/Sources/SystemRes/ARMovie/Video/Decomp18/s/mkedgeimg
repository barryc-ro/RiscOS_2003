;**************************************************************************
;
;  File:        s.mkedgeimg
;
;  Description:
;
;  Version:     0.0 11 Mar 1998
;
;  Author:      RJW
;
;  Modification History:
;
;    Date         Name                  Description
;    ----------------------------------------------------------------------
;
;**************************************************************************

        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:APCS.<APCS>

        AREA	|C$$code|, CODE, READONLY

	EXPORT	make_edge_image

make_edge_image
	; r0 = unsigned char *src
	; r1 = int width
	; r2 = int height
	; r3 = int edge

        FunctionEntry "r4-r11"

	ADD	r9,r1,r3,LSL#1		; r9 = lx
	ADD	r8,r1,r3
	; First, top left corner
	LDRB	r4,[r0]
	SUB	r14,r0,r3
	ADD	r12,r3,#1
	ORR	r4,r4,r4,LSL#8
	ORR	r4,r4,r4,LSL#16
	MOV	r5,r4
	MOV	r6,r4
	MOV	r7,r4
mei_lp1
	STMIA	r14!,{r4-r7}
	CMP	r3,#16
	STMGTIA	r14!,{r4-r7}
	SUBS	r12,r12,#1
	SUBGT	r14,r14,r9
	SUBGT	r14,r14,r3
	BGT	mei_lp1

	; Now the top edge
	MOV	r7,r1
mei_lp3
	MOV	r12,r3
	LDMIA	r0!,{r4-r5}
	MOV	r6,r14
mei_lp2
	STMIA	r6,{r4-r5}
	ADD	r6,r6,r9
	SUBS	r12,r12,#1
	BGT	mei_lp2
	ADD	r14,r14,#8
	SUBS	r7,r7,#8
	BGT	mei_lp3

	; Now, top right corner
	AND	r5,r5,#&FF000000
	ORR	r5,r5,r5,LSR#8
	ORR	r5,r5,r5,LSR#16
	MOV	r12,r3
	MOV	r4,r5
	MOV	r6,r4
	MOV	r7,r4
mei_lp4
	STMIA	r14!,{r4-r7}
	CMP	r3,#16
	STMGTIA	r14!,{r4-r7}
	SUBS	r12,r12,#1
	ADD	r14,r14,r8
	BGT	mei_lp4

	; Now, left and right edges...
	; r0 = ptr to right most pixel of top line+1
	; r1 = width
	; r2 = height
	; r3 = edge
mei_lp5
	; Load the left...
	LDRB	r7,[r0,r3,LSL#1]
	; ...and right pixels
	LDRB	r5,[r0,#-1]
	; Expand them to words, and duplicate
	ORR	r7,r7,r7,LSL#8
	ORR	r7,r7,r7,LSL#16
	MOV	r14,r7
	ORR	r5,r5,r5,LSL#8
	ORR	r5,r5,r5,LSL#16
	MOV	r6,r5
	; Store 'em
	STMIA	r0!,{r5,r6}
	CMP	r3,#16
	STMGTIA	r0!,{r5,r6}
	STMGTIA	r0!,{r5,r6}
	STMIA	r0!,{r5,r6,r7,r14}
	STMIA	r0!,{r7,r14}
	STMGTIA	r0!,{r7,r14}
	STMGTIA	r0!,{r7,r14}
	; Go to next line
	ADD	r0,r0,r1
	; More to do?
	SUBS	r2,r2,#1
	BGT	mei_lp5

	; Now, bottom left corner
	; r0 = Pointing to first pixel in right corner
	SUB	r2,r0,r1
	; r2 = Pointing to first pixel in bottom edge section
	SUB	r14,r2,r9
	; r14 = Pointing to first pixel on bottom line of image
	LDRB	r4,[r14],r9
	MOV	r12,r3
	SUB	r14,r14,r3
	; r14 = Pointing to top left pixel of bottom left corner
	ORR	r4,r4,r4,LSL#8
	ORR	r4,r4,r4,LSL#16
	MOV	r5,r4
	MOV	r6,r4
	MOV	r7,r4
mei_lp6
	STMIA	r14!,{r4-r7}
	CMP	r3,#16
	STMGTIA	r14!,{r4-r7}
	SUBS	r12,r12,#1
	ADD	r14,r14,r8
	BGT	mei_lp6

	; Now, bottom centre edge
	SUB	r14,r2,r9
mei_lp8
	LDMIA	r14!,{r4-r5}
	MOV	r6,r2
	MOV	r12,r3
mei_lp7
	STMIA	r6,{r4-r5}
	ADD	r6,r6,r9
	SUBS	r12,r12,#1
	BGT	mei_lp7
	ADD	r2,r2,#8
	SUBS	r1,r1,#8
	BGT	mei_lp8

	; Finally, bottom right corner
	AND	r5,r5,#&FF000000
	ORR	r5,r5,r5,LSR#8
	ORR	r5,r5,r5,LSR#16
	MOV	r4,r5
	MOV	r6,r5
	MOV	r7,r5
	MOV	r12,r3
	ADD	r14,r14,r9
mei_lp9
	STMIA	r14!,{r4-r7}
	CMP	r3,#16
	STMGTIA	r14!,{r4-r7}
	SUBS	r12,r12,#1
	ADD	r14,r14,r8
	BGT	mei_lp9

        Return  "r4-r11"

	END
