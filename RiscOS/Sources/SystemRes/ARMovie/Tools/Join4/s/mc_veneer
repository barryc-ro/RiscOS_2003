;	s.mc_veneer
;
;**************************************************************************
;
;	Veener for calling a non-apcs m/c routine from C
;       assembly part
;
;	Pete Goodliffe (pgoodliffe@acorn.com)
;	28 Aug 97

	AREA	|C$$code|, CODE, READONLY

; EXPORT the function herein

	EXPORT	|mc_routine|

        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:APCS.<APCS>

;**************************************************************************
;* mc_routine

|mc_routine|
	; call m/c code at given address
	;   on entry:
	;     r0: address to call
	;     r1: address of registers structure
	;         an array of 9 32 bit numbers
	;   returns:
	;     r0,r1: corrupted
	;         updated register structure

        FunctionEntry "r4-r9"

	mov	r9, r0				; temp store call address
        str     r1, [r13, #-4]!                 ; store the register
						; structure address
						; for use after m/c call

	mov	r14, r1				; put input register values
        ldmia   r14, {r0-r8}                    ; into the registers

	adr	lr, returnAddress		; set up link register
	mov	pc, r9				; call m/c routine

returnAddress
        ldr     r14, [r13], #4                  ; put register values into
        stmia   r14, {r0-r8}                    ; the register block
                                                ; as output of routine
        Return  "r4-r9"

;**************************************************************************
;* End of file
;**************************************************************************

	END

