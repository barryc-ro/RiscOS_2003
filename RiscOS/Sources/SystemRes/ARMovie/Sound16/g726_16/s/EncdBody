;**************************************************************************
;
;  File:        s.EncdBody
;
;  Description: Assembler veneer code body for Acorn Replay sound compressor
;               g726_16 version
;
;  Version:     0.0 3 Oct 1997
;
;  Author:      Pete Goodliffe (pgoodliffe@acorn.nom)
;
;  Modification History:
;
;    Date         Name                  Description
;    ----------------------------------------------------------------------
;
;**************************************************************************

; requires stereo to be defined

	ASSERT	:DEF: stereo

	AREA	|C$$code|, CODE, READONLY

; EXPORT the functions herein

	EXPORT	|encode_init1|
	EXPORT	|encode_init2|
	EXPORT	|encode_compress|

;**************************************************************************
;* Set up functions to call

	GBLS	SIZEOF
	GBLS	INIT
	GBLS	CODER
	GBLA	CFS			; compressed frame size (bytes)
	GBLA	M_CFS			; compressed frame size per channel
	[ :LNOT: stereo
	IMPORT	|g726_16_mono_coder|	; encode samples in g726_16 format
	IMPORT	|g726_16_mono_sizeof|	; find size of necessary workspace
	IMPORT	|g726_16_mono_init|	; initialise workspace
SIZEOF	SETS	"g726_16_mono_sizeof"
INIT	SETS	"g726_16_mono_init"
CODER	SETS	"g726_16_mono_coder"
CFS	SETA	1
M_CFS	SETA	1
	]
	[ stereo
	IMPORT	|g726_16_stereo_coder|	; encode samples in g726_16 format
	IMPORT	|g726_16_stereo_sizeof|	; find size of necessary workspace
	IMPORT	|g726_16_stereo_init|	; initialise workspace
SIZEOF	SETS	"g726_16_stereo_sizeof"
INIT	SETS	"g726_16_stereo_init"
CODER	SETS	"g726_16_stereo_coder"
CFS	SETA	1*2
M_CFS	SETA	1
	]

;**************************************************************************
;* Main code body
;**************************************************************************

;**************************************************************************
;* Initialisation

encode_init1
	; initialise for encoding
	;   on entry:
	;     r0: sample frequency in Hz for information
	;     r1: (parameter string; ignored)
        ;   returns
        ;     r0: workspace size required
	;     r1: zero or pointer to error block
	;     r2: frame size of compression type

        str     lr, [r13, #-4]!

	str	r0, sampleHz		; record the sample rate

	bl	$SIZEOF			; workspace size returned in r0
	mov	r1, #0			; no error
	mov	r2, #$M_CFS		; frame size per channel

        ldr     pc, [r13], #4


;**************************************************************************

encode_init2
	; initialise workspace
	;   on entry
	;     r0: sample frequency in Hz for information
	;     r1: pointer to workspace
	;     r2: size of workspace
	;   returns
	;     r0: either 0 or pointer to error block

        str     lr, [r13, #-4]!
        mrs     lr, cpsr
        str     lr, [r13, #-4]!

	; check workspace size
	; ???

	mov	r0, r1			; copy workspace pointer
	bl	$INIT			; initialise workspace

	mov	r0, #0			; return success
        ldr     lr, [r13], #4
        msr     cpsr_f, lr              ; have to preserve flags across call
        ldr     pc, [r13], #4           ; for backwards compatibility :-(


;**************************************************************************
;* Compression
;**************************************************************************

encode_compress
	; compress a frame
	;   on entry
	;     r0: current bit offset in output buffer (updated on exit)
	;     r1: pointer to output buffer
	;     r2: number of samples (usually the input frame sample size)
	;     r3: pointer to samples
	;     r4: pointer to workspace
	;     r5: true (non zero) if frame is start of chunk, (add workspace)
	;         false otherwise
	;   returns
	;     r0: new current bit offset in output buffer
	;     r1: either 0 or pointer to error block
	;     r2: number of samples actually encoded per channel

        str     lr, [r13, #-4]!
        mrs     lr, cpsr
        str     lr, [r13, #-4]!

	mov 	r6, r0, LSR#3		; convert bit offset to byte offset
					; we can assume for this codec that
					; that it will be on a byte boundary

	; prepare registers for call to |g726_16_X_coder|

	mov	r0, r3			; src ptr
	add	r1, r1, r6		; dest ptr
	mov	r2, r4			; workspace

	; for |g726_16_X_coder| entry
	;   r0 = source samples pointer
	;   r1 = pointer to destination
	;   r2 = pointer to workspace
	;
	; the function consumes 2 16-bit words and produces 1 byte in mono
	;   and consumes 4 16-bits words and produces 1 byte in stereo
	;   it preserves r4-r9

	bl	$CODER

	; calculate return values
					; calculate new bit offset to return
	add	r0, r6, #CFS		; by adding CFS to orignal byte count
	mov	r0, r0, LSL#3		; and converting it to bits

	mov	r1, #0			; assume success
	mov	r2, #4			; number of samples encoded per chnl

        ldr     lr, [r13], #4
        msr     cpsr_f, lr              ; have to preserve flags across call
        ldr     pc, [r13], #4           ; for backwards compatibility :-(


;**************************************************************************
;* The data area
;**************************************************************************

sampleHz
	DCD	0			; sample rate given to init1

;**************************************************************************
;* End of file
;**************************************************************************

	END
