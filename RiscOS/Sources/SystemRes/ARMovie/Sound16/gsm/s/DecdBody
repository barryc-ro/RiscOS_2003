;**************************************************************************
;
;  File:        s.DecdBody
;
;  Description: Assembler veneer code body for Acorn Replay sound decomprssr
;               GSM version
;
;  Version:     0.0 3 Oct 1997
;
;  Author:      Pete Goodliffe (pgoodliffe@acorn.nom)
;
;  Modification History:
;
;    Date         Name                  Description
;    ----------------------------------------------------------------------
;    1 Oct 1997   Pete Goodliffe        Decode->DecdBody requiring stereo
;                                       to be predefined; stereo code added
;    7 Oct 1997   Pete Goodliffe        Removed the prepending of chunk by
;                                       a copy of the workspace
;    10 Oct 1997  Pete Goodliffe        Removed 8000 Hz freq restriction
;
;**************************************************************************

; requires stereo to be defined

	ASSERT	:DEF: stereo

	AREA	|C$$code|, CODE, READONLY

; EXPORT the functions herein

	EXPORT	|decode_init1|
	EXPORT	|decode_init2|
	EXPORT	|decode_decompress|

;**************************************************************************
;* Set up functions to call

	GBLS	SIZEOF
	GBLS	INIT
	GBLS	DECODER
	GBLA	M_DFS			; frame size per channel
	[ :LNOT: stereo
	IMPORT	|gsm_mono_decoder|	; decode samples in GSM format
	IMPORT	|gsm_mono_sizeof|	; find size of necessary workspace
	IMPORT	|gsm_mono_init|		; initialise workspace
SIZEOF	SETS	"gsm_mono_sizeof"
INIT	SETS	"gsm_mono_init"
DECODER	SETS	"gsm_mono_decoder"
M_DFS	SETA	33
	]
	[ stereo
	IMPORT	|gsm_stereo_decoder|	; decode samples in GSM format
	IMPORT	|gsm_stereo_sizeof|	; find size of necessary workspace
	IMPORT	|gsm_stereo_init|	; initialise workspace
SIZEOF	SETS	"gsm_stereo_sizeof"
INIT	SETS	"gsm_stereo_init"
DECODER	SETS	"gsm_stereo_decoder"
M_DFS	SETA	33
	]

;**************************************************************************
;* Main code body
;**************************************************************************

;**************************************************************************
;* Initialisation

decode_init1
	; initialise for encoding
	;   on entry:
	;     r0: sample frequency in Hz for information
	;     r1: (parameter string; ignored)
        ;   returns
        ;     r0: workspace size required
	;     r1: zero or pointer to error block
	;     r2: frame size of compression type

        str     lr, [r13, #-4]!

	str	r0, sampleHz		; record the sample rate

	bl	$SIZEOF			; workspace size returned in r0
	mov	r1, #0			; no error
	mov	r2, #$M_DFS		; frame size per channel

        ldr     pc, [r13], #4

;**************************************************************************

decode_init2
	; initialise workspace
	;   on entry
	;     r0: sample frequency in Hz for information
	;     r1: pointer to workspace
	;     r2: size of workspace
	;   returns
	;     r0: either 0 or pointer to error block

        str     lr, [r13, #-4]!
        mrs     lr, cpsr
        str     lr, [r13, #-4]!

;	; test sample rate
;	cmp	r0, #8000		; compare with 8000 Hz
;	adrne	r0, error_wrongFrequency; error message
;	ldmnefd	r13!, {pc}

	; check workspace size
	; ???

	mov	r0, r1			; copy workspace pointer
	bl	$INIT			; initialise workspace

	mov	r0, #0			; return success
        ldr     lr, [r13], #4
        msr     cpsr_f, lr              ; have to preserve flags across call
        ldr     pc, [r13], #4           ; for backwards compatibility :-(

;**************************************************************************
;* Compression
;**************************************************************************

decode_decompress
	; compress a frame
	;   on entry
	;     r0: current bit offset in input buffer (updated on exit)
	;     r1: pointer to input buffer
	;     r2: number of samples (usually the input frame sample size)
	;     r3: pointer to output samples
	;     r4: pointer to workspace
	;     r5: true (non zero) if frame is start of chunk,
	;         false otherwise
	;   returns
	;     r0: new current bit offset in output buffer ???????????????
	;     r1: either 0 or pointer to error block ????????????????????
	;     r2: actual number of samples produced

        str     lr, [r13, #-4]!
        mrs     lr, cpsr
        str     lr, [r13, #-4]!

	mov 	r6, r0, LSR#3		; convert bit offset to byte offset
					; we can assume for this codec that
					; that it will be on a byte boundary

	; if at start of a chunk, read the header, and put it into the
	; workspace
	; since using the saved workspace makes no appreciable difference
	; to the sound playback quality, this has been removed

;	cmp	r5, #0
;	beq	chunkStart_end
;	stmfd	r13!, {r1-r12}
;	bl	$SIZEOF			; let r0 = ws size == loop counter
;	ldmfd	r13!, {r1-r12}
;	add	r5, r1, r6		; let r5 = address of source
;	add	r6, r6, r0		; update byte offset
;	mov	r8, r4			; let r8 = address of destination
;loop	ldr	r9, [r5], #4
;	str	r9, [r8], #4
;	subs	r0, r0, #4
;	bgt	loop
;chunkStart_end

	; prepare registers for call to |gsm_X_decoder|

	add	r0, r1, r6		; calculate source address
	mov	r1, r3			; dest ptr
	mov	r2, r4			; workspace

	; for |gsm_decode| entry
	;   r0 = GSM data frame pointer
	;   r1 = output sample buffer
	;   r2 = pointer to workspace

	bl	$DECODER

	; calculate return values
	[ :LNOT: stereo
					; calculate new bit offset to return
	add	r0, r6, #33		; by adding 33 to orignal byte count
	mov	r0, r0, LSL#3		; and converting it to bits

	mov	r1, #0			; assume success
	mov	r2, #160		; number of samples produced
	| ;stereo
					; calculate new bit offset to return
	add	r0, r6, #66		; by adding 66 to orignal byte count
	mov	r0, r0, LSL#3		; and converting it to bits

	mov	r1, #0			; assume success
	mov	r2, #320		; number of samples produced
	]

        ldr     lr, [r13], #4
        msr     cpsr_f, lr              ; have to preserve flags across call
        ldr     pc, [r13], #4           ; for backwards compatibility :-(

;**************************************************************************
;* The data area
;**************************************************************************

sampleHz
	DCD	0;			; sample rate given to init1

;**************************************************************************
;* The error blocks
;**************************************************************************

;error_wrongFrequency
;	DCD	0;			; error number
;	DCB	"Incorrect GSM sample rate; should be 8000 Hz", 0
;					; error message
;	ALIGN

;**************************************************************************
;* End of file
;**************************************************************************

	END
