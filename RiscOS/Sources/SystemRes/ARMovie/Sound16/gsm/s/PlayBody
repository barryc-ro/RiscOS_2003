;**************************************************************************
;
;  File:        s.Decompress.gsm
;
;  Description: GSM decompression bobbins
;
;  Version:     0.0 3 Oct 1997
;
;  Author:      Pete Goodliffe (pgoodliffe@acorn.nom)
;
;  Modification History:
;
;    Date         Name                  Description
;    ----------------------------------------------------------------------
;
;**************************************************************************
;  This is included via a GET directive and is not assembled as is
;**************************************************************************

; requires stereo to be defined

        AREA    |Asm$$Code|, CODE, READONLY

        IMPORT  _CodeStart              ; to pull in veneer code from library
	EXPORT	decompress		; decompression entry point
	IMPORT	|__RelocCode|		; reloc code from module linkage

	GBLS	INIT
	GBLS	DECODER
	GBLA	SSIZE			; size of state structure
	GBLA	CFS			; compressed frame size (bytes)
	GBLA	DFS			; decompressed frame size (bytes)
	[ :LNOT: stereo
	IMPORT	|gsm_mono_init|		; init workspace
	IMPORT	|gsm_mono_decoder|	; the GSM library decompressor
INIT	SETS	"gsm_mono_init"
DECODER	SETS	"gsm_mono_decoder"
SSIZE 	SETA	652
CFS	SETA	33
DFS	SETA	160*2
	!	0, "Mono GSM playback"
	]
	[ stereo
	IMPORT	|gsm_stereo_init|	; init workspace
	IMPORT	|gsm_stereo_decoder|	; the GSM library decompressor
INIT	SETS	"gsm_stereo_init"
DECODER	SETS	"gsm_stereo_decoder"
SSIZE	SETA	1304
CFS	SETA	66
DFS	SETA	160*2*2
	!	0, "Stereo GSM playback"
	]

;**************************************************************************
; gsm decompression code

decompress

	; decompress a chunk of gsm sound data
	; IN:  r0 = Source
	;      r1 = Length of source in bytes
	;      r3 = Destination
	; OUT: r1 = length of output produced in bytes
	;      all registers preserved

	stmdb	r13!, {r14}
	stmdb	r13!, {r0, r2-r12}

	; relocation initalisation, performed once at first call
	ldr	r4, hasinit
	cmp	r4, #0
	beq	init
init_ret

	;add	r4, r0, #SSIZE			; store input pointer
	;sub	r5, r1, #SSIZE			; store input size
	;mov	r8, r0				; store workspace

	mov	r4, r0				; input pointer
	mov	r5, r1				; input size (loop counter)
	mov	r6, r3				; dest pointer
	mov	r7, #0				; output size counter

loop	;mov	r0, r8				; workspace

	; get input ready for |gsm_XXX_decoder|

	mov	r0, r4				; source
	mov	r1, r6				; dest buffer
	adr	r2, statespace			; workspace

	; entry to |gsm_XXX_decoder|
	;   r0 - source GSM frame
	;   r1 - output sample buffer
	;   r2 - workspace pointer
	bl	$DECODER

	add	r4, r4, #CFS			; update src ptr
	add	r6, r6, #DFS			; update dest ptr
	add	r7, r7, #DFS			; update length
	subs	r5, r5, #CFS			; update counter
	bne	loop

	mov	r1, r7

gsm_end
	ldmia	r13!, {r0, r2-r12}
	ldmia	r13!, {pc}^


;**************************************************************************
; init
;     this sorts out the C code that has to be reloated prior to use
;     init must be called once before the C code is called

hasinit
	DCD	0				; has init occurred?

init
	stmfd	r13!, {r0-r12}

	bl	|__RelocCode|			; relocate this code
	mov	r0,#0				; synchronise code areas
	swi	&2006e				;   (don't confuse SA)

	mov	r4, #1
	str	r4, hasinit

	adr	r0, statespace			; intialise GSM state space
	bl	$INIT

	ldmfd	r13!, {r0-r12}
	b	init_ret


;**************************************************************************

statespace
	%	SSIZE				; zeroed state space


;**************************************************************************

	END
