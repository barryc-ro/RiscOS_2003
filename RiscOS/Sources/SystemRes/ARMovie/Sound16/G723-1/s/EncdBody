;**************************************************************************
;
;  File:        s.EncdBody
;
;  Description: Assembler veneer code body for Acorn Replay sound compressor
;               g721 version
;
;  Version:     0.0 3 Oct 1997
;
;  Author:      Pete Goodliffe (pgoodliffe@acorn.nom)
;
;  Modification History:
;
;    Date         Name                  Description
;    ----------------------------------------------------------------------
;
;**************************************************************************

; requires stereo to be defined

	ASSERT	:DEF: stereo

	AREA	|C$$code|, CODE, READONLY

; EXPORT the functions herein

	EXPORT	|encode_init1|
	EXPORT	|encode_init2|
	EXPORT	|encode_compress|

;**************************************************************************
;* Set up functions to call

	IMPORT	pre_init		; encode samples in g721 format
	IMPORT	init			; find size of necessary workspace
	IMPORT	compress		; initialise workspace

;**************************************************************************
;* Main code body
;**************************************************************************

;**************************************************************************
;* Initialisation

encode_init1
	; initialise for encoding
	;   on entry:
	;     r0: sample frequency in Hz for information
	;     r1: (parameter string; ignored)
        ;   returns
        ;     r0: workspace size required
	;     r1: zero or pointer to error block
	;     r2: frame size of compression type - WAAAA!!!

        str     lr, [r13, #-4]!

	STMFD	r13!,{r1-r12}
	ADD	r2,r13,#4		; r2 = pointer to restored r1
	ADD	r3,r13,#8		; r3 = pointer to restored r2
					; r3 is NOT CURRENTLY USED!
	bl	pre_init		; workspace size returned in r0
	LDMFD	r13!,{r1-r12}

        ldr     pc, [r13], #4


;**************************************************************************

encode_init2
	; initialise workspace
	;   on entry
	;     r0: sample frequency in Hz for information
	;     r1: pointer to workspace
	;     r2: size of workspace
	;   returns
	;     r0: either 0 or pointer to error block

        str     lr, [r13, #-4]!
        mrs     lr, cpsr
        str     lr, [r13, #-4]!

	; check workspace size
	; ???

	bl	init

        ldr     lr, [r13], #4
        msr     cpsr_f, lr              ; have to preserve flags across call
        ldr     pc, [r13], #4           ; for backwards compatibility :-(


;**************************************************************************
;* Compression
;**************************************************************************

encode_compress
	; compress a frame
	;   on entry
	;     r0: current bit offset in output buffer (updated on exit)
	;     r1: pointer to output buffer
	;     r2: number of samples (usually the input frame sample size)
	;     r3: pointer to samples
	;     r4: pointer to workspace
	;     r5: true (non zero) if frame is start of chunk, (add workspace)
	;         false otherwise
	;   returns
	;     r0: new current bit offset in output buffer
	;     r1: either 0 or pointer to error block
	;     r2: number of samples actually encoded per channel

        str     lr, [r13, #-4]!
        mrs     lr, cpsr
        str     lr, [r13, #-4]!

	STMFD	r13!,{r1-r12}

	MOV	r6,r13		; r6 => restored r1
	ADD	r7,r13,#4	; r7 => restored r2
	STMFD	r13!,{r4-r7}

	BL	compress
	ADD	r13,r13,#4*4

	LDMFD	r13!,{r1-r12}

        ldr     lr, [r13], #4
        msr     cpsr_f, lr              ; have to preserve flags across call
        ldr     pc, [r13], #4           ; for backwards compatibility :-(


;**************************************************************************
;* End of file
;**************************************************************************

	END
