	AREA	|C$code|,CODE,READONLY

        GET     Hdr:ListOpts
        GET     Hdr:APCS.<APCS>
        GET     Hdr:Macros

	EXPORT	aLsp_Qnt

	IMPORT	div_s
	IMPORT	norm_s
	IMPORT	LspDcTable
Wvect
	% 4*10
LspDcTablePtr
	DCD	LspDcTable
aLsp_Qnt
	; r0 = Word32 *CurrLsp
	; r1 = Word32 *PrevLsp
        FunctionEntry "r4-r11"

	LDMIA	r0,{r2,r3,r4,r5,r6,r7,r8,r9,r10,r11}	; r2 = CurrLsp[0]
							; r3 = CurrLsp[1]
	SUB	r2,r3,r2	; r2 = CurrLsp[1] - CurrLsp[0] = Wvect[0]
	SUB	r3,r4,r3	; r3 = CurrLsp[2] - CurrLsp[1]
	SUB	r4,r5,r4	; r4 = CurrLsp[3] - CurrLsp[2]
	SUB	r5,r6,r5	; r5 = CurrLsp[4] - CurrLsp[3]
	SUB	r6,r7,r6	; r6 = CurrLsp[5] - CurrLsp[4]
	SUB	r7,r8,r7	; r7 = CurrLsp[6] - CurrLsp[5]
	SUB	r8,r9,r8	; r8 = CurrLsp[7] - CurrLsp[6]
	SUB	r9,r10,r9	; r9 = CurrLsp[8] - CurrLsp[7]
	SUB	r11,r11,r10	; r10= CurrLsp[9] - CurrLsp[8] = Wvect[9]
	MOV	r10,r11
	CMP	r10,r9
	MOVGT	r10,r9		; r10= Wvect[8]
	CMP	r9,r8
	MOVGT	r9,r8		; r9 = Wvect[7]
	CMP	r8,r7
	MOVGT	r8,r7		; r8 = Wvect[6]
	CMP	r7,r6
	MOVGT	r7,r6		; r7 = Wvect[5]
	CMP	r6,r5
	MOVGT	r6,r5		; r6 = Wvect[4]
	CMP	r5,r4
	MOVGT	r5,r4		; r5 = Wvect[3]
	CMP	r4,r3
	MOVGT	r4,r3		; r4 = Wvect[2]
	CMP	r3,r2
	MOVGT	r3,r2		; r3 = Wvect[1]
	ADR	r14,Wvect
	STMIA	r14,{r2,r3,r4,r5,r6,r7,r8,r9,r10,r11}
	MOV	r4,r0
	MOV	r9,r1
	MOV	r5,#0
	MOV	r6,#10
	MOV	r7,r14
	MOV	r8,#&7f00
	ORR	r8,r8,#&ff
alq_lp1
	LDR	r1,[r7]
	MOV	r0,#32
	CMP	r1,#32	; if (Wvect[i] <= 32)
	MOVLE	r0,r8	;      Wvect[i] = MAX_16
	BLGT	div_s	; else Wvect[i] = div_s(32, Wvect[i])
	STR	r0,[r7],#4
	CMP	r0,r5	; if (Wvect[i] > Tmp1)
	MOVGT	r5,r0	;   Tmp1 = Wvect[i]
	SUBS	r6,r6,#1
	BGT	alq_lp1

	; Normalise the weight vector
	MOV	r0,r5
	BL	norm_s

	; r4 = Word32 *CurrLsp
	; r7 = &Wvect[10]
	; r9 = Word32 *PrevLsp
	MOV	r6,#10
	LDR	r10,LspDcTablePtr
alq_lp2
	LDR	r1,[r7,#-4]!
	LDR	r2,[r10],#4		; r2 = LspDcTable[i]
	LDR	r3,[r4]			; r3 = CurrLsp[i]
	LDR	r12,[r9],#4		; r12= PrevLsp[i]
	SUBS	r6,r6,#1
	MOV	r1,r1,LSL r0
	SUB	r12,r12,r2		; r12 =  PrevLsp[i] - LspDcTable[i]
	ADD	r12,r12,r12,LSL#1	; r12 = (                          )*3
	ADD	r12,r12,#4		; r12 = (                          )*3+4
	ADD	r2,r2,r12,ASR#3		; r2 = LspDcTable[i] - (r12>>3)
	SUB	r3,r3,r2
	STR	r1,[r7]
	STR	r3,[r4],#4
	BGT	alq_lp2

	MOV	r0,r7

        Return  "r4-r11"

	END
