        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System

        Push    "lr"

        SWI     XOS_EnterOS
        BVS     exit

        MOV     r0, #&86
        SWI     XOS_ReadDynamicArea
        BVS     exit
        MOV     r4, r0                  ; r4=freebase
        ADD     r5, r4, r2              ; r5=freebase+freemax

        SWI     XOS_ReadMemMapInfo      ; r1=npages
        BVS     exit

        MOV     r2, #&01E00000          ; r2=camstart=&01E02000
        ADD     r2, r2, #&00002000
        ADD     r3, r2, r1, LSL #3      ; r3=camend=camstart+npages*3
        SUB     r3, r3, #1              ; r3=camend-1 for CMP below

        MOV     r6, #&02C00000          ; r6=L2PTapp=&02C00000
        ADD     r7, r6, #&01C00000:SHR:10 ; r7=L2PTappend=L2PTapp+(&01C00000>>>10)
        SUB     r7, r7, #1              ; r7=L2PTappend-1 for CMP below

        ADD     r4, r6, r4, LSR #10     ; r4=L2PTfree=L2PTapp+(freebase>>>10)
        ADD     r5, r6, r5, LSR #10     ; r5=L2PTfreeend=L2PTapp+((freebase+freemax)>>>10)
        SUB     r5, r5, #1              ; r5=L2PTfreeend-1 for CMP below

        MOV     r8, #&01C00000          ; r8=SVCstack=&01C00000
        ADD     r9, r8, #&00002000      ; r9=SVCstackend=&01C02000
        SUB     r9, r9, #1              ; r9=SVCstackend-1 for CMP below

        ADD     r10, r6, r6, LSR #10    ; r10=L2PTl2pt=L2PTapp+(L2PTapp>>>10)

        MOV     r0, r2                  ; r0=camstart
nextpage
        CMP     r0, r3                  ; If r0>camend-1
        BGT     exit                    ; then finished.

        LDR     r1, [r0], #8            ; Get logaddr and move r0 to next CAM entry.

        CMP     r1, r2                  ; If logaddr >= camstart
        CMPCS   r3, r1                  ;   and logaddr < camend
        BCS     protect                 ; then protect page.

        CMP     r1, r4                  ; If logaddr >= L2PTfree
        CMPCS   r5, r1                  ;   and logaddr < L2PTfreeend
        BCS     protect                 ; then protect page.

        CMP     r1, r6                  ; If logaddr >= L2PTapp
        CMPCS   r7, r1                  ;   and logaddr < L2PTappend
        BCS     protect                 ; then protect page.

        CMP     r1, r8                  ; If logaddr >= SVCstack
        CMPCS   r9, r1                  ;   and logaddr < SVCstackend
        BCS     protect                 ; then protect page.

        TEQ     r1, r10                 ; If logaddr <> L2PTl2pt
        BNE     nextpage                ; then try next page.
protect
        LDR     lr, [r0, #-4]
        ORR     lr, lr, #&00002000
        STR     lr, [r0, #-4]
        B       nextpage

exit
        MOV     lr, pc                 ; Exit in USER mode but preserve flags.
        BIC     lr, lr, #3
        TEQP    lr, #0
        MOV     r0, r0
        Pull    "pc"

        DCB     "MemFix 1.01 16-Jun-94 © Acorn Computers Ltd 1994", 0
        ALIGN
        END
