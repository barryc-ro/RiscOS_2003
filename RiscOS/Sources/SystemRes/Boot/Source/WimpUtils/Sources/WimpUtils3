; > Sources.WimpUtils3

;;----------------------------------------------------------------------------
;; Patch module for RISC OS 3.50

;; In RISC OS 3.50, the Wimp has two calls to the routine which conditionally
;; sets up the grey-scale palette. One of these (after a *WimpMode command has
;; been executed) has a bug in the addressing of the flag which determines that
;; a grey-scale palette is required. The location it actually reads is in the
;; system heap, in the second half of the kernel's 'TextExpand' block. The
;; second half of the block is only used in 16 million colour modes. If the
;; last text background colour set while in one of these modes was not black,
;; then the Wimp thinks it should set up a grey-scale palette. From then on,
;; any mode change done via the DisplayManager tries to select a grey-scale
;; mode, producing strange effects in 2-colour, 256-colour and
;; 32-thousand-colour modes. Doing F12 then RETURN temporarily clears the
;; condition, but any reselection of a mode from the DisplayManager returns you
;; to the naff state. It can only be cleared properly by reselecting a 16
;; million colour mode - from then on it is OK, because the default background
;; colour is black (although F12 sets it to be white).

;; This bug could arise if the user ran a program that selected a 16 million
;; colour mode and changed the text background colour. On return to the
;; desktop, the initial mode change would be OK, but subsequent ones selected
;; from the DisplayManager would be wrong.

;; The bug is fixed by this module, which on Service_ModeChange always pokes
;; the location in the system heap to zero, which is safe, since that part of
;; the block is only used in 16 million colour modes, and that location
;; defaults to zero after such a mode change anyway.

;;
;; Change List
;; -----------
;; 01-Mar-94    3.50  TMD  File created
;;
;;
;;----------------------------------------------------------------------------

        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:Services
        GET     Version3

BodgeLocation   *       &01C03D4C
EndOfROM        *       &03A00000

Module_BaseAddr
        DCD     0                               ; Start
        DCD     0                               ; Init
        DCD     0                               ; Die
        DCD     Service        - Module_BaseAddr
        DCD     Title          - Module_BaseAddr
        DCD     Helpstr        - Module_BaseAddr
        DCD     0                               ; Helptable
        DCD     0                               ; MySWIBase
        DCD     0                               ; SWIdecode
        DCD     0                               ; SWInames
        DCD     0                               ; SWI name decode

Title   DCB     "WindowUtils", 0

Helpstr DCB     "Window Utils", 9, "$VString ($Date)", 0
        ALIGN

Service ROUT
        TEQ     r1, #Service_ModeChange
        MOVNE   pc, lr
        Push    "r0-r5"
        MOV     r0, #EndOfROM   ; compare ROM checksum and CRCs
        LDMDB   r0, {r0-r2}
        ADR     r3, ROMChecksums ; with what they should be for 3.50
        LDMIA   r3, {r3-r5}
        EORS    r0, r0, r3
        EOREQS  r0, r1, r4
        EOREQS  r0, r2, r5
        LDREQ   r1, =BodgeLocation ; store zero in location
        STREQ   r0, [r1]
        Pull    "r0-r5"
        MOV     pc, lr

        LTORG

ROMChecksums
        &       &F0561D7C, &EAE37D58, &C33AE184

        END
