        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:Proc
        GET     Hdr:ModHand
        GET     Hdr:ResourceFS

        GBLL    Debug
Debug   SETL    {FALSE}

        GBLS    GetAroundBleedingAAsmYetAgain
 [ Debug
GetAroundBleedingAAsmYetAgain SETS " GET Hdr:Debug"
 |
GetAroundBleedingAAsmYetAgain SETS "; No debug required"
 ]
$GetAroundBleedingAAsmYetAgain

 [ Debug
Host_Debug SETL {FALSE}
Debug_MaybeIRQ SETL {FALSE}
 ]

Start
        MOV     r0, #17
        SWI     XOS_File
        SUBVSS  r0, r0, r0      ; CLRV and set r0 to 0 - behave as if file not there
        TEQ     r0, #0
        LDREQ   r0, If0
        LDRNE   r0, If1
        MOV     r2, r12
        STR     r0, [r2], #4
SkipFileName
        LDRB    r0, [r1], #1
        CMP     r0, #" "
        BHI     SkipFileName
        B       IntoCopyLoop
StartCopyLoop
        LDRB    r0, [r1], #1
        CMP     r0, #" "
IntoCopyLoop
        STRB    r0, [r2], #1
        BHS     StartCopyLoop
        MOV     r0, r12
        SWI     XOS_CLI
        MOV     pc, lr

If0     DCB     "If 0"
If1     DCB     "If 1"

 [ Debug
        InsertDebugRoutines
 ]

        DCB     "IfExists 1.00 29-Nov-1993  © Acorn Computers Ltd 1993", 0
        ALIGN

        END
