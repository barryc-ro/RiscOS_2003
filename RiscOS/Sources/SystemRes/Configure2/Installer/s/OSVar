; /****** OSVar.s ***********************************************************\ 
; 
; Project:	Ursula (RISC OS for Risc PC II)
; Component:	Configure Installer module (for SysMerge/FontMerge/BootMerge)
; This file:	OS variable handling
; 
; History:
; Date		Who	Change
; ----------------------------------------------------------------------------
; 20/01/1998	BJGA	Created
; 
; \**************************************************************************/

; This stuff is necessary because the standard C wrappers can't cope with the
; concept of a SWI which returns an error, where a register other than R0
; contains pertinent information...
	
	GET     Hdr:ListOpts
	GET     Hdr:Macros
	GET     Hdr:APCS.<APCS>
	
XOS_ReadVarVal	*	&20023
	
	AREA |OSVar|, CODE, READONLY
	
; BOOL OSVar_Exists (const char *var)
; In:	Pointer to OS variable name
; Out:	TRUE/FALSE depending on whether the variable exists or not
	
	EXPORT OSVar_Exists
OSVar_Exists
        FunctionEntry "v1"
	MOV	r1, #0
	MOV	r2, #-1
	MOV	r3, #0
	MOV	r4, #0
	SWI	XOS_ReadVarVal
	CMP	r2, #0
	MOVGE	r0, #0 ; variable doesn't exist
	MOVLT	r0, #1 ; variable does exist
	Return  "v1"
	
; int OSVar_Length (const char *var);
; In:	Pointer to OS variable name
; Out:	Length of OS variable, not including a terminator
	
	EXPORT OSVar_Length
OSVar_Length
        FunctionEntry "v1"
	MOV	r1, #0
	MOV	r2, #-1
	MOV	r3, #0
	MOV	r4, #0
	SWI	XOS_ReadVarVal
	MVN	r0, r2
	Return  "v1"
	
	END
