	AREA	|ObjAsm$$Code|,CODE,READONLY

XOS_Byte	*	&20006
XOS_ReadMemMapInfo	*	&20051

	ENTRY
CheckMem
	SWI	XOS_ReadMemMapInfo
	MOVVS	PC,LR

	MOV	R8,R0

	MOV	R0,#161
	MOV	R1,#145		; System heap size
	SWI	XOS_Byte
	MOVVS	PC,LR
	MUL	R0,R2,R8
	CMPS	R0,#8*1024
	BGE	%F2
	; Reconfigure system heap size to 8K
	STMFD	R13!,{LR}
	MOV	R0,#8*1024
	MOV	R1,R8
	BL	Div
	MOV	R2,R0
	MOV	R0,#162
	MOV	R1,#145
	SWI	XOS_Byte
	LDMFD	R13!,{LR}
	MOVVS	PC,LR
2	MOV	R0,#161
	MOV	R1,#146
	SWI	XOS_Byte
	MOVVS	PC,LR
	MUL	R0,R2,R8
	CMPS	R0,#256*1024
	MOVGE	PC,LR
	; Reconfigure RMA size to 256K
	STMFD	R13!,{LR}
	MOV	R0,#256*1024
	MOV	R1,R8
	BL	Div
	MOV	R2,R0
	MOV	R0,#162
	MOV	R1,#146
	SWI	XOS_Byte
	LDMFD	R13!,{PC}

Div
	STMFD	R13!,{R1-R3}
	MOV	R3,R1
	CMPS	R3,R0,LSR #1
Div1	MOVLS	R3,R3,LSL #1
	CMPS	R3,R0,LSR #1
	BLS	Div1
	MOV	R2,#0
Div2	CMPS	R0,R3
	SUBCS	R0,R0,R3
	ADC	R2,R2,R2
	MOV	R3,R3,LSR #1
	CMPS	R3,R1
	BHS	Div2
	MOV	R0,R2
	LDMFD	R13!,{R1-R3}
	MOVS	PC,LR

	= "CheckMem 1.00 (23 May 1996) © Acorn Computers Ltd 1996"

	END
