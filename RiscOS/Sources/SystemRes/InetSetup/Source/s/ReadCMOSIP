	AREA	|ObjAsm$$Code|,CODE,READONLY

XOS_Byte	*	&20006
OSByte_ReadCMOS	*	161
CMOS_IPAddress0 *	108
CMOS_IPAddress1 *	109
CMOS_IPAddress2 *	110
CMOS_IPAddress3 *	0
XOS_SetVarVal	*	&20024
XOS_ConvertCardinal1 *	&200D5

	ENTRY
ReadCMOSIP
	MOV	R0,#OSByte_ReadCMOS
	MOV	R1,#CMOS_IPAddress0
	SWI	XOS_Byte
	MOVVS	PC,LR
	MOV	R4,R2

	MOV	R1,#CMOS_IPAddress1
	SWI	XOS_Byte
	MOVVS	PC,LR
	MOV	R5,R2

	MOV	R1,#CMOS_IPAddress2
	SWI	XOS_Byte
	MOVVS	PC,LR
	MOV	R6,R2

	MOV	R1,#CMOS_IPAddress3
	SWI	XOS_Byte
	MOVVS	PC,LR
	MOV	R7,R2

	; If the address is 0.0.0.0 don't do anything
	; If it's 0.0.0.x then treat as 10.0.0.x
	TEQS	R4,#0
	TEQEQS	R5,#0
	TEQEQS	R6,#0
	MOVEQ	R4,#10
	TEQEQS	R7,#0
	MOVEQS	PC,LR

	MOV	R0,R4
	MOV	R1,R12
	MOV	R2,#512
	SWI	XOS_ConvertCardinal1
	MOVVS	PC,LR
	MOV	R0,#"."
	STRB	R0,[R1],#1

	MOV	R0,R5
	SWI	XOS_ConvertCardinal1
	MOVVS	PC,LR
	MOV	R0,#"."
	STRB	R0,[R1],#1

	MOV	R0,R6
	SWI	XOS_ConvertCardinal1
	MOVVS	PC,LR
	MOV	R0,#"."
	STRB	R0,[R1],#1

	MOV	R0,R7
	SWI	XOS_ConvertCardinal1
	MOVVS	PC,LR

	SUB	R2,R1,R12
	ADR	R0,VarName
	MOV	R1,R12
	MOV	R3,#0
	MOV	R4,#0
	SWI	XOS_SetVarVal
	MOV	PC,LR

VarName
	= "Inet$CMOSIPAddr", 0

	= "ReadCMOSIP 1.00 (02 Jul 1996) © Acorn Computers Ltd 1996"

	END
