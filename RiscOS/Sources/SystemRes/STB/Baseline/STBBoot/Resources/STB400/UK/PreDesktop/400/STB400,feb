| Replace the !PlugPlay module if the ROM version is too old
RMCheck !PlugPlay 0.25 !PlugPlay

| Program the STB's flash with the latest Internet module and ensure the MBuf pool is
| big enough for the direct interfaces (i.e. the new UDPv)
SetEval New$Internet 1
RMEnsure Internet 5.44 SetEval New$Internet 0
RMEnsure DHCP     0.17 SetEval New$Internet 0
RMEnsure HelpDHCP 1.04 SetEval New$Internet 0
If New$Internet = 0 Then SplashText Please wait for flash update
If New$Internet = 0 Then Run Resources:$.!Boot.Utils.FlashPod -v -e -p STBHookMisc:FlashImg { > null: }
AlterCMOS -offset 46 -value 14 -reset
If New$Internet = 0 Then STBUtilities:Reboot
Unset New$Internet

| Do we need to load a newer Interlace module? This requires a utility to remove the
| older version first. It also requires NHTwitter to be ReInited.
SetEval New$Interlace 0
SetEval New$NHTwitter 0
RMEnsure Interlace 0.62 SetEval New$Interlace 1
RMEnsure Interlace 0.62 Run STBUtilities:InterLoad System:Modules.Interlace
RMEnsure Interlace 0.62 Error Interlace module version 0.62 or later required

| Do we need a newer NHTwitter? Load it if we do, or ReInit it if we've loaded a newer
| Interlace (above).
RMEnsure NHTwitter 0.10 SetEval New$NHTwitter 1
RMCheck NHTwitter  0.10 NHTwitter
If New$Interlace = 1 AND New$NHTwitter = 0 Then RMReinit NHTwitter
Unset New$Interlace
Unset New$NHTwitter

| Ensure that the contents of Resources:$.Apps are decompressed so the Desktop module
| can Filer_Boot all the applications.
RMEnsure ShrinkWrap 0.05 RMReInit ShrinkWrap
RMEnsure ShrinkWrap 0.05 Do ShrinkWrapAddZone <STBResFSApps$Dir>

| Load the DENCDriver before the desktop to enable Macrovision and closed-captions (NTSC)
RMCheck DENCDriver 0.13 DENCDriver
