| >!STBBoot.Utilities.PreDesktop
| Copyright © Pace Micro Technology plc, 2002
| All rights reserved.

| Move various key files into ResourceFS (if not there already)
SetEval InResFS 0
If "<STBBoot$Dir>" LEFT 10 = "Resources:" Then SetEval InResFS 1
If InResFS = 0 Then X AddToRes STBLibrary:AddToRes !Boot.Utils.AddToRes
If InResFS = 0 Then Set Alias$AddToRes /Resources:$.!Boot.Utils.AddToRes %%*0
If InResFS = 1 Then Set Alias$AddToRes /STBLibrary:AddToRes %%*0
If "<X$Error>" <> "" Then Obey

| Load the splash screen stuff if it is configured to be on (display screen if network booting)
| NOTE: the SplashText string MUST use hard spaces (Alt-Space) as 'Do' screws things up nicely with
| quotes or soft spaces
If SplashScreen Then X RMEnsure STBSplash 1.27 Run STBUtilities:KillSplash
If SplashScreen Then X RMCheck  STBSplash 1.27 STBSplash
SplashImage <Splash$Picture>
If "<TV$Type>" = "NTSC" Then Run STBUtilities:NTSCSplash
SplashText  
SplashIcons STBBoot:!Sprites22
SplashNext !STBBoot
If "<X$Error>" <> "" Then Obey

| Set sys vars for uniqie ID, RAM size and location of public directory
Set NFS$CreateAccess 0777
X Run STBResources:!Internet.bin.IPVars
X Run STBUtilities:Unique
If "<X$Error>" <> "" Then Obey

| Load various modules on all STBs if not there already
X RMCheck SharedCLibrary 5.34 CLib
X RMCheck STBUtils       0.02 STBUtils
X RMCheck BootCommands   1.25 BootCmds
X RMCheck Serial         0.26 Serial
{ > null: }MemoryA 3010FE4 5
| This MemoryA is to disable MODEM interrupts (these cause box stiffs)
X RMCheck DeviceFS       0.59 DeviceFS
X RMCheck TaskWindow     0.68 TaskWindow
X RMCheck TaskModule     0.20 TaskModule
X RMCheck Toolbox        1.50 Toolbox.Toolbox
X RMCheck ToolAction     0.24 Toolbox.ToolAction
X RMCheck Window         1.66 Toolbox.Window
X RMCheck BootCommands   1.20 BootCmds
X RMCheck MessageQueue   0.06 MsgQueue
X RMCheck IIC            0.17 IIC
X RMCheck IconHigh       0.33 IconHigh
X RMCheck IR             2.19 IR
X RMCheck PowerOn        0.09 PowerOn
X RMCheck SharedSound    1.03 SSound
If "<X$Error>" <> "" Then Obey

| Ensure that CacheFS is loaded because NCFresco uses it, and <Choices$Write> may also
X RMCheck CacheFS 0.28 CacheFS
If "<CacheFS$Size>" <> "" Then Do CacheFS_SetMax <CacheFS$Size>
If "<Wimp$ScrapDir>" = "" Then Set Wimp$ScrapDir Cache:$.ScrapDir
If "<Wimp$Scrap>" = ""    Then Set Wimp$Scrap    <Wimp$ScrapDir>.ScrapFile

| Switch the current working directory to CacheFS
X CacheFS

| Put the TerrFont script into ResourceFS because potentially multiple territory
| components will be calling it
If InResFS = 0 Then X AddToRes STBUtilities:TerrFont !Boot.Utils.TerrFont
If InResFS = 0 Then Set Alias$TerrFont /Resources:$.!Boot.Utils.TerrFont %%*0
If InResFS = 1 Then Set Alias$TerrFont /STBUtilities:TerrFont %%*0
Unset InResFS

| Execute all the global PreDesktop scripts
If BootRun$PreDesk Then BootPreRun <STBPreDesk$Dir>

| Execute all the STB-specific PreDesktop scripts
If BootRun$HookPreDesk Then BootPreRun <STBHookPreDesk$Dir>
