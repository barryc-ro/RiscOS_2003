| >!STBBoot.!Run
| © Pace Micro Technology plc, 1999

| Initialise the main boot application's $Dir and $Path variables
Set STBBoot$Dir  <Obey$Dir>
Set STBBoot$Path <STBBoot$Dir>.

| Initialise some useful aliases and system variables
Set Alias$RMLoad Root:!STBBoot.Utilities.RMLoad2 %*0
RMEnsure BootCommands 1.16 Set Alias$IfThere Root:!STBBoot.Library.IfThere %*0
RMEnsure BootCommands 1.16 Set Alias$Do Root:!STBBoot.Library.Do %*0
RMEnsure Filer        1.85 Set Alias$Filer_Boot IfThere %%*0.!Boot Then Run %%*0.!Boot
RMEnsure Filer        1.85 Set Alias$Filer_Run Run %%*0
Run Root:!STBBoot.Utilities.WhatSTB
If OS$Version < 300 Then Error "««STBBRun_OSVersion»»"

| Expose the relocatable module resources
Run Root:!STBBoot.Resources.!System
If OS$Version > 220 Then STBStateOn
If OS$Version > 220 Then Root:!STBBoot.Utilities.Hon
If OS$Version = 220 Then RMLoad System:Modules.STBHour

| Trap certain fatal errors during booting
RMEnsure TrapError 1.06 RMLoad System:Modules.TrapError
Root:!STBBoot.Utilities.BootErrors

| Get all of the other Baseline system variables initialised
Run Root:!STBBoot.Utilities.BootVars

| Ensure that CacheFS is loaded because NCFresco uses it, and <Choices$Write> may also
RMEnsure CacheFS   0.21 RMLoad System:Modules.CacheFSRes
RMEnsure CacheFS   0.21 RMLoad System:Modules.CacheFS
RMEnsure FileWatch 0.03 RMLoad System:Modules.FileWatch
If "<CacheFS$Size>" <> "" Then Do CacheFS_SetMax <CacheFS$Size>
If "<Wimp$ScrapDir>" = "" Then Set Wimp$ScrapDir Cache:$
If "<Wimp$Scrap>" = ""    Then Set Wimp$Scrap    <Wimp$ScrapDir>.ScrapFile

| Set sys vars for uniqie ID, RAM size and location of public directory
Run STBUtilities:Unique
IfThere Root:!STBBoot.Custom Then Run Root:!STBBoot.Custom

| Load the splash screen stuff if it is configured to be on
If SplashScreen = 0 Then Set Alias$SplashImage ||
If SplashScreen = 0 Then Set Alias$SplashText  ||
If SplashScreen = 0 Then Set Alias$SplashReset ||
If SplashScreen Then RMEnsure STBSplash 1.00 RMLoad System:Modules.STBSplash
If SplashScreen Then Obey STBChoices:Splash
SplashImage <Splash$Picture>
Do SplashText "««STBBRun_BLVersion»» <Baseline$Version>"

| Load a clear iconsprite to stop the mouse pointer from appearing
IconSprites STBUtilities:ClearPtr

| Execute all the global PreDesktop scripts
If BootRun$PreDesk Then Repeat Run <STBPreDesk$Dir> -files -applications

| Execute all the STB-specific PreDesktop scripts
If BootRun$HookPreDesk Then Repeat Run <STBHookPreDesk$Dir> -files -applications

| Start the Desktop side of the Boot sequence (or the Internet5 softload, if there)
IfThere STBHookRes:!Internet5 Then Run STBHookRes:!Internet5
Desktop -file STBUtilities:Desktop
