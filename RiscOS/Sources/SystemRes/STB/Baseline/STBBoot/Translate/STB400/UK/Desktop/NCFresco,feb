SplashText "««NCFresco_Splash»»"

| Set the defaults for the browser
Set Browser$Default "!NCFresco"
If "<NCFresco$SSL>"        = ""    Then Set NCFresco$SSL        HTTPMod
If "<Inet$LocalDomain>"    = ""    Then Set Inet$LocalDomain    eng.cam.pace.co.uk
If "<NCFresco$Help>"       = ""    Then Set NCFresco$Help       file:/STBContent:HTML/<OS$Version>/Help/index.html
If "<NCFresco$Welcome>"    = ""    Then Set NCFresco$Welcome    -url file:/STBContent:HTML/<OS$Version>/index.html
If "<NCFresco$Home>"       = ""    Then Set NCFresco$Home       -url file:/STBContent:HTML/<OS$Version>/index.html
If "<TV$Type>"             = "PAL" Then Set NCFresco$ConfigDisplayMarginLeft   8 Else Set NCFresco$ConfigDisplayMarginLeft   5
If "<TV$Type>"             = "PAL" Then Set NCFresco$ConfigDisplayMarginBottom 6 Else Set NCFresco$ConfigDisplayMarginBottom 6
If "<TV$Type>"             = "PAL" Then Set NCFresco$ConfigDisplayMarginRight  8 Else Set NCFresco$ConfigDisplayMarginRight  5
If "<TV$Type>"             = "PAL" Then Set NCFresco$ConfigDisplayMarginTop    6 Else Set NCFresco$ConfigDisplayMarginTop    6

| Ensure that the !Unicode application in our !STBBoot.Resources directory has been seen
IfThere STBResources:!Unicode Then Run STBResources:!Unicode

| Run the configuration script for NCFresco
Obey STBChoices:NCFresco

| Set the icon highlight colour to light red/pink
IconHigh_SetHighlightColour 0x6B6BFD00

| Ensure that the Resolver module is configured given the Inet$ system variables
ResolverConfig

| Set the options toolbar button to go to the main options page and the help button goes to the main help page
Set OptScr$Path Resources:$.!Boot.HTML.Setup.
Set NCFresco$EventURL00 NCOptions_OpenPage:?file_template=OptScr:Options&quit_page=ncint:dboxremove
Set NCFresco$EventURL01 <NCFresco$Help>

| Check for- and load various modules which NCFresco requires
RMCheck  MimeMap     0.12 Network.MimeMap
ReadMimeMap
If "<NCFresco$SSL>" = "HTTPMod"  Then RMReInit HTTP
If "<NCFresco$SSL>" = "HTTPMod"  Then RMEnsure HTTP 1.16 RMLoad System:Modules.Network.HTTPMod
If "<NCFresco$SSL>" <> "HTTPMod" Then RMLoad System:Modules.Network.<NCFresco$SSL>
RMEnsure HTTP        1.16 Error HTTP module version 1.16 or later required
If "<TV$Type>" = "NTSC"  Then RMCheck ChoicesUSA 1.04 ChoicesUSA
If "<TV$Type>" <> "NTSC" Then RMCheck ChoicesUK  1.04 ChoicesUK
RMCheck  NCOptions   0.50 NCOptions
RMCheck  Window      1.65 Toolbox.Window
RMCheck  WebGopher   0.17 Network.WebGopher
RMCheck  WebFTP      0.31 Network.WebFTP
RMCheck  MimeMap     0.12 Network.MimeMap

| Yucky cludge to copy various HTML templates to STB's local store
If "<Obey$Dir>" LEFT 10 <> "Resources:" Then Run STBUtilities:NCFPatch

| Start the Watchdog (and web browser) application
Run Resources:$.Apps.!Watchdog
