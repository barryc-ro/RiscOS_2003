; > $.Source.PMF.osword

maxword * &16                                   ; highest known osword

; *****************************************************************************

        MACRO
        WordReturnV $cond

        ASSERT  "$cond"="" :LOR: "$cond"="VS"

        [ "$cond"=""
        Pull    "R0-R4,R11,WsPtr,link,PC", VC
        ]
        ADDVS   R13, R13, #4                    ; junk stacked R0
        Pull    "R1-R4,R11,WsPtr,link,PC", VS

        MEND

; Main OsWord entry point
; R0,R1,R2 are parameters


OsWord
        Push    "R0-R4, R11, R12, R14"
        CMP     R0, #(maxword+1)
        BLCC    OsWordGo                        ; Call the subsid entry pt.
        LDMIA   R13, {R2-R4}                    ; R2=A, R3=X, R4=Y
        MOV     R1, #Service_UKWord             ; osword service reason
        CLRPSR  V_bit, R0                       ; in case there's no service
        IssueService
        TEQ     R1, #0
        STMEQIA R13, {R2-R4}                    ; if claimed, then update
                                                ; returned R0-R2
        Pull    "R0-R4, R11, R12, PC"           ; pass V back from service

GoMyOsword
        CLRPSR  V_bit, R4
        Pull    "R0-R4, R11, R12, R14, PC"

; *****************************************************************************

OsWordGo ROUT
        BYTEWS  WsPtr
10                                              ; Point to despatch table
        ADD     PC, PC, R0, LSL #2              ; add in the action*4 and go
        &       0
        ASSERT  DespatchWord-%BT10 = 8

; *****************************************************************************

DespatchWord

        BAL     OsWord00
        BAL     OsWord01
        BAL     OsWord02
        BAL     OsWord03
        BAL     OsWord04
        BAL     OsWord05
        BAL     OsWord06
        BAL     OsWord07

        BAL     OsWord08
        BAL     OsWord09
        BAL     OsWord0A
        BAL     OsWord0B
        BAL     OsWord0C
        BAL     OsWord0D
        BAL     OsWord0E
        BAL     OsWord0F

        BAL     OsWord10
        BAL     OsWord11
        BAL     OsWord12
        BAL     OsWord13
        BAL     OsWord14
        BAL     OsWord15
        BAL     OsWord16

; *****************************************************************************
; That's All Folks
; *****************************************************************************

; *****************************************************************************
; The OsWord routines themselves
; *****************************************************************************

; Osword Zero : Input a line

OsWord00 ROUT
        LDRB    R0, [R1, #0]            ; lo-byte of address
        LDRB    R2, [R1, #1]            ; hi-byte of address
        ORR     R0, R0, R2, LSL #8      ; R0 := address
        LDRB    R2, [R1, #3]            ; lo limit
        LDRB    R3, [R1, #4]            ; hi limit
        LDRB    R1, [R1, #2]            ; length of buffer
        SWI     XOS_ReadLine
        WordReturnV VS

        MOV     R2, R1                  ; put line length into R2
        Pull    "R0,R1,R3"              ; don't overwrite R2
        Pull    "R3, R4, R11, R12, R14, PC"

; *****************************************************************************

; Read/Write System Clock
; entered with IRQs off
; in:   R0 = 1 => read clock
;       R0 = 2 => write clock

OsWord01 ROUT
OsWord02 ROUT
        CMP     R0, #2                  ; C=1 <=> write clock
        LDRB    R0, TimerState
        EORCS   R0, R0, #&0F            ; if writing, then write to other state
                                        ; in case user resets in middle
        TEQ     R0, #5                  ; 5 => alpha, 10 => beta (C preserved)
        ADREQ   R2, TimerAlpha
        ADRNE   R2, TimerBeta
        Swap    R1, R2, CS              ; if writing then R2 is destination
        MOV     R3, #5
10
        LDRB    R4, [R2], #1
        STRB    R4, [R1], #1
        SUBS    R3, R3, #1
        BNE     %BT10
        STRB    R0, TimerState          ; if writing, switch state
                                        ; (if reading, write current state)
        MyOsWord

; *****************************************************************************

; Read/Write Interval Timer
; entered with IRQs off
; in:   R0 = 3 => read interval timer
;       R0 = 4 => write interval timer

OsWord03 ROUT
OsWord04 ROUT
        CMP     R0, #4                  ; C=1 => write timer
        MOVCS   R2, R1                  ; if writing then R1 is source
        ADRCS   R1, IntervalTimer
        ADRCC   R2, IntervalTimer       ; else R2 is source
        MOV     R0, #5
10
        LDRB    R3, [R2], #1
        STRB    R3, [R1], #1
        SUBS    R0, R0, #1
        BNE     %BT10
        MyOsWord

; *****************************************************************************

; Perform a SOUND command
OsWord07 ROUT
        TST     R1, #3
        LDMEQIA R1, {R0,R1}
        SWIEQ   XSound_ControlPacked
        MyOsWord EQ

; Block not word aligned, so push it on the stack

        SUB     R13, R13, #8            ; create stack frame of 8 bytes
        MOV     R0, #7
10
        LDRB    R2, [R1, R0]            ; copy block into stack frame
        STRB    R2, [R13, R0]
        SUBS    R0, R0, #1
        BCS     %BT10

        Pull    "R0, R1"                ; then pull stack frame into R0 and R1
        SWI     XSound_ControlPacked
        MyOsWord

; *****************************************************************************
; Read the logical colour of a Pixel ( BASIC'S POINT function)
; Uses SWI ReadPoint

OsWord09 ROUT
        Push    R1                      ; save pointer

        LDRB    R2, [R1, #0]            ; X lo-byte
        LDRB    R0, [R1, #1]            ; X hi-byte
        ORR     R0, R2, R0, LSL #8

        MOV     R0, R0, LSL #16         ; sign extend X
        MOV     R0, R0, ASR #16

        LDRB    R2, [R1, #2]            ; Y lo-byte
        LDRB    R1, [R1, #3]            ; Y hi-byte
        ORR     R1, R2, R1, LSL #8

        MOV     R1, R1, LSL #16         ; sign extend Y
        MOV     R1, R1, ASR #16

        SWI     XOS_ReadPoint   ; in: R0=X, R1=Y
                                ; out: R2=colour, R3=tint, R4=0/-1 (on/off)
        Pull    R1
        STRB    R2, [R1, #4]
        WordReturnV

; *****************************************************************************

; Read a character definition

OsWord0A ROUT
        ByteToNosbod DoReadFont
        MyOsWord

; *****************************************************************************

; Read the palette setting (VDU19,L,P,R,G,B)

OsWord0B ROUT
        ByteToNosbod DoReadPalette
        MyOsWord

; *****************************************************************************

; Write the palette setting (see VDU19)

OsWord0C ROUT
        ByteToNosbod DoSetPalette
        MyOsWord

; *****************************************************************************

; Read the last two graphics cursor positions

OsWord0D ROUT
         ByteToNosbod DoOsWord13
         MyOsWord

; *****************************************************************************

; Osword 14 (&0E) -- Read Real Time Clock
; Four (was six) different calls

; Read CMOS clock
OsWord0E ROUT
        Push    "R5-R8, R14"            ; R0-R4 saved by Osword

        MOV     R4, R1                  ; pointer to the Osword Block

        LDRB    R0, [R4, #0]
        CMP     R0, #1
        BCC     OsWord0EAlpha
        BEQ     OsWord0EBeta
        CMP     R0, #3
        BCC     OsWord0EGamma
        BEQ     OsWord0EDelta

        Pull    "R5-R8, PC"             ; unknown option

; *****************************************************************************
;
;       StoreHexPair - Store a hex number in R0 at R4+R6 (hi) and R4+R6+1 (lo)
;       Corrupts R5,R6

StoreHexPair
        Push    R14
        BL      HexToBCD
        Pull    R14
StoreBCDPair
        MOV     R5, R0, LSR #4
        ADD     R5, R5, #"0"
        STRB    R5, [R6, R4]!
        AND     R5, R0, #&0F
        ADD     R5, R5, #"0"
        STRB    R5, [R6, #1]
        MOV     PC, R14

; *****************************************************************************
;
;       StoreTLA - Store 3-letter alpha string in R0 at R4+R6+(0..2),
;       uppercasing first character
;
; out:  R0, R6 corrupt

StoreTLA
        BIC     R0, R0, #&20            ; upper case 1st char
        STRB    R0, [R6, R4]!           ; store 1st char
        MOV     R0, R0, LSR #8
        STRB    R0, [R6, #1]            ; store 2nd char
        MOV     R0, R0, LSR #8
        STRB    R0, [R6, #2]            ; store 3rd char
        MOV     PC, R14

; *****************************************************************************
;
;       OsWord0EAlpha - Read time as a string in the form
;       eg Wed,01 Jan 1986.12:34:56
;
; in:   R1 -> buffer for string
;

OsWord0EAlpha ROUT

; TMD 30-May-89: We want to enable IRQs here, but OS_ConvertDateAndTime
; loads bytes out of the block, and if IRQs are on it might end using an
; inconsistent value, so we must make a copy of the block on the stack
; and use that. The label OsWord0EDandT was used by a commented out routine
; in file 'RealTime' which will have to be rewritten if it needs to be
; included again.

        ADR     R0, RealTime            ; load snapshot of 5 bytes of real time
        LDMIA   R0, {R0, R2}            ; while IRQs are still off
        Push    "R0, R2"                ; save on stack
        CLRPSR  I_bit, R0               ; enable IRQs now
OSWord0EReturnString
        MOV     R0, R13                 ; point to stacked copy
        MOV     R2, #25                 ; 24 + CR terminator
        ADR     R3, TimeFormat
        SWI     XOS_ConvertDateAndTime
        ADD     R13, R13, #8            ; junk stack frame
        MOVVC   R0, #13                 ; if no error
        STRVCB  R0, [R1]                ; overwrite terminating 0 with CR

        Pull    "R5-R8,R14"
        WordReturnV

TimeFormat
        =       "%w3,%dy %m3 %ce%yr.%24:%mi:%se", 0
        ALIGN


; *****************************************************************************
;
;       OsWord0EBeta - Read time in BCD format
;
; in:   R4 -> parameter block
;
; out:  [R4, #0] = year         (00-99)
;       [R4, #1] = month        (01-12)
;       [R4, #2] = day of month (01-31)
;       [R4, #3] = day of week  (01-07) Sun=01
;       [R4, #4] = hours        (00-23)
;       [R4, #5] = minutes      (00-59)
;       [R4, #6] = seconds      (00-59)
;

OsWord0EBeta ROUT


        ADR     R0, RealTime            ; load snapshot of 5 bytes of real time
        LDMIA   R0, {R0, R2}            ; while IRQs are still off
        Push    "R0, R2"                ; save on stack
        CLRPSR  I_bit, R0               ; this may take some time

        MOV     R0,#-1
        MOV     R1,SP
        SUB     SP,SP,#36               ; Space for ordinals.
        MOV     R2,SP
        SWI     XTerritory_ConvertTimeToOrdinals
        ADDVS   SP,SP,#36+(2*4)
        BVS     OSWord0Eerror

;   [R2]    = CS.                     ; all values are for LOCAL time
;   [R2+4]  = Second
;   [R2+8]  = Minute
;   [R2+12] = Hour (out of 24)
;   [R2+16] = Day number in month.
;   [R2+20] = Month number in year.
;   [R2+24] = Year number.
;   [R2+28] = Day of week.
;   [R2+32] = Day of year

        LDR     R0,[R2,#24]             ; Get year
        LDR     R1,=1900
        SUB     R0,R0,R1                
01
        CMP     R0,#100
        SUBGT   R0,R0,#100
        BGT     %BT01                   ; Get year MOD 100.

        STRB    R0,[R4,#0]              ; Store it.
        LDR     R0,[R2,#20]
        STRB    R0,[R4,#1]
        LDR     R0,[R2,#16]
        STRB    R0,[R4,#2]
        LDR     R0,[R2,#28]
        STRB    R0,[R4,#3]
        LDR     R0,[R2,#12]
        STRB    R0,[R4,#4]
        LDR     R0,[R2,#8]
        STRB    R0,[R4,#5]
        LDR     R0,[R2,#4]
        STRB    R0,[R4,#6]

        ADD     SP,SP,#36+(2*4)        ; junk stack frame and 5 byte time.


; now we have the time in hex in the parameter block
; so convert each item into BCD

        MOV     R1, #6                  ; seven bytes to convert
     
10
        LDRB    R0, [R4, R1]
        BL      HexToBCD
        STRB    R0, [R4, R1]
        SUBS    R1, R1, #1
        BPL     %BT10


        B       OsWord0Eend

; *****************************************************************************
;
;       OsWord0EGamma - Convert time in BCD format (at offsets 1..7)
;       into string format at offsets (0..24)
;
; in:   R4 -> BCD time
;

OsWord0EGamma ROUT   

;build a block for Territory_ConvertOrdinalsToTime
;   [R2]    = CS.                     ; all values are for LOCAL time
;   [R2+4]  = Second
;   [R2+8]  = Minute
;   [R2+12] = Hour (out of 24)
;   [R2+16] = Day number in month.
;   [R2+20] = Month number in year.
;   [R2+24] = Year number.

        SUB     SP,SP,#28
        MOV     R2,SP
        MOV     R0,#0
        STR     R0,[R2]

        LDRB    R0,[R4,#7]              ; Seconds
        BL      BCDToHex
        STR     R0,[R2,#4]

        LDRB    R0,[R4,#6]              ; Minutes
        BL      BCDToHex
        STR     R0,[R2,#8]

        LDRB    R0,[R4,#5]              ; Hours
        BL      BCDToHex
        STR     R0,[R2,#12]

        LDRB    R0,[R4,#3]              ; Day of month
        BL      BCDToHex
        STR     R0,[R2,#16]

        LDRB    R0,[R4,#2]              ; Month
        BL      BCDToHex
        STR     R0,[R2,#20]

        
        LDRB    R0,[R4,#1]              ; Year 
        BL      BCDToHex
        LDR     R1,=1900
        ADD     R0,R0,R1
        STR     R0,[R2,#24]

        MOV     R0,#-1
        ADD     R1,SP,#20               ; Put value on satck
        SWI     XTerritory_ConvertOrdinalsToTime
        ADDVS   SP,SP,#28
        BVS     OSWord0Eerror
                        
        ADD     SP,SP,#20
        MOV     R1,R4
        B       OSWord0EReturnString    ; Now we have 5 byte value on stack,
                                        ; use same code as OSWord0EAlpha

; *****************************************************************************
;
;       OsWord0EDelta - Read 5-byte RealTime
;
; in:   R4 -> block
;
; out:  [R4, #0..4] = RealTime
;

OsWord0EDelta ROUT
        LDR     R1, RealTime +0
        STRB    R1, [R4, #0]
        MOV     R1, R1, LSR #8
        STRB    R1, [R4, #1]
        MOV     R1, R1, LSR #8
        STRB    R1, [R4, #2]
        MOV     R1, R1, LSR #8
        STRB    R1, [R4, #3]
        LDRB    R1, RealTime +4
        STRB    R1, [R4, #4]

; and drop thru to ...

OsWord0Eend
        Pull    "R5-R8, R14"
        MyOsWord

OSWord0Eerror
        Pull    "R5-R8, R14"
        WordReturnV

; *****************************************************************************
;
;       GetDecimalPair - Get pair of decimal digits from [R4+R1+0..1]
;
; in:   R1 is offset from R4 to find 1st digit
;
; out:  if valid, R1=value of pair of digits, C=0
;       if invalid, R1=undefined, C=1
;       R10 is corrupted
;

GetDecimalPair ROUT
        LDRB    R10, [R1, R4]!          ; get hi-digit
        SUB     R10, R10, #"0"          ; put in range 0..9
        CMP     R10, #10                ; C=1 if bad digit
        ADD     R10, R10, R10, LSL #2   ; R10 = 5*hi

        LDRB    R1, [R1, #1]            ; get lo-digit
        SUB     R1, R1, #"0"            ; put in range 0..9
        CMPCC   R1, #10                 ; C=1 if bad digit
        ADD     R1, R1, R10, LSL #1     ; R1 = lo + 2*(5*hi)

        MOV     PC, R14

; *****************************************************************************

; Osword 15 (&0F) Write the Real Time Clock.
; Three different calls

OsWord0F ROUT
        CLRPSR  I_bit, R0               ; this may take some time

        Push    "R5-R10, R14"
        MOV     R4, R1                  ; Copy the parameter block pointer
        LDRB    R0, [R1]
        MOV     R9, #0

        TEQ     R0, #8                  ; write hours, minutes, seconds
        MOVEQ   R9, #1

        TEQ     R0, #15                 ; write day, date, month, year
        MOVEQ   R9, #2

        TEQ     R0, #24                 ; write all of time
        MOVEQ   R9, #3

        TEQ     R9, #0
        Pull    "R5-R10, PC", EQ         ; unknown call, pass it on


        ADD     r10, r0, #3+1           ; round up number of bytes in block to word boundary, including null terminator
        BIC     r10, r10, #3
        SUB     sp, sp, r10

        ADD     r2, r1, #1              ; point at actual string
        MOV     r1, #0
02
        LDRB    r14, [r2, r1]           ; copy string (not terminated) on stack
        STRB    r14, [sp, r1]
        ADD     r1, r1, #1
        TEQ     r1, r0                  ; have we copied all bytes of string?
        BNE     %BT02                   ; loop if not

        MOV     r14, #0                 ; null terminator
        STRB    r14, [sp, r0]

        MOV     r0,#-1                  ; set things up for territory SWI - r0 = -1 for current territory
        MOV     r1, r9                  ; r1 = reason code (1, 2 or 3)
        MOV     r2, sp                  ; r2 -> terminated string on stack
        SUB     sp, sp, #36             ; get space for result.
        MOV     r3, sp

        SWI     XTerritory_ConvertTimeStringToOrdinals
        ADDVS   sp, sp, #36             ; if error then junk return block
        ADDVS   sp, sp, r10             ; and junk variable length string on stack
        BVS     Bad0F

        CMP     r9, #2                  ; if just writing the date, write it !
        BEQ     %FT10
        BGT     %FT05                   ; if writing everything just get UTC time

; We only have the time from the string, we now need the date
; because changing the time may change it.

        ADR     r0, RealTime
        LDMIA   r0, {r0,r1}             ; LDM is atomic wrt interrupts
          
        Push    "r0,r1"                 ; put value on stack
        MOV     r0,#-1                  ; use configured territory.
        ADD     r2, sp, #8
        LDMIA   r2, {r3-r6}             ; preserve time values from entry string
        MOV     r1, sp
        SWI     XTerritory_ConvertTimeToOrdinals        ; get ordinals for current time
        ADDVS   sp, sp, #44             ; 36 From above + 8 for 5 byte time
        ADDVS   sp, sp, r10             ; and junk string as well
        BVS     Bad0F
        ADD     sp, sp, #8              ; dump 5 byte time on TOS
        STMIA   r2, {r3-r6}             ; restore the time we read from the string.

05
; Now [SP] -> ordinals in local time, but we want time in UTC
; First convert the ordinals to 5 byte UTC time
                 
        MOV     r0, #-1                 ; use configured territory.
        MOV     r2, sp                  ; r2 -> ordinals block
        SUB     sp, sp, #8              ; two more words to contain 5 byte time
        MOV     r1, sp
        SWI     XTerritory_ConvertOrdinalsToTime
        ADDVS   sp, sp, #44             ; 36 From above + 8 for 5 byte time.
        ADDVS   sp, sp, r10             ; and junk string as well
        BVS     Bad0F

; Now we have a 5 byte UTC time, convert it to UTC ordinals

        MOV     r1, sp                  ; our 5 byte time
        ADD     r2, sp, #8              ; place to put ordinals
        SWI     XTerritory_ConvertTimeToUTCOrdinals
        ADDVS   sp, sp, #44             ; 36 bytes ordinals + 8 for 5 byte time.
        ADDVS   sp, sp, r10             ; and junk string as well
        BVS     Bad0F     
                               
        ADD     sp, sp, #8              ; discard 5 byte time

10
; Load the registers. (SP->Ordinals)

        LDR     r8, [sp], #4            ; centiseconds
        LDR     r7, [sp], #4            ; seconds
        LDR     r1, [sp], #4            ; minutes
        Pull    "r0,r2,r3,r5"           ; hours, day of month,  month, year
        ADD     sp, sp, #8              ; junk day of week, day of year
        ADD     sp, sp, r10             ; and string on stack
        MOV     r4, #100
        DivRem  r6, r5, r4, r14         ; r5 = Year (lo), r6 = Year (hi)


        BL      SetTime                 ; also updates 5-byte RealTime

Bad0F                                   ; come here if setting invalid
        Pull    "R5-R10, R14"
        MyOsWord

; *****************************************************************************


; *****************************************************************************
;
;       CheckYear - Check for year wrap (if months have gone down)
;       and for leap year fudging
;

CheckYear ROUT
        [ NewClockChip
        Push    "R0,R1,R2,R14"
        MOV     R0, #5                  ; year address
        BL      ReadStraight
        MOV     R1, R0, LSR #6          ; year in range 0..3
        MOV     R0, #YearCMOS
        BL      Read
        AND     R2, R0, #3
        SUBS    R2, R1, R2              ; same year ?
        Pull    "R0,R1,R2,PC", EQ       ; [yes, so no bother]
        ADDCC   R2, R2, #4              ; if lower, then must be carry
        ADD     R2, R0, R2              ; new year value
        CMP     R2, #100
        BCC     %FT10                   ; no carry thru to next century

        SUB     R2, R2, #100
        MOV     R0, #YearCMOS +1
        BL      Read
        ADD     R1, R0, #1
        TEQ     R1, #100
        MOVEQ   R1, #0                  ; wrap century
        MOV     R0, #YearCMOS +1
        BL      Write
10
        MOV     R1, R2
        MOV     R0, #YearCMOS
        BL      Write

        BL      RTCToRealTime
        Pull    "R0,R1,R2,PC"

        |
        Push    "R0-R8, R14"

        BL      ReadTime                ; R3 = clock month (in hex)

        MOV     R0, #MonthCMOS
        BL      Read                    ; R0 = CMOS RAM month

        CMP     R3, R0
        BLCC    HappyNewYear            ; The months have gone down

; now make sure that Feb 29th is dealt with correctly

        CMP     R3, #3                  ; if Jan or Feb then no problem
        BCC     %FT10

        MOV     R0, #LeapCMOS           ; if already fudged it
        BL      Read                    ; then no problem
        TEQ     R0, #0
        BNE     %FT10

        TST     R5, #3                  ; if year MOD 4 is not zero
        BNE     %FT10                   ; then no problem

        TEQ     R5, #0                  ; if not century start
        BNE     %FT20                   ; then is leap year

        TST     R6, #3                  ; else if not mult of 400
        BNE     %FT10                   ; then no problem

20                                      ; it is a leap year
        SUBS    R2, R2, #1              ; day := day-1
        SUBEQ   R3, R3, #1              ; if day=0 then month := month-1
        ADREQ   R4, MonthLengths
        LDREQB  R2, [R4, R3]            ; and day := last day of month

        MOV     R0, #-1                 ; don't change hours
        MOV     R1, #-1                 ; or minutes
        MOV     R7, #-1                 ; or seconds
        MOV     R8, #-1                 ; or centiseconds
        BL      SetTime

        MOV     R1, #1                  ; we have now fudged it
        MOV     R0, #LeapCMOS
        BL      Write

        MOV     R1, R3                  ; remember the month
        MOV     R0, #MonthCMOS
        BL      Write
10
        Pull    "R0-R8, PC"

; *****************************************************************************
;
;       HappyNewYear - Update the year
;
; in:   R3 = months
;       R5 = year(lo)
;       R6 = year(hi)
;
; out:  R5 = new year(lo)
;       R6 = new year(hi)

HappyNewYear ROUT
        Push    "R0,R1, R14"

        ADD     R5, R5, #1              ; R2 := new year(lo)
        TEQ     R5, #100
        BNE     %FT10                   ; [don't increment century]

        MOV     R5, #0                  ; wrap year
        ADD     R6, R6, #1              ; R1 := new year(hi)
        TEQ     R6, #100
        MOVEQ   R6, #0                  ; wrap century

        MOV     R1, R6
        MOV     R0, #YearCMOS+1
        BL      Write                   ; write year(hi)
10
        MOV     R1, R5
        MOV     R0, #YearCMOS
        BL      Write                   ; write year(lo)

        BL      RTCToRealTime           ; update 5-byte RealTime

        MOV     R1, R3
        MOV     R0, #MonthCMOS
        BL      Write                   ; write new months

        MOV     R1, #0
        MOV     R0, #LeapCMOS
        BL      Write                   ; clear leapyear flag

        Pull    "R0,R1, PC"

        ]

; *****************************************************************************

MonthLengths
 ;  F  J  F  M  A  M  J  J  A  S  O  N  D
 = 28,31,29,31,30,31,30,31,31,30,31,30,31
         ALIGN

; *****************************************************************************
;
;       DayOfTheWeek - Compute day of the week
;
; in:   R2 = day of the month
;       R3 = month
;       R7 = year MOD 400
;
; out:  R0 = day of week (0..6 = Sun..Sat) (I think!)
;       Other registers preserved
;

DayOfTheWeek
        Push    "R1,R4-R7, R14"
        SUB     R0, R2, #1              ; R0 = day of the month starting at 0
        ADR     R4, DaysToDate-4        ; adjust cos months start at 1
        LDR     R5, [R4, R3, LSL #2]    ; get the month start offset
        ADD     R0, R0, R5              ; number of days since start of year

; now adjust for this year being leap

        TST     R7, #3
        BNE     NotLeapYr               ; non-multiples of 3 are not leap

        TEQ     R7, #100
        TEQNE   R7, #200
        TEQNE   R7, #300
        BEQ     NotLeapYr               ; if 100 or 200 or 300 then not leap

IsLeapYr
        CMP     R3, #3                  ; is leap, so if March 1st or after
        ADDCS   R0, R0, #1              ; then add extra day
NotLeapYr
        TEQ     R7, #0                  ; if mult of 400
        MOVEQ   R7, #400                ; then set to 400
        ADD     R0, R0, R7              ; add one for each year

        SUB     R7, R7, #1              ; R7 = last year to cope with
        ADD     R0, R0, R7, LSR #2      ; add one for each leap year

        CMP     R7, #100                ; subtract one if counting year 100
        SUBCS   R0, R0, #1
        CMP     R7, #200                ; subtract one if counting year 200
        SUBCS   R0, R0, #1
        CMP     R7, #300                ; subtract one if counting year 300
        SUBCS   R0, R0, #1

;       ADD     R0, R0, #0              ; fudge factor cos 1 Jan 1986 is Wed
        MOV     R5, #7
        DivRem  R1, R0, R5, R6

        Pull    "R1,R4-R7, PC"

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

DaysToDate
 & 0,31,59,90,120,151,181,212,243,273,304,334
;  J F  M  A  M   J   J   A   S   O   N   D

; *****************************************************************************

; Define hardware cursor size, shape and active point

OsWord15
        ByteToNosbod DoPointerStuff
        MyOsWord

; *****************************************************************************

; Set start of screen address (for VDU drivers and display)
; [R1, #0] = bit mask (bit0 set => change drivers; bit1 set => change display)
; [R1, #1..4] = byte offset from start of screen memory

OsWord16
        ByteToNosbod DoSetScreenStart
        MyOsWord

; **************************************************************************

; All the unused OS_Word calls

; Read Byte of I/O proc memory
OsWord05 ROUT
; Write byte of I/O proc memory
OsWord06 ROUT
; Define an ENVELOPE
OsWord08 ROUT
; Allocated to the net
OsWord10
OsWord11
OsWord12
OsWord13
OsWord14
        Unused

        END
