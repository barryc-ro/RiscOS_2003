; > s.growp


;grow slot by taking pages from FreePool; add them to AppSpace


; entry:
;     R1 =  new number of pages for slot (more than current value)
;     R2 -> AMB node (page table already allocated/grown to size)
;
; exit: -

growpages
        Push    "R0-R7,LR"

        MOV     R6,R1                      ;save entry R1
        LDR     R5,[R2,#AMBNode_Npages]
        CMP     R5,R6
        Pull    "R0-R7,PC",EQ              ;done if no. of pages unchanged

        MOV     R0,R2                      ;R0 -> AMB node

        LDR     R1,=FreePoolDANode         ;R1 := source for pages
        LDR     R2,=AppSpaceDANode         ;R2 := dest for pages

        LDR     R7,[R0,#AMBNode_Npages]    ;R7 := current no. of pages
        SUB     R3,R6,R7                   ;no. of pages required from FreePool
        LDR     R4,[R1,#DANode_Size]
        MOV     R4,R4,LSR #Log2PageSize    ;no. of pages in FreePool
        CMP     R3,R4
        MOVGT   R3,R4                      ;R3 := no. of pages we will move
        CMP     R3,#0
        BEQ     %FT02                      ;done if can't move any pages
        ADD     R4,R3,R7
        STR     R4,[R0,#AMBNode_Npages]    ;new no. of pages

        ADD     R4,R0,#AMBNode_pages
        ADD     R4,R4,R7,LSL #2            ;R4 -> 1st page table entry for grow
        LDR     R5,[R1,#DANode_Base]
        LDR     LR,[R1,#DANode_Size]
        ADD     R5,R5,LR                   ;current end of FreePool
        SUB     R5,R5,R3,LSL #Log2PageSize ;R5 := first required page address

        ;entry: R3 = no. of pages, R4 -> buffer for page entries,
        ;       R5 := start logical address
        BL      AMB_FindMemMapEntries      ;find page nos.

        LDR     R5,[R0,#AMBNode_startaddr]
        ADD     R5,R5,R7,LSL #Log2PageSize ;R5 := first new page new address
        LDR     R6,[R0,#AMBNode_PPL]       ;R6 := dest PPL flags

        ;entry: R3 = no. of pages, R4 -> list of page entries,
        ;       R5 = start logical address, R6 = PPL
        BL      AMB_SetMemMapEntries       ;remap

        LDR     R5,[R1,#DANode_Size]
        SUB     R5,R5,R3,LSL #Log2PageSize
        STR     R5,[R1,#DANode_Size]       ;new FreePool size

        LDR     R5,[R2,#DANode_Size]
        ADD     R5,R5,R3,LSL #Log2PageSize
        STR     R5,[R2,#DANode_Size]       ;new AppSpace size
        MOV     R6,#0
        STR     R5,[R6,#MemLimit]          ;update MemLimit

02
        STRVS   R0,[SP]
        Pull    "R0-R7,PC"

    END
