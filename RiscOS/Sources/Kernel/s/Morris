; > Morris

;
; ROM entry code for RISC OS Black
;

MorrisHeaderCodeSize * (24*4)   ;Code occupies less than this, we pad to this boundary so POST code
                                ;has a nicely defined place for its romsize and code entry points
; VECTOR AREA:

        LDR     pc, .+ResetIndirection
        LDR     PC, UNDEF_VEC
        LDR     PC, SWI_VEC
        LDR     PC, PREF_VEC
        LDR     PC, DATA_VEC
        LDR     PC, RES_VEC                   ; addr. exn not generated
        LDR     PC, IRQ_VEC
        LDR     PC, FIQ_VEC

;;;        ! 0, "*** WARNING *** assembling Morris code"

; The above 8 instructions will operate as expected in 32-bit ROM mode,
; or in 16-bit ROM mode with a 16-bit ROM used.  In 16-bit ROM mode, and
; with 32-bit wide ROMs in use, they will instead be intepreted as 4
; NV-condition instructions (exact meaning not determined and should be
; irrelevant) which should do nothing and so allow control to drop
; through to this point, still in 16-bit mode.  Force IOMD into 32-bit
; ROM mode for bank 0.  The following instruction sequence has been
; produced in 16-in-32 form by extracting hex values from a listing...

        DCD     &0000B632, &0000E3A0    ; 20: MOV R11, #IO+IOMDREGS - point at IOMD
        DCD     &00000000, &0000E3A0    ; 24: MOV R0, #&0 - ROMCR:32b,slow,218.75us,no burst
        DCD     &00000080, &0000E5CB    ; 28: STRB R0,[R11,#ROMCR0] - switch mode
        DCD     &0000F000, &0000E3A0    ; 2C: MOV PC, #0 - jump to 0 (this instr pre-fetched)


; vector absolute targets for use from physical vector instructions

UNDEF_VEC       DCD     UndInstInReset-ROM
SWI_VEC         DCD     SWIInReset    -ROM
PREF_VEC        DCD     PrefAbInReset -ROM
DATA_VEC        DCD     DataAbInReset -ROM
RES_VEC         DCD     AddrExInReset -ROM
IRQ_VEC         DCD     IRQInReset    -ROM
FIQ_VEC         DCD     FIQInReset    -ROM

UndInstInReset
SWIInReset
PrefAbInReset
DataAbInReset
AddrExInReset
IRQInReset
FIQInReset
        SUB     pc, pc, #8

        ASSERT  .-ROM <= MorrisHeaderCodeSize
        %       MorrisHeaderCodeSize-(.-ROM)

        END
