        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System

        GET     Hdr:OSEntries

        GET     hdr.StaticWS
        GET     hdr.Hardware

        AREA    |Asm$$Code|, CODE, READONLY, PIC

        EXPORT  HAL_MatrixColumns
        EXPORT  HAL_MatrixScan
        EXPORT  Matrix_Init

Matrix_Init
        LDR     a4, KEYBOARD_BASE_Address
        MOV     a1, #&1000
        STR     a1, [a4, #KBDMR]        ; select Keyboard mode (not GPIO)
        MOV     a1, #1
        STR     a1, [a4, #KBKSR]        ; all pins low
        LDR     a4, GPIO_BASE_Address
        MOV     a1, #0
        STRB    a1, [a4, #PADDR]        ; all GPIO A input
        STRB    a1, [a4, #PAIMR]        ; no interrupts
        MOV     pc, lr

HAL_MatrixColumns
        MOV     a1, #12
        MOV     pc, lr

HAL_MatrixScan
        LDR     a4, KEYBOARD_BASE_Address
        ADD     a1, a1, #4              ; handles -1 too :)
        STR     a1, [a4, #KBKSR]        ; activate the appropriate column
        LDR     a3, GPIO_BASE_Address
        LDRB    a1, [a3, #PADR]         ; hope it responds in time :)
        MOV     a2, #1
        STR     a2, [a4, #KBKSR]        ; all columns low again
        MOV     pc, lr

        END
