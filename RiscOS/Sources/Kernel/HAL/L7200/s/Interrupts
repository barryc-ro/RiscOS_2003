        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:Machine.<Machine>
        GET     Hdr:ImageSize.<ImageSize>

        GET     Hdr:OSEntries

        GET     hdr.StaticWS
        GET     hdr.Hardware
        GET     hdr.Video

        AREA    |Asm$$Code|, CODE, READONLY, PIC

        EXPORT  HAL_IRQEnable
        EXPORT  HAL_IRQDisable
        EXPORT  HAL_IRQClear
        EXPORT  HAL_IRQSource
        IMPORT  HAL_IRQClear_Video
        EXPORT  HAL_FIQDisableCode

HAL_IRQEnable
        LDR     a3, INT_BASE_Address
        MOV     ip, #1
        CMP     a1, #32
        SUBHS   a1, a1, #32
        MOV     a1, ip, LSL a1
        STRLO   a1, [a3, #IRQLEnableSet]
        STRHS   a1, [a3, #IRQHEnableSet]
        MOV     pc, lr

HAL_IRQDisable
        LDR     a3, INT_BASE_Address
        MOV     ip, #1
        CMP     a1, #32
        SUBHS   a1, a1, #32
        MOV     a1, ip, LSL a1
        STRLO   a1, [a3, #IRQLEnableClear]
        STRHS   a1, [a3, #IRQHEnableClear]
        MOV     pc, lr

HAL_IRQClear
        TEQ     a1, #4
        TEQNE   a1, #5
        BEQ     HAL_IRQClear_Timer
        TEQ     a1, #CLCD_IRQ
        BEQ     HAL_IRQClear_Video
        MOV     pc, lr

HAL_IRQClear_Timer
        LDR     a4, TIMER_BASE_Address
        SUB     a1, a1, #4
        ADD     a4, a4, a1, LSL #5
        STR     a1, [a4, #TIMER1CLEAR]
        MOV     pc, lr

 [ {FALSE}
HAL_IRQSource
        LDR     a3, INT_BASE_Address
        MOV     a1, #0
        LDR     ip, [a3, #IRQLStatus]
05      MOVS    ip, ip, LSR #1
        MOVCS   pc, lr
        BEQ     %FT30
        ADD     a1, a1, #1
        B       %BT05
30      MOV     a1, #32
        LDR     ip, [a3, #IRQHStatus]
10      MOVS    ip, ip, LSR #1
        MOVCS   pc, lr
        ADD     a1, a1, #1
        BNE     %BT10
        MOV     a1, #64
        MOV     pc, lr
 |
HAL_IRQSource
        LDR     a3, INT_BASE_Address
        LDR     a1, [a3, #IRQLStatus]
                                                ; ARM7  ARM7M ARM9    SA1
        LDR     ip, =&07DCD629                  ; 3     3     1       1
        ORRS    a1, a1, a1, LSR #1              ; 1     1     1       1
        BEQ     NotIRQL
        ORR     a1, a1, a1, LSR #2              ; 1     1     1       1
        ORR     a1, a1, a1, LSR #4              ; 1     1     1       1
        ORR     a1, a1, a1, LSR #8              ; 1     1     1       1
        ORR     a1, a1, a1, LSR #16             ; 1     1     1       1
        MLA     a1, ip, a1, ip                  ; 2-17  3-7   3-7     1-3
        LDRB    a1, [pc, a1, LSR #27]           ; 3     3     1       1
                                                ; ----- ----- -----   ----
                                                ; 14-29 15-19 11-15   9-11
        MOV     pc, lr

irq_table
        =       31, 0,22, 1,28,23,13, 2,29,26,24,17,19,14, 9, 3
        =       30,21,27,12,25,16,18, 8,20,11,15, 7,10, 6, 5, 4

NotIRQL
        LDR     a1, [a3, #IRQHStatus]
        ORR     a1, a1, a1, LSR #1
        ORR     a1, a1, a1, LSR #2
        ORR     a1, a1, a1, LSR #4
        ORR     a1, a1, a1, LSR #8
        ORRS    a1, a1, a1, LSR #16
        MLANE   a1, ip, a1, ip
        ADR     a2, irq_table
        LDRNEB  a1, [a2, a1, LSR #27]
        MOVEQ   a1, #64
        ADDNE   a1, a1, #32
        MOV     pc, lr
 ]

HAL_FIQDisableCode
        ADR     a2, FIQOff_Code
        LDMIA   a2!, {a3,a4,ip}
        STMIA   a1!, {a3,a4,ip}
        LDR     ip, INT_BASE_Address
        LDMIA   a2!, {a3,a4}
        STMIA   a1!, {a3,a4,ip}
        MOV     pc, lr

FIQOff_Code
        LDR     r8, FIQOff_INT_BASE
        MVN     r9, #0
        STR     r9, [r8, #FIQLEnableClear]
        STR     r9, [r8, #FIQHEnableClear]
        SUBS    pc, lr, #4
FIQOff_INT_BASE

        END
