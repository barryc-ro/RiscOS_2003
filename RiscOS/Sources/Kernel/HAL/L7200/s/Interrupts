        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:Machine.<Machine>
        GET     Hdr:ImageSize.<ImageSize>

        GET     Hdr:OSEntries

        GET     hdr.StaticWS
        GET     hdr.Hardware

        AREA    |Asm$$Code|, CODE, READONLY, PIC

        EXPORT  HAL_IRQEnable
        EXPORT  HAL_IRQDisable
        EXPORT  HAL_IRQClear
        EXPORT  HAL_IRQSource

HAL_IRQEnable
        LDR     a3, INT_BASE_Address
        MOV     ip, #1
        CMP     a1, #32
        SUBHS   a1, a1, #32
        MOV     a1, ip, LSL a1
        STRLO   a1, [a3, #IRQLEnableSet]
        STRHS   a1, [a3, #IRQHEnableSet]
        MOV     pc, lr

HAL_IRQDisable
        LDR     a3, INT_BASE_Address
        MOV     ip, #1
        CMP     a1, #32
        SUBHS   a1, a1, #32
        MOV     a1, ip, LSL a1
        STRLO   a1, [a3, #IRQLEnableClear]
        STRHS   a1, [a3, #IRQHEnableClear]
        MOV     pc, lr

HAL_IRQClear
        TEQ     a1, #4
        TEQNE   a1, #5
        BEQ     HAL_IRQClear_Timer
        MOV     pc, lr

HAL_IRQClear_Timer
        LDR     a4, TIMER_BASE_Address
        SUB     a1, a1, #4
        ADD     a4, a4, a1, LSL #5
        STR     a1, [a4, #TIMER1CLEAR]
        MOV     pc, lr

HAL_IRQSource
        LDR     a3, INT_BASE_Address
        MOV     a1, #0
        LDR     ip, [a3, #IRQLStatus]
05      MOVS    ip, ip, LSR #1
        MOVCS   pc, lr
        BEQ     %FT30
        ADD     a1, a1, #1
        B       %BT05
30      MOV     a1, #32
        LDR     ip, [a3, #IRQHStatus]
10      MOVS    ip, ip, LSR #1
        MOVCS   pc, lr
        ADD     a1, a1, #1
        BNE     %BT10
        MOV     a1, #64
        MOV     pc, lr

        END
