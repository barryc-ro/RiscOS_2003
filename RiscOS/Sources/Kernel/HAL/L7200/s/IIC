        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:Machine.<Machine>
        GET     Hdr:ImageSize.<ImageSize>

        GET     Hdr:Proc
        GET     Hdr:HALEntries
        GET     Hdr:OSEntries

        GET     hdr.StaticWS
        GET     hdr.Hardware

        AREA    |Asm$$Code|, CODE, READONLY, PIC

        EXPORT  HAL_IICBuses
        EXPORT  HAL_IICType
        EXPORT  HAL_IICSetLines
        EXPORT  HAL_IICReadLines

 [ SDB
HAL_IICBuses
        MOV     a1, #0
        MOV     pc, lr
 |
HAL_IICBuses
        MOV     a1, #1
        MOV     pc, lr

HAL_IICType
        MOV     a1, #IICFlag_LowLevel
        MOV     pc, lr

; In:  a1 = bus number, a2 = SDA, a3 = SCL
; Out: a1 = SDA, a2 = SCL
HAL_IICSetLines
        MRS     ip, CPSR
        LDR     a4, KEYBOARD_BASE_Address
        ADD     a2, a3, a2, LSL #1
        ORR     a1, ip, #I32_bit
        MSR     CPSR_c, a1
        LDR     a1, [a4, #KBDR]
        BIC     a1, a1, #2_11 :SHL: 6
        STR     a1, [a4, #KBDR]                 ; Ensure data register contains zeros
        LDR     a1, [a4, #KBDMR]
        BIC     a1, a1, #2_11 :SHL: 6
        ORR     a1, a1, a2, LSL #6              ; Set zero bits to outputs (DMR bit=0)
        STR     a1, [a4, #KBDMR]
        LDR     a3, [a4, #KBDR]
        MSR     CPSR_c, ip
        MOV     ip, #1
        AND     a1, ip, a3, LSR #7
        AND     a2, ip, a3, LSR #6
        MOV     pc, lr

HAL_IICReadLines
        LDR     a4, KEYBOARD_BASE_Address
        LDR     a3, [a4, #KBDR]
        MOV     ip, #1
        AND     a1, ip, a3, LSR #7
        AND     a2, ip, a3, LSR #6
        MOV     pc, lr
 ]

        END
