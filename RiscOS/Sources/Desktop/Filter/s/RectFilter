; > s.rectFilter

;;----------------------------------------------------------------
;; rect_filter_handler
;;
;; This is called by the Wimp on entry to int_get_rectangle
;;
;; Entry:
;;
;;      R2 - Task handle of calling task.
;;
;; Exit:
;;
;;      All other registers preserved.
;;
;;      All interested filters called.
;;
;;

rect_filter_handler

        Push    "r0-r2,LR"		; If you change this, change the
					; sortout_fakepostrect routine
        MOV     R2,R2,ASL #16
        MOV     R2,R2,LSR #16

        LDR	LR, fake_postrect	; If we're faking post-rectangle
	TEQ	LR, #0			; filters, and we have any
	LDRNE	LR, post_rect_filters	; claimants, go poke the
	TEQNE	LR, #0			; Window Manager's stack
	BLNE	sortout_fakepostrect

        LDR     r0, get_handle_from_r10 ; find the window being redrawn
        TEQ     r0, #0
        LDREQ   r0, [sp,#16+12+0]	; BTW - you didn't see that...
        ADDEQ   r0, r0, #1
        ADDNE   r0, r10, #1		; turn pointer into window handle

        LDR     r1,rect_filters
rect_filter_exit
        CMP     r1,#0
        Pull    "r0-r2,PC",EQ,^          ; End of list.
        LDR     r14,[r1,#next_ptr]
        Push    "r14"

        LDR     r14,[r1,#f_task]
        TEQ     r14,R2
        CMPNE   r14,#0
        Pull    "r1",NE
        BNE     rect_filter_exit        ; Filter not interested.

        LDR     r12,[r1,#f_R12]        ; Set R12.

	[ {TRUE}
        MOV     LR,PC
        LDR     PC, [r1,#f_address]
        NOP
	|
	STMFD	sp!,{r0-r12}
	LDR	r3,[r1,#f_address]
	MOV	r1,sp
	MOV	r0,#0
	MOV	r2,pc
	AND	r2,r2,#3		; should be 3
	SWI	&69cc0
        LDMFD	sp!,{r0-r12}
	]
        Pull    "r1"
        B       rect_filter_exit
        LNK     s.PoRtFilter


