; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; In    r2 = new options
;       r3 = bit mask to write under

WriteCMOSBits ENTRY "r0-r3"

 [ debugmenu
 DREG r2,"setting bits ",cc,Byte
 DREG r3," under mask ",,Byte
 ]
        AND     r0, r2, r3              ; Clear out bits we'll not alter

        BL      ReadCMOSBits
        LDRVC   r14, [sp, #4*3]         ; r3in
        BICVC   r3, r3, r14             ; Clear out bits we'll alter

        ORRVC   r2, r3, r0              ; Merge bits
 [ debugmenu
 DREG r2,"writing cmos state ",,Byte
 ]
        MOVVC   r0, #162
        MOVVC   r1, #DesktopCMOS
        SWIVC   XOS_Byte
        STRVS   R0,[sp]
        EXIT

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; Out   r3 = current CMOS state

ReadCMOSBits ENTRY "r0-r2"

        MOV     r0, #161                ; Read current state
        MOV     r1, #DesktopCMOS
        SWI     XOS_Byte
        MOV     r3, r2
 [ debugmenu
 DREG r3,"current cmos state is ",,Byte
 ]           
        STRVS   R0,[SP]
        EXIT
        END
