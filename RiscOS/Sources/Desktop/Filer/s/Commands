Filer_OpenDir_Accept
        DCB     "dir=directory/A/G,"
        DCB     "x0=topleftx,y1=toplefty,w=width,h=height,"
        DCB     "li=largeicons/S,si=smallicons/S,fi=fullinfo/S,"
        DCB     "sn=sortbyname/S,st=sortbytype/S,ss=sortbysize/S,"
        DCB     "sd=sortbydate/S"
        DCB     0

        ALIGN

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; Had *command to enter Filer, so start up via module handler

Desktop_Filer_Code ENTRY
 [ version >= 113
        LDR     wp, [r12]
        LDR     r14, mytaskhandle
        CMP     r14, #-1
        BLNE    UseDesktopError
        EXIT    VS
 ]

        MOV     r0, #ModHandReason_Enter
        addr    r1, Filer_TitleString
        SWI     XOS_Module
        EXIT

 [ openat
; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; Get an integer from a ReadArgs vector
;
; In    r2 -> vector
;       r4 = parameter offset
;
; Out   EQ set if value not present r1=0
;       NE otherwise and
;       r1 contains value

readargs_getint ENTRY   "r0,r2,r4,r5"

        ; Check for parameter present
        LDR     r1, [r2, r4]
        TEQ     r1, #0
        EXIT    EQ

        ; Check for negative
        LDRB    r5, [r1]
        TEQ     r5, #"-"
        ADDEQ   r1, r1, #1      ; advance past it

        ; If present, convert it and accept any answer
        MOV     r0, #10         ; Base 10
        MOV     r2, #0          ; Don't care much
        SWI     XOS_ReadUnsigned
        CLRV                    ; Ignore any error

        TEQ     r5, #"-"        ; was it minus?
        RSBEQ   r2, r2, #0      ; neg it

        MOV     r1, r2
        MOVS    r14, #1         ; Clear the Z flag
        EXIT
 ]

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; In    r0-r6 trashable
;
; Format of a directory request
;

                ^       0
dirreq_link     #       4       ; Next request
dirreq_closeflags #     0       ; flags for CloseDir
dirreq_x0       #       4       ; Window location
dirreq_filetype #       0
dirreq_y1       #       4
dirreq_w        #       4
dirreq_h        #       4
dirreq_reason   #       1       ; Type of request (open, close, run, boot etc)
dirreq_viewmode #       1       ; View mode (for open)
dirreq_dirname  #       0
dirreq_size     *       :INDEX: @

drr_open        *       0
drr_close       *       1
drr_run         *       2
drr_boot        *       3

; Fields in the ReadArgs output vector
                ^       0
fopendir_dir    #       4
fopendir_x0     #       4
fopendir_y1     #       4
fopendir_w      #       4
fopendir_h      #       4
fopendir_li     #       4
fopendir_si     #       4
fopendir_fi     #       4
fopendir_sn     #       4
fopendir_ss     #       4
fopendir_st     #       4
fopendir_sd     #       4

Filer_OpenDir_Code ENTRY "r9"

        MOV     r9, #drr_open

01      LDR     wp, [r12]               ; Can't do anything if not started
        LDR     r14, mytaskhandle       ; Don't allow queueing outside the wimp
        CMP     r14, #0
        BLLE    UseDesktopError
        EXIT    VS

02      ; Set up and perform a OS_ReadArgs into userdata
        MOV     r1, r0
        ADRL    r0, Filer_OpenDir_Accept
        ADR     r2, userdata
        MOV     r3, #userdata_size
        SWI     XOS_ReadArgs
        EXIT    VS

        ; Get pointer to directory name
        LDR     r1, [r2, #fopendir_dir]

        ; Get length of string
        LDRB    r3, [r1]
        LDRB    r14, [r1, #1]
        ORR     r3, r3, r14, LSL #8
        MOV     r5, r3          ; Hold length for later

        ; Add size of header
        ADD     r3, r3, #dirreq_size + 1 ; for terminator

        ; Claim a that size chunk
        MOV     r0, #ModHandReason_Claim
        SWI     XOS_Module
        EXIT    VS
        MOV     r3, r2

    ; Fill in the fields

      ; x0 field
        ADR     r2, userdata
        MOV     r4, #fopendir_x0
        BL      readargs_getint
        BEQ     %FT10
        STR     r1, [r3, #dirreq_x0]

      ; y1 field
        MOV     r4, #fopendir_y1
        BL      readargs_getint
        BEQ     %FT10
        STR     r1, [r3, #dirreq_y1]

      ; w field
        MOV     r4, #fopendir_w
        BL      readargs_getint
        BEQ     %FT20
        CMP     r1, #0                  ; if negative
        MOVLT   r1, #0                  ; then use default
        STR     r1, [r3, #dirreq_w]

      ; h field
        MOV     r4, #fopendir_h
        BL      readargs_getint
        BEQ     %FT20
        CMP     r1, #0                  ; if negative
        MOVLT   r1, #0                  ; then use default
        STR     r1, [r3, #dirreq_h]
        B       %FT30

10      STR     r1, [r3, #dirreq_x0]
        STR     r1, [r3, #dirreq_y1]
20      STR     r1, [r3, #dirreq_w]
        STR     r1, [r3, #dirreq_h]


30      ; Get the display switches
        MOV     r0, #0
        LDR     r1, [r2, #fopendir_fi]
        MOVS    r1, r1
        MOVNE   r0, #1 :SHL: dbu_displaymode :OR: db_dm_fullinfo :SHL: dbb_displaymode

        LDR     r1, [r2, #fopendir_si]
        MOVS    r1, r1
        MOVNE   r0, #1 :SHL: dbu_displaymode :OR: db_dm_smallicon :SHL: dbb_displaymode

        LDR     r1, [r2, #fopendir_li]
        MOVS    r1, r1
        MOVNE   r0, #1 :SHL: dbu_displaymode :OR: db_dm_largeicon :SHL: dbb_displaymode

        ; Get the sorting switches
        MOV     r4, #0
        LDR     r1, [r2, #fopendir_sd]
        MOVS    r1, r1
        MOVNE   r4, #1 :SHL: dbu_sortmode :OR: db_sm_date :SHL: dbb_sortmode

        LDR     r1, [r2, #fopendir_st]
        MOVS    r1, r1
        MOVNE   r4, #1 :SHL: dbu_sortmode :OR: db_sm_type :SHL: dbb_sortmode

        LDR     r1, [r2, #fopendir_ss]
        MOVS    r1, r1
        MOVNE   r4, #1 :SHL: dbu_sortmode :OR: db_sm_size :SHL: dbb_sortmode

        LDR     r1, [r2, #fopendir_sn]
        MOVS    r1, r1
        MOVNE   r4, #1 :SHL: dbu_sortmode :OR: db_sm_name :SHL: dbb_sortmode

        ; combine the two flags and store them
        ORR     r0, r0, r4
        STRB    r0, [r3, #dirreq_viewmode]

        ; Zero byte beyond end of dirname to terminate string
        ; This must be done after reading other parameters, as it
        ; may corrupt them.
        LDR     r1, [r2, #fopendir_dir]
        ADD     r1, r1, #2      ; move past length 2 bytes
        MOV     r14, #0
        STRB    r14, [r1, r5]   ; Stick a zero on the end

        MOV     r2, r1
        ADD     r1, r3, #dirreq_dirname
        BL      strcpy

        BL      Queue_Request_To_Self
        EXIT

; .............................................................................

Filer_CloseDir_Code ALTENTRY

        LDR     wp, [r12]               ; Can't do anything if not started
        LDR     r14, mytaskhandle       ; Don't allow queueing outside desktop
        CMP     r14, #0
        BLLE    UseDesktopError
        EXIT    VS

      [ debugclosedir
        DSTRING r0,"Filer_CloseDir "
      ]
        MOV     r1, r0
        MOV     r2, #0
        BL      Queue_CloseDir_Request_To_Self

        EXIT

Filer_Run_Code  ALTENTRY
        ; Ensure Filer is initialised
        LDR     wp, [r12]               ; Can't do anything if not started
        LDR     r14, mytaskhandle       ; Don't allow queueing outside desktop
        CMP     r14, #0
        BLLE    UseDesktopError
        EXIT    VS
        
        ; Find the object type of the object
        MOV     r1, r0
        MOV     r0, #OSFile_ReadInfo
        SWI     XOS_File                ; r1 preserved
        EXIT    VS

        ; Ensure the object was found
        CMP     r0, #object_nothing
        ADREQ   r0, ErrorBlock_Global_NotFound
        MOVEQ   r4, r1
        BLEQ    LookupError
        EXIT    VS

        ; Get the file type from the info
        BL      FindFileType_FromInfo

        MOV     r4, r1
        MOV     r0, #&81
        MOV     r1, #&FF
        MOV     r2, #&FF
        SWI     XOS_Byte                ; Get SHIFT state (r1=&FF => SHIFT pressed)

        CMP     r1, #&FF
        BNE     %FT35
        CMP     r3, #filetype_directory
        CMPNE   r3, #filetype_application
        LDRNE   r3, =&FFF               ; SHIFTed file => text file
        MOVEQ   r0, r4
        BEQ     %BT02                   ; SHIFTed dir/app => *Filer_OpenDir <dir>
35
        MOV     r8, r3
        MOV     r1, r4

        ; Find size of block required
        BL      strlen
        ADD     r3, r3, #dirreq_size + 1 ; for terminator

        ; Claim a that size chunk
        MOV     r0, #ModHandReason_Claim
        SWI     XOS_Module
        EXIT    VS
        MOV     r3, r2

        ; Copy the filename into the block
        MOV     r2, r1
        ADD     r1, r3, #dirreq_dirname
        BL      strcpy

        STR     r8, [r3, #dirreq_filetype]
        MOV     r9, #drr_run
        BL      Queue_Request_To_Self

        ; If DATAOPEN is bounced then we don't wan't an old window handle hanging around
        MOV     r0, #0
        STR     r0, windowhandle

        EXIT

ErrorBlock_Global_NotFound
        DCD     &D6
        DCB     "NoFile",0
        ALIGN

Filer_Boot_Code ALTENTRY
        MOV     r9, #drr_boot
        LDR     wp, [r12]               ; Can't do anything if not started
        LDR     r14, mytaskhandle       ; Don't allow desktop things outside the desktop
        CMP     r14, #0
        BLLE    UseDesktopError
        EXIT    VS

        MOV     r1, r0
        BL      nul_terminate
        BL      SussPlingApplic_GivenName
        EXIT    VC
        LDR     r2, [r0]
        LDR     r14, =ErrorNumber_WimpBadOp
        TEQ     r2, r14
        BEQ     %FT10
        LDR     r14, =ErrorNumber_WimpNoTasks
        TEQ     r2, r14
        BEQ     %FT10
        ; Any other errors are the caller's problem!
        EXIT

10
        ; Queue the action for when the wimp will let us do
        ; the biz.
        CLRV
        BL      strlen

        ; Add size of header
        ADD     r3, r3, #dirreq_size + 1 ; for terminator

        ; Claim a that size chunk
        MOV     r0, #ModHandReason_Claim
        SWI     XOS_Module
        EXIT    VS
        MOV     r3, r2

        MOV     r2, r1
        ADD     r1, r3, #dirreq_dirname
        BL      strcpy_excludingspaces

        BL      Queue_Request_To_Self
        EXIT

; -----------------------------------------------------------------------
;
; Queue_Request_To_Self
;
; In
;  r3 = filled in request to be queued (RMAlloced)
;  r9 = reason code for request
;
; Out
;  request queued with reason code filled in
;
Queue_Request_To_Self ENTRY

        ; Fill in reason code
        STRB    r9, [r3, #dirreq_reason]

        ; Attach it to the queue
        MOV     r14, #Nowt
        STR     r14, [r3, #dirreq_link]
        LDR     r14, DirRequestEndLink
        STR     r3, [r14]
        ADD     r3, r3, #dirreq_link
        STR     r3, DirRequestEndLink

        ; Poke the application just in case...
        LDR     r3, poll_word
        ORR     r3, r3, #poll_word_command_waiting
        STR     r3, poll_word

        EXIT

; -----------------------------------------------------------------------
;
; Queue_CloseDir_Request_To_Self
;
; In
;  r1 = pointer to space or ctrl/char-terminated directory name
;  r2 = flags:
;       bit     meaning when set
;       0       don't canonicalise before queueing
;       1-31    unused - set to 0
;
; Out
;  VSet and error possible
;  queued request has 'don't canonicalise on reception' flag set
;
Queue_CloseDir_Request_To_Self ENTRY "r0-r5,r9"

        TST     r2, #1
        BNE     %FT10

        ; Canonicalise the path before use
        MOV     r0, #FSControl_CanonicalisePath
        ADR     r2, userdata
        MOV     r3, #0
        MOV     r4, #0
        MOV     r5, #userdata_size
        SWI     XOS_FSControl
      [ version < 145
        BVS     %FT90
      ]

        ADRVC   r1, userdata

10
 [ debugclosedir
        DSTRING r1, "Queue CloseDir on "
 ]
        ; Claim block of required size
        BL      strlen_excludingspaces

        ; Add size of header
        ADD     r3, r3, #dirreq_size + 1 ; for terminator

        ; Claim a that size chunk
        MOV     r0, #ModHandReason_Claim
        SWI     XOS_Module
        BVS     %FT90

        ; Fill the block in
        MOV     r3, r2

        MOV     r2, r1
        ADD     r1, r3, #dirreq_dirname
        BL      strcpy_excludingspaces

        MOV     r0, #1
        STR     r0, [r3, #dirreq_closeflags]

        ; queue it
        MOV     r9, #drr_close
        BL      Queue_Request_To_Self

90
        STRVS   r0, [sp]
        EXIT

        END
