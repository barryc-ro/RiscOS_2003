; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; In    r0 -> oscli command
;       r1 -> title to give popup window if OSCLI does WriteC

; Out   VClear always: errors don't creep out of here

DoOSCLIInBoxLookup ENTRY "r0"

        MOV     r0, r1
        BL      messagetrans_lookup
        B       %FT10

DoOSCLIInBox ALTENTRY

 [ debug
 DSTRING r0, "oscli command ",cc
 DSTRING r1, ", window title "
 ]
        MOV     r0, r1
10
        SWI     XWimp_CommandWindow
        EXITS   VS                      ; Assumes VClear in entry lr. Eat error

        LDR     r0, [sp]
        SWI     XOS_CLI
        BVS     %FT50

        MOV     r0, #0                  ; 'Press SPACE' if command printed 'owt
        SWI     XWimp_CommandWindow
        EXITS                           ; Assumes VClear in entry lr


50      BL      LocalReportError        ; Prints the error; gives 'Press SPACE'

        MOV     r0, #-1                 ; Close command window without prompt
        SWI     XWimp_CommandWindow
        EXITS                           ; Assumes VClear in entry lr

        END
