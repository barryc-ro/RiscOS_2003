; s.Drag
;
; Comes here when a drag ends.
;
;
drag_end
        Push    "LR"

        Debug   sa,"Drag end"

        SWI     XDragASprite_Stop       ; Clear any DragASprite operations

        LDR     r0,DragType
        TEQ     r0,#DragType_SelectDrag
        BEQ     SelectDrag_end
        TEQ     r0,#DragType_AdjustDrag
        BEQ     AdjustDrag_end
        TEQ     r0,#DragType_SelectDragIcon
        BEQ     SelectDragIcon_end
        TEQ     r0,#DragType_AdjustDragIcon
        BEQ     SelectDragIcon_end

        TEQ     r0,#DragType_Iconized
        BEQ     Iconized_DragEnd

        TEQ     r0,#DragType_Save
        BEQ     Save_DragEnd
        Pull    "PC"

AdjustDrag_end                          ; Invert state of icons
        MOV     r3,#is_selected
        MOV     r4,#0
        B       %FT11

SelectDrag_end

        MOV     r3,#is_selected         ; Select icons.
        MOV     r4,#is_selected

11
        ADR     r1,dataarea
        LDMIA   r1,{r6,r7,r8,r9}        ; x0,y0,x1,y1
        CMP     r6,r8
        MOVGT   r14,r6
        MOVGT   r6,r8
        MOVGT   r8,r14
        CMP     r7,r9
        MOVGT   r14,r7
        MOVGT   r7,r9
        MOVGT   r9,r14

        LDR     r0,backdrop_handle
        STR     r0,[r1]
        LDR     r5,Icon_list
01
        CMP     r5,#0
        Pull    "PC",EQ                 ; End of list.

        LDR     r0,[r5,#ic_icon]
        STR     r0,[r1,#4]
        SWI     XWimp_GetIconState
        Pull    "PC",VS

        LDR     r14,[r1,#8]             ; Icon's x0
        CMP     r14,r8
        BGT     %FT10                   ; > x1 !

        LDR     r14,[r1,#16]            ; Icon's x1
        CMP     r14,r6
        BLT     %FT10                   ; < x0 !

        LDR     r14,[r1,#12]            ; Icon's y0
        CMP     r14,r9
        BGT     %FT10                   ; > y1 !

        LDR     r14,[r1,#20]            ; Icon's y1
        CMP     r14,r7
        BLT     %FT10                   ; < y0 !

        LDR     r0,Pinboard_Selected
        LDR     r14,[r1,#24]            ; Get icon flags
        TST     r14,#is_selected
        ADDEQ   r0,r0,#1                ; If it wasn't selected it will be
        CMPNE   r4,#is_selected         ; If it was is this adjust ?
        SUBNE   r0,r0,#1                ; If it is, it won't be.
        STR     r0,Pinboard_Selected
        Debug   pi,"Selected icons = ",r0

        STR     r3,[r1,#8]
        STR     r4,[r1,#12]
        SWI     XWimp_SetIconState
        Pull    "PC",VS

10
        LDR     r5,[r5,#ic_next]
        B       %BT01

SelectDragIcon_end      ROUT
        ; Adjust bbox to same size as original (for DragASprite).
        ADR     r1,DragBBOX
        LDMIA   r1,{r6,r7,r8,r9}
        SUB     r2,r8,r6
        SUB     r3,r9,r7
        ADR     r1,dataarea
        LDMIA   r1,{r6,r7,r8,r9}
        ADD     r8,r6,r2
        SUB     r7,r9,r3

        ADR     r1,PointerInfo
        SWI     XWimp_GetPointerInfo    ; Find out where it ended.
        LDR     r2,[r1,#12]             ; Window
        LDR     r3,[r1,#16]             ; Icon
        ADR     r1,dataarea

        CMP     r2,#-2
        CMPLE   r3,#-1
        BEQ     Run_Selection           ; Dragged to empty part of icon bar, run it !

; Send a Message_FilerSelection

        ADD     r14,r1,#ms_data
        STMIA   r14!,{r6,r7,r8,r9}      ; Drag BBOX
        ADR     r0,DragBBOX
        LDMIA   r0,{r6,r7,r8,r9}        ; Get original box.

        MOV     r4,#grid_x_spacing
        STR     r4,[r14],#4
        MOV     r5,#grid_y_spacing
        ADD     r7,r7,r5
        SUB     r7,r7,#1
        STR     r5,[r14],#4             ; Width & height of icons.

        MOV     r0,#0
        STR     r0,[r14],#4             ; Display mode is always 0 (large icons sort by name)

        DivRem  r10,r6,r4,r0
        STR     r10,[r14],#4            ; Leftmost column
        Debug    pi,"Left: ",r10

        LDR     r10,Screen_y1
        SUB     r9,r10,r9
        DivRem  r10,r9,r5,r0            ; Top row
        STR     r10,[r14],#4
        Debug    pi,"Top: ",r10

        DivRem  r10,r8,r4,r0            ; Rightmost column
        STR     r10,[r14],#4
        Debug    pi,"Right: ",r10

        LDR     r10,Screen_y1
        SUB     r7,r10,r7
        DivRem  r10,r7,r5,r0            ; Bottom row
        STR     r10,[r14],#4
        Debug    pi,"Bottom: ",r10

        MOV     r0,#0
        STR     r0,[r1,#ms_myref]
        STR     r0,[r1,#ms_yourref]
        MOV     r0,#ms_data+11*4
        STR     r0,[r1,#ms_size]
        LDR     r0,=Message_FilerSelection
        STR     r0,[r1,#ms_action]
        MOV     r0,#17
        SWI     XWimp_SendMessage
        Pull    "PC",VS

        LDR     r14,Filer_taskid
        TEQ     r2,r14
        BEQ     SendToFiler

        Debug   pi,"Not filer"

        ; This isn't the filer, simply send data load messages for each icon.
        ; And deselect it.


        LDR     r10,Icon_list
01
        CMP     r10,#0
        Pull    "PC",EQ

        LDR     r3,DragWindow
        ADR     r1,dataarea
        LDR     r0,[r10,#ic_window]      ; Not in drag window.
        TEQ     r0,r3
        BNE     %FT10

        LDR     r0,[r10,#ic_icon]
        STR     r3,[r1]
        STR     r0,[r1,#4]
        SWI     XWimp_GetIconState
        Pull    "PC",VS

        LDR     r0,[r1,#24]
      [ Version >= 31
        TST     r0,#selected
      |
        TST     r0,#is_selected
      ]
        BEQ     %FT10                   ; Not selected

        MOV     r0,#0
        STR     r0,[r1,#8]
      [ Version >= 31
        CMP     r3,#0
        MOVGT   r0,#is_selected
        MOVLT   r0,#selected
      |
        MOV     r0,#is_selected
      ]
        STR     r0,[r1,#12]
        SWI     XWimp_SetIconState      ; Deselect

        CMP     r3,#0
        LDRGE   r0,Pinboard_Selected
        LDRLE   r0,TinyDirs_Selected
        SUB     r0,r0,#1
        STRGE   r0,Pinboard_Selected
        STRLE   r0,TinyDirs_Selected
        Debug   pi,"Selected icons = ",r0

        ADR     r0,PointerInfo
        LDMIA   r0,{r4,r5,r6,r7,r8}     ; x,y,b,w,i
        ADD     r0,r1,#ms_data
        STMIA   r0!,{r7,r8}       ; w,i
        STMIA   r0!,{r4,r5}
        MOV     r4,#1
        STR     r4,[r0],#4              ; estimated size.
        LDR     r4,[r10,#ic_filetype]
        STR     r4,[r0],#4
        MOV     r1,r0
        ADD     r0,r10,#ic_path
        BL      Copy_r0r1
        ADD     r1,r1,#3+1              ; Include null.
        BIC     r1,r1,#3
        MOV     r0,#-1
        STR     r0,[r1],#4
        STR     r0,[r1],#4              ; Column, Row !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

        MOV     r0,r1
        ADR     r1,dataarea
        SUB     r0,r0,r1
        STR     r0,[r1,#ms_size]
        MOV     r0,#Message_DataLoad
        STR     r0,[r1,#ms_action]
        MOV     r0,#0
        STR     r0,[r1,#ms_myref]
        STR     r0,[r1,#ms_yourref]
        MOV     r0,#18
        LDR     r2,[r1,#ms_data]
        LDR     r3,[r1,#ms_data+4]
        SWI     XWimp_SendMessage
        Pull    "PC",VS

10
        LDR     r10,[r10,#ic_next]
        B       %BT01

        Pull    "PC"

SendToFiler     ROUT

; We want to save files to the filer, send
; DataSave to find out the directory.

        Debug   pi,"Send to filer"

        ADR     r1,dataarea
        ADR     r14,PointerInfo
        MOV     r0,#0
        STR     r0,[r1,#ms_myref]
        STR     r0,[r1,#ms_yourref]
        MOV     r0,#Message_DataSave
        STR     r0,[r1,#ms_action]
        LDR     r0,[r14,#12]             ; Window
        STR     r0,[r1,#ms_data]
        LDR     r0,[r14,#16]             ; Icon
        STR     r0,[r1,#ms_data+4]
        LDR     r0,[r14]                 ; X
        STR     r0,[r1,#ms_data+8]
        LDR     r0,[r14,#4]              ; Y
        STR     r0,[r1,#ms_data+12]
        MOV     r0,#1                    ; Estimated size.
        STR     r0,[r1,#ms_data+16]
        MOV     r0,#0                    ; File type
        STR     r0,[r1,#ms_data+20]
        MOV     r0,#65
        STR     r0,[r1,#ms_data+44]      ; Proposed leaf name ("A").

        MOV     r0,#18
        SWI     XWimp_SendMessage

        Pull    "PC"

Run_Selection   ROUT

        LDR     r10,Icon_list
01
        CMP     r10,#0
        Pull    "PC",EQ

        LDR     r14,[r10,#ic_window]
        LDR     r3,DragWindow
        TEQ     r14,r3
        BNE     %FT10

        ADR     r1,dataarea
        LDR     r0,[r10,#ic_icon]
        STR     r3,[r1]
        STR     r0,[r1,#4]
        SWI     XWimp_GetIconState
        Pull    "PC",VS

        LDR     r0,[r1,#24]
      [ Version >= 31
        TST     r0,#selected
      |
        TST     r0,#is_selected
      ]
        BEQ     %FT10                   ; Not selected

        MOV     r0,#0
        STR     r0,[r1,#8]
      [ Version >= 31
        CMP     r3,#0
        MOVGT   r0,#is_selected
        MOVLT   r0,#selected
      |
        MOV     r0,#is_selected
      ]
        STR     r0,[r1,#12]
        SWI     XWimp_SetIconState      ; Deselect

        CMP     r3,#0
        LDRGE   r0,Pinboard_Selected
        LDRLE   r0,TinyDirs_Selected
        SUB     r0,r0,#1
        STRGE   r0,Pinboard_Selected
        STRLE   r0,TinyDirs_Selected
        Debug   pi,"Selected icons = ",r0

        ADR     r1,dataarea
        ADR     r0,Run_command
        BL      Copy_r0r1
        ADD     r0,r10,#ic_path
        BL      Copy_r0r1
        ADR     r0,dataarea
        SWI     XWimp_StartTask
        Pull    "PC",VS

10
        LDR     r10,[r10,#ic_next]
        B       %BT01

Run_command     DCB     "Filer_Run ",0
        ALIGN

        LTORG

        LNK     s.Menu
