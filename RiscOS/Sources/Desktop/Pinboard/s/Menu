; s.Menu
;
; Handle the menus.
;
Menu_Pinboard           *       1
Menu_TinyDirs           *       2
Menu_TinyDirsIcon       *       3


        ^ :INDEX:menu_store, workspace

tinydirs_menu_layout    #       28+24*4
rom_tinydirs_menu
tinydirs_menu_layout    Menu    T1
tm_selectall            Item    M11
tm_clearselection       Item    M12
tm_removeselection      Item    M13
tm_openparent           Item    M14
        DCB 0

        ^ :INDEX:menu_store, workspace

tinydirs_icon_menu_layout    #       28+24*1
rom_tinydirs_icon_menu
tinydirs_icon_menu_layout    Menu    T2
;tim_info                Item    M21, tinydirs_menu_layout
tim_quit                Item    M22, tinydirs_menu_layout
        DCB 0

        ^ :INDEX:menu_store, workspace

pinboard_menu_offset    *       0

 [ show_backdrop_options
pinboard_menu_size      *       28+24*7
 |
pinboard_menu_size      *       28+24*5
 ]

pinboard_menu_layout    #       pinboard_menu_size
rom_pinboard_menu
pinboard_menu_layout    Menu    T3
pm_selection            Item    M31, selection_menu_layout
pm_selectall            Item    M33, selectall_menu_layout
pm_clear_selection      Item    M34
pm_options              Item    M35, options_menu_layout
pm_save                 Item    M36
 [ show_backdrop_options
pm_backdrop             Item    M37, backdrop_menu_layout
pm_nobackdrop           Item    M38
 ]
        DCB 0

backdrop_menu_offset    *       pinboard_menu_offset + pinboard_menu_size
backdrop_menu_size      *       28+24*3
backdrop_menu_layout    #       backdrop_menu_size
rom_backdrop_menu
backdrop_menu_layout    Menu    T4
bm_scaled               Item    M41
bm_tiled                Item    M42
bm_centred              Item    M43
        DCB 0

iconise_menu_offset     *       backdrop_menu_offset + backdrop_menu_size
iconise_menu_size       *       28+24*8
iconise_menu_layout     #       iconise_menu_size
rom_iconise_menu
iconise_menu_layout     Menu    T6
im_atclose              Item    M61
im_to_iconbar           Item    M62
im_to_topleft           Item    M63
im_to_bottomleft        Item    M64
im_to_topright          Item    M65
im_to_bottomright       Item    M66,,"-"
im_stackhorizontally    Item    M67
im_stackvertically      Item    M68
        DCB 0
        
tidy_menu_offset        *       iconise_menu_offset + iconise_menu_size
tidy_menu_size          *       28+24*6
tidy_menu_layout        #       tidy_menu_size
rom_tidy_menu
tidy_menu_layout        Menu    T7
tm_to_topleft           Item    M71
tm_to_bottomleft        Item    M72
tm_to_topright          Item    M73
tm_to_bottomright       Item    M74,,"-"
tm_stackhorizontally    Item    M75
tm_stackvertically      Item    M76
        DCB 0

options_menu_offset     *       tidy_menu_offset + tidy_menu_size
options_menu_size       *       28+24*3
options_menu_layout     #       options_menu_size
rom_options_menu
options_menu_layout     Menu    T8
om_iconisewindows       Item    M81, iconise_menu_layout
om_tidyfiles            Item    M82, tidy_menu_layout
om_gridlock             Item    M83
        DCB 0

selection_menu_offset   *       options_menu_offset + options_menu_size
selection_menu_size     *       28+24*3
selection_menu_layout   #       selection_menu_size
rom_selection_menu
selection_menu_layout   Menu    T9
se_tidy                 Item    M91
se_open                 Item    M92
se_remove               Item    M93
        DCB 0
        
selectall_menu_offset   *       selection_menu_offset + selection_menu_size
selectall_menu_size     *       28+24*3
selectall_menu_layout   #       selectall_menu_size
rom_selectall_menu
selectall_menu_layout   Menu    T10
sa_files                Item    M101
sa_windows              Item    M102
sa_windowsandfiles      Item    M103
        DCB 0

totalmenusize           *       selectall_menu_offset + selectall_menu_size

 ! 0, "Menu data size is ":CC:(:STR:(totalmenusize)):CC:" bytes"

        ALIGN


create_menu          ROUT

; r1 = window
; r2 = icon
; r10 = x
; r11 = y

        Debug   pi,"Create menu w,i",r1,r2

        Push    "r1-r11"
        BL      IntMenuDeleted
        Pull    "r1-r11"

        LDR     r14,backdrop_handle
        TEQ     r1,r14
        BEQ     create_pinboard_menu
        CMP     r1,#-2
        Pull    "PC",NE

 [ iconise_to_iconbar
        ;LDR     lr, Pinboard_options
        ;TST     lr, #PinboardOption_IconiseToIconBar
        ;BEQ     %FT00

        Push    "r6,r7"
        MOV     r6, r2
        BL      find_iconized
        TEQ     r7, #0
        Pull    "r6,r7"
        Pull    "PC",NE
00
 ]

create_iconbar_menu      ROUT

        LDR     r14,TinyDirs_Handle
        CMP     r2,r14
        BEQ     TinyDirsIcon_Menu



        LDR     r14,TinyDirs_Selected
        CMP     r14,#0
        BNE     %FT01

        Debug   pi,"No selected icons, do soft selection"

        STR     r1,soft_selection_window
        STR     r2,soft_selection_icon

        MOV     r14,r1
        ADR     r1,dataarea
        STR     r14,[r1]
        STR     r2,[r1,#4]
        MOV     r14,#is_selected
        STR     r14,[r1,#8]
        STR     r14,[r1,#12]
        SWI     XWimp_SetIconState
        Pull    "PC",VS

        MOV     r14,#1
        STR     r14,TinyDirs_Selected

recreate_iconbar_menu
01
        ADR     r0, message_file_block + 4
        ADRL    r1, rom_tinydirs_menu
        ADRL    r2, tinydirs_menu_layout
        MOV     r3, #menu_space
        SWI     XMessageTrans_MakeMenus
        Pull    "PC",VS

        LDR     r0,TinyDirs_Selected
        CMP     r0,#1                                   ; Is there more than one selected icon ?
                                                        ; Grey out the 'open parent entry'  if so

        LDR     r14,menu_store+28+tm_openparent*24+8          ;
        ORRNE   r14,r14,#is_shaded
        BICEQ   r14,r14,#is_shaded
        STR     r14,menu_store+28+tm_openparent*24+8

        LDR     r14,menu_store+28+tm_clearselection*24+8          ;
        ORRLT   r14,r14,#is_shaded
        BICGE   r14,r14,#is_shaded
        STR     r14,menu_store+28+tm_clearselection*24+8
        LDR     r14,menu_store+28+tm_removeselection*24+8          ;
        ORRLT   r14,r14,#is_shaded
        BICGE   r14,r14,#is_shaded
        STR     r14,menu_store+28+tm_removeselection*24+8
        BLE     %FT01

        ADR     r0,message_file_block+4                 ; Change to remove selection if more than
        ADRL    r1,remove_selection_token               ; one selected icon.
        ADRL    r2,menu_store+400
        MOV     r3,#menu_space-400
        SWI     XMessageTrans_Lookup
        Pull    "PC",VS


        MOV     r1,#12
        ADD     r1, r1, r3, LSL #4
        LDR     r4, menu_store+16
        CMP     r1, r4
        STRGT   r1, menu_store+16

        DebugS  pi,"String is ",r2
        CMP     r2,#0
        STR     r2,menu_store+28+tm_removeselection*24+12
        ADD     r3,r3,#1
        STR     r3,menu_store+28+tm_removeselection*24+20
        MOV     r2,#0
        STR     r2,menu_store+28+tm_removeselection*24+16

        LDR     r14,menu_store+28+tm_removeselection*24+8
        ORR     r14,r14,#if_indirected
        STR     r14,menu_store+28+tm_removeselection*24+8



01
        ADR     r1,menu_store
        SUB     r2,r10,#144
        MOV     r3,#96+4*44
        SWI     XWimp_CreateMenu

        MOV     r14,#Menu_TinyDirs
        STR     r14,CurrentMenu

        Pull    "PC"


TinyDirsIcon_Menu       ROUT

        ADR     r0, message_file_block + 4
        ADRL    r1, rom_tinydirs_icon_menu
        ADRL    r2, tinydirs_icon_menu_layout
        MOV     r3, #menu_space
        SWI     XMessageTrans_MakeMenus
        Pull    "PC",VS

        ;LDR     r14,info_dbox_handle                    ; Point info entry at info box.
        ;STR     r14,menu_store+28+tim_info*24+4

        ADR     r1,menu_store
        SUB     r2,r10,#64
        MOV     r3,#96+1*44
        SWI     XWimp_CreateMenu

        MOV     r14,#Menu_TinyDirsIcon
        STR     r14,CurrentMenu

        Pull    "PC"

create_pinboard_menu ROUT

        Debug   pi,"Create pinboard menu"

        CMP     r2,#-1
        BEQ     %FT01

        Debug   pi,"Menu on an icon"

        MOV     R6,R2
        BL      find_iconized
        CMP     r7,#0
        BEQ     %FT01

        MOV     r0,#&79                 ; If an iconized window check for Shift to produce app. menu.
        MOV     r1,#&80
        SWI     XOS_Byte
        CMP     r1,#&FF
        BEQ     app_menu

01
        LDR     r14,Pinboard_Selected
        CMP     r14,#0
        BNE     %FT01

        Push    "r1,r2"
        BL      FindIcon
        Pull    "r1,r2"
        BNE     %FT01

        Debug   pi,"No selected icons, do soft selection"

        STR     r1,soft_selection_window
        STR     r2,soft_selection_icon

        MOV     r14,r1
        ADR     r1,dataarea
        STR     r14,[r1]
        STR     r2,[r1,#4]
        MOV     r14,#is_selected
        STR     r14,[r1,#8]
        STR     r14,[r1,#12]
        SWI     XWimp_SetIconState
        Pull    "PC",VS

        MOV     r14,#1
        STR     r14,Pinboard_Selected

recreate_pinboard_menu
01
        ADR     r0, message_file_block + 4
        ADRL    r1, rom_backdrop_menu
        ADRL    r2, backdrop_menu_layout
        MOV     r3, #menu_space
        SWI     XMessageTrans_MakeMenus
        Pull    "PC",VS

        ADR     r0, message_file_block + 4
        ADRL    r1, rom_iconise_menu
        ADRL    r2, iconise_menu_layout
        MOV     r3, #menu_space
        SWI     XMessageTrans_MakeMenus
        Pull    "PC",VS

        ADR     r0, message_file_block + 4
        ADRL    r1, rom_tidy_menu
        ADRL    r2, tidy_menu_layout
        MOV     r3, #menu_space
        SWI     XMessageTrans_MakeMenus
        Pull    "PC",VS

        ADR     r0, message_file_block + 4
        ADRL    r1, rom_options_menu
        ADRL    r2, options_menu_layout
        MOV     r3, #menu_space
        SWI     XMessageTrans_MakeMenus
        Pull    "PC",VS

        ADR     r0, message_file_block + 4
        ADRL    r1, rom_selection_menu
        ADRL    r2, selection_menu_layout
        MOV     r3, #menu_space
        SWI     XMessageTrans_MakeMenus
        Pull    "PC",VS

        ADR     r0, message_file_block + 4
        ADRL    r1, rom_selectall_menu
        ADRL    r2, selectall_menu_layout
        MOV     r3, #menu_space
        SWI     XMessageTrans_MakeMenus
        Pull    "PC",VS

        ADR     r0, message_file_block + 4
        ADRL    r1, rom_pinboard_menu
        ADR     r2, menu_store
        MOV     r3, #menu_space
        SWI     XMessageTrans_MakeMenus
        Pull    "PC",VS

        ;LDR     r14,info_dbox_handle                    ; Point info entry at info box.
        ;STR     r14,menu_store+28+pm_info*24+4

        LDR     r14,saveas_handle                       ; Point save entry at save box.
        STR     r14,menu_store+28+pm_save*24+4

        LDR     r14,Pinboard_Icons
        LDR     r0, Window_Icons
        ADD     r14, r14, r0
        TEQ     r14,#0                                  ; Are there any icons on the pinboard ?

       [ Version < 68
        LDR     r14,menu_store+28+pm_tidy*24+8          ; Shade 'Tidy' if no icons.
        ORREQ   r14,r14,#is_shaded
        BICNE   r14,r14,#is_shaded
        STR     r14,menu_store+28+pm_tidy*24+8
       ]

        LDR     r14, menu_store+28+pm_selectall*24+8
        ORREQ   r14, r14, #is_shaded
        BICNE   r14, r14, #is_shaded
        STR     r14, menu_store+28+pm_selectall*24+8

        ;LDR     r14,menu_store+28+pm_select_all*24+8    ; Shade 'Select all' if no icons.
        ;ORREQ   r14,r14,#is_shaded
        ;BICNE   r14,r14,#is_shaded
        ;STR     r14,menu_store+28+pm_select_all*24+8

        LDR     r0,Pinboard_Selected
        LDR     r14, Windows_Selected
        ADD     r0, r0, r14
        TEQ     r0,#0                                   ; Are there any selected icons on the pinboard ?
        
        LDR     r14, menu_store+28+pm_selection*24+8
        ORREQ   r14, r14, #is_shaded
        BICNE   r14, r14, #is_shaded
        STR     r14, menu_store+28+pm_selection*24+8

        LDR     r14,menu_store+28+pm_clear_selection*24+8       ; Shade 'Clear Selection' if no selected icons.
        ORREQ   r14,r14,#is_shaded
        BICNE   r14,r14,#is_shaded
        STR     r14,menu_store+28+pm_clear_selection*24+8

        ;LDR     r14,menu_store+28+pm_remove*24+8        ; Shade 'Remove icon' if no selected icons.
        ;ORREQ   r14,r14,#is_shaded
        ;BICNE   r14,r14,#is_shaded
        ;STR     r14,menu_store+28+pm_remove*24+8

        CMP     r0,#1                                   ; Is there more than one selected icon ?
        BLE     %FT01

        ;ADR     r0,message_file_block+4                 ; Change to remove selection if more than
        ;ADR     r1,remove_selection_token               ; one selected icon.
        ;ADRL    r2,menu_store+400
        ;MOV     r3,#menu_space-400
        ;ADRL    r2,menu_store+1024-112
        ;MOV     r3,#112
        ;SWI     XMessageTrans_Lookup
        ;Pull    "PC",VS


        ;MOV     r1,#12
        ;ADD     r1, r1, r3, LSL #4
        ;LDR     r4, menu_store+16
        ;CMP     r1, r4
        ;STRGT   r1, menu_store+16

        ;DebugS  pi,"String is ",r2
        ;CMP     r2,#0
        ;STR     r2,menu_store+28+pm_remove*24+12
        ;ADD     r3,r3,#1
        ;STR     r3,menu_store+28+pm_remove*24+20
        ;MOV     r2,#0
        ;STR     r2,menu_store+28+pm_remove*24+16

        ;LDR     r14,menu_store+28+pm_remove*24+8
        ;ORR     r14,r14,#if_indirected
        ;STR     r14,menu_store+28+pm_remove*24+8


01
 [ show_backdrop_options
        LDR     r14,menu_store+28+pm_backdrop*24+8      ; Shade 'Make Backdrop' if not 1 icon selected.
        ORRNE   r14,r14,#is_shaded
        BICEQ   r14,r14,#is_shaded
        STR     r14,menu_store+28+pm_backdrop*24+8

        BNE     %FT03

        LDR     r0,backdrop_handle
        ADR     r1,dataarea
        MOV     r2,#is_selected
        MOV     r3,#is_selected
        SWI     XWimp_WhichIcon
        Pull    "PC",VS


        LDR     r2,[r1]
        LDR     r1,backdrop_handle
        Debug   pi,"Find icon ",r1,r2
        BL      FindIcon                                ; r2 -> Icon block or 0 if not found.
        BNE     %FT01

        Debug   pi,"Icon found."

        LDR     r2,[r2,#ic_filetype]
        LDR     r14,=&ff9
        TEQ     r2,r14
        BEQ     %FT03
        
        LDR     r14, =&C85
        TEQ     r2, r14
        BEQ     %FT03

01
        LDR     r14,menu_store+28+pm_backdrop*24+8      ; Shade 'Make Backdrop' if selected icon is not a sprite.
        ORR     r14,r14,#is_shaded
        STR     r14,menu_store+28+pm_backdrop*24+8

03
        LDR     r0,backdrop_options
        TST     r0,#bd_OptionActive
        LDR     r14,menu_store+28+pm_nobackdrop*24+8    ; Shade 'Remove backdrop' if no backdrop.
        ORREQ   r14,r14,#is_shaded
        BICNE   r14,r14,#is_shaded
        STR     r14,menu_store+28+pm_nobackdrop*24+8
 ]

        ; Shade Selection->Remove if any windows are selected
        LDR     r0, Windows_Selected
        LDR     r14, menu_store + selection_menu_offset + 28 + se_remove*24+8
        CMP     r0, #0
        ORRNE   r14, r14, #is_shaded
        BICEQ   r14, r14, #is_shaded
        STR     r14, menu_store + selection_menu_offset + 28 + se_remove*24+8

        ; Shade 'Select all->Windows' if no windows on backdrop
        LDR     r0, Window_Icons
        LDR     r14, menu_store + selectall_menu_offset + 28 + sa_windows*24+8
        CMP     r0, #0
        ORREQ   r14, r14, #is_shaded
        BICNE   r14, r14, #is_shaded
        STR     r14, menu_store + selectall_menu_offset + 28 + sa_windows*24+8

        ; Shade 'Select all->Files' if no files on backdrop
        LDR     r0, Pinboard_Icons
        LDR     r14, menu_store + selectall_menu_offset + 28 + sa_files*24+8
        CMP     r0, #0
        ORREQ   r14, r14, #is_shaded
        BICNE   r14, r14, #is_shaded
        STR     r14, menu_store + selectall_menu_offset + 28 + sa_files*24+8

        ; Shade 'Select all->Windows and files' if there's either no files or no windows
        LDR     r0, Pinboard_Icons
        CMP     r0, #0
        LDRNE   r0, Window_Icons
        LDR     r14, menu_store + selectall_menu_offset + 28 + sa_windowsandfiles*24+8
        CMP     r0, #0
        ORREQ   r14, r14, #is_shaded
        BICNE   r14, r14, #is_shaded
        STR     r14, menu_store + selectall_menu_offset + 28 + sa_windowsandfiles*24+8
        
        ; Tick 'Grid' if grid enabled.
        LDR     r0,Pinboard_options
        TST     r0,#PinboardOption_Grid
        LDR     r14, menu_store+options_menu_offset+28+om_gridlock*24+0
        ORRNE   r14,r14,#1
        BICEQ   r14,r14,#1
        STR     r14,menu_store+options_menu_offset+28+om_gridlock*24+0

        ; Now tick the options on the Iconise to... menu
        ; Work out where the menu data is - there's two menu's before it.
        ADR     r2, menu_store
        ADD     r2, r2, #iconise_menu_offset+28
        
        ; Get rid of all current ticks on iconise window menu
        MOV     r3, #0
10
        LDR     r14, [r2]
        BIC     r14, r14, #1
        STR     r14, [r2]
        ADD     r3, r3, #1
        ADD     r2, r2, #24
        CMP     r3, #8
        BLT     %BT10
        SUB     r2, r2, #(8*24)

        ; Now decide which options to tick
        LDR     r0, Pinboard_options
        TST     r0, #PinboardOption_UseWinToCorner
        BEQ     %FT20

        ; Calculate which corner we're in (0-3)
        MOV     r1, #0
        TST     r0, #PinboardOption_WinToCornerLR
        ADDNE   r1, r1, #2
        TST     r0, #PinboardOption_WinToCornerTB
        ADDNE   r1, r1, #1
        ADD     r1, r1, #2
        
        ; Work our which menu option to tick
        MOV     r0, #24
        MUL     r3, r1, r0
        LDR     r14, [r2, r3]
        ORR     r14, r14, #1
        STR     r14, [r2, r3]
      
        ; Work out which of horizontal or vertical to tick
        LDR     r0, Pinboard_options
        TST     r0, #PinboardOption_WinToCornerHV
        MOVNE   r1, #7
        MOVEQ   r1, #6
        MOV     r0, #24
        MUL     r3, r1, r0
        LDR     r14, [r2, r3]
        ORR     r14, r14, #1
        STR     r14, [r2, r3]
        B       %FT30

20      ; Tick Iconise to iconbar?
        TST     r0, #PinboardOption_IconiseToIconBar
        ADDNE   r2, r2, #24
        LDR     r14, [r2]
        ORR     r14, r14, #1
        STR     r14, [r2]
30
        ; Now tick the options on the Tidy to... menu
        ; Work out where the menu data is - there's two menu's before it.
        ADR     r2, menu_store
        ADD     r2, r2, #tidy_menu_offset + 28

        ; Get rid of all current ticks on iconise window menu
        LDR     r0, Pinboard_options
        MOV     r3, #0
40
        LDR     r14, [r2]
        BIC     r14, r14, #1
        STR     r14, [r2]
        ADD     r3, r3, #1
        ADD     r2, r2, #24
        CMP     r3, #6
        BLT     %BT40
        SUB     r2, r2, #(6*24)

        ; Now decide which options to tick
        ; Calculate which corner we're in (0-3)
        MOV     r1, #0
        TST     r0, #PinboardOption_TidyToCornerLR
        ADDNE   r1, r1, #2
        TST     r0, #PinboardOption_TidyToCornerTB
        ADDNE   r1, r1, #1
        
        ; Work our which menu option to tick
        MOV     r0, #24
        MUL     r3, r1, r0
        LDR     r14, [r2, r3]
        ORR     r14, r14, #1
        STR     r14, [r2, r3]

        ; Work out which of horizontal or vertical to tick
        LDR     r0, Pinboard_options
        TST     r0, #PinboardOption_TidyToCornerHV
        MOVNE   r1, #5
        MOVEQ   r1, #4
        MOV     r0, #24
        MUL     r3, r1, r0
        LDR     r14, [r2, r3]
        ORR     r14, r14, #1
        STR     r14, [r2, r3]

        ADR     r1,menu_store
        SUBS    r2,r10,#64
        MOVMI   r2,#0
        MOV     r3,r11
        SWI     XWimp_CreateMenu

        MOV     r14,#Menu_Pinboard
        STR     r14,CurrentMenu

        Pull    "PC"

; ----------------------------------------------------------------------------------------------------------------------
; app_menu, send menu message to application.
;
app_menu
        ADR     r1,dataarea
        MOV     r2,r10
        MOV     r3,r11
        MOV     r4,#2
        LDR     r5,[r7,#w_window_handle]
        MOV     r6,#-1
        STMIA   r1,{r2-r6}
        MOV     r0,#6      ; fake mouse event
        LDR     r2,[r7,#w_task]
        SWI     XWimp_SendMessage

        Pull    "PC"

remove_selection_token  DCB     "M34s",0
        ALIGN

        LNK     s.MenuSelect
