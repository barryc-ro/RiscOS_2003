; s.Mouse
;
; Handle mouse clicks
;

mouse_click     ROUT

        Push    "LR"

        LDMIA   r1!,{r10,r11}    ; x,y
        LDMIA   r1,{r0-r2}

; r0 = Buttons
; r1 = Window
; r2 = Icon.
        Debug   pi,"Mouse click ",r0,r1,r2
        LDR     r14,saveas_handle
        CMP     r1,r14
        BEQ     save_click
        Debug   pi,"Not saveas"
        CMP     r1,#-2
        LDRNE   r14,backdrop_handle
        CMPNE   r1,r14
        Pull    "PC",NE

        Debug   pi,"Backdrop click ",r0

        TST     r0,#2                   ; Is it a menu request?
        BNE     create_menu

        CMP     r1,#-2
        LDREQ   r14,TinyDirs_Handle
        TEQ     r2,r14
        Pull    "PC",EQ

        CMP     r0,#&400
        BEQ     Select_Click
        CMP     r0,#&100
        BEQ     Adjust_Click
        CMP     r0,#&40
        BEQ     Select_Drag
        CMP     r0,#&10
        BEQ     Adjust_Drag
        CMP     r0,#&4
        BEQ     SelectDoubleClick
        CMP     r0,#&1
        BEQ     AdjustDoubleClick

        Pull    "PC"

Select_Click    ROUT

        Debug   pi,"Select click",r1,r2

        CMP     r2,#-1
        BNE     %FT01

        MOV     r0,r1
        BL      DeselectIcons
        Pull    "PC"
01
        Push    "r1,r2"
        BL      FindIcon
 [ iconise_to_iconbar
        BEQ     %FT02
        Pull    "r1,r2"
        LDR     lr, Pinboard_options
        TST     lr, #PinboardOption_IconiseToIconBar
        BNE     IconizedReopen
        Pull    "pc"
02
 |
        Pull    "r1,r2,pc",NE
 ]
        LDMIA   sp,{r1,r2}

        MOV     r0,r1
        MOV     r3,r0

        ADR     r1,dataarea
        STR     r0,[r1]
        STR     r2,[r1,#4]
        SWI     XWimp_GetIconState
        Pull    "r1,r2,PC",VS

        Debug   pi,"Got icon state"

        ADR     r1,dataarea
        LDR     r14,[r1,#24]            ; Get icon flags
        TST     r14,#selected
        Pull    "r1,r2,PC",NE           ; Ignore if already selected.

        Debug   pi,"Not already selected"

        MOV     r0,r3
        BL      DeselectIcons

        Debug   pi,"Icons deselected ",r3

        ADR     r1,dataarea
        STR     r3,[r1]
        STR     r2,[r1,#4]
        MOV     r0,#is_selected
        STR     r0,[r1,#8]
        STR     r0,[r1,#12]
        SWI     XWimp_SetIconState
        Pull    "r1,r2,PC",VS

        MOV     r1,#1
        CMP     r3,#0
        STRGE   r1,Pinboard_Selected
        STRLT   r1,TinyDirs_Selected
        Debug   pi,"selected icons = ",r1

        Pull    "r1,r2,PC"

Adjust_Click    ROUT

        CMP     r2,#-1
        Pull    "PC",EQ

        Push    "r1,r2"
        BL      FindIcon
        Pull    "r1,r2"
        Pull    "PC",NE                 ; An iconized icon.

        MOV     r3,r1
        ADR     r1,dataarea
        STR     r3,[r1]
        STR     r2,[r1,#4]
        SWI     XWimp_GetIconState
        Pull    "PC",VS

        LDR     r14,[r1,#24]            ; Get icon flags
        TST     r14,#selected
        MOVEQ   r2,#1
        MOVNE   r2,#-1

        CMP     r3,#0
        LDRGE   r0,Pinboard_Selected
        LDRLT   r0,TinyDirs_Selected
        ADD     r0,r0,r2
        STRGE   r0,Pinboard_Selected
        STRLT   r0,TinyDirs_Selected
        Debug   pi,"selected icons = ",r0

        MOVGE   r0,#is_selected         ; Toggle icon's state.
        MOVLT   r0,#selected
        STR     r0,[r1,#8]
        MOV     r0,#0
        STR     r0,[r1,#12]
        SWI     XWimp_SetIconState

        Pull    "PC"

AdjustDoubleClick       ROUT

        CMP     r2,#-1
        Pull    "PC",EQ

        Push    "r1,r2"

        BL      FindIcon                ; r2 -> icon block
        Pull    "r1,r2,PC",NE           ; Not found (Iconized window).

        MOV     r0,#BufferReason_Remove
        ADD     r2,r2,#ic_path
        BL      strlen
        DebugS  pi,"Path = ",r2
        Debug   pi,"Action = ",r0
        Debug   pi,"Window = ",r1
        Debug   pi,"length = ",r3
        BL      BufferIcon
        SUB     r2,r2,#ic_path
        B       %FT01

SelectDoubleClick

        CMP     r2,#-1
        Pull    "PC",EQ

        Push    "r1,r2"

        BL      FindIcon                ; r2 -> icon block
        Pull    "r1,r2",NE              ; Not found (Iconized window).
        BNE     IconizedReopen

01
        ADR     r0,FilerRun_command
        ADR     r1,dataarea
        BL      Copy_r0r1
        ADD     r0,r2,#ic_path
        BL      Copy_r0r1
        ADR     r0,dataarea
        DebugS  pi,"Command: ",r0
        SWI     XOS_CLI
        Pull    "r2,r3"
        Pull    "PC",VS

        ADR     r1,dataarea
        STR     r2,[r1]
        STR     r3,[r1,#4]
        MOV     r0,#0
        STR     r0,[r1,#8]
        CMP     r2,#0
        MOVLT   r0,#selected
        MOVGT   r0,#is_selected
        STR     r0,[r1,#12]
        SWI     XWimp_SetIconState
        Pull    "PC",VS

        CMP     r2,#0
        LDRGE   r0,Pinboard_Selected
        LDRLT   r0,TinyDirs_Selected
        SUB     r0,r0,#1
        STRGE   r0,Pinboard_Selected
        STRLT   r0,TinyDirs_Selected
        Debug   pi,"selected icons = ",r0

        Pull    "PC"

FilerRun_command        DCB     "Filer_Run ",0
        ALIGN

Adjust_Drag     ROUT

        CMP     r2,#-1
        BNE     Adjust_DragIcon

        MOV     r0,#DragType_AdjustDrag
        STR     r0,DragType

        B       %FT01

Select_Drag

        CMP     r2,#-1
        BNE     Select_DragIcon

        MOV     r0,#DragType_SelectDrag
        STR     r0,DragType

01
        MOV     r0,r1
        ADR     r1,dataarea
        LDR     r14,[r1]
        STR     r14,[r1,#8]             ; Initial x0
        STR     r14,[r1,#16]            ; Initial x1
        LDR     r14,[r1,#4]
        STR     r14,[r1,#12]            ; Initial y0
        STR     r14,[r1,#20]            ; Initial y1
        LDR     r14,icon_bar_height     ; Parent y0
        STR     r14,[r1,#28]
        MOV     r14,#6                  ; Drag type
02
        STR     r14,[r1,#4]
        MOV     r14,#0
        STR     r14,[r1,#24]            ; Parent x0
        LDR     r14,Screen_x1           ; Parent x1
        STR     r14,[r1,#32]
        LDR     r14,Screen_y1           ; Parent y1
        STR     r14,[r1,#36]
        SWI     XWimp_DragBox

        Pull    "PC"


Adjust_DragIcon
        Push    "r0-r2"
        BL      FindIcon
        STREQ    r2,[sp,#8]

        Pull    "r0-r2"
        Pull    "PC",NE                 ; Iconized window !


        MOV     r0,#DragType_AdjustDragIcon
        STR     r0,DragType

        MOV     r3,r1
        ADR     r1,dataarea
        STR     r3,[r1]
        STR     r2,[r1,#4]
        SWI     XWimp_GetIconState
        Pull    "PC",VS

        LDR     r14,[r1,#24]            ; Get icon flags
        TST     r14,#selected
        MOVNE   r1,r3
        BNE     %FT10                   ; Ignore if already selected.

        STR     r3,[r1]
        STR     r2,[r1,#4]
        MOV     r0,#is_selected
        STR     r0,[r1,#8]
        STR     r0,[r1,#12]
        SWI     XWimp_SetIconState
        Pull    "PC",VS

        CMP     r3,#0
        LDRGE   r1,Pinboard_Selected
        LDRLT   r1,TinyDirs_Selected
        ADD     r1,r1,#1
        STRGE   r1,Pinboard_Selected
        STRLT   r1,TinyDirs_Selected
        Debug   pi,"selected icons = ",r1
        MOV     r1,r3
        B       %FT10


Select_DragIcon

        LDR     r14,saveas_handle
        TEQ     r1,r14
        BEQ     save_drag

        Push    "r0-r2"
        BL      FindIcon                ; r2 -> icon block if found
        Debug   tmp,"r2 (icon blk ptr) = ",r2
        STREQ    r2,[sp,#8]
        Pull    "r0-r2"
 [ iconise_to_iconbar
        BEQ     %FT09
        LDR     lr, Pinboard_options
        TST     lr, #PinboardOption_IconiseToIconBar
        BEQ     Iconized_Drag
        Pull    "pc"
09
 |
        BNE     Iconized_Drag
 ]
        MOV     r0,#DragType_SelectDragIcon
        STR     r0,DragType

10
        MOV     r0,r1
        STR     r0,DragWindow

; Check for DragASprite
        Push    "r0-r2"
        MOV     r0,#ReadCMOS
        MOV     r1,#FileSwitchCMOS
        SWI     XOS_Byte
        MOVVS   r3,#0
        ANDVC   r3,r2,#DragASpriteCMOSBit
        Pull    "r0-r2"

; If using DragASprite then FindBBOX will set up the drag for us.
        Debug   tmp,"r2 (icon blk ptr) = ",r2
        Debug   tmp,"r3 (DAS) = ",r3
        BL      FindBBOX

      [ Version >= 31
        CMP     r0,#0
        ADDLT   r7,r7,#24
        ADDLT   r9,r9,#24
      ]
        Debug   tmp,"BBOX is ",r6,r7,r8,r9

        SUB     r0,r8,r6
        CMP     r0,#grid_x_spacing
        RSBLT   r0,r0,#grid_x_spacing
        SUBLT   r6,r6,r0,LSR #1
        ADDLT   r8,r8,r0,LSR #1
        SUB     r0,r9,r7
        CMP     r0,#grid_y_spacing
        RSBLT   r0,r0,#grid_y_spacing
        SUBLT   r7,r7,r0,LSR #1
        ADDLT   r9,r9,r0,LSR #1

        Debug   tmp,"adjusted BBOX is ",r6,r7,r8,r9
        ADR     r14,DragBBOX
        STMIA   r14,{r6,r7,r8,r9}

        CMP     r3,#0
        Pull    "pc",NE         ; Don't do Wimp_DragBox if using DragASprite

        ADRL    r1,dataarea+8
        STMIA   r1,{r6,r7,r8,r9}
        SUB     r1,r1,#8

        SUB     r6,r6,r10
        STR     r6,[r1,#24]             ; Parent x0

        SUB     r7,r7,r11
        STR     r7,[r1,#28]             ; Parent y0

        LDR     r14,Screen_x1
        SUB     r8,r8,r10
        ADD     r14,r14,r8
        STR     r14,[r1,#32]            ; Parent x1

        LDR     r14,Screen_y1
        SUB     r9,r9,r11
        ADD     r14,r14,r9
        STR     r14,[r1,#36]

        MOV     r14,#5
        STR     r14,[r1,#4]
        SWI     XWimp_DragBox

        Pull    "PC"

        LNK     s.Drag
