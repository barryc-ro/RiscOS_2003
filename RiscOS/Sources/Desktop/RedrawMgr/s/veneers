;Veneers for C functions which are used with the OS

XOS_WriteC	*	&20000
XOS_ServiceCall	*	&20030
XOS_WriteN	*	&20046
XOS_WriteI	*	&20100

Service_RedrawManagerInstalled	*	&A5
Service_RedrawManagerDying	*	&A6

; Keep in line with CMHG file
	GBLA	Version
Version	SETA	5

        GBLL Debug
Debug   SETL {FALSE}

 [ Debug
        IMPORT warning
        IMPORT _sprintf
 ]

        MACRO
$l      StartVeneer
$l
        STMFD   sp!,{r0-r12,lr}
        [ Debug
        MOV     v3,r14
        ]

        MOV     sl,sp, LSR #20
        MOV     sl,sl, LSL #20                  ; sl -> bottom of supervisor stack

        LDMIA   sl,{v1-v2}                      ; v1,v2 preserved by apcs so no need to stack

        LDR     r12,[r12]
        LDMIB   r12,{r0-r1}                     ; this modules reloc offsets
        STMIA   sl,{r0-r1}

        ADD     sl,sl,#540
        MOV     r0,sp                           ; parameter is _kernel_swi_regs *
	MOV	fp,#0
        [ Debug
        STMFD   sp!,{v3}
        ]
        MEND

        MACRO
$l      EndVeneer
$l
        [ Debug
        LDMFD   sp!,{r0}
        TEQ     r0,v3
        MOVNE   r1,v3
        MOVNE   v3,r0
        BLNE    warning

        LDR     r2,[sp,#13*4]
        SUB     sp,sp,#256
        MOV     r0,sp
        ADRL    r1,leaving_str
        BL      _sprintf
        MOV     r0,sp
        BL      tube_writes
        ADD     sp,sp,#256
        ]
        SUB     sl,sl,#540
        STMIA   sl,{v1-v2}                      ; restore old reloc offsets
        LDMFD   sp!,{r0-r12,pc}^
        MEND

        MACRO
$l      Veneer $imp,$exp
$l
        IMPORT $imp
        EXPORT $exp
$exp
        StartVeneer
        BL      $imp
        EndVeneer
        MEND

        AREA    |Asm$Code|, CODE, READONLY

	Veneer post_rect_filter,register_post_rect
	Veneer post_icon_filter,register_post_icon

	[ Debug
        EXPORT __sl
__sl
        MOV     r0,sl
        MOVS    pc,lr

        EXPORT tube_writec
tube_writec
        MOV     ip,lr
        SWI     &60102
        MOVS    pc,ip

        EXPORT tube_writes
tube_writes
        MOV     r2,lr
        MOV     r1,r0
00
        LDRB    r0,[r1],#1
        TEQ     r0,#0
        BLNE    tube_writec
        BNE     %BT00
        MOV     r0,#13
        BL      tube_writec
        MOV     r0,#10
        BL      tube_writec
        MOVS    pc,r2

	]

        EXPORT register_cb
register_cb
        STMFD   sp!,{r0-r1,r14}
	MOV	R0,#Version
	MOV	R1,#Service_RedrawManagerInstalled
        SWI     XOS_ServiceCall
	[ Debug
        ADR     r0,start_string
        BL      tube_writes
	]
        LDMFD   sp!,{r0-r1,pc}^

	[ Debug
start_string
        DCB     "Sent service call",0
	]

	EXPORT	clipping_rectangle
clipping_rectangle
	MOV	r2,lr
	MOV	r1,r0
	SWI	XOS_WriteI + 24
	LDRVC	r0,[r1,#0]
	SWIVC	XOS_WriteC
	MOVVC	r0,r0,LSR #8
	SWIVC	XOS_WriteC
	LDRVC	r0,[r1,#4]
	SWIVC	XOS_WriteC
	MOVVC	r0,r0,LSR #8
	SWIVC	XOS_WriteC
	LDRVC	r0,[r1,#8]
	SWIVC	XOS_WriteC
	MOVVC	r0,r0,LSR #8
	SWIVC	XOS_WriteC
	LDRVC	r0,[r1,#12]
	SWIVC	XOS_WriteC
	MOVVC	r0,r0,LSR #8
	SWIVC	XOS_WriteC
	MOVS	pc,r2

        END

