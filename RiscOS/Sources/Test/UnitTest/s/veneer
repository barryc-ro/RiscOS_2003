; > veneer.s
;
;       Veneer for events and vectors.
;

        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:ModHand
        GET     Hdr:APCS.<APCS>


; Explicit imports/exports.
;
        IMPORT  |_Lib$Reloc$Off$DP|

; Macro for defining input veneers.
;
        MACRO
$label  InputVeneer $function
        EXPORT  $label
        IMPORT  $function
$label.Indirect
        DCD     $function
$label
        FunctionEntry "r10-r12"
        LDR     r11, $label.Indirect
        B       CallC
        MEND


        AREA    |veneer$$code|,REL,CODE,READONLY

veneer_storedinfo_callback      InputVeneer     storedinfo_callback
veneer_storedinfo_transient     InputVeneer     storedinfo_transient

CallC
; In:   r11->routine to call
;       r12->private word
;       registers "r10-r12,lr" stacked

        SavePSR r10
        TST     r10, #3
        SWIEQ   XOS_EnterOS             ; If in user mode then we need to call SWI (shouldn't happen often).
        WritePSRc SVC_mode,r12          ; If in IRQ mode then switch to SVC mode.
        NOP
10
        Push    "r0-r10,lr"             ; Save regs r0-r9 (for call) + old mode in r10 + SVC_lr
        MOV     r10, r13, LSR #20
        MOV     r10, r10, ASL #20
        LDMIA   r10, {r4,r5}            ; Save old relocation modifiers.
        MOV     r9, r11
        MOV     r1, r12                 ; Pass private word in r1.
        LDR     r12, [r12]              ; Set up our relocation modifiers.
        LDMIB   r12, {r11,r12}
        STMIA   r10, {r11,r12}
        ; This is equivalent of 'ADD r10, r10, #0' + |_Lib$Reloc$Off$DP|
        DCD     |_Lib$Reloc$Off$DP| + &E28AA000
        MOV     r11, #0                 ; Halt C backtrace here!

        MOV     r0, sp                  ; Parameter is _kernel_swi_regs *
        MOV     lr, pc                  ; Branch to the code.
        MOV     pc, r9

        ; This is equivalent of 'SUB r10, r10, #0' + |_Lib$Reloc$Off$DP|
        DCD     |_Lib$Reloc$Off$DP| + &E24AA000
        STMIA   r10, {r4,r5}            ; Restore old relocation modifiers.
        Pull    "r0-r10,lr"             ; Get output registers + old mode in r10 + SVC_lr

        RestPSR r10,,cf                 ; Restore old processor mode.
        NOP
        Return  "r10-r12"               ; Return to caller.


        END
