
; this will compile to check the hardware of the STB to determine
; if it is PAL or NTSC

XMacrovision_PlatformFeatures	*	&6D543

	AREA	CheckDATA, DATA

PAL_STRING =	"PAL", 0
	ALIGN

NTSC_STRING = 	"NTSC", 0
	ALIGN

TV_TYPE =	"TV$Type", 0
	ALIGN

EngMenu =	"EngMenu$Start", 0
	ALIGN

; Default EngMenu launch keys are "Return". Already EORed with
; &80 for the keyboard scanning OS_Byte...
Default	DCB	0xC9
	ALIGN

	AREA	CheckCODE, CODE

        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System

	; Load the key codes to scan for from NVRAM...
	MOV	R0, #161
	MOV	R1, #&110
	SWI	XOS_Byte
	MOVVC	R3, R2
	ADDVC	R1, R1, #1
	SWIVC	XOS_Byte
	MOVVC	R4, R2
	ADDVC	R1, R1, #1
	SWIVC	XOS_Byte
	MOVVC	R5, R2
	ADDVC	R1, R1, #1
	SWIVC	XOS_Byte
	MOVVC	R6, R2
	MOVVS	PC, LR
	; If the NVRAM word was zero-initialised, use the default keys...
	TEQ	R3, #0
	TEQEQ	R4, #0
	TEQEQ	R5, #0
	TEQEQ	R6, #0
	LDREQB	R3, Default
	MOVEQ	R4, R3
	MOVEQ	R5, R3
	MOVEQ	R6, R3
	; Check for the (maximum) four keys being pressed...
	MOV	R0, #121
	MOV	R1, R3
	SWI	XOS_Byte
	MOVVS	PC, LR
	TEQ	R1, #&FF
	MOVNE	PC, LR
	MOV	R1, R4
	SWI	XOS_Byte
	MOVVS	PC, LR
	TEQ	R1, #&FF
	MOVNE	PC, LR
	MOV	R1, R5
	SWI	XOS_Byte
	MOVVS	PC, LR
	TEQ	R1, #&FF
	MOVNE	PC, LR
	MOV	R1, R6
	SWI	XOS_Byte
	MOVVS	PC, LR
	TEQ	R1, #&FF
	MOVNE	PC, LR
	; Determine what the Pseudo system is PAL or NTSC

	; macrovision call to determine the modulator type, X bit set
	MOV	R0, #3
	SWI	XMacrovision_PlatformFeatures	; if no error has occurred compare modulator
	LDRVC	R0, [R1,#8]
	TEQVC	R0, #2	; determine if it is TAMC-H007F (NTSC) else (PAL)
	BVC	%FT30	; and go straight to setting the env. var.
	CMP	PC,LR
	BEQ	%FT30

	; failing to get the macrovision call use TV$Type
	ADR	R0, TV_TYPE
	MOV	R1, #0
	MVN	R2, #0		; determine what it is by its length
	MOV	R3, #0
	MOV	R4, #0
	SWI	XOS_ReadVarVal
	CMP	R2, #:NOT:4	; compare it to not 4

30	; Set the Variable to NTSC
	ADREQ	R1, NTSC_STRING
	MOVEQ	R2, #4
	; Set the Variable to PAL
	ADRNE	R1, PAL_STRING
	MOVNE	R2, #3
	; set EngMenu$Start
	ADR	R0, EngMenu
	MOV	R3, #0
	MOV	R4, #0
	SWI	XOS_SetVarVal

	; get out of jail free
	CLRV
	MOV	PC, R14

	END
