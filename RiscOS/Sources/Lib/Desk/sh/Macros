;
;   ####	     #	  #	# #
;   #	#	     #	  #	  #	     The FreeWare C library for
;   #	#  ##	###  #  # #	# ###		  RISC OS machines
;   #	# #  # #     # #  #	# #  #	 ___________________________________
;   #	# ####  ###  ##	  #	# #  #
;   #	# #	   # # #  #	# #  #	  Please refer to the accompanying
;   ####   ### ####  #  # ##### # ###	 documentation for conditions of use
;   ________________________________________________________________________
;
;   File:    DeskLib:sh.Macros
;   Author:  Probably Jason Williams
;   Version: 1.00 (01 Nov 1995)
;   Purpose: Standard macros for assembler code



;============================================================================
;
;  Macros
;
;============================================================================

	MACRO
	PREAMBLE
	AREA |D$$code|, CODE, READONLY
	MEND

	MACRO
	STARTCODE $name
	EXPORT $name
$name
	MEND

	MACRO
	Desk_Error2_CheckV
	IMPORT	Desk_Error2_CheckOS
	BLVS	Desk_Error2_CheckOS
	MEND

        MACRO
        FunctionEntry $SaveList, $MakeFrame
        LCLS    Temps
        LCLS    TempsC
Temps   SETS    "$SaveList"
TempsC  SETS    ""
 [ Temps <> ""
TempsC SETS Temps :CC: ","
 ]

 [ "$MakeFrame" = ""
        STMFD   sp!, {$TempsC.lr}
 |
        MOV     ip, sp
        STMFD   sp!, {$TempsC.fp,ip,lr,pc}
        SUB     fp, ip, #4
 ]
        MEND

; ******************************
; ***  APCS function return  ***
; ******************************
        MACRO
        Return  $ReloadList, $Base, $CC, $linkreg
        LCLS    Temps
        LCLS    Tempr
Temps   SETS    "$ReloadList"
Tempr   SETS    "$linkreg"

 [ "$Base"<>"LinkNotStacked" :LAND: "$Base"<>"" :LAND: "$Base"<>"fpbased"
 ! 1, "Unrecognised value for $Base"
 ]

 [ "$Base"="LinkNotStacked" :LAND: "$ReloadList"=""
   [ "$Tempr" = ""
Tempr   SETS    "lr"
   ]
 |
   [ "$Tempr" <> ""
   ! 1, "Return address cannot be held in register when LR is stacked"
   ]
 ]

 [ {CONFIG} = 26
   [ "$Base" = "LinkNotStacked" :LAND: "$ReloadList"=""
        MOV$CC.S pc, $Tempr
   |
     [ Temps <> ""
Temps   SETS    Temps :CC: ","
     ]
     [ "$Base" = "fpbased"
        LDM$CC.DB fp, {$Temps.fp,sp,pc}^
     |
        LDM$CC.FD sp!, {$Temps.pc}^
     ]
   ]
 |
   [ "$Base" = "LinkNotStacked" :LAND: "$ReloadList"=""
        MOV$CC  pc, $Tempr
   |
     [ Temps <> ""
Temps   SETS    Temps :CC: ","
     ]
     [ "$Base" = "fpbased"
        LDM$CC.DB fp, {$Temps.fp,sp,pc}
     |
        LDM$CC.FD sp!, {$Temps.pc}
     ]
   ]
 ]
        MEND

	END
