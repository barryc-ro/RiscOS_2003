; Dynamic.s


        AREA |ASM$$code|, CODE, READONLY

        GET     hdr:ListOpts
        GET     hdr:Macros
        GET     hdr:System
        GET     hdr:Machine.<Machine>
        GET     hdr:APCS.<APCS>


;---------------------------------------------------------------------------
; DynamicArea_Alloc
;
;   os_error *DynamicArea_Alloc( int nSize, char *pAreaName,
;                                DynamicArea *pArea,
;                                void **ppStart );

            EXPORT DynamicArea_Alloc
DynamicArea_Alloc
            FunctionEntry "r2-r8"           ;   8 words
            MOV     R8,R1                   ;   area name
            MOV     R7,#0
            MOV     R6,#0
            MOV     R5,#-1
            MOV     R4,#&80                 ;   green bar please
            MOV     R3,#-1
            MOV     R2,R0                   ;   initial size
            MOV     R1,#-1
            MOV     R0,#0                   ; create
            SWI     XOS_DynamicArea
            Return  "r2-r8",,VS
            LDR     R0,[R13,#4]             ; R3 passed in
            STR     R3,[R0]
            LDR     R0,[R13]                ; R2 passed in
            STR     R1,[R0]
            MOV     R0,#0
            Return  "r2-r8"

;---------------------------------------------------------------------------
; DynamicArea_Realloc
;
;   os_error *DynamicArea_Realloc( DynamicArea area, int *pSize );
;
            EXPORT DynamicArea_Realloc
DynamicArea_Realloc
            FunctionEntry "r1"
            LDR     R1,[R1]
            SWI     XOS_ChangeDynamicArea
            LDMFD   R13!,{R2}               ; saved R1
            STR     R1,[R2]                 ; *not* STRVC
            MOVVC   R0,#0
            Return                          ; already pulled R1

;---------------------------------------------------------------------------
; DynamicArea_Free
;
;   os_error *DynamicArea_Free( DynamicArea *pArea );
;
            EXPORT DynamicArea_Free
DynamicArea_Free
            FunctionEntry
            MOV     R12,R0
            LDR     R1,[R12]
            MOV     R0,#1
            SWI     XOS_DynamicArea
            MOVVC   R0,#0
            STRVC   R0,[R12]
            Return

;---------------------------------------------------------------------------
; MemoryPageSize
;
;   int MemoryPageSize(void)
;
            EXPORT MemoryPageSize
MemoryPageSize
            FunctionEntry
            SWI     XOS_ReadMemMapInfo
            Return

;
            END
