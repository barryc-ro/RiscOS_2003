        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:APCS.<APCS>

        EXPORT  _get_t0_count

        AREA    |Asm$$Code|, CODE, READONLY
_get_t0_count
        FunctionEntry "r4"
        [ {CONFIG}<>26
        MRS     r2, CPSR
        ]
        SWI     XOS_EnterOS             ; Must be in SVC mode to peek IOC/IOMD
        MOV     r3,#IOC
; Shut off interrupts (briefly) to ensure an atomic read of these
; silly hardware registers.
        [ {CONFIG}=26
        TEQP    pc,#I_bit :OR: SVC_mode
        |
        MRS     r4, CPSR
        ORR     r4, r4, #I32_bit
        MSR     CPSR_c, r4              ; Disable interrupts
        ]
        STRB    r3,[r3,#Timer0LR]
        LDRB    r1,[r3,#Timer0CL]
        LDRB    r0,[r3,#Timer0CH]
        ORR     r0,r1,r0,LSL #8
        [ {CONFIG}<>26
        MSR     CPSR_cf, r2             ; Restore entry mode and irq state
        ]
        Return  "v1"


        END
