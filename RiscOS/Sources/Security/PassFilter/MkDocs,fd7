| !MkDocs
|
| Author:	Neil Bingham.
| Version:	0.01 (Sun 10th January 1999).
|
| Purpose:	Create documentation based on the source code using Andrew
| 		Hodgkinson's BuildHTML & BuildSHelp utilities.

| ************
| Global Setup
| ************
|
| Name of app being documented.
Set AppName PassFilter
| Output directory for HTML - change as necessary.
Set HTMLOut$Dir <obey$dir>.docs.html

| Create list of files to document.
dir <Obey$Dir>
Echo **Enumerating Directories...
IfThere <Obey$Dir>.c Then Enumdir <Obey$Dir>.c <Wimp$ScrapDir>.List-C
IfThere <Obey$Dir>.c++ Then Enumdir <Obey$Dir>.c++ <Wimp$ScrapDir>.List-C++

| ********************************
| Create HTML version of the docs.
| ********************************
echo **Creating HTML Docs
IfThere <HTMLOut$Dir> Then wipe <HTMLOut$Dir> FR~C~V
CDir <HTMLOut$Dir>
ifthere <Wimp$ScrapDir>.List-C Then BuildHTML <Obey$Dir>.c. <HTMLOut$Dir>. <Wimp$ScrapDir>.List-C
ifthere <Wimp$ScrapDir>.List-C++ Then BuildHTML <Obey$Dir>.c++. <HTMLOut$Dir>. <Wimp$ScrapDir>.List-C++
Echo Creating Index
HTMLIndex <HTMLOut$Dir> <HTMLOut$Dir>.index/html <appname>

| **************************************
| Create STRONGHELP version of the docs.
| **************************************
Echo **Creating StrongHelp Docs
IfThere <Obey$Dir>.SHelpDocs Then wipe <Obey$Dir>.SHelpDocs FR~C~V
CDir <Obey$Dir>.SHelpDocs
| Check that !StrongHelp is running.
RMEnsure StrongHelp 0.00 Error StrongHelp not found!
RMEnsure StrongHelp 0.00 Run <StrongHelp$Dir>.!Run
| Build Manuals.
Ifthere <Wimp$ScrapDir>.List-C Then BuildSHelp c. SHelpDocs. <Wimp$ScrapDir>.List-C
Ifthere <Wimp$ScrapDir>.List-C++ Then BuildSHelp c++. SHelpDocs. <Wimp$ScrapDir>.List-C++
| Build Index manual.
| Remove the old copy before building the Index.
Remove <Obey$Dir>.Docs.<appname>
IFThere <Obey$Dir>.SHelpDocs Then Echo Indexing manuals.
IFThere <Obey$Dir>.SHelpDocs Then SHIndexer <Obey$Dir>.SHelpDocs <Obey$Dir>.Docs.<appname>
| CleanCopy the manual to remove garbage.
Ifthere Docs.<appname> Then Echo Creating CleanCopy of manual.
Ifthere Docs.<appname> Then Run <StrongHelp$Dir>.Utilities.CleanCopy <Obey$Dir>.Docs.<appname>

| *********************************************************
| Open the directories where the documentation was created.
| *********************************************************
IfThere <HTMLOut$Dir> Then Filer_OpenDir <HTMLOut$Dir>
IfThere <Obey$Dir>.Docs.<appname> Then Filer_OpenDir <Obey$Dir>.Docs

| ********
| Tidy up.
| ********
Echo **Tidying up.
Remove <Wimp$ScrapDir>.List-C
Remove <Wimp$ScrapDir>.List-C++
IfThere <Obey$Dir>.SHelpDocs Then wipe <Obey$Dir>.SHelpDocs FR~C~V
| Unset system variables
Unset appname
Unset HTMLOut$Dir
| Return the CSD to where it was.
back
