; >Format

discnam	DCB	"DiscName",0
rtf_msg	DCB	"FormReady",0
fdn_msg	DCB	"FormDone",0
title	DCB	"TitleFPCC",0
titlepc	DCB	"TitleFDOS",0
bttn	DCB	"FormButtn",0
bttndon	DCB	"FormBtnDon",0
	ALIGN

interactive_format		;r3 is ascii drive number, r4 is menu entry. Return by pull "pc"
	Debug	wt,"Entered interactive_format for drive",r3
	LDR	r1, ictvform_slotindir
	Debug	wt,"Ictvform_slotindir reckoned to be at",r1
	STRB	r3, [r1, #5]	;Thats the slot number box sorted.
	SUB	r9, r3, #"0"	;keep the drive number for later

	CMP	r4, #1
	ADREQ	r1, title
	ADRNE	r1, titlepc
	LDR	r2, ictvform_titlindir
	MOV	r3, #32
	DebugS	wt,"About to look up the token ",r1
	Debug	wt,"which is located at",r2
	BL	msgtrans_lookuptoken	;That's the titlebar text sorted.
	Debug	wt,"Done titlebar text"

	ADDR	r1, rtf_msg
	LDR	r2, ictvform_msgsindir
	MOV	r3, #32
	BL	msgtrans_lookuptoken	;That's the message bar sorted.
	Debug	wt,"Done messagebar text"

	ADDR	r1, bttn
	LDR	r2, ictvform_buttindir
	MOV	r3, #7
	BL	msgtrans_lookuptoken	;That's the button sorted.
	Debug	wt,"Done messagebar text"

	;Also need to work out some sort of discname....
	Debug	wt,"About to work out a discname"
	ADDR	r1, discnam
	ADDR	r2, miscdata
	MOV	r3, #16
	BL	msgtrans_lookuptoken	;Ok, so we've got the format string...
	DebugS	wt,"String read from the messages file ",r2
	MOV	r0, #3
	ADD	r1, r2, #32
	STRB	r0, [r1]
	MOV	r0, #14
	SWI     XOS_Word		;Got the 5-byte time (UTC)
	MOV	r0, r1
	LDR	r1, ictvform_nameindir
	MOV	r2, #11
	SUB	r3, r0, #32
	SWI	XOS_ConvertDateAndTime	;Done the evaluation.
	DebugS	wt,"Done the discname", r0

	;Having done all this, let's open the window
	ADR	r1, miscdata
	LDR	r2, ictvform_handle
	STR	r2, [r1]
	SWI	XWimp_GetWindowState
	MOV	r2, #-1
	STR	r2, [r1, #28]
	SWI     XWimp_OpenWindow
	MOVVC	r0, r4, LSL #6		;PCC/Dos into top 2 bits of byte
	ORRVC	r0, r0, r9		;Orr the ASCII slotnumber in too (the format status is 0 too)
	Debug	wt,"R9 is now ",r9
	Debug	wt,"R0 is being stored in formatopen as ",r0
	STRVCB	r0, formatopen

	Pull	"pc"
;--------------------------------------------------
format_closewindow
	CLRV
	SWI	XWimp_CloseWindow
	MOV	r0, #0
	STRB	r0, formatopen
	B	WimpPoll

;--------------------------------------------------
mouse_press_format ;R3 contains the window handle

	LDRB	r1, formatopen
	Debug	wt,"Formatopen contains ",r1

	TST	r1, #1<<5

	ADRNE	r1, ictvform_handle
	BNE	format_closewindow

	CMP	r1, #0
	ADREQ	r1, ictvform_handle
	BEQ	format_closewindow


	ORR	r1, r1, #1<<5
	STRB	r1, formatopen

	;Lets build the command in miscdata...
	ADDR	r1, format
	BL	createcommand		;Returns with r2 pointing to the last char, R0 points to cmd start

	BL	findslotfromwindow	;returns slot number (ASCII) in r4, format menu entry in r5 (1=pcc, 2=dos)
	STRB	r4, [r2, #-2]		;Stuff the number over the n

	CMP	r5, #2
	ADDR	r1, formatpcc
	ADDR	r1, formatdos, EQ

50	LDRB	r3, [r1], #1
	CMP	r3, #0
	STRNEB	r3, [r2], #1
	BNE	%BT50			;Copy the 'format type'

	;now dump the name in place
	LDR	r1, ictvform_nameindir
	Debug	wt,"Reading disc name from ",r1
60	LDRB	r3, [r1], #1
	CMP	r3, #0
	STRNEB	r3, [r2], #1
	BNE	%BT60			;Copy the disc name

	MOV	r1, #" "
	STRB	r1, [r2], #1
	MOV	r1, #"y"
	STRB	r1, [r2], #1
	MOV	r1, #0
	STRB	r1, [r2]

	;Hourglass on here
	SWI	XHourglass_On

	DebugS	wt,"Calling OS_CLI with the command ",r0
	SWI	XOS_CLI
	BVC	%FT70
	BL	ExpandTaskTitle
        MOV     r1, #1
	SWI     XWimp_ReportError

	;Hourglass off here
70	SWI	XHourglass_Off

	;Now, change the message & redraw the messagebar
	ADDR	r1, fdn_msg
	LDR	r2, ictvform_msgsindir
	MOV	r3, #32
	BL	msgtrans_lookuptoken	;That's the message bar sorted.
	Debug	wt,"Done messagebar text"
	LDR	r0, ictvform_handle
	MOV	r1, #5
	MOV	r2, #0
	MOV	r3, #0
	Push	"r0-r3"
	MOV	r1, sp
	SWI	Wimp_SetIconState
	Pull	"r0-r3"

	ADDR	r1, bttndon
	LDR	r2, ictvform_buttindir
	MOV	r3, #7
	BL	msgtrans_lookuptoken	;That's the button sorted.
	Debug	wt,"Done button text"
	LDR	r0, ictvform_handle
	MOV	r1, #3	;Format button
	MOV	r2, #0
	MOV	r3, #0
	Push	"r0-r3"
	MOV	r1, sp
	SWI	Wimp_SetIconState
	Pull	"r0-r3"

	SUB	r0, r4, #&30
	BL	UpdateIcon

	B	WimpPoll

;------------------------
findslotfromwindow
	LDRB	r5, formatopen
	AND	r4, r5, #&7
	ADD	r4, r4, #"0"
	MOV	r5, r5, LSR #6		;leaves us 2 for dos, 1 for pccardfs

	Debug	wt,"r4 and r5 are",r4,r5
	MOV	pc, lr


	LNK	s.Tail
