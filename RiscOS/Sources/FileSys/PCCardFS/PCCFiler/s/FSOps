; > FSops

;------------------------------
; Find Icon Info: Entry: R8-> indirected block
;			 R7 = slotnumber
;			 All regs preserved on exit
;			 However, the first 96 bytes of miscdata are corrupt.
FindIconInfo	ROUT
	Push	"r0-r8,lr"
        MOV     r0, #FSOp_CardInfo
        MOV     r1, r7
	Debug	wt,"Calling PCMCIA_FSOp with r0,r1 ",r0,r1
	SWI     XPCMCIA_FSOp            ;Let's sort out the icon.....
	Debug	wt,"Called PCMCIA_FSOp. Got r0,r1,r2 ",r0,r1,r2
        ADR     r0, IconNames
        ADD     r0, r0, r2, LSL #3      ;r0 points to the icon name to be used.
        LDMIA   r0, {r3,r4}             ;Pick up the name,
        ADD     r8, r8, #4
        STMIA   r8, {r3,r4}             ;and dump it into the indirected block.

	CMP	r2, #3			;Slot occupied by a memory card?
	BNE	%FT05			;Nope, so name the icon ':x'

	ADR	r1, DiscSpecifier       ;Now, let's do the text for under the icon
	ADR	r0, miscdata
	LDR	r3, [r1]
	STR	r3, [r0]
	ADD	r1, r7, #48		;Convert 0->'0' etc
	STRB	r1, [r0, #1]

	ADD	r1, r0, #16		;Start of return block
	SWI	XPCCardFS_DescribeDisc	;
	ADD	r1, r1, #32		;r1 points to the disc 'filetype'
	LDR	r1, [r1]		;Read the filetype into r1
	MOV	r6, #&1000
	SUB	r6, r6, #3
	CMP	r1, r6			;data filetype? (ie not formatted/ not understood)
	BEQ	%FT05			;name as ':x'
	ADD	r1, r0, #38		;r1 points to start of discname
	DebugS	wt,"Discname read back as",r1, 10
	ADD	r6, r8, #8		;Point R6 to the start of the text area
	MOV	r3, #10
02	LDRB	r4, [r1], #1
	CMP	r4, #&20		;Space character
	STRGTB	r4, [r6], #1
	SUBGTS	r3, r3, #1
	BGT	%BT02

	RSB	r3, r3, #10		;R3 is length of discname
	MOV	r3, r3, LSL #4		;Convert to OS units
	STR	r3, [r8, #20]

	MOV	r3, #0
	STRB	r3, [r6]		;Bung a terminating 0 at the end
	Pull	"r0-r8,pc"

05	ADD	r8, r8, #8		;Points to start of text indirection
	MOV	r0, #":"
	ADD	r1, r7, #48
	ORR	r0, r0, r1, LSL #8
	STR	r0, [r8]
	Pull	"r0-r8,pc"

DiscSpecifier
	DCB	":0",0,0
	ALIGN

IconNames
	DCB	"pcmciae",0		;Empty
	DCB	"pcmciaf",0		;No CIS
	DCB	"pcmciaq",0		;CIS but no recognised partition
	DCB	"pcmciaf",0		;PCCardFS/DOS partition
	DCB	"pcmciai",0		;DriverC
	DCB	"pcmciai",0		;DriverA
	DCB	"pcmciai",0		;'I/O' card (without driver partition)

;--------------------------------------
	LNK	s.Free
