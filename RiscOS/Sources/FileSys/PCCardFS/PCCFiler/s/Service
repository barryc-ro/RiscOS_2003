; > Service

;---------------------------------------------------------------------------------------------------
Service
	Push	"r9-r10,lr"
	TEQ	r1, #Service_StartFiler			;This is handled even if we have no workspace
	BEQ	svc_startfiler

	MOV	r10, r12				;Keep the ws pointer - filerdying uses it
	LDR	r12, [r12]				;Get the ws ptr into r12
	CMP	r12, #0
	Pull	"r9-r10,pc",LE,^			;No ws, so ignore the other service calls

    ;Check the rest of the service calls here
    ;++++++++++++++++++++++++++++++++++++++++
	TEQ	r1, #Service_FilerDying
	BEQ	svc_filerdying

	TEQ     r1, #Service_PCMCIA
        CMPEQ   r0, #7
        BEQ     svc_slotstatuschange

	Pull	"r9-r10,pc",,^


;---------------------------------------------------------------------------------------------------
; svc_startfiler
;   This tells the filer the *command to use to kick us into life...
svc_startfiler
	LDR	r2, [r12]				;Get the ws ptr
	CMP	r2, #0
	Pull	"r9-r10,pc",NE,^			;Are already running, or failed earlier.

	MOV	r9, r0					;Tuck the filer taskhandle safely out of the way

	MOV	r0, #FSOp_NumDrives
	SWI     XPCMCIA_FSOp
;        Pull    "r9-r10,pc",VS,^                        ;No PCMCIA manager! - let's pack it in
        CMP     r0, #0
        Pull    "r9-r10,pc",EQ,^                        ;No drives to represent - let's pack it in
        MOV     r10, r0                                 ;Keep it safe...

        MOV	r0, #ModHandReason_Claim                ;No ws, so let's get some before we enter the Wimp code
;	MOV	r3, #WorkspaceSize
	MOV	r3, #&ff0	;TEMPORARY FIX (4KB)

	SWI	XOS_Module				;Get the workspace
	MOVVS	r2, #-1					;if we fail
	STR	r2, [r12]				;Put the ws address in our private word
	Pull	"r9-r10,pc",VS				;If we failed, we might as well leave now :-(

	;Right, got the workspace, let's initialise it!
	MOV	r0, r12
	MOV	r12, r2					;R12 now points to the ws
	STR	r0, private_word_address
	STR	r9, filer_taskhandle			;Stuff this into the workspace
        STRB    r10, num_slots                          ;and this too.
	BL	msgtrans_openfile			;Open the message file
	MOV	r0, #0
	STR	r0, my_taskhandle
	STR	r0, slotupdate
	STRB	r0, formatopen

	ADR	r0, startupcommand
	MOV	r1, #0					;Claim the service
	Pull	"r9-r10,pc",,^

startupcommand
	DCB	"Desktop_PCCardFSFiler",0
	ALIGN

;------------------------------------------------
svc_filerdying
	MOV	r12, r10
	BL	Die
	MOV	r0, #0
	STR	r0, [r1]
	Pull	"r9-r10,pc"

;------------------------------------------------
svc_slotstatuschange
	ADD	r2, r2, #1				;Change to 1+ instead of 0+
	STR	r2, slotupdate
	Pull	"r9-r10,pc",,^

;------------------------------------------------
        LNK     s.StarCmd
