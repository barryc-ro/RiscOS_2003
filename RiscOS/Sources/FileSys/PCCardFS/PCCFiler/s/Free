; >Free

InteractiveFree
	ADD	pc, pc, r0, LSL #2
	MOV	r0, r0				;NOP

	LDMIA	sp!, {pc}			;Reason Code 0 - No-op
	B	GetDeviceName
	B	GetFreeSpace


CompareDevice
	CMP	r0, #3				;Set Z
	LDMIA	sp!, {pc}


GetDeviceName	ROUT
	MOV	r11, r1
	MOV	r10, r2
	MOV	r9, r3				;Keep entry registers safe

	MOV	r0, r3
	ADR	r1, miscdata + &100
	SWI	XPCCardFS_DescribeDisc
	SWIVS	OS_GenerateError
	MOVVS	r1, r11
	MOVVS	r2, r10
	MOVVS	r3, r9
	LDMVSIA	sp!, {pc}

	ADD	r1, r1, #22			;Points to start of discname
	MOV	r0, #10
01	LDRB	r3, [r1], #1
	CMP	r3, #&20			;Space character or less?
	STRGTB	r3, [r2], #1
	SUBGTS	r0, r0, #1
	BGT	%BT01

	MOV	r0, #0
	STRB	r0, [r2]			;Bung the terminating 0 on!

	MOV	r0, #11				;Filename max length (10+terminator)

	MOV	r1, r11
	MOV	r2, r10
	MOV	r3, r9				;Restore entry registers
	LDMIA	sp!, {pc}


GetFreeSpace
	MOV	r11, r0
	MOV	r10, r1
	MOV	r9,  r2
	MOV	r8,  r3				;Keep entry registers safe

	MOV 	r0, #49
	MOV	r1, r3
	SWI     OS_FSControl

        MOV     r3, r0                          ;Total in r2, freespace in r3
	SUB	r4, r2, r3			;Used in R4 (total-free)
	STMIA	r9, {r2-r4}

	MOV	r0, r11
	MOV	r1, r10
	MOV	r2,  r9
	MOV	r3,  r8				;Restore entry registers
	LDMIA	sp!, {pc}

;--------------------------
	LNK	s.Format
