
; Message file handling code


; OSS Translate an error block, with one substituted parameter.

; In:   r0 -> Error block containing the token
;       r1 -> %0 parameter to substitute

; Out:  r0 -> Translated error block or another error (token no found etc.)
;       All other registers preserved, V always set, other flags undefined

copy_error_one ENTRY "r2-r7"
        BL      open_message_file               ; Ensure file is open
        EXIT    VS                              ; Return the error 

        MOV     r4, r1                          ; Move input %0
        ADR     r1, message_file_block          ; Messages file handle
        MOV     r2, #0                          ; Use MessageTrans buffer
        MOV     r5, #0                          ; No %1
        MOV     r6, #0                          ; No %2
        MOV     r7, #0                          ; No %3
        SWI     XMessageTrans_ErrorLookup       ; Always sets the V flag
        MOV     r1, r4                          ; Preserve input r1
        EXIT


message_filename
        DCB     "Resources:$.Resources.RAMFS.Messages", 0
        ALIGN


; OSS Open the messages file if it is closed.

open_message_file ENTRY "r0-r2"
        LDR     r1 , message_file_open
        TEQ     r1, #0                          ; Check the open flag
        EXIT    NE                              ; Return - it is open

        ADR     r0, message_file_block          ; Messages file handle
        ADR     r1, message_filename            ; Messages filename
        MOV     r2, #0                          ; Buffer in RMA/access direct
        SWI     XMessageTrans_OpenFile
        STRVS   r0, [sp]                        ; Return the error
        EXIT    VS

        MOV     r1, #1
        STR     r1, message_file_open           ; Flag the file as open
        EXIT

        END
