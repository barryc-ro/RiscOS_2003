        SUBT    ==> &.Arthur.NetFS.Configure

        ; ************************************
        ; ***  Configuration  and  Status  ***
        ; ************************************

ConfigureFS     ROUT
        Push    "r0-r4, lr"
        LDR     wp, [ r12 ]
        CMP     r0, #1
        BGT     DoConfigureFS                           ; Configure
        BEQ     StatusOfFS                              ; Print status
PrintSyntaxOfConfigureFS
        [       TokeniseHelp
        ADR     r1, Token_FSSyntaxOnly
PrintToken
        LD      r0, MessageBlockAddress
        CMP     r0, #0                                  ; Clears V
        BNE     DoLookup
        Push    "r1, r2"
        ADR     r0, MessageBlock
        ADRL    r1, MessageFileName
        MOV     r2, #0                                  ; Use the file where she lies
        SWI     XMessageTrans_OpenFile
        ADRVC   r0, MessageBlock
        STRVC   r0, MessageBlockAddress
        Pull    "r1, r2"
DoLookup
        Push    "r2-r7"
        ADR     r2, TemporaryBuffer
        MOV     r3, #?TemporaryBuffer
        MOV     r4, #0
        MOV     r5, #0
        MOV     r6, #0
        MOV     r7, #0
        SWIVC   XMessageTrans_Lookup
        MOVVC   r0, r2                                  ; The passed buffer
        MOV     r1, r3                                  ; Length of the resultant string
        SWIVC   XOS_WriteN
        SWIVC   XOS_NewLine
        Pull    "r2-r7"
        B       ExitConfigure

Token_FSSyntaxOnly
        DCB     "CNFSCFS", 0
        ALIGN
        |
        ADRL    r0, SyntaxOnlyOfConfigureFS
        SWI     XOS_Write0
        SWIVC   XOS_NewLine
        B       ExitConfigure
        ]

StatusOfFS                                              ; Print the status
        [       UseMsgTrans
        BL      TextualiseConfiguration
        ADREQ   r1, Token_FSName
        ADRNE   r1, Token_FSNumber
        ADR     r2, TemporaryBuffer
        MOV     r3, #?TemporaryBuffer
        ADR     r4, LogonDisc
        BLVC    MessageTransGSLookup1
        MOVVC   r0, r2                                  ; The passed buffer
        MOV     r1, r3                                  ; Length of the resultant string
        SWIVC   XOS_WriteN
        B       ExitConfigure

Token_FSName
        DCB     "FSName", 0
Token_FSNumber
        DCB     "FSNumb", 0
        ALIGN
        |       ; UseMsgTrans
        BL      TextualiseConfiguration
        BVS     ExitConfigure
        MOVEQ   r4, #"'"                                ; Configuration is a name
        MOVNE   r4, #0
        SWI     XOS_WriteS
        DCB     "File server ", 0
        ALIGN
        BVS     ExitConfigure
        MOVS    r0, r4
        SWINE   XOS_WriteC
        ADRVC   r0, LogonDisc
        SWIVC   XOS_Write0
        BVS     ExitConfigure
        MOVS    r0, r4
        SWINE   XOS_WriteC
        SWIVC   XOS_NewLine
        B       ExitConfigure
        ]       ; UseMsgTrans

DoConfigureFS
        LDRB    r1, [ r0 ]
        BL      IsItANumber
        BNE     ConfigureFSName                         ; Named file server
        MOV     r1, r0
        SWI     XEconet_ReadStationNumber               ; R3.R2
        MOV     r4, r2                                  ; Station, so it's now R3.R4
        MOV     r1, #NetFSIDCMOS
        BLVC    MyReadCMOS
        BVS     ExitConfigure
        TEQ     r2, #0
        BNE     %65                                     ; Previous was number
        CMP     r3, #-1
        CMPNE   r4, #-1
        Pull    "r0-r4", EQ
        BEQ     ExitUnableToDefault
65
        MOVS    r2, r4
        BLPL    MyWriteCMOS
        BVS     ExitConfigure
        MOV     r1, #NetFSIDCMOS + 1
        MOVS    r2, r3
        BLPL    MyWriteCMOS
        B       ExitConfigure

ConfigureFSName
        CMP     r1, #" "
        BLT     PrintSyntaxOfConfigureFS
        MOV     r2, #0
75
        LDRB    r1, [ r0, r2 ]
        INC     r2
        CMP     r1, #" "
        BGT     %75
        CMP     r2, #17
        BGT     ExitConfigureBadArgument
        MOV     r4, r0
        MOV     r1, #NetFSIDCMOS
        MOV     r2, #0
        BL      MyWriteCMOS
        MOVVC   r1, #NetFSIDCMOS + 1
        LDRVCB  r2, [ r4 ], #1
        BLVC    MyWriteCMOS
        BVS     ExitConfigure
        MOV     r1, #FSNameCMOS
        MOV     r3, #0
80
        LDRB    r2, [ r4, r3 ]
        INC     r3
        CMP     r2, #" "
        BLE     %85
        BL      MyWriteCMOS
        BVS     ExitConfigure
        INC     r1
        B       %80 

85
        CMP     r3, #16
        BGE     ExitConfigure
        MOV     r2, #0
        BL      MyWriteCMOS
        B       ExitConfigure

ExitUnableToDefault
        ;       Return address on the stack
        ADR     r0, ErrorUnableToDefault
        Pull    lr
        [       UseMsgTrans
        B       MakeError

ErrorUnableToDefault
        DCD     ErrorNumber_UnableToDefault
        DCB     "FullNet", 0
        ALIGN
        |
        ORRS    pc, lr, #VFlag

        Err     UnableToDefault
        ALIGN
        ]       ; UseMsgTrans

TextualiseConfiguration                                 ; Into LogonDisc, a 17 byte buffer
        Push    "r0-r3, lr"
        MOV     r1, #NetFSIDCMOS
        BL      MyReadCMOS
        BVS     ExitTextualiseConfiguration 
        TEQ     r2, #0                                  ; Check flag for Numeric or Textual
        BEQ     ConfigIsName                            ; Text
        MOV     r3, r2                                  ; Save station number
        MOV     r1, #NetFSIDCMOS + 1
        BL      MyReadCMOS
        BVS     ExitTextualiseConfiguration
        MOV     r1, r3
        Push    "r1, r2"
        MOV     r0, sp
        ADR     r1, LogonDisc
        MOV     r2, #?LogonDisc
        SWI     XOS_ConvertNetStation
        INCS    sp, 8
        B       ExitTextualiseConfiguration

ConfigIsName
        ADR     r3, LogonDisc
        MOV     r1, #NetFSIDCMOS + 1
        BL      MyReadCMOS
        BVS     ExitTextualiseConfiguration
        TEQ     r2, #0
        BEQ     TerminateConfigName
        STRB    r2, [ r3 ], #1
        MOV     r1, #FSNameCMOS
ConfigNameLoop
        BL      MyReadCMOS
        BVS     ExitTextualiseConfiguration
        STRB    r2, [ r3 ], #1
        INC     r1
        CMP     r2, #0
        BEQ     ExitTextualiseConfiguration
        TEQ     r1, #FSNameCMOS + 15
        BNE     ConfigNameLoop
TerminateConfigName
        MOVS    r2, #0
        STRB    r2, [ r3 ], #1
ExitTextualiseConfiguration
        STRVS   r0, [ sp, #0 ]
        Pull    "r0-r3, pc"

ConfigureLib    ROUT
        Push    "r0-r4, lr"
        LDR     wp, [ r12 ]
        CMP     r0, #1
        BGT     DoConfigureLib                          ; Configure
        BEQ     StatusOfLib                             ; Print status
        [       TokeniseHelp
        ADR     r1, Token_LibSyntaxOnly
        B       PrintToken

Token_LibSyntaxOnly
        DCB     "CNFSCLB", 0
        ALIGN
        |
        ADRL    r0, SyntaxOnlyOfConfigureLib
        SWI     XOS_Write0
        SWIVC   XOS_NewLine
        B       ExitConfigure
        ]

StatusOfLib
        [       UseMsgTrans
        ADR     r1, Token_Libry
        ADR     r2, TemporaryBuffer
        MOV     r3, #?TemporaryBuffer
        BL      MessageTransGSLookup0
        BVS     ExitConfigure
        ADD     r4, r2, r3                              ; Compute the address of the terminator
        |       ; UseMsgTrans
        ADR     r1, TemporaryBuffer
        MOV     r3, #20
        BL      AddString
        DCB     "Library ", 0
        ALIGN
        MOV     r4, r1
        ]       ; UseMsgTrans
        MOV     r1, #NetFilerCMOS
        BL      MyReadCMOS
        BVS     ExitConfigure
        MOV     r1, r4
        TST     r2, #BitOne
        BEQ     %20                                     ; Default
        BL      AddString
        DCB     "'ArthurLib'", 10, 13, 0
        ALIGN
        BVS     ExitConfigure
        B       %30

20
        [       UseMsgTrans
        MOV     r2, r1
        MOV     r0, #"'"
        STRB    r0, [ r2 ], #1
        ADR     r1, Token_Def
        ADR     r3, TemporaryBuffer + ?TemporaryBuffer
        SUB     r3, r3, r2
        BL      MessageTransGSLookup0
        BVS     ExitConfigure
        ADD     r1, r2, r3                              ; Compute the address of the terminator
        BL      AddString
        DCB     "'", 10, 13, 0
        ALIGN
        |       ; UseMsgTrans
        BL      AddString
        DCB     "'<Default>'", 10, 13, 0
        ALIGN
        ]       ; UseMsgTrans
        BVS     ExitConfigure
30
        ADR     r0, TemporaryBuffer
        SWI     XOS_Write0
        B       ExitConfigure

        [       UseMsgTrans
Token_Libry
        DCB     "Libry", 0
Token_Def
        DCB     "Def", 0
        ALIGN
        ]       ; UseMsgTrans

DoConfigureLib
        MOV     r1, r0                                  ; Pointer to argument
        MOV     r0, #10
        SWI     XOS_ReadUnsigned
        BVS     ExitConfigure
        CMP     r2, #1
        BHI     ExitConfigureBadArgument
NastyConfigureLibOption
        MOV     r3, r2, LSL #1                          ; Save new value in the right bit (BitOne)
        MOV     r1, #NetFilerCMOS
        BL      MyReadCMOS
        BIC     r2, r2, #BitOne
        ORR     r2, r2, r3                              ; Put in the new option
        BLVC    MyWriteCMOS
        B       ExitConfigure

ExitConfigureBadArgument
        MOV     r0, #2                                  ; Special case error known to kernel
        SETV
ExitConfigure
        STRVS   r0, [ sp ]
        [       OldOs :LAND: (:LNOT: ReleaseVersion)
        STRVS   r0, [ sp, #24 ]                         ; Poke into calling stack; OS 2.00 bug
        ]

        Pull    "r0-r4, pc"

        LTORG

        END
