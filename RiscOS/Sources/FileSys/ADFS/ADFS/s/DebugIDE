; Debug routines and macros for IDE debugging.

        MACRO
$label  TLINE   $str,$cc
$label
        B       %f20
10
 [ "$str" <> ""
        =       "$str"
 ]
 [ "$cc" = ""
        =       13,10
 |
  [ "$cc" <> "cc"
   ! 1,"Error in TLINE with 'cc': '$cc' used instead"
   MEXIT
  ]
 ]
        =       0
        ALIGN
20
        Push    "R1,LR"
        ADR     R1,%b10
        BL      TubeString
        Pull    "R1,LR"
        MEND

;*********************************************************************

        MACRO
$label  TREG    $reg,$str,$cc
$label
        Push    "R0,LR"
 [ "$str" = ""
        Push    "R0,LR"
        MOV     R0,#" "
        BL      TubeChar
        Pull    "R0,LR"
 |
        TLINE   "$str",cc
 ]
        Tword   $reg
 [ "$cc" = ""
        MOV     R0,#13
        BL      TubeChar
        MOV     R0,#10
        BL      TubeChar
 |
  [ "$cc" <> "cc"
   ! 1,"Error in TLINE with 'cc': '$cc' used instead"
   MEXIT
  ]
 ]
        Pull    "R0,LR"
        MEND

;*********************************************************************

        MACRO
$label  TBYTE   $reg,$str,$cc
$label
        Push    "R0,LR"
 [ "$str" = ""
        Push    "R0,LR"
        MOV     R0,#" "
        BL      TubeChar
        Pull    "R0,LR"
 |
        TLINE   "$str",cc
 ]
        MOV     R0,$reg
        BL      TubeHexByte
 [ "$cc" = ""
        MOV     R0,#13
        BL      TubeChar
        MOV     R0,#10
        BL      TubeChar
 |
  [ "$cc" <> "cc"
   ! 1,"Error in TLINE with 'cc': '$cc' used instead"
   MEXIT
  ]
 ]
        Pull    "R0,LR"
        MEND

;*********************************************************************

        MACRO
$label  TPRINTV
$label
        Push    "LR"
        BL      TubePrintV
        Pull    "LR"
        MEND

;*********************************************************************

TubePrintV      ROUT
        Push    "LR"
        BVC     %FT10
        TLINE   "VS",cc
        B       %FT20
10
        TLINE   "VC",cc
20
        Pull    "PC",,^

;*********************************************************************

TubeString      ROUT
        Push    "R0,LR"
10
        LDRB    R0,[R1],#1
        TEQS    R0,#0
        BLNE    TubeChar
        BNE     %BT10
        Pull    "R0,PC",,^

;*********************************************************************

TubeHexByte     ROUT
        Push    "R0,R1,LR"
        MOV     R1,R0
        MOV     R0,R0,LSR #4
        BL      %FT10
        MOV     R0,R1
        BL      %FT10
        Pull    "R0,R1,PC",,^
10
        AND     R0,R0,#&0F
        CMPS    R0,#10
        ADDCC   R0,R0,#"0"
        ADDCS   R0,R0,#"A"-10
        B       TubeChar

;*********************************************************************

        END
