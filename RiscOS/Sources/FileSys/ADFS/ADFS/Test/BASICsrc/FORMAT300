REM>$.ADFS.TEST.FORMAT300
REM Format a double density disk using disk format spec.
:
density%=3:spt%=9:sectorsize%=9:size%=2^sectorsize%
lowsector%=1
tracks%=80:heads%=2
:
INPUT"Format which drive (0..3)? "drive%
:
DIM dfs% 1024:REM Disk format specification
dfs%!0=size%
dfs%!4=50:REM gap1, with IAM
dfs%!8=dfs%!4
dfs%!12=84:REM gap3
dfs%?16=spt%
dfs%?17=density%
dfs%?18=1:REM IAM, alt sides
IFheads%=1 dfs%?18+=(3<<2):REM sequence sides
IFtracks%=40 dfs%?18+=2:REM double step
dfs%?19=&A5:REM  fill byte
dfs%!20=tracks%+1:REM cylinders
:
DIM discrec% 64:REM disc record
discrec%?0=sectorsize%
discrec%?1=spt%
discrec%?2=heads%
discrec%?3=density%
discrec%?8=lowsector%
discrec%!16=heads%*tracks%*spt%*size%
:
discaddress%=0
FORtrack%=0TOtracks%-1:FORhead%=0TOheads%-1
FORI%=0TOspt%-1:dfs%!(36+(I%<<2))=((sectorsize%-7)<<24)+((I%+lowsector%)<<16)+(head%<<8)+track%:NEXT
PRINT"Formatting track ";track%;" head ";head%;
SYS"ADFS_DiscOp",0,4+(discrec%<<6),(drive%<<29)+discaddress%,0,dfs%
PRINTCHR$13;
discaddress%+=size%*spt%
NEXThead%:NEXTtrack%
:
PRINT'"Verifying...";
SYS"ADFS_DiscOp",0,0+(discrec%<<6),(drive%<<29),,discaddress%-size%*spt%
PRINT"ok"
REM SYS"XADFS_DescribeDisc",":"+STR$(drive%),discrec%
