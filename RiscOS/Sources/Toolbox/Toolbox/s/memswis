; Title:   s.memswis
; Purpose: Assembler veneers onto OS_Heap for speed
; Author:  KJB
; History: 23-Nov-96: KJB: Created
;

XOS_Heap	*	&2001D
XOS_Module	*	&2001E

	AREA	MemSWIs,CODE,READONLY,PIC

	EXPORT	xosheap_initialise
	EXPORT	xosheap_alloc
	EXPORT	xosheap_free
	EXPORT	xosheap_realloc
	EXPORT	xosheap_resize
	EXPORT	xosheap_read_size
	EXPORT	xosmodule_realloc

xosheap_initialise
	MOV	R12,LR
	MOV	R3,R1
	MOV	R1,R0
	MOV	R0,#0
	SWI	XOS_Heap
	; MOVVC	R0,#0		; Not needed because R0 will be preserved
	MOVS	PC,R12

xosheap_alloc
	STMFD	R13!,{LR}
	MOV	R12,R2
	MOV	R3,R1
	MOV	R1,R0
	MOV	R0,#2
	SWI	XOS_Heap
	STRVC	R2,[R12]
	MOVVC	R0,#0
	LDMFD	R13!,{PC}^

xosheap_free
	MOV	R12,LR
	MOV	R2,R1
	MOV	R1,R0
	MOV	R0,#3
	SWI	XOS_Heap
	MOVVC	R0,#0
	MOVS	PC,R12

xosheap_realloc
	STMFD	R13!,{LR}
	MOV	R12,R3
	MOV	R3,R2
	MOV	R2,R1
	MOV	R1,R0
	MOV	R0,#4
	SWI	XOS_Heap
	STRVC	R2,[R12]
	MOVVC	R0,#0
	LDMFD	R13!,{PC}^

xosheap_resize
	MOV	R12,LR
	MOV	R3,R1
	MOV	R1,R0
	MOV	R0,#5
	SWI	XOS_Heap
	MOVVC	R0,#0
	MOVS	PC,R12

xosheap_read_size
	STMFD	R13!,{LR}
	MOV     R12,R2
	MOV	R2,R1
	MOV	R1,R0
	MOV	R0,#6
	SWI	XOS_Heap
	STRVC	R3,[R12]
	MOVVC	R0,#0
	LDMFD	R13!,{PC}^

xosmodule_realloc
	MOV	R12,LR
	MOV	R3,R1
	MOV	R1,R2
	MOV	R2,R0
	MOV	R0,#13
	SWI	XOS_Module
	STRVC	R2,[R1]
	MOVVC	R0,#0
	MOVS	PC,R12

	END
