; Title:   s.callback
; Purpose: To send a Service_ToolboxTaskBorn
; Author:  IDJ
; History: 26-Jul-94: IDJ: Created
;          24-Jan-95: IDJ: only send round toolbox_starting svc call, if
;                          NOT during ROM start-up.
;

r0      RN      0
r1      RN      1
r2      RN      2
r3      RN      3

r4      RN      4
r5      RN      5
r6      RN      6
r7      RN      7
r8      RN      8
r9      RN      9

r10     RN     10
r11     RN     11
r12     RN     12
r13     RN     13

sl      RN     10
fp      RN     11
ip      RN     12
sp      RN     13
lr      RN     14
pc      RN     15
                   
XOS_ServiceCall  EQU  &20030

        EXPORT task_inform_callback_handler
        EXPORT toolbox_starting_callback_handler


        AREA    |C$$Code|, CODE, READONLY

task_inform_callback_handler

        ; Entry
        ; R12 = address of "callback block" containing:
        ;       offset 0 == wimp task handle
        ;       offset 4 == address of global "task" variable
        ;       offset 8 == value of task descriptor to put in "task"
        
        TEQ     r12,    #0
        MOVEQS  pc,     lr

        STMFD   sp!,    {r0-r9, r10, r11, r12, lr}

        TEQP    pc,     #3                     ;IRQs on, SVC mode 
        MOV     r0,     r0                     ;NOP after mode change  

                                               ; send service call for this task
                                               
                                               
        LDMIA   r12,    {r0-r2}                ; get callback block contents
                                                     
        LDR     r3,     [r1]                   ; save original task
        STMFD   sp!,    {r1, r3}

        STR     r2,     [r1]                   ; do "task swap"

        LDR     r1,     =&44ec2                ; Service_ToolboxTaskBorn
        SWI     XOS_ServiceCall 

        LDMFD   sp!,    {r1, r3}               ; restore original task
        STR     r3,     [r1]

        LDMFD   sp!,    {r0-r9, r10, r11, r12, pc}^     


toolbox_starting_callback_handler

        ; Entry
        ; R12 = 0 => RAM start
        ; [R12] = 0 => RMReinit ROM Toolbox
        ; [R12] = 1 => ROM start-up of Toolbox
        
        STMFD   sp!,    {r0-r9, r10, r11, r12, lr}

        TEQP    pc,     #3                     ;IRQs on, SVC mode 
        MOV     r0,     r0                     ;NOP after mode change  
        
        TEQ     r12,    #0                     ; are we in RAM?
        LDRNE   r1,     [r12]                  ; get value of ROM_started
        TEQNE   r1,     #0                     ; is it during ROM start-up?
        MOVNE   r1,     #0                     ; yes, then reset ROM_started
        STRNE   r1,     [r12]

        LDREQ   r1,     =&44ec1                ; Service_ToolboxStarting
        SWIEQ   XOS_ServiceCall 

        LDMFD   sp!,    {r0-r9, r10, r11, r12, pc}^     

        END
