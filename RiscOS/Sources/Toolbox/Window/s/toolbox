; Title:   s.toolbox
; Purpose: toolbox functions
; Author:  NK
; History: 31-Aug-94: NK : Created
;          16-Nov-99: SNB: 32-bit support plus other fixes

; APCS-R/APCS-32 compliant as per {CONFIG}
;   a1-a4,ip (R0-R3,R12) may be corrupted

        EXPORT  |toolbox_delete_object|
        EXPORT  |resourcefs_deregister_files|
        EXPORT  |resourcefs_register_files|
        EXPORT  |register_with_toolbox|
        EXPORT  |deregister_with_toolbox|
        EXPORT  |read_mode_variable|
        EXPORT  |wimp_version|
        EXPORT  |window_starting|
        EXPORT  |whats_a_decimal_point|

        AREA    |Veneers$$Code|, CODE, READONLY, PIC

        GET     hdr:ListOpts
        GET     hdr:Macros
        GET     hdr:System
        GET     hdr:Proc
        GET     hdr:ResourceFS
        GET     hdr:Wimp
        GET     hdr:Toolbox
        GET     hdr:Window
        GET     hdr:FSNumbers
        GET     hdr:NewErrors
        GET     hdr:Territory
        GET     hdr:Services

        MACRO
        MOVRet  $lr,$cond
        [ {CONFIG}=26
        MOV$cond.S    pc,$lr
        |
        MOV$cond      pc,$lr
        ]
        MEND

        MACRO
        LDMRet  $regs,$cond
        [ {CONFIG}=26
        LDM$cond.FD   sp!,{$regs,pc}^
        |
        LDM$cond.FD   sp!,{$regs,pc}
        ]
        MEND


toolbox_delete_object
        MOV     ip,lr
        SWI     XToolbox_DeleteObject
        MOVVC   a1,#0
        MOVRet  ip

resourcefs_deregister_files
        MOV     ip,lr
        SWI     XResourceFS_DeregisterFiles
        MOVVC   a1,#0
        MOVRet  ip

resourcefs_register_files
        MOV     ip,lr
        SWI     XResourceFS_RegisterFiles
        MOVVC   a1,#0
        MOVRet  ip

register_with_toolbox
        MOV     ip,lr
        MOV     a1,#0
        LDR     a2,=Window_ClassSWI
        MOV     a3,a2
        MOV     a4,#0
        SWI     XToolbox_RegisterObjectModule
        MOVVC   a1,#0
        MOVRet  ip

read_mode_variable
        MOV     ip,lr
        SWI     XOS_ReadModeVariable
        MOVVC   a1,R2
        MOVVS   a1,#0
        MOVRet  ip

wimp_version
        MOV     ip,lr
        MOV     a1,#7
        SWI     XWimp_ReadSysInfo
        MOVVS   a1,#200
        MOVRet  ip

deregister_with_toolbox
        MOV     ip,lr
        MOV     a1,#0
        LDR     a2,=Window_ClassSWI
        MOV     a3,a2
        SWI     XToolbox_DeRegisterObjectModule
        MOVRet  ip

window_starting
        STMFD   sp!,{r0-r7,lr}
        LDR     r1,=Service_WindowModuleStarting
        SWI     XOS_ServiceCall
        LDMRet  r0-r7

whats_a_decimal_point
        MOV     ip,lr
        MOV     a1,#-1
        MOV     a2,#0           ; read decimal
        SWI     XTerritory_ReadSymbols
        MOVVS   a1,#'.'
        LDRVCB  a1,[a1]
        MOVRet  ip

        END

