; Title:   s.toolbox
; Purpose: toolbox functions
; Author:  NK
; History: 31-Aug-94: NK : Created
;

; APCS compliant, a1-a4 (R0-R3) may be corrupted
    
; to save stacking, use a4 as a temporary link register
; unless we know the SWI will corrupt it.

; don't bother to handle errors unless the external code requires it

XToolbox_DeleteObject           * &64ec1
XToolbox_RegisterObjectModule   * &64efd
XToolbox_DeregisterObjectModule * &64efa
XResourceFS_DeregisterFiles     * &61B41
XResourceFS_RegisterFiles       * &61B40
XOS_ReadModeVariable            * &20035
XWimp_ReadSysInfo               * &600f2
XOS_ServiceCall                 * &20030
XTerritory_ReadSymbols          * &6305e

        EXPORT  |toolbox_delete_object|
        EXPORT  |resourcefs_deregister_files|
        EXPORT  |resourcefs_register_files|
        EXPORT  |register_with_toolbox|
        EXPORT  |deregister_with_toolbox|
        EXPORT  |read_mode_variable|
        EXPORT  |wimp_version|
        EXPORT  |window_starting|
        EXPORT  |whats_a_decimal_point|

        AREA    |C$$Code|, CODE, READONLY

toolbox_delete_object
        MOV     a4,lr
        SWI     XToolbox_DeleteObject
        MOV     pc,a4

resourcefs_deregister_files
        MOV     a4,lr
        SWI     XResourceFS_DeregisterFiles
        MOV     pc,a4

resourcefs_register_files
        MOV     a4,lr
        SWI     XResourceFS_RegisterFiles
        MOVVC   a1,#0
        MOV     pc,a4

register_with_toolbox   
        STMFD   sp!,{lr}
        MOV     a1,#0
        MOV     a2,#&82000
        ORR     a2,a2,#&80
        MOV     a3,a2
        MOV     a4,#0
        SWI     XToolbox_RegisterObjectModule
        LDMFD   sp!,{pc}

read_mode_variable
        MOV     a4,lr
        SWI     XOS_ReadModeVariable
        MOVVC   a1,R2
        MOVVS   a1,#0
        MOV     pc,a4

wimp_version
        MOV     a4,lr
        MOV     R0,#7
        SWI     XWimp_ReadSysInfo
        MOV     pc,a4

deregister_with_toolbox   
        MOV     a4,lr
        MOV     a1,#0
        MOV     a2,#&82000
        ORR     a2,a2,#&80
        SWI     XToolbox_DeregisterObjectModule
        MOV     pc,a4

window_starting
        STMFD   sp!,{r0-r7,lr}
        LDR     r1,=&82881
        SWI     XOS_ServiceCall
        LDMFD   sp!,{r0-r7,pc}^

whats_a_decimal_point
        MOV     a4,lr  
        MOV     a1,#-1
        MOV     a2,#0           ; read decimal
        SWI     XTerritory_ReadSymbols    
        MOVVS   a1,#'.'
        LDRVC   a1,[a1]
        MOV     pc,a4

        END

