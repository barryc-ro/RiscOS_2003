; > Hdr:APCS.Common

OldOpt  SETA    {OPT}
        OPT     OptNoList+OptNoP1List

 [ :LNOT: :DEF: Included_Hdr_APCS
        GBLL    Included_Hdr_APCS
Included_Hdr_APCS SETL {TRUE}

; All the following macros use {CONFIG} to determine the APCS in force.
; This means they are NOT directly affected by the APCS variable.
; This allows them to work when the APCS has been overridden - for example
; the SharedCLibrary will build using APCS-R to keep 26-bit compatibility
; where possible.

; This also means that these macros are not AAsm compatible.


; *****************************************
; ***  Pull registers given in reglist  ***
; *****************************************
        MACRO
$label  PullAPCS $reglist, $cond
        [ "$reglist" :RIGHT: 2 <> "pc" :LAND: "$reglist" :RIGHT: 2 <> "PC"
        ! 1, "PullAPCS must pull PC at the end. Use Pull instead"
        ]
 [ {CONFIG}=26
$label  LDM$cond.FD sp!, {$reglist}^
 |
$label  LDM$cond.FD sp!, {$reglist}
 ]
        MEND

; *****************************
; ***  APCS function entry  ***
; *****************************
        MACRO
        FunctionEntry $SaveList, $MakeFrame
        LCLS    Temps
        LCLS    TempsC
Temps   SETS    "$SaveList"
TempsC  SETS    ""
 [ Temps <> ""
TempsC SETS Temps :CC: ","
 ]

 [ "$MakeFrame" = ""
        Push    "$TempsC.lr"
 |
        MOV     ip, sp
        Push    "$TempsC.fp,ip,lr,pc"
        SUB     fp, ip, #4
 ]
        MEND

; ******************************
; ***  APCS function return  ***
; ******************************
        MACRO
        Return  $ReloadList, $Base, $CC
        LCLS    Temps
Temps   SETS    "$ReloadList"

 [ {CONFIG} = 26
   [ "$Base" = "LinkNotStacked" :LAND: "$ReloadList"=""
        MOV$CC.S pc, lr
   |
     [ Temps <> ""
Temps   SETS    Temps :CC: ","
     ]
     [ "$Base" = "fpbased"
        LDM$CC.DB fp, {$Temps.fp,sp,pc}^
     |
        LDM$CC.FD sp!, {$Temps.pc}^
     ]
   ]
 |
   [ "$Base" = "LinkNotStacked" :LAND: "$ReloadList"=""
        MOV$CC  pc, lr
   |
     [ Temps <> ""
Temps   SETS    Temps :CC: ","
     ]
     [ "$Base" = "fpbased"
        LDM$CC.DB fp, {$Temps.fp,sp,pc}
     |
        LDM$CC.FD sp!, {$Temps.pc}
     ]
   ]
 ]
        MEND

 ] ; :LNOT::DEF:Included_Hdr_APCS

        OPT     OldOpt
        END
