        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:Machine.<Machine>
        GET     Hdr:ModHand
        GET     Hdr:Standby

KeyV                            *       &13
KeyV_TransitionUp               *       &1
KeyV_TransitionDown             *       &2

;--------------------------get global definitions--------------------------------
			GET		s.header

  [ :LNOT: No32bitCode
flags DCD ModuleFlag_32bit
  ]

;--------------------------------------------------------------------------------
CTRL_DOWN		*		1
ALT_DOWN		*		2

; RISC OS Low-level key codes (Ref. PRM 1-156)
LEFT_CTRL		*		&3B
RIGHT_CTRL		*		&61
LEFT_ALT		*		&5E
RIGHT_ALT		*		&60
KBD_DEL			*		&34 ; Keyboard delete
KPAD_DEL		*		&66 ; Keypad delete


init
			STMDB		sp!, {r0-r12, lr}

			MOV		r0, #0
			STR		r0, [r12]  ; set initial state

			MOV		r0, #KeyV
			ADR		r1, keyv_handler
			MOV		r2, r12		; ptr -> private word
			SWI		XOS_Claim

			LDMIA		sp!, {r0-r12, pc}

finish
			STMDB		sp!, {r0-r12, lr}
			MOV		r0, #KeyV
			ADR		r1, keyv_handler
			MOV		r2, r12
			SWI		XOS_Release

			LDMIA		sp!, {r0-r12, pc}

keyv_handler
			STMDB		sp!, {r0-r12, lr}
			LDR		r3, [r12]	; get private word

			CMP		r0, #KeyV_TransitionUp
			BEQ		key_released
			CMP		r0, #KeyV_TransitionDown
			BNE		exit_keyv_handler
key_pressed
	[ SpecialKeyStandby <> -1
			TEQ		r1, #SpecialKeyStandby
			BEQ		do_power_on
	]

			CMP		r1, #LEFT_CTRL
			CMPNE		r1, #RIGHT_CTRL
			ORREQ		r3, r3, #CTRL_DOWN

			CMP		r1, #LEFT_ALT
			CMPNE		r1, #RIGHT_ALT
			ORREQ		r3, r3, #ALT_DOWN

			B		exit_keyv_handler

key_released
			CMP		r1, #LEFT_CTRL
			CMPNE		r1, #RIGHT_CTRL
			BICEQ		r3, r3, #CTRL_DOWN

			CMP		r1, #LEFT_ALT
			CMPNE		r1, #RIGHT_ALT
			BICEQ		r3, r3, #ALT_DOWN

			CMP		r1, #KBD_DEL
			CMPNE		r1, #KPAD_DEL
			BNE		exit_keyv_handler

			AND		r4, r3, #CTRL_DOWN:OR:ALT_DOWN
			CMP		r4, #CTRL_DOWN:OR:ALT_DOWN
			BNE		exit_keyv_handler ; not a PowerOn
do_power_on
			ADR		r0, callback_handler
			SWI		XOS_AddCallBack


exit_keyv_handler
			STR		r3, [r12]	; stash private word
			LDMIA		sp!, {r0-r12, pc}

callback_handler
			STMDB		sp!, {r0-r12, lr}

			MOV		r0, #0
			SWI		XStandby_State
			TEQ		r0, #0		; 0 = on, non-0 = off
                        BNE             %F01
			SWI		XStandby_Standby
			LDMIA		sp!, {r0-r12, pc}
01
			MOV		r0, #0
			SWI		XStandby_Online
			LDMIA		sp!, {r0-r12, pc}

			END
