; >s.Strings


; ****************************************************
; *
; * Given a number, a conversion routine and a width in characters, return a
; * pointer to a zero-terminated string containing the converted number and
; * padded with spaces upto the specified width.
; *
; * Note: the buffer is in our module workspace, so it has a maximum size as
; * defined in "s.Module".
; *
; * In...
; *   R0	= number to convert
; *   R2	= width of buffer
; *   R10	= conversion SWI to call (i.e. XOS_ConvertHex8)
; *   R12	= module workspace pointer
; *   R13	= full-descending stack pointer
; *   R14	= return address
; * Out...
; *   R0	= pointer to buffer containing string (or pointer to error block)
; *   R1-R13	= preserved
; *   V flag	= set if R0 is a pointer to an error block
; *
NumPad	Entry "R1-R3"
	; Fill the buffer with spaces...
	LDR	R14, =&20202020
	ADD	R1, R12, #WSP_ScratchBuffer
	MOV	R3, R2
00	STR	R14, [R1], #4
	SUBS	R3, R3, #4
	BHI	%BT00
	; Plonk a zero-terminator at the end...
	ADD	R1, R12, #WSP_ScratchBuffer
	MOV	R3, #0
	STRB	R3, [R1, R2]
	; Convert the number into a string...
	SWI	XOS_CallASWI
	MOVVC	R2, #" "
	STRVCB	R2, [R1]
	EXIT


; ****************************************************
; *
; * Convert a 2 MHz timing into a percentage of CPU utilisation and
; * output the result to (upto) four decimal places.
; *
; * Note: values at or above 2,000,000 will display as "100%".
; *
; * In...
; *   R4	= 2 MHz timing
; *   R12	= module workspace pointer
; *   R13	= full-descending stack pointer
; *   R14	= return address
; * Out...
; *   R0-R13	= preserved
; *   V flag	= set if final OS_WriteX call failed
; *
CPUPcnt	Entry "R0-R2"
	LDR	R2, =1000000
	MOV	R1, R4, LSR #1
	CMP	R1, R2
	BHS	MhzFull
	; Tens
	LDR	R2, =100000
	DivRem	R0, R1, R2, R14
	TEQ	R0, #0
	ADDNE	R0, R0, #"0"
	SWINE	XOS_WriteC
	; Units
	LDR	R2, =10000
	DivRem	R0, R1, R2, R14
	ADD	R0, R0, #"0"
	SWI	XOS_WriteC
	TEQ	R1, #0
	BEQ	MhzExit
	; Decimal point
	SWI	XOS_WriteI+"."
	; Tenths
	MOV	R2, #1000
	DivRem	R0, R1, R2, R14
	ADD	R0, R0, #"0"
	SWI	XOS_WriteC
	TEQ	R1, #0
	BEQ	MhzExit
	; Hundredths
	MOV	R2, #100
	DivRem	R0, R1, R2, R14
	ADD	R0, R0, #"0"
	SWI	XOS_WriteC
	TEQ	R1, #0
	BEQ	MhzExit
	; Thousandths
	MOV	R2, #10
	DivRem	R0, R1, R2, R14
	ADD	R0, R0, #"0"
	SWI	XOS_WriteC
	; Ten thousandths
	TEQ	R1, #0
	ADDNE	R0, R1, #"0"
	SWINE	XOS_WriteC
MhzExit SWIVC	XOS_WriteI+"%"
	EXIT
MhzFull	SWI	XOS_WriteS
	DCB	"100%", 0
	ALIGN
	EXIT


; ****************************************************
; *
; * Attempt to convert the specified string into a vector number.
; *
; * In...
; *   R1	= pointer to source string
; *   R12	= module workspace pointer
; *   R13	= full-descending stack pointer
; *   R14	= return address
; * Out...
; *   R0	= preserved (or pointer to error block)
; *   R1	= vector number (or corrupt if error)
; *   R2-R9	= preserved
; *   R10-R12	= corrupted
; *   R13	= preserved
; *   V flag	= set if R0 is a pointer to an error block
; *
Str_to_vector Entry "R0, R2-R3"
	; Copy the source string into our 20 byte buffer. Pad the
	; end of the string with zero bytes upto the following word
	; boundary. If the string doesn't fit, it's not a valid
	; vector name, so we can exit with an error.
	ADD	R0, R12, #WSP_ScratchBuffer
	ADD	R2, R0, #20
	BL	Strcpy_LO
	BVS	%FT10
	ADR	R2, Vector_names_LC
	MOV	R1, #Max_Vector-1
	; R0 - copied input string
	; R1 - current vector number
	; R2 - pointer to vector name table
00	LDR	R3, [R2, R1, LSL #2]
	ADD	R3, R2, R3
	BL	Strcmp
	BEQ	%FT11
	SUBS	R1, R1, #1
	BHS	%BT00
10	; This is not a valid vector number. Generate an error...
	Pull	"R0, R2-R3, LR"
	B	bad_name_err
11	; Found a match, exit with its number...
	EXIT


; ****************************************************
; *
; * Copy a ctrl- or space-terminated string from the specified pointer into
; * the specified buffer. Force the copied string to lower-case. Return flags
; * to indicate success or failure.
; *
; * Internal format is to start the string on a word boundary and terminate
; * it with a zero byte. If there are any further bytes before the next word
; * boundary, place zeros in those. For example...
; *
; *   : 0  1  2  3 : 4  5  6  7 :
; *   :---------------:---------------:
; *   : H  e  l  l : o  0  0  0 :
; *
; * This format allows for fast strcmp type functions.
; *
; * In...
; *   R0	= pointer to destination buffer
; *   R1	= pointer to source string
; *   R2	= size (in bytes) of destination buffer
; *   R13	= full-descending stack pointer
; *   R14	= return address
; * Out...
; *   R0-R13	= preserved
; *   Flags	= EQ string copied OK, NE string didn't fit
; *
Strcpy_LO Entry "R0-R3"
	; Buffer must be a whole number of words and word-aligned...
	TST	R0, #2_11
	BNE	%FT03
	BIC	R2, R2, #2_11
	; String copy routine (byte-by-byte)
00	LDRB	R14, [R1], #1
	CMP	R14, #"A"
	RSBHSS	R3, R14, #"Z"
	ADDHS	R14, R14, #"a"-"A"
	STRB	R14, [R0], #1
	; Is the source string too long?
	CMP	R0, R2
	BHS	%FT03
	; Have we reached the end of the source string?
	CMP	R14, #" "
	BHI	%BT00
	; Make sure it is zero-terminated...
	MOV	R2, #0
	STRB	R2, [R0, #-1]
	; Pad upto next word boundary with zeros...
	ANDS	R1, R0, #2_11
	BEQ	%FT02
	CMP	R1, #2_10
	STRB	R2, [R0], #1
	STRLSB	R2, [R0], #1
	STRLOB	R2, [R0]
02	; Exit with flags EQ if the source string fitted...
	CLRV
	EXIT
03	; Exit with flags NE if we exhaust the input buffer...
	SETV
	EXIT


; ****************************************************
; *
; * Compare two strings (in internal format) and return the result in
; * the processor flags.
; *
; * In...
; *   R0	= pointer to string
; *   R3	= pointer to string
; *   R13	= full-descending stack pointer
; *   R14	= return address
; * Out...
; *   R0-R13	= preserved
; *   Flags	= EQ string match, NE string don't match
; *
Strcmp	Entry "R0-R4"
00	LDR	R1, [R0], #4
	LDR	R2, [R3], #4
	TEQ	R1, R2
	BNE	%FT01
	TST	R1, #0xFF000000
	BNE	%BT00
01	; Exit with flags EQ or NE
	EXIT


; ****************************************************
; *
; * Array of vector name string offsets. For speed reasons, these strings
; * are in the internal format and are already converted into lower-case.
; *
Vector_names_LC
	DCD	Vec00LC - Vector_names_LC
	DCD	Vec01LC - Vector_names_LC
	DCD	Vec02LC - Vector_names_LC
	DCD	Vec03LC - Vector_names_LC
	DCD	Vec04LC - Vector_names_LC
	DCD	Vec05LC - Vector_names_LC
	DCD	Vec06LC - Vector_names_LC
	DCD	Vec07LC - Vector_names_LC
	DCD	Vec08LC - Vector_names_LC
	DCD	Vec09LC - Vector_names_LC
	DCD	Vec0ALC - Vector_names_LC
	DCD	Vec0BLC - Vector_names_LC
	DCD	Vec0CLC - Vector_names_LC
	DCD	Vec0DLC - Vector_names_LC
	DCD	Vec0ELC - Vector_names_LC
	DCD	Vec0FLC - Vector_names_LC
	DCD	Vec10LC - Vector_names_LC
	DCD	Vec11LC - Vector_names_LC
	DCD	Vec12LC - Vector_names_LC
	DCD	Vec13LC - Vector_names_LC
	DCD	Vec14LC - Vector_names_LC
	DCD	Vec15LC - Vector_names_LC
	DCD	Vec16LC - Vector_names_LC
	DCD	Vec17LC - Vector_names_LC
	DCD	Vec18LC - Vector_names_LC
	DCD	Vec19LC - Vector_names_LC
	DCD	Vec1ALC - Vector_names_LC
	DCD	Vec1BLC - Vector_names_LC
	DCD	Vec1CLC - Vector_names_LC
	DCD	Vec1DLC - Vector_names_LC
	DCD	Vec1ELC - Vector_names_LC
	DCD	Vec1FLC - Vector_names_LC
	DCD	Vec20LC - Vector_names_LC
	DCD	Vec21LC - Vector_names_LC
	DCD	Vec22LC - Vector_names_LC
	DCD	Vec23LC - Vector_names_LC
	DCD	Vec24LC - Vector_names_LC
	DCD	Vec25LC - Vector_names_LC
	DCD	Vec26LC - Vector_names_LC
	DCD	Vec27LC - Vector_names_LC
	DCD	Vec28LC - Vector_names_LC
	DCD	Vec29LC - Vector_names_LC
	DCD	Vec2ALC - Vector_names_LC
	DCD	Vec2BLC - Vector_names_LC
	DCD	Vec2CLC - Vector_names_LC
	DCD	Vec2DLC - Vector_names_LC
	DCD	Vec2ELC - Vector_names_LC
	DCD	Vec2FLC - Vector_names_LC
Vec00LC	DCB	"userv", 0
	ALIGN
Vec01LC	DCB	"errorv", 0
	ALIGN
Vec02LC	DCB	"irqv", 0
	ALIGN
Vec03LC	DCB	"wrchv", 0
	ALIGN
Vec04LC	DCB	"rdchv", 0
	ALIGN
Vec05LC	DCB	"cliv", 0
	ALIGN
Vec06LC	DCB	"bytev", 0
	ALIGN
Vec07LC	DCB	"wordv", 0
	ALIGN
Vec08LC	DCB	"filev", 0
	ALIGN
Vec09LC	DCB	"argsv", 0
	ALIGN
Vec0ALC	DCB	"bgetv", 0
	ALIGN
Vec0BLC	DCB	"bputv", 0
	ALIGN
Vec0CLC	DCB	"gbpbv", 0
	ALIGN
Vec0DLC	DCB	"findv", 0
	ALIGN
Vec0ELC	DCB	"readlinev", 0
	ALIGN
Vec0FLC	DCB	"fscv", 0
	ALIGN
Vec10LC	DCB	"eventv", 0
	ALIGN
Vec11LC	DCB	"uptv", 0
	ALIGN
Vec12LC	DCB	"netv", 0
	ALIGN
Vec13LC	DCB	"keyv", 0
	ALIGN
Vec14LC	DCB	"insv", 0
	ALIGN
Vec15LC	DCB	"remv", 0
	ALIGN
Vec16LC	DCB	"cnpv", 0
	ALIGN
Vec17LC	DCB	"ukvdu23v", 0
	ALIGN
Vec18LC	DCB	"ukswiv", 0
	ALIGN
Vec19LC	DCB	"ukplotv", 0
	ALIGN
Vec1ALC	DCB	"mousev", 0
	ALIGN
Vec1BLC	DCB	"vduxv", 0
	ALIGN
Vec1CLC	DCB	"tickerv", 0
	ALIGN
Vec1DLC	DCB	"upcallv", 0
	ALIGN
Vec1ELC	DCB	"changeenvironmentv", 0
	ALIGN
Vec1FLC	DCB	"spritev", 0
	ALIGN
Vec20LC	DCB	"drawv", 0
	ALIGN
Vec21LC	DCB	"econetv", 0
	ALIGN
Vec22LC	DCB	"colourv", 0
	ALIGN
Vec23LC	DCB	"palettev", 0
	ALIGN
Vec24LC	DCB	"serialv", 0
	ALIGN
Vec25LC	DCB	"fontv", 0
	ALIGN
Vec26LC	DCB	"pointerv", 0
	ALIGN
Vec27LC	DCB	"timesharev", 0
	ALIGN
Vec28LC	DCB	"sparevector8", 0
	ALIGN
Vec29LC	DCB	"sparevector7", 0
	ALIGN
Vec2ALC	DCB	"sparevector6", 0
	ALIGN
Vec2BLC	DCB	"sparevector5", 0
	ALIGN
Vec2CLC	DCB	"sparevector4", 0
	ALIGN
Vec2DLC	DCB	"sparevector3", 0
	ALIGN
Vec2ELC	DCB	"sparevector2", 0
	ALIGN
Vec2FLC	DCB	"sparevector1", 0
	ALIGN


; ****************************************************
; *
; * Array of vector name string offsets. These are normal zero-terminated
; * strings, not in the lower-case format.
; *
Vector_names
	DCD	Vec00 - Vector_names
	DCD	Vec01 - Vector_names
	DCD	Vec02 - Vector_names
	DCD	Vec03 - Vector_names
	DCD	Vec04 - Vector_names
	DCD	Vec05 - Vector_names
	DCD	Vec06 - Vector_names
	DCD	Vec07 - Vector_names
	DCD	Vec08 - Vector_names
	DCD	Vec09 - Vector_names
	DCD	Vec0A - Vector_names
	DCD	Vec0B - Vector_names
	DCD	Vec0C - Vector_names
	DCD	Vec0D - Vector_names
	DCD	Vec0E - Vector_names
	DCD	Vec0F - Vector_names
	DCD	Vec10 - Vector_names
	DCD	Vec11 - Vector_names
	DCD	Vec12 - Vector_names
	DCD	Vec13 - Vector_names
	DCD	Vec14 - Vector_names
	DCD	Vec15 - Vector_names
	DCD	Vec16 - Vector_names
	DCD	Vec17 - Vector_names
	DCD	Vec18 - Vector_names
	DCD	Vec19 - Vector_names
	DCD	Vec1A - Vector_names
	DCD	Vec1B - Vector_names
	DCD	Vec1C - Vector_names
	DCD	Vec1D - Vector_names
	DCD	Vec1E - Vector_names
	DCD	Vec1F - Vector_names
	DCD	Vec20 - Vector_names
	DCD	Vec21 - Vector_names
	DCD	Vec22 - Vector_names
	DCD	Vec23 - Vector_names
	DCD	Vec24 - Vector_names
	DCD	Vec25 - Vector_names
	DCD	Vec26 - Vector_names
	DCD	Vec27 - Vector_names
	DCD	Vec28 - Vector_names
	DCD	Vec29 - Vector_names
	DCD	Vec2A - Vector_names
	DCD	Vec2B - Vector_names
	DCD	Vec2C - Vector_names
	DCD	Vec2D - Vector_names
	DCD	Vec2E - Vector_names
	DCD	Vec2F - Vector_names
Vec00	DCB	"UserV", 0
Vec01	DCB	"ErrorV", 0
Vec02	DCB	"IrqV", 0
Vec03	DCB	"WrchV", 0
Vec04	DCB	"RdchV", 0
Vec05	DCB	"CliV", 0
Vec06	DCB	"ByteV", 0
Vec07	DCB	"WordV", 0
Vec08	DCB	"FileV", 0
Vec09	DCB	"ArgsV", 0
Vec0A	DCB	"BGetV", 0
Vec0B	DCB	"BPutV", 0
Vec0C	DCB	"GBPBV", 0
Vec0D	DCB	"FindV", 0
Vec0E	DCB	"ReadLineV", 0
Vec0F	DCB	"FSCV", 0
Vec10	DCB	"EventV", 0
Vec11	DCB	"UPTV", 0
Vec12	DCB	"NETV", 0
Vec13	DCB	"KEYV", 0
Vec14	DCB	"INSV", 0
Vec15	DCB	"REMV", 0
Vec16	DCB	"CNPV", 0
Vec17	DCB	"UKVDU23V", 0
Vec18	DCB	"UKSWIV", 0
Vec19	DCB	"UKPLOTV", 0
Vec1A	DCB	"MouseV", 0
Vec1B	DCB	"VDUXV", 0
Vec1C	DCB	"TickerV", 0
Vec1D	DCB	"UpCallV", 0
Vec1E	DCB	"ChangeEnvironmentV", 0
Vec1F	DCB	"SpriteV", 0
Vec20	DCB	"DrawV", 0
Vec21	DCB	"EconetV", 0
Vec22	DCB	"ColourV", 0
Vec23	DCB	"PaletteV", 0
Vec24	DCB	"SerialV", 0
Vec25	DCB	"FontV", 0
Vec26	DCB	"PointerV", 0
Vec27	DCB	"TimeShareV", 0
Vec28	DCB	"SpareVector8", 0
Vec29	DCB	"SpareVector7", 0
Vec2A	DCB	"SpareVector6", 0
Vec2B	DCB	"SpareVector5", 0
Vec2C	DCB	"SpareVector4", 0
Vec2D	DCB	"SpareVector3", 0
Vec2E	DCB	"SpareVector2", 0
Vec2F	DCB	"SpareVector1", 0
	ALIGN


	END
