;
; 		Copyright 1996 Acorn Network Computing
;
;  This material is the confidential trade secret and proprietary
;  information of Acorn Network Computing. It may not be reproduced,
;  used, sold, or transferred to any third party without the prior
;  written consent of Acorn Network Computing. All rights reserved.
;
; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; SWI decode table

swi_decodetable	= "Cron",0
		= "AddByRealTime",0
		= "RemoveByRealTime",0
		= "AddByUnixTime",0
		= "RemoveByRealTime",0
		= 0

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; SWI handler
;  Entry:
;   r0-r9 depend on SWI
;   r11=swi number within chunk
;   r12=private word pointer
;  Exit:

swi_handler	LDR	wp, [r12]
		CMP	r11, #(%FT12-%FT11)/4
		ADDLO	pc, pc, r11, LSL #2
		B	%FT12
11		; Start of SWI table
		B	SWI_AddByReal
 [ {TRUE}
		B	%FT12
 |
		B	SWI_RemoveByReal
 ]
		B	SWI_AddByUnix
;		B	SWI_RemoveByUnix
12		; End of SWI table
		ENTRY	"r1-r4"
		ADR	r0, %FT20
		MOV	r1, #0
		MOV	r2, #0
		ADR	r4, title
		SWI	XMessageTrans_ErrorLookup
		PullEnv
		ORRS	pc, r14, #SR_V
20
		DCD	&1E6
		= "BadSWI", 0

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; Handler for Cron_AddByReal
;  Entry:
;   r0=flags (type)
;   r1=pointer to 5 byte UTC
;   r2=address of function/command line
;   r3=r0 value for function
;   r4=r12 value for function
;   wp=private word
;  Exit:

SWI_AddByReal	ENTRY	"r1-r7"
		MOV	r5, r0
		! 0, "Check flags"
		MOV	r6, r3
		MOV	r7, r2
 [ Type_Mask = 1 :LAND: Type_Func = 0 :LAND: Type_Cmd = 1
		TST	r0, #Type_Mask
		MOVEQ	r3, #ss_size
		BEQ	%FT20			; For function
		MOV	r6, #0
10		LDRB	r0, [r2, r6]		; Get a byte
		ADD	r6, r6, #1		; Incr index
		TST	r0, r0			; Check for zero byte
		BNE	%BT10			; Repeat if non-zero
		ADD	r3, r6, #:INDEX: ss_cmdline
 |
		! 1, "Type changed"
 ]
20		MOV	r0, #ModHandReason_Claim
		SWI	XOS_Module
		BVS	%FT90
		MOV	r0, r4
		MOV	ssp, r2
		; r0=funcr12, r1=utc ptr, r4=ssp=block
		; r5=flags, r6=funcr0/cmd len, r7=(func/cmd) addr
		MOV	r2, #0
		STR	r2, ss_cldate
		STR	r2, ss_cltime
		LDR	r2, [r1]		; Get low time word
		STR	r2, ss_utctime		; Store it
		LDRB	r2, [r1, #4]		; Get high byte
		AND	r1, r5, #Type_Mask
		ORR	r2, r2, r0, ASL #TypeShift
		STR	r2, ss_flags		; Store with flags
 [ Type_Mask = 1 :LAND: Type_Func = 0 :LAND: Type_Cmd = 1
		TST	r5, #Type_Mask		; Store function params
		BEQ	%FT30
		STR	r7, ss_funcaddr
		STR	r6, ss_funcr0
		STR	r0, ss_funcr12
		B	%FT50

30		ADR	r2, ss_cmdline		; Copy command line
40		SUBS	r6, r6, #1		; (Backwards of course)
		LDRB	r0, [r7, r6]
		STRB	r0, [r2, r6]
		BNE	%BT40
 |
		! 1, "Type changed"
 ]
50		BL	AddToList
		EXITS	VC
		MOV	r1, r0			; Problem.
		MOV	r2, ssp
		MOV	r0, #ModHandReason_Free
		SWI	XOS_Module
		MOV	r0, r1
90
		PullEnv
		ORRS	pc, lr, #SR_V

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; Handler for Cron_AddByUnix
;  Entry:
;   r0=flags
;   r1=4 byte UTC
;   r2=address of function/command line
;   r3=r0 value for function
;   r4=r12 value for function
;   wp=private word
;  Exit:

SWI_AddByUnix	ENTRY	"r1,r5",8
		MOV	r5, #0
		ADDS	r1, r1, r1, LSL #2	; Mul 5 (1)
		ADC	r5, r5, r1, LSR #30	; Add remainder
		ADD	r5, r5, r5, LSL #2	; Mul 5 high word
		ADDS	r1, r1, r1, LSL #2	; Mul 5 low word
		ADC	r5, r5, r1, LSR #30	; Add remainder
		MOV	r5, r5, LSL #2		; Mul 4 high word
		ADD	r5, r5, r1, LSR #30	; Shift bits from low word
		MOV	r1, r1, LSL #2		; Mul 4 low word
		STMIA	r13, {r1,r5}		; Store
		MOV	r1, r13			; Get pointer
		BL	SWI_AddByReal		; Call real swi handler
		EXITS	VC
		EXIT	VS

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

		END
