        AREA RMASPY, CODE


        EXPORT EntryInterceptAddress
        EXPORT ExitInterceptAddress
        EXPORT RealEntryPointHolder
        EXPORT RealExitPointHolder

;  /* beware: random choice, NOT registered with Acorn */
RMASpy_Entry           EQU    &d5540
RMASpy_Exit            EQU    &d5544
OS_Module              EQU    &1E



EntryInterceptAddress  STMDB  R13!, {R14}
                       SWI  RMASpy_Entry
                       LDMIA  R13!, {R14}
                       LDR  PC, RealEntryPointHolder
RealEntryPointHolder   DCD  0


ExitInterceptAddress   STMDB  R13!, {R4, R5, R6, R14}
                       LDR  R5, [R13, #28]       ;if this is a Claim call from _kernel_RMAalloc
                                                 ; inside the shared C Library, then this will
                                                 ; be the return address from malloc (..)
                                                 ;    or ...
                                                 ;if this is a Free call from _kernel_RMAfree
                                                 ; inside the shared C Library, then this will
                                                 ; be the return address from free (..)
                       LDR  R6, [R13, #20]       ;    and ...
                                                 ;if this is a Claim or Free call as above,
                                                 ; R6 will address a backtrace structure
                       MOV R4, R14
                       BIC  R14, R11, #&FF000000
                       BIC  R14, R14, #&00020000
                       CMP  R14, #OS_Module
                       SWIEQ  RMASpy_Exit
                       LDMIA  R13!, {R4, R5, R6, R14}
                       TST  R14, #&10000000
                       LDR PC, RealExitPointHolder
RealExitPointHolder    DCD  0


        END
