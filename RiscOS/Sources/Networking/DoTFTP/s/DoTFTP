        TTL     > DoTFTP - Psuedo filing system to boot from TFTP

; ***********************************
; ***    C h a n g e   L i s t    ***
; ***********************************

; Date       Name  Version Description
; ----       ----  ------- -----------
; 20-Sep-99  KJB    0.01   Created

        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:Services
        GET     Hdr:Proc
        GET     Hdr:FSNumbers
        GET     Hdr:HighFSI
        GET     Hdr:LowFSI
        GET     Hdr:NewErrors
        GET     Hdr:MsgTrans

        GET     VersionASM

; Temporary - proper assignment coming later
fsnumber_tftp * 162

        AREA    |!!!Module|, CODE, READONLY

Module_Base
        &       0
        &       Init - Module_Base
        &       Die - Module_Base
        &       Service - Module_Base
        &       Title - Module_Base
        &       Help - Module_Base
        &       0

Title
        =       "DoTFTP", 0
Help
        =       "DoTFTP", 9, 9, "$Module_MajorVersion ($Module_Date)"
      [ Module_MinorVersion <> ""
        =       " $Module_MinorVersion"
      ]
        =       0
FSStartup
        =       "Acorn "
FSName  =       "TFTP", 0
        ALIGN

Init
        Entry
        MOV     R0, #FSControl_AddFS
        ADR     R1, Module_Base
        MOV     R2, #FSBlock - Module_Base
        MOV     R3, #0
        SWI     XOS_FSControl
        EXIT

Die
        Entry
        MOV     R0, #FSControl_RemoveFS
        ADR     R1, FSName
        SWI     XOS_FSControl
        EXITS

ServiceTable
        DCD     0
        DCD     ServiceEntry
        DCD     Service_FSRedeclare

        DCD     ServiceTable - Module_Base
Service
        MOV     R0, R0
        TEQ     R1, #Service_FSRedeclare
        MOVNES  PC, LR
ServiceEntry
        Entry   "R0-R6"
        BL      Init
        EXITS

FSBlock
        &       FSName - Module_Base
        &       FSStartup - Module_Base
        &       0
        &       0
        &       0
        &       0
        &       0
        &       0
        &       fsnumber_tftp
        &       FSEntry_Func - Module_Base
        &       0

FSEntry_Func
        TEQ     R0, #fsfunc_ShutDown
        MOVEQS  PC, LR
        TEQ     R0, #fsfunc_Bootup
        BEQ     FSEntry_Func_Bootup

BadFSOp
        Entry   "R1"
        ADR     R0, ErrorBlock_BadFilingSystemOperation
        ADR     R1, FSName
        BL      copy_error_one
        EXIT

        MakeErrorBlock  BadFilingSystemOperation

FSEntry_Func_Bootup
        Entry   "R0"
        ADR     R0, TFTPCmd
        SWI     XOS_CLI
        STRVS   R0, [R13]
        EXIT

TFTPCmd
        =       "TFTPObey <Inet$BootServer> <Inet$BootFile>", 0


; In:   r0 -> Error block containing the token
;       r1 -> %0 parameter to substitute

; Out:  r0 -> Translated error block or another error (token no found etc.)
;       All other registers preserved, V always set, other flags undefined

copy_error_one Entry "r2-r7"
        MOV     r4, r1                          ; Move input %0
        MOV     r1, #0                          ; Global messages
        MOV     r2, #0                          ; Use MessageTrans buffer
        MOV     r5, #0                          ; No %1
        MOV     r6, #0                          ; No %2
        MOV     r7, #0                          ; No %3
        SWI     XMessageTrans_ErrorLookup       ; Always sets the V flag

        MOV     r1, r4                          ; Preserve input r1
        EXIT

        END
