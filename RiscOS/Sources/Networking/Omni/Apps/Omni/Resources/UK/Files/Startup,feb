| > <Omni$Path>Startup
|
| Append this file with commands to load protocol modules.
|
| Program configuration (see manual for details).
UnSet Omni$Error
Set Omni$Options 	a s1 d1
Set Omni$User 		user
|
| Protocol module selection - set to 0 to stop a protocol module loading.
SetEval Omni$LanMan	1
SetEval	Omni$Net	1
SetEval	Omni$Share	1
SetEval	Omni$NFS	1
SetEval	Omni$Apple	0
|
| NFS protocol stack (at least DCI-2 Internet)
If Omni$NFS 	then RMEnsure Internet 	0.00 Set Omni$Error NFS requires an Internet stack
If Omni$NFS 	then RMEnsure Internet 	0.00 SetEval Omni$NFS 0
If Omni$NFS 	then If "<NFS$TruncateLength>" = "" Then Set NFS$TruncateLength 30
If Omni$NFS 	then Set NFSFiler$Running Yes
If Omni$NFS 	then RMEnsure NFS  	2.30 RMLoad <Omni$Dir>.RMStore.NFS
If Omni$NFS 	then RMEnsure OmniNFS  	0.64 RMLoad <Omni$Dir>.RMStore.OmniNFS
|
| LanManager protocol stack (DCI-4 Internet for TCP/IP, or Mbuf Manager for NetBEUI)
If Omni$LanMan 	then RMEnsure Internet 	4.03 Set Omni$Error LanManager TCP/IP requires a DCI4 Internet stack
If Omni$LanMan 	then RMEnsure Internet 	4.03 SetEval Omni$LanMan 0
If Omni$LanMan 	then RMEnsure MbufManager 0.13 Set Omni$Error LanManager requires a DCI4 MbufManager
If Omni$LanMan 	then RMEnsure MbufManager 0.13 SetEval Omni$LanMan 0
|If Omni$LanMan  then Set LanMan$NameServer 127.0.0.1
If Omni$LanMan 	then RMEnsure LanManFS 	1.80 Run <Omni$Dir>.RMStore.LanManFS
|
| AUN/Econet protocol stack (may be true Econet, or Internet/InternetA-based)
If Omni$Net	then RMEnsure NetFS 	0.00 RMEnsure Net 0.00 Set Omni$Error NetFS requires Econet hardware or AUN
If Omni$Net	then RMEnsure NetFS 	0.00 RMEnsure Net 0.00 SetEval Omni$Net 0
If Omni$Net 	then RMEnsure NetFiler 	1.14 RMLoad <Omni$Dir>.RMStore.NetFiler
|
| Access/Access+ protocol stack (at least DCI-2 InternetA)
If Omni$Share	then RMEnsure Internet 	0.00 RMEnsure InternetA 0.00 Set Omni$Error Access requires AUN or an Internet stack
If Omni$Share	then RMEnsure Internet 	0.00 RMEnsure InternetA 0.00 SetEval Omni$Share 0
If Omni$Share	then Run <Omni$Path>StartShare
|
| AppleTalk protocol stack (at least Mbuf Manager)
If Omni$Apple 	then RMEnsure MbufManager 0.13 Set Omni$Error Access requires a DCI4 MbufManager
If Omni$Apple 	then RMEnsure MbufManager 0.13 SetEval Omni$Apple 0
If Omni$Apple 	then RMEnsure UtilityModule 3.70 RMEnsure CallASWI 0.01 RMLoad System:Modules.CallASWI
If Omni$Apple	then RMEnsure AppleTalk	0.33 RMLoad <Omni$Dir>.RMStore.AppleTalk
|
| Append new protocol module commands here:

|
| UnSet our temporary variables
UnSet Omni$NFS
UnSet Omni$LanMan
UnSet Omni$Net
UnSet Omni$Share
UnSet Omni$Apple
