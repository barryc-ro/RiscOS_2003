; Title:   s.ExtLibAsm
; Purpose: Extensions in assembler
;

        GET     hdr.regdefs
        GET     hdr.swinos
        GET     hdr.macros

        AREA	ExtLibAsm, CODE, READONLY
	IMPORT	taskhandle

; Issue Service_OmniAction on a transient callback

	EXPORT	serviceomni_delay

serviceomni_delay
	STMFD	sp!, {lr}
	MOV	a2, a1			; save R0 parameter (called as r12)
	ADR 	a1, serviceomni_cb
	SWI	OS_AddCallBack + XOS_Bit
        LDMFD	sp!, {pc}

serviceomni_cb
	STMFD	sp!, {a1-a2, lr}
	MOV	a1, ip			; issue service call
	MOV	a2, #Service_OmniAction
        SWI	OS_ServiceCall + XOS_Bit
        LDMFD	sp!, {a1-a2, pc}

; Wimp_SpriteReadInfo

        EXPORT  Wimp_SpriteReadInfo

Wimp_SpriteReadInfo
        STMFD   sp!, {a2, v1, v2, v3, lr}
        MOV     a3, a1
        MOV     a1, #40
        SWI     Wimp_SpriteOp + XOS_Bit
        LDMFD   sp!, {a2}
        STMVCIA a2, {a4, v1, v2, v3}
        MOVVC   a1, #0
        LDMFD   sp!, {v1, v2, v3, pc}^

; RMA alloc/dealloc routines

	EXPORT	rma_alloc
	EXPORT	rma_free

rma_alloc
	STMFD	sp!, {lr}
	MOV	a4, a1
	MOV	a1, #6
	SWI	OS_Module + XOS_Bit
	MOVVS	a1, #0
	MOVVC	a1, a3
	LDMFD	sp!, {pc}

rma_free
	STMFD	sp!, {lr}
	MOV	a3, a1
	MOV	a1, #7
	SWI	OS_Module + XOS_Bit
	LDMFD	sp!, {pc}

; SetVar_Code - sets up/removes a code system variable

        EXPORT  SetVar_Code
	IMPORT	Image__Var_State

SetVar_Code
        STMFD   sp!, {v1, lr}
	MOV	a3, a1			; r0 = -ve on entry to remove variable
	STR	a2, var_taskhandle	; r1 = address of taskhandle int
	ADR	a1, var_name
	ADR	a2, var_code
	MOV	a4, #0
	MOV	v1, #16
	CMP	a3, #0
	MOVGE	a3, #var_end-var_code
	SWI	OS_SetVarVal + XOS_Bit
        LDMFD   sp!, {v1, pc}^
var_name
	=	"Omni$Running", 0
	ALIGN

; var_code - evaluates the system variable

var_code
	B	var_write		; write entry point
	LDR	a1, var_taskhandle	; read entry point
	LDR	a1, [a1]
	CMP	a1, #0
	MOVGT	a2, #"1"
	MOVLE	a2, #"0"
	LDR	a1, var_state
	STRB	a2, [a1, #0]
	MOV	a3, #1
var_write
	MOVS	pc, lr
var_taskhandle
	DCD	0
var_state
	DCD	Image__Var_State
var_end

        END
