; File		: veneer.s
; Purpose	: Veneers to call URI handler SWIs from C
; Author	: Ben Laughton
; History	: 17/04/1997

	GET	hdr:ListOpts
	GET	hdr:Macros
	GET	hdr:System

	GET	hdr.URI

	AREA	|C$$code|, CODE, READONLY

	EXPORT	|uri_version|
	EXPORT	|uri_dispatch|
	EXPORT	|uri_request_uri|
	EXPORT	|uri_invalidate_uri|

|uri_version|
	Push	"lr"
	SWI	XURI_Version
	Pull	"pc", VS
	TEQ	r1, #0
	STRNE	r0, [r1]
	MOV	r0, #0
	Pull	"pc"

|uri_dispatch|
	Push	"r3-r5, lr"
	SWI	XURI_Dispatch
	Pull	"r3-r5, pc", VS
	ADD	r5, sp, #16
	LDMFD	r5, {r4-r5}
	TEQ	r5, #0
	STRNE	r3, [r5]
	TEQ	r4, #0
	STRNE	r2, [r4]
	Pull	"r3"
	TEQ	r3, #0
	STRNE	r0, [r3]
	MOV	r0, #0
	Pull	"r4-r5, pc"

|uri_request_uri|
	Push	"r4, lr"
	SWI	XURI_RequestURI
	Pull	"r4, pc", VS
	ADD	r4, sp, #8
	LDMFD	r4, {r4}
	TEQ	r4, #0
	STRNE	r2, [r4]
	MOV	r0, #0
	Pull	"r4, pc"

|uri_invalidate_uri|
	Push	"lr"
	MOV	r3, r1
	SWI	XURI_InvalidateURI
	MOV	r0, #0
	Pull	"pc"

	END



