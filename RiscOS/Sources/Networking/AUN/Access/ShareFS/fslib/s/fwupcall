;
; Copyright (c) 1995 ANT Ltd., Cambridge, England
;
; :RCS Log discontinued:
; Revision 1.1  95/01/04  19:08:36  kwelton
; Initial revision
;

        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:APCS.<APCS>

	AREA |C$$code|, CODE, READONLY

	DCB	"fwupcall"
	DCB	&00,&00,&00,&00
	DCD	&ff00000C

	IMPORT	|_Lib$Reloc$Off$DP|
	IMPORT	|fwupcall_handler|

	EXPORT	fwupcall
fwupcall
	TEQ	a1,#12
        Return  ,LinkNotStacked,NE
        FunctionEntry "a1-a4,v1-v6,sl,fp"
	MOV	a1,sp
        [ No26bitCode :LOR: :LNOT: No32bitCode
        MRS     v3,CPSR
        TST     v3,#&1C                 ; Z set if in 26-bit mode
        BIC     v4,v3,#&1F
        ORREQ   v4,v4,#SVC26_mode
        ORRNE   v4,v4,#SVC32_mode
        MSR     CPSR_c, v4              ; switch mode
        |
	MOV	v3,pc
	BIC	a4,v3,#3
	TEQP	a4,#3
	MOV	r0, r0			; NOP to remove assembler warning
        ]
	MOV	fp,#0
	MOV	v4,lr
	MOV	sl,sp,LSR #20
	MOV	sl,sl,LSL #20
	LDMIA	sl,{v1,v2}
	MOV	a2,ip
	LDR	ip,[ip]
	LDMIB	ip,{fp,ip}
	STMIA	sl,{fp,ip}
	MOV	fp,#0
	DCD	|_Lib$Reloc$Off$DP| + &E28AA000 ;ADD	sl,sl,#xxx
	BL	fwupcall_handler
	DCD	|_Lib$Reloc$Off$DP| + &E24AA000	;SUB	sl,sl,#xxx
	STMIA	sl,{v1,v2}
	MOV	lr,v4                   ; Restore SVC mode lr (why??)
        [ No26bitCode :LOR: :LNOT: No32bitCode
        MSR     CPSR_c, v3              ; Restore entry mode
        |
	TEQP	v3,#0
	CMP	a1,#0
        ]
        Return  "a1-a4,v1-v6,sl,fp,lr",,EQ      ; pass on upcall
        Return  "a1-a4,v1-v6,sl,fp",,NE         ; claim upcall

	END

; EOF fwupcall.s
