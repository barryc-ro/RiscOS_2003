
        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:APCS.<APCS>
        GET     Hdr:ModHand

STACK_SIZE * 8192


	IMPORT	|Image$$RO$$Base|
	IMPORT	|_Lib$Reloc$Off$DP|
	IMPORT	tserver

	EXPORT	|_entermodule|

	AREA	|_entermodulearea|,REL,CODE,READONLY

        ; Entered in USR26 or USR32 mode
|_entermodule|
	LDR	r12,[r12]	;Workspace ptr from private word
	LDMIB	r12,{v2,v3}	;Module relocations
	MOV	r0,#ModHandReason_Claim
	MOV	r3,#STACK_SIZE
	SWI	OS_Module
	MOV	sl,r2
	ADD	sp,sl,r3
	STMIA	sl,{v2,v3}

	; This is equivalent of 'ADD r10, r10, #0' + |_Lib$Reloc$Off$DP|
	DCD	|_Lib$Reloc$Off$DP| + &E28AA000

	MOV	fp,#0
	BL	tserver

	; This is equivalent of 'SUB r10, r10, #0' + |_Lib$Reloc$Off$DP|
	DCD	|_Lib$Reloc$Off$DP| + &E24AA000

	MOV	v1,r0

	MOV	r2,sl
	MOV	r0,#7
	SWI	XOS_Module
	MOV	r0,#0

	MOV	r0,v1
	SWI	OS_Exit

	END

; EOF cstart.s
