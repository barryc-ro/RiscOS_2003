        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:Machine.<Machine>
        GET     Hdr:Debug
        GET     Hdr:Proc
        GET     Hdr:CMOS
        GET     Hdr:ModHand
        GET     Hdr:Services
        GET     Hdr:MsgTrans
        GET     Hdr:FSNumbers
        GET     Hdr:NewErrors

        AREA	microdelay, PIC, CODE, REL

        EXPORT  DoMicroDelay

; Delay a specified number of 1/2 microsecond units.
; Code courtesy of Tim Dobson.
;
; Entry:
;   R0 = number of 1/2 microsecond units to wait
;   MODE: SVC or IRQ
;   IRQ state: undefined
;
; Exit:
;   R0 undefined
;   All other registers preserved
;
; C prototype: void DoMicroDelay (unsigned int)

DoMicroDelay

        Push    "R1,R2,LR"

        MOV     R2,#IOC                 ; R2 -> IOC
        STRB    R0,[R2,#Timer0LR]       ; copies counter to output latch
        LDRB    R1,[R2,#Timer0CL]       ; R1 = low output latch

; loop waiting for counter to change (decremented at 2MHz)
10
        STRB    R0,[R2,#Timer0LR]       ; copies counter to output latch
        LDRB    LR,[R2,#Timer0CL]       ; LR = low output latch
        TEQS    R1,LR                   ; has counter changed?
        BEQ     %BT10                   ; else wait for it to change

; counter has changed, decrement our count of ticks

        MOV     R1,LR                   ; update copy of counter
        SUBS    R0,R0,#1                ; decrement ticks
        BNE     %BT10                   ; ...and continue if not done

; delay has expired

        Pull    "R1,R2,PC",,^

        END
