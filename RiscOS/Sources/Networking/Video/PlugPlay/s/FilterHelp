; File:    FilterHelp.s
;          (C) 2000 Pace Micro Technology PLC
;          All rights reserved
;
; Purpose: Set the environment up so a C function can be called from a
;          piece of filter handler code.
;
; Author:  K.Bracey.
;
; History: 28-Apr-2000 (KJB): Created.

        EXPORT  filterhelp_veneer
        IMPORT  filter_code

        AREA    FilterHelp,CODE,PIC,READONLY

filterhelp_veneer
        STMFD   sp!,{a1-a4,sl,fp,lr}

        MOV     fp, #0                  ; frame pointer 0 to terminate backtrace

        MOV     sl, sp, LSR #20         ; stack limit to megabyte boundary
        MOV     sl, sl, LSL #20

        LDR     a2, [r12]               ; get pointer to workspace

        LDMIB   a2, {a1, a2}            ; get offset pointery things
        LDMIA   sl, {a3, a4}            ; remember bottom of stack

        STMFD   sp!, {a3, a4}

        STMIA   sl, {a1, a2}            ; put our magic stuff in

        ADD     sl, sl, #540            ; magic number

        BL      filter_code

        LDMFD   sp!, {a1, a2}           ; put old stuff back
        SUB     sl, sl, #540
        STMIA   sl, {a1, a2}

        LDMFD   sp!, {a1-a4,sl,fp,pc}

        END
