# Generate an assembler header file containing ApplicationName and
# ApplicationVersion using information in VersionNum for versioning
# and Messages for the application name. Launch via.:
#
#   awk -f AppName [LDIR:Messages] [VersionNum] > [Assembler]
#
# 24-Jun-1997 (BAL): Made generic for toolbox apps. Extracts app name
#                    from _TaskName tag and gets version number and
#                    date from Version tag.
#
# 26-Apr-2000 (ADH): Since version information comes from VersionNum
#                    now, with AddToMsgs adding it to the Messages
#                    file, this script gets the version from there
#                    too. Messages is used for the task name. Using
#                    BAL's different approach this time.

# From Version Num

/#define Module_MajorVersion_CMHG/ { version = $3; }
/#define Module_Date_CMHG/         { date    = $3 " " $4 " " $5; }

# From Messages

/_TaskName/  { name = substr ($1, index ($1, ":") + 1); }

END {
    printf("; AppName - generated from VersionNum and Messages, used by ModuleWrap\n")
    printf("\n")
    printf("                   GBLS    ApplicationName\n")
    printf("                   GBLS    ApplicationVersion\n")
    printf("\n")
    printf("ApplicationName    SETS    \"%s\"\n", name)
    printf("ApplicationVersion SETS    \"%s (%s)\"\n", version, date)
    printf("\n")
    printf("                   END\n")
}
