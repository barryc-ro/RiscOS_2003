| !Run file for PlugPlay (RAM module)
| (C) Pace Micro Technologies plc 2000
|
| Last modified: 28-Apr-2000 (ADH)

Set      PlugPlay$Dir           <Obey$Dir>
Set      PlugPlay$Path          <PlugPlay$Dir>.,Resources:$.Resources.PlugPlay.

| Plug-in support

Set      File$Type_BF8          MPEG
Set      Plugin$Type_BF8        PlugPlay
Set      Plugin$About_BF8       PlugPlay:About
SetMacro Alias$@PluginType_BF8  PlugPlay:!Run

| URI handler support

Set Alias$Open_URI_rtsp         PlugPlay:!Run
Set Alias$Open_URI_mpegv        PlugPlay:!Run
Set Alias$Open_URI_mpeg1ss      PlugPlay:!Run
Set Alias$Open_URI_mpeg2ts      PlugPlay:!Run

| Support the ANT URL protocol

SetMacro Alias$URLOpen_RTSP     Run PlugPlay:!Run -url %%*0
SetMacro Alias$URLOpen_MPEGV    Run PlugPlay:!Run -url %%*0
SetMacro Alias$URLOpen_MPEG1SS  Run PlugPlay:!Run -url %%*0
SetMacro Alias$URLOpen_MPEG2TS  Run PlugPlay:!Run -url %%*0

| Routine actions...

IconSprites PlugPlay:!Sprites

| Can't do much if we haven't got various key system resources...

If "<System$Path>"       = "" Then Set System$Path_Message System resources not found.
If "<InetDBase$Path>"    = "" Then Error Internet resources not found.
If "<Inet$MimeMappings>" = "" Then Set Inet$MimeMappings InetDBase:Mimemap

| Ensure specific versions of Toolbox modules (or later ones)
| are present

RMEnsure Toolbox        1.36 RMLoad System:Modules.Toolbox.Toolbox
RMEnsure Toolbox        1.36 Error PlugPlay requires version 1.36 or later of the Toolbox module
RMEnsure Window         1.54 RMLoad System:Modules.Toolbox.Window
RMEnsure Window         1.54 Error PlugPlay requires version 1.54 or later of the Window module

| Important networking modules

RMEnsure UDPV           1.15 RMLoad System:Modules.Network.UDPV
RMEnsure UDPV           1.15 Error PlugPlay requires version 1.15 or later of the UDPV module
RMEnsure SVBmip         1.05 RMLoad System:Modules.Network.SVBmip
RMEnsure SVBmip         1.05 Error PlugPlay requires version 1.05 or later of the SVBmip module
RMEnsure URL_Fetcher    0.45 RMLoad System:Modules.Network.URL
RMEnsure URL_Fetcher    0.45 Error PlugPlay requires version 0.45 or later of the URL module
RMEnsure CheckURL       0.07 RMLoad System:Modules.Network.CheckURL
RMEnsure CheckURL       0.07 Error PlugPlay requires version 0.07 or later of the CheckURL module
RMEnsure VideoControl   0.20 RMLoad System:Modules.VideoCtrl.VideoCtrl
RMEnsure VideoControl   0.20 Error PlugPlay requires version 0.20 or later of the VideoCtrl module
RMEnsure RTSPOVS        0.14 RMLoad System:Modules.VideoCtrl.RTSPOVS
RMEnsure RTSPOVS        0.14 Error PlugPlay requires version 0.14 or later of the RTSPOVS module
RMEnsure SVBGeneric     0.09 RMLoad System:Modules.VideoCtrl.SVBGeneric
RMEnsure SVBGeneric     0.09 Error PlugPlay requires version 0.09 or later of the SVBGeneric module
RMEnsure LVFGeneric     0.03 RMLoad System:Modules.VideoCtrl.LVFGeneric
RMEnsure LVFGeneric     0.03 Error PlugPlay requires version 0.03 or later of the LVFGeneric module

| TaskModule for catching service calls (not required for
| some UIs, so let it fail silently); also the URI handler,
| which may be present but can do without it.

RMEnsure AcornURI       0.12 X RMLoad System:Modules.Network.URI
RMEnsure TaskModule     0.11 RMLoad System:Modules.TaskModule

| Both of the above start Wimp task sides, so want to do a
| '*Desktop' when inside the application after Wimp_Initialise
| to start those tasks. Set a system variable if this is NOT
| needed.

Set PlugPlay$IssueDesktopCommand "yes"
RMEnsure AcornURI   0.12 Set PlugPlay$IssueDesktopCommand "no"
RMEnsure TaskModule 0.11 Set PlugPlay$IssueDesktopCommand "no"

| Routine actions...

IconSprites PlugPlay:!Sprites

| Set the slot size and run the application.

WimpSlot -Min 64K
WimpSlot -Min 32K -Max 32K

RMEnsure !PlugPlay 0.00 RMLoad PlugPlay:!PlugPlay
Desktop_PlugPlay %*0
