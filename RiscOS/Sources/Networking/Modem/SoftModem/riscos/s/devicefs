	AREA	DeviceFSCode,CODE,READONLY

	EXPORT	register_with_devicefs
	EXPORT	deregister_from_devicefs
	EXPORT	device_handle

;
; _kernel_oserror *register_with_devicefs(void)
;
register_with_devicefs
	Entry	"R4-R7"
	MOV	R0,#2			; Full duplex character device
	ADR	R1,device_list
	ADR	R2,device_entry
	LDR	R3,CARD_BASE_adcon
	LDR	R3,[R3]			; Is this needed?
	LDR	R4,private_word_adcon
	LDR	R4,[R4]
	MOV	R5,#0			; No validation string...
	MOV	R6,#1			; 1 input stream
	MOV	R7,#1			; 1 output stream
	SWI	XDeviceFS_Register
	EXITS	VS
	LDR	R1,device_handle_adcon
	STR	R0,[R1]
	EXITS

;
; _kernel_oserror *deregister_from_devicefs(void)
;
deregister_from_devicefs
	Entry
	LDR	R0,device_handle_adcon
	LDR	R0,[R0]
	SWI	XDeviceFS_Deregister
	MOVVC	R0,#0
	EXITS

;
; Our DeviceFS entry point. On entry R0=reason code
;
device_entry
	CMP	R0,#12
	ADDLO	PC,PC,R0,LSL #2
	MOVS	PC,LR			; Is this correct???
	B	Device_Initialise
	B	Device_Finalise
	B	Device_WakeUpTX
	B	Device_WakeUpRX
	B	Device_SleepRX
	MOVS	PC,LR			; Device_EnumDir
	B	Device_CreateTXBuffer
	B	Device_CreateRXBuffer
	B	Device_Halt
	B	Device_Resume
	MOVS	PC,LR			; Device_EndOfData
	B	Device_StreamCreated

	EXITS

device_list
	DCD	device_name
	DCD	1			; Buffered, no "SoftModem$Path"
	DCD	0			; RX flags
	DCD	256			; RX buffer size
	DCD	0			; TX flags
	DCD	256			; TX buffer size
	DCD	0			; end of table

device_name
	=	"SoftModem", 0
	ALIGN

device_handle_adcon
	DCD	device_handle

private_word_adcon
	IMPORT	private_word
	DCD	private_word

	AREA	DeviceFSData,DATA
device_handle
	DCD	0

	END
