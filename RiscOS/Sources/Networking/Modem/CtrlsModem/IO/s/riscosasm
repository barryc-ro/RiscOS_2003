	GET	Hdr:ListOpts
	GET	Hdr:Macros
	GET	Hdr:System

CARD_BASE * &88000000

	AREA	|ObjAsm$$Code|,CODE,READONLY,PIC

	EXPORT	irqs_off[LEAF]
irqs_off
	AND	R0,LR,#I_bit
	ORRS	PC,LR,#I_bit

	EXPORT	irqs_on[LEAF]
irqs_on
	AND	R0,LR,#I_bit
	BICS	PC,LR,#I_bit

	EXPORT	irqs_restore[LEAF]
irqs_restore
	BIC	LR,LR,#I_bit
	ORRS	PC,LR,R0


	EXPORT	call_buffermanager
call_buffermanager
	STMFD	R13!,{R4,R5,LR}
	MOV	R4,R0
	MOV	R5,R1
	MOV	R12,R2
	LDMIA	R4!,{R0-R3}
	MOV	LR,PC
	MOV	PC,R5
	STMDB	R4,{R2-R3}
	MOV	R0,PC
	LDMFD	R13!,{R4,R5,PC}^

	EXPORT	user_mode_do_nothing
user_mode_do_nothing
	STMFD 	r13!,{lr}

	; Execute a SWI in user mode to trigger callbacks
	TEQP 	pc, #0			; To user mode
	MOV 	r0, #0
	MOV 	r1, #1
	SWI 	XOS_Byte		; Read OS version
	SWI 	XOS_EnterOS		; Back to SVC mode

	LDMFD 	r13!,{pc}^

	EXPORT	enable_iomd_interrupts
enable_iomd_interrupts
	TEQP	pc, #I_bit:OR:F_bit:OR:SVC_mode
	MOV	r0, #IOC
	LDR	r1, [r0, #IOCIRQMSKB]
	ORR	r1, r1, #podule_IRQ_bit
	STR	r1, [r0, #IOCIRQMSKB]
	MOVS	pc, lr

	EXPORT	disable_iomd_interrupts
disable_iomd_interrupts
	TEQP	pc, #I_bit:OR:F_bit:OR:SVC_mode
	MOV	r0, #IOC
	LDR	r1, [r0, #IOCIRQMSKB]
	BIC	r1, r1, #podule_IRQ_bit
	STR	r1, [r0, #IOCIRQMSKB]
	MOVS	pc, lr

	END
