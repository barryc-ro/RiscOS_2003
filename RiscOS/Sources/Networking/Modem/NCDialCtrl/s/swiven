
        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:ModHand
        GET     Hdr:APCS.<APCS>

        AREA |Veneers$$code|, CODE, READONLY, PIC

        MACRO
        QUICKVENEER $name, $regs, $swiname
        EXPORT $name
$name
        FunctionEntry $regs
        SWI $swiname
        Return $regs
        MEND

XNCAccessManager_Enquiry * &6F000
XNCRegistry_Enquiry      * &6D382

        QUICKVENEER MAM_Enq, "a2-a3", XNCAccessManager_Enquiry
        QUICKVENEER ncregenq, "a2-a3", XNCRegistry_Enquiry
        QUICKVENEER callback, "a1-a2", XOS_AddCallBack
        QUICKVENEER callafter, "a1-a3", XOS_CallAfter
        QUICKVENEER removeticker, "a1-a2", XOS_RemoveTickerEvent
        QUICKVENEER monotonic_time, ,XOS_ReadMonotonicTime
        QUICKVENEER oscli, "a1", XOS_CLI

        EXPORT  osmodget
osmodget
        FunctionEntry "a2-a4"
        MOV     a4, a1
        MOV     a1, #ModHandReason_Claim
        SWI     XOS_Module
        MOVVS   a1, #0
        MOVVC   a1, a3
        Return  "a2-a4"

        EXPORT  osmodfree
osmodfree
        FunctionEntry "a1-a3"
        MOV     a3, a1
        MOV     a1, #ModHandReason_Free
        SWI     XOS_Module
        Return  "a1-a3"

        END
