        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:ExtraLong
        GET     Hdr:Machine.<Machine>
        GET     Hdr:APCS.<APCS>

        AREA    |Asm$$Code|, CODE, READONLY

        EXPORT  extralong_add[LEAF]
        EXPORT  extralong_uadd[LEAF]
extralong_add
extralong_uadd
        LDR     ip, [sp, #0]
        ADDS    a2, a2, a4
        ADC     a3, a3, ip
        STMIA   a1, {a2, a3}
        Return  ,LinkNotStacked

        EXPORT  extralong_subtract[LEAF]
        EXPORT  extralong_usubtract[LEAF]
extralong_subtract
extralong_usubtract
        LDR     ip, [sp, #0]
        SUBS    a2, a2, a4
        SBC     a3, a3, ip
        STMIA   a1, {a2, a3}
        Return  ,LinkNotStacked

        EXPORT  extralong_multiply[LEAF]
        EXPORT  extralong_umultiply[LEAF]
extralong_multiply
extralong_umultiply
        FunctionEntry "v1"
        LDR     v1, [sp, #8]
        mextralong_multiply ip,lr, a2,a3, a4,v1
        STMIA   a1, {ip, lr}
        Return  "v1"

        EXPORT  extralong_divide[LEAF]
extralong_divide
        FunctionEntry "v1-v4"
        LDR     v1, [sp, #20]
        mextralong_divide ip,lr, a2,a3, a4,v1, v2,v3,v4
        STMIA   a1, {ip, lr}
        Return  "v1-v4"

        EXPORT  extralong_div2[LEAF]
extralong_div2
        MOVS    a3, a3, ASR #1
        MOV     a2, a2, RRX
        STMIA   a1, {a2, a3}
        Return  ,LinkNotStacked

        EXPORT  extralong_neg[LEAF]
extralong_neg
        RSBS    a2, a2, #0
        RSC     a3, a3, #0
        STMIA   a1, {a2, a3}
        Return  ,LinkNotStacked

        END
