   10DEFPROCMemory
   20LOCAL er$
   30er$="Memory"
   40PRINT "PROCMemory"
   50[OPT I%
   60
   70
   80.memoryStartVal% EQUD memoryStart%
   90
  100\----
  110
  120.startMem%
  130
  140    STMDB R13!,{R14}
  150
  160    LDR R3,memoryStartVal%
  170
  180    MOV R0,#6
  190    SWI "XOS_Module"
  200
  210    STR R2,[R12]
  220
  230    MOVVS R3,#0     \  Flag an error - and dont's start module
  240    BVS   startMemX%
  250
  260    MOV R10,R12     \ Use R10 as tempory private word pointer, for sound installation
  270    MOV R12,R2
  280    STR R3,[R12,#work_memSize%]
  290    STR R10,[R12,#work_privateWord%]
  300
  310.startMemX%
  320
  330    LDMIA R13!,{PC}
  340
  350\ -------------------------------- Workspace routines ------------------------
  360
  370.initWorkspace%
  380
  390    STMDB R13!,{R14}
  400
  410    MOV   R0,#0
  420    STR   R0,[R12,#work_currentDriver%] \ No driver installed yet
  430
  440    STR   R0,[R12,#work_SampleFrequency%]
  450    STR   R0,[R12,#work_SamplePeriod%]
  460
  470    STR   R0,[R12,#work_SoundActive%]   \ No sound currently active
  480
  490    STR   R0,[R12,#work_ControlWord%]   \ Control word for Replay
  500
  510    STR   R0,[R12,#work_ImmediateHandlers%]
  520    STR   R0,[R12,#work_CallBackHandlers%]
  530    STR   R0,[R12,#work_ProcessHandlers%]
  540
  550    \ .. Initialise handler table by setting handler_Address% entries to 0
  560
  570    ADD   R0,R12,#work_handlerTable%   \ Position of table
  580    MOV   R1,#handlerMax%              \ Number in table
  590    MOV   R2,#0
  600
  610.initWorkHandlerTableLoop%
  620
  630    STR   R2,[R0,#handler_Address%]    \ Store 0 in handler_Address%
  640    ADD   R0,R0,#handlerTableLen%      \ Increment to next table entry
  650    SUBS  R1,R1,#1                     \ Decrement count
  660    BNE   initWorkHandlerTableLoop%    \ Branch to start of loop
  670
  680
  690    \ .. Initialise driver table by setting driver_Address% entries to 0
  700
  710    ADD   R0,R12,#work_driverTable%    \ Position of table
  720    MOV   R1,#driverMax%               \ Number in table
  730    MOV   R2,#0
  740
  750.initWorkDriverTableLoop%
  760
  770    STR   R2,[R0,#driver_Address%]     \ Store 0 in handler_Address%
  780    ADD   R0,R0,#driverTableLen%       \ Increment to next table entry
  790    SUBS  R1,R1,#1                     \ Decrement count
  800    BNE   initWorkDriverTableLoop%     \ Branch to start of loop
  810
  820    MOV   R0,#0
  830    STR   R0,[R12,#work_callBack_Active%]
  840    STR   R0,[R12,#work_callBack_Count%]
  850    STR   R0,[R12,#work_CallBack_BufferFullCount%]
  860    STR   R0,[R12,#work_CallBack_CurrentSoundBuffer%]
  870    STR   R0,[R12,#work_CallBack_CurrentOutBuffer%]
  880
  890    MOV   R0,#defaultNumCallBackBuffers%
  900    STR   R0,[R12,#work_CallBack_numCallBackBuffers%]
  910
  920    LDMIA R13!,{PC}
  930]
  940ENDPROC
