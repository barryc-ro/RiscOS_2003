; > s.u8to16

		GET	hdr:ListOpts
		GET	hdr:Macros
		GET	hdr:System

		GET	hdr:Proc

; r0 -> input data (word-aligned)
; r1 -> output data (word-aligned)
; r2 = number of samples
; r14 = return address

; update r0 and r1 on output
; preserve all other registers

		AREA	|snd$$ASM|,CODE,READONLY

		IMPORT	check_interleave

		EXPORT	snd_U8To16
		EXPORT	snd_S8To16

snd_S8To16	Entry	"r0-r12"
		MOV	r5, #0
		B	To16

snd_U8To16	ALTENTRY

		LDR	r5, =&80808080

To16
		; do in blocks of 4
00		CMP	r2, #4
		BLT	%ft90

		LDR	lr, [r0], #4
		EOR	lr, lr, r5
		AND	r3, lr, #&000000ff
		MOV	r3, r3, LSL #8
		AND	r4, lr, #&0000ff00
		ORR	r3, r3, r4, LSL #16

		AND	r4, lr, #&ff000000
		AND	lr, lr, #&00ff0000
		ORR	r4, r4, lr, LSR #8
		STMIA	r1!, {r3,r4}

		SUBS	r2, r2, #4
		BGT	%bt00

		B	check_interleave

		LTORG

		; finish off 1 by 1
90		MOV	r3, #0
91		LDRB	lr, [r0], #1
		EOR	lr, lr, #&80
		STRB	r3, [r1], #1
		STRB	lr, [r1], #1

		SUBS	r2, r2, #1
		BGT	%bt91

		B	check_interleave

		END

eof s.u8to16
