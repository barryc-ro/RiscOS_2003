; > veneers.s

; © SJ Middleton, 1995

                GET     hdr:ListOpts
                GET     hdr:Macros
                GET     hdr:APCS.<APCS>
		GET	hdr.soundfile

		AREA	|Veneers$$CODE|,CODE,READONLY

 [ {FALSE}

; sound_linearhandler enters here

; r0 = data value passed to SWI
; r1 -> buffer for sound data
; r2 -> end of buffer
; r3 = flags
; r4 = sample frequency << 10

; IRQ or SVC mode, interrupts undefined
; can corrupt r0-r10
; preserve r11,r12,r13

		^ 0
o_vol_l		#	4
o_vol_r		#	4
o_step		#	4
o_phase		#	4
o_data		#	4
o_fn		#	4

		EXPORT	linear_handler_veneer1

linear_handler_veneer1
                FunctionEntry

                MOV     lr, pc
		LDR	pc, [r0, #o_fn]

                Return

 ]

; r0 = soundfile_format
; r1 = address of handler
; r2 = r12 for handler

; extern os_error *call_dispose_handler(soundfile_format *fmt, void *handler, byte *r12)

		EXPORT	call_dispose_handler
call_dispose_handler
                FunctionEntry "v1-v6,sl,fp"

		MOV	r12, r2

                CMP     r0, r0          ; clear V for entry to dispose handler
                MOV     lr, pc
		MOV	pc, r1

		MOVVC	r0, #0

                Return  "v1-v6,sl,fp"


; r0 = in
; r1 = out
; r2 = nbytes
; r3 = soundfile_format
; [sp, #0] -> used out

; extern os_error *call_to16_handler(const void *in, void *out, int nbytes, soundfile_format *fmt, int *out_used)

		EXPORT	call_to16_handler
call_to16_handler
                FunctionEntry "a2,v1-v6,sl,fp"

    	    	LDR 	r12, [r3, #SoundFile_Format_handler_r12]

                CMP     r0, r0          ; clear V for entry to to16 handler
                MOV     lr, pc
		LDR	pc, [r3, #SoundFile_Format_to16_handler]
		MOVVC	r0, #0

                LDR     r3, [sp, #4*10] ; get address of out_used variable from stack
    	    	CMP 	r3, #0
    	    	LDRNE 	r2, [sp, #0]	; original out value
    	    	SUBNE 	r1, r1, r2  	; n bytes written out
    	    	STRNE 	r1, [r3]

                ADD     sp, sp, #1*4    ; skip the stacked second parameter - don't need to restore it
                Return  "v1-v6,sl,fp"

		END

eof veneers.s
