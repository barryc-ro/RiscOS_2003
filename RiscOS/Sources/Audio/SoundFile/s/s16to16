; > s.s16to16

		GET	hdr:ListOpts
		GET	hdr:Macros
                GET     hdr:APCS.<APCS>
		GET	hdr:System

; r0 -> input data (word-aligned)
; r1 -> output data (word-aligned)
; r2 = number of bytes
; r14 = return address

; update r0 and r1 on output
; preserve all other registers

		AREA	|snd$$ASM|,CODE,READONLY

		IMPORT	check_interleave

		EXPORT	snd_S16To16

snd_S16To16
                FunctionEntry "a1-a2,a4,v1-v6,sl,fp"

		CMP	r0, r1			; quick exit if not being moved anywhere
		BEQ	check_interleave

		MOV	r2, r2, ASR #1

; do in blocks of 16

00		CMP	r2, #16
		BLT	%ft90

		LDMIA	r0!, {r3-r10}
		STMIA	r1!, {r3-r10}

		SUBS	r2, r2, #16
		BGT	%bt00

		B	check_interleave

; finish off 1 by 1

90		LDRB	lr, [r0], #1
		STRB	lr, [r1], #1
		LDRB	lr, [r0], #1
		STRB	lr, [r1], #1

		SUBS	r2, r2, #1
		BGT	%bt90

		B	check_interleave

		END

eof s.s16to16
