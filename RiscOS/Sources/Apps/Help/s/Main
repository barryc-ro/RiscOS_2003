; > Sources.Main

; *********************************
; ***    C h a n g e   L i s t  ***
; *********************************

; Date       Description
; ----       -----------
; 19-Feb-90     0.01    File created
; 11-Jul-91     0.02    PJC: Destroys Help$Dir on Service_Reset
;

Module_BaseAddr

        GET     s.AppName         ; read ApplicationName

        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:ModHand
        GET     Hdr:Services
        GET     Hdr:Proc
        GET     Hdr:ResourceFS
        GET     VersionASM

        LEADR   Module_LoadAddr


; **************** Module code starts here **********************

        ASSERT  (.-Module_BaseAddr) = 0

        DCD     0 ; App_Start    - Module_BaseAddr
        DCD     App_Init     - Module_BaseAddr
        DCD     App_Die      - Module_BaseAddr
        DCD     App_Service  - Module_BaseAddr
        DCD     App_Title    - Module_BaseAddr
        DCD     App_HelpStr  - Module_BaseAddr
        DCD     0
        DCD     0
        DCD     0
        DCD     0
        DCD     0

App_Title     DCB     "!$ApplicationName", 0
App_HelpStr   DCB   "!$ApplicationName", 9, 9, "$Module_FullVersion $Module_Date", 0
                ALIGN

; *****************************************************************************
;
;       App_Init - Initialisation entry
;

App_Init ENTRY "r0"

        ADR     R0, resourcefsfiles
        SWI     XResourceFS_RegisterFiles   ; ignore errors (starts on Service_ResourceFSStarting)

        EXITS

; *****************************************************************************
;
;       App_Die - Die entry
;

App_Die ENTRY "r0"

        ADR     R0, resourcefsfiles
        SWI     XResourceFS_DeregisterFiles

        EXITS                   ; ignore errors (ResourceFS might be dead)

; *****************************************************************************
;
;       App_Service - Main entry point for services
;

App_Service ROUT

        TEQ     R1, #Service_Reset
        BEQ     svc_clearsysvar                 ; on a reset, clear the var

        TEQ     R1, #Service_ResourceFSStarting
        MOVNE   PC, LR

; In    R2 -> address inside ResourceFS module to call
;       R3 = workspace pointer for module
; Out   R2 called with R0 -> files, R3 -> workspace

svc_resourcefsstarting ENTRY "R0-R3"

        ADR     R0, resourcefsfiles
        MOV     LR, PC                          ; get return address
        MOV     PC, R2                          ; R2 = address to call

        EXITS

resourcefsfiles
        GET     s.ResFiles
        DCD     0

svc_clearsysvar ENTRY "R0-R4"

        ADR     R0, sysvarname
        MOV     R1, #0
        MVN     R2, #0                          ; destroy the variable!
        MOV     R3, #0
        MOV     R4, #0
        SWI     XOS_SetVarVal

        EXITS

sysvarname
        DCB     "Help$Dir",0

        END
