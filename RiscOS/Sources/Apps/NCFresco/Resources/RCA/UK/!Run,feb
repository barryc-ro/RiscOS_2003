| > !NCFresco.!Run
|
| World Wide Web browsing software
| (c) ANT Limited, 1995
|
IconSprites <Obey$Dir>.!Sprites
|
Set NCFresco$Dir <Obey$Dir>
SetMacro NCFresco$Path <NCFresco$Dir>.
|
| -----------------------------------------------------------------------
|
| You must edit the following lines to remove the | at the front and
| change the IP addresses to be those of your DNS resolvers
| change the domain name to your domain
|
IF "<Inet$Resolvers>" = "" Then Set Inet$resolvers 136.170.131.2 136.170.131.3 
IF "<Inet$LocalDomain>" = "" Then Set Inet$LocalDomain acorn.co.uk
|
| -----------------------------------------------------------------------
|
IF "<Inet$Resolvers>" = "" Then Error You must configure !NCFresco.!Run with the name of at least one DNS resolver
IF "<Inet$LocalDomain>" = "" Then Error You must configure !NCFresco.!Run with your domain name
|
If "<Inet$MimeMappings>" = "" Then SetMacro Inet$MimeMappings <NCFresco$Dir>.MimeMap
|
| -----------------------------------------------------------------------
| Operating system variables
|
If "<NCFresco$Hotlist>" = "" AND "<Choices$Write>" <> "" Then SetMacro NCFresco$Hotlist <Choices$Write>.NCFresco.Hotlist
If "<NCFresco$Cookies>" = "" AND "<Choices$Write>" <> "" Then SetMacro NCFresco$Cookies <Choices$Write>.NCFresco.Cookies
If "<NCFresco$Users>" = "" AND "<Choices$Write>" <> "" Then SetMacro NCFresco$Users <Choices$Write>.NCFresco.Users
If "<NCFresco$PlugIns>" = "" AND "<Choices$Write>" <> "" Then SetMacro NCFresco$PlugIns <Choices$Write>.NCFresco.PlugIns
If "<NCFresco$Allow>" = "" AND "<Choices$Write>" <> "" Then SetMacro NCFresco$Allow <Choices$Write>.NCFresco.Allow
If "<NCFresco$Deny>" = "" AND "<Choices$Write>" <> "" Then SetMacro NCFresco$Deny <Choices$Write>.NCFresco.Deny
|
If "<NCFresco$Errors>" = "" Then Set NCFresco$Errors 2> null:
If "<NCFresco$Home>" = "" Then SetMacro NCFresco$Home <NCFresco$Dir>.Welcome
If "<NCFresco$Welcome>" = "" Then SetMacro NCFresco$Welcome <NCFresco$Dir>.Welcome
|
If "<NCFresco$Help>" = "" AND "<NCHelp$Path>" <> "" Then Set NCFresco$Help NCHelp:NCFresco.index/html
If "<NCFresco$Help>" = "" Then SetMacro NCFresco$Help <NCFresco$Dir>.Help
|
| -----------------------------------------------------------------------
|
If "<NCFresco$Related>" = "" Then Set NCFresco$Related -url http://www.altavista.digital.com/cgi-bin/query?pg=q&what=web&q=
If "<NCFresco$Search>" = "" Then Set NCFresco$Search -url http://www.altavista.digital.com/
If "<NCFresco$Offline>" = "" Then SetMacro NCFresco$Offline UI:<UI$MainMenu>
|
| -----------------------------------------------------------------------
| Set up the command to use to redial
|
If "<Alias$NCFrescoRedial>" = "" Then Set Alias$NCFrescoRedial NCDialUI_Start
|
| -----------------------------------------------------------------------
| Set UseTB to 0 or 1 here or set to something before calling
|
If "<NCFresco$UseTB>" = "" Then SetEval NCFresco$UseTB 1
|
| -----------------------------------------------------------------------
| Boot file stuff
|
Set File$Type_695 GIF
Set File$Type_B28 URL
Set File$Type_B60 PNG
Set File$Type_B61 XBitMap
Set File$Type_B62 Gopher
Set File$Type_C85 JPEG
Set File$Type_FAF HTML
|
Set Alias$URLOpen_HTTP   /NCFresco:!Run -URL %%*0
Set Alias$URLOpen_GOPHER /NCFresco:!Run -URL %%*0
Set Alias$URLOpen_FILE   /NCFresco:!Run -URL %%*0
Set Alias$URLOpen_FTP    /NCFresco:!Run -URL %%*0
|
| -----------------------------------------------------------------------
| Network modules
|
RMEnsure Resolver       0.43 RMLoad System:Modules.Network.Resolver
RMEnsure Resolver       0.43 Error You need Resolver 0.43 or later
|
RMEnsure HTTP 		0.90 RMLoad <NCFresco$Dir>.HTTPMod
RMEnsure HTTP 		0.90 Error You need HTTPMod 0.90 or later
RMEnsure WebGopher 	0.11 RMLoad <NCFresco$Dir>.WebGopher
RMEnsure WebGopher 	0.11 Error You need WebGopher 0.11 or later
RMEnsure WebFTP 	0.25 RMLoad <NCFresco$Dir>.WebFTP
RMEnsure WebFTP 	0.25 Error You need WebFTP 0.25 or later
|
| -----------------------------------------------------------------------
| Toolbox modules
|
If NCFresco$UseTB Then RMEnsure Toolbox 1.36 RMLoad System:Modules.Toolbox.Toolbox
If NCFresco$UseTB Then RMEnsure Toolbox 1.36 Error You need Toolbox 1.36 or later
If NCFresco$UseTB Then RMEnsure Window  1.31 RMLoad System:Modules.Toolbox.Window
If NCFresco$UseTB Then RMEnsure Window  1.31 Error You need Window 1.30 or later
If NCFresco$UseTB Then RMEnsure Menu    0.24 RMLoad System:Modules.Toolbox.Menu
If NCFresco$UseTB Then RMEnsure Menu    0.24 Error You need Menu 0.23 or later
If NCFresco$UseTB Then RMEnsure ToolAction 0.14 RMLoad System:Modules.Toolbox.ToolAction
If NCFresco$UseTB Then RMEnsure ToolAction 0.14 Error You need ToolAction 0.14 or later
|
| -----------------------------------------------------------------------
| Ensure we have a scrap directory
|
If "<Wimp$ScrapDir>" = "" Then /<NCFresco$Dir>.SetScrap
If "<Wimp$Scrap>" = "" Then SetMacro Wimp$Scrap <Wimp$ScrapDir>.ScrapFile
CDir <Wimp$ScrapDir>
|
If "<Choices$Write>" <> "" Then IfThere <Choices$Write> Then CDir <Choices$Write>.NCFresco
|
| -----------------------------------------------------------------------
| Set Config Cache variables depending on cache location
|
SetEval NCFresco$ScrapTemp ((Wimp$ScrapDir LEFT 1) = "c") OR ((Wimp$ScrapDir LEFT 1) = "C")
|
If NCFresco$ScrapTemp Then RMEnsure FileWatch 0.02 RMLoad System:Modules.FileWatch
|
| Default is now to keep cache always in case we have to kill and restart the browser
| We only need to keep the list up to date off adfs
|
If NOT NCFresco$ScrapTemp Then Set NCFresco$ConfigCacheKeepUpToDate Yes
|
| -----------------------------------------------------------------------
|
WimpSlot -min 864k -max 864K
|
| -----------------------------------------------------------------------
|
Set Alias$NCFrescoRun /<NCFresco$Dir>.!RunImage <NCFresco$Errors> %*0 <NCFresco$Welcome>
NCFrescoRun
