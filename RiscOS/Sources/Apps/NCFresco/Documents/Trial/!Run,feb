| > !STBWeb.!Run
|
| World Wide Web browsing software
| (c) ANT Limited, 1995
|
IconSprites <Obey$Dir>.!Sprites
|
Set STBWeb$Dir <Obey$Dir>
SetMacro STBWeb$Path <STBWeb$Dir>.
|
| -----------------------------------------------------------------------
|
SetMacro Alias$URLOpen_MPEGA /stbweb:plaympeg %%0 -novideo %%*1
SetMacro Alias$URLOpen_MPEGV /stbweb:plaympeg %%*0
SetMacro Alias$URLOpen_BACKMUSIC /TrialApps:BasicApps.!BackMusic -x %%0 -t STBWeb %%*1
SetMacro Alias$URLOpen_TELETEXT /TrialApps:BasicApps.WebITV -c %%0 -i %%1 %%*2
|
| -----------------------------------------------------------------------
|
| Set this variable to kill STBWeb's modules when it exits
|
SetEval STBWeb$KillModules "<STB$2>"<>""
|
| -----------------------------------------------------------------------
|
| You must edit the following lines to remove the | at the front and
| change the IP addresses to be those of your DNS resolvers
| change the domain name to your domain
|
| Trial configuration
IF "<Inet$Resolvers>" = "" Then Set Inet$resolvers 28.1.1.17
If "<Inet$LocalDomain>" = "" Then Set Inet$LocalDomain acorn.co.uk
|
|IF "<Inet$Resolvers>" = "" Then Set Inet$resolvers 136.170.131.2 136.170.131.3 
|IF "<Inet$LocalDomain>" = "" Then Set Inet$LocalDomain acorn.co.uk
|
| -----------------------------------------------------------------------
|
IF "<Inet$Resolvers>" = "" Then Error You must configure !STBWeb.!Run with the name of at least one DNS resolver
IF "<Inet$LocalDomain>" = "" Then Error You must configure !STBWeb.!Run with your domain name
|
If "<Inet$MimeMappings>" = "" Then SetMacro Inet$MimeMappings <STBWeb$Dir>.MimeMap
|
| -----------------------------------------------------------------------
| Operating system variables
|
If "<STBWeb$Errors>" = "" Then Set STBWeb$Errors 2> null:
If "<STBWeb$Config>" = "" Then SetMacro STBWeb$Config <STBWeb$Dir>.Config
If "<STBWeb$Help>" = "" Then SetMacro STBWeb$Help <STBWeb$Dir>.Help
If "<STBWeb$Hotlist>" = "" Then SetMacro STBWeb$Hotlist <Wimp$ScrapDir>.Hotlist
If "<STBWeb$Home>" = "" Then SetMacro STBWeb$Home <STBWeb$Dir>.Welcome
If "<STBWeb$Welcome>" = "" Then SetMacro STBWeb$Welcome <STBWeb$dir>.Welcome
If "<STBWeb$Cookies>" = "" Then SetMacro STBWeb$Cookies <Wimp$ScrapDir>.Cookies
|
| -----------------------------------------------------------------------
| Set UseTB to 0 or 1 here or set to something before calling
|
If "<STBWeb$UseTB>" = "" Then SetEval STBWeb$UseTB 0
|
| -----------------------------------------------------------------------
| Boot file stuff
|
Set File$Type_695 GIF
Set File$Type_B60 PNG
Set File$Type_B61 XBitMap
Set File$Type_B62 Gopher
Set File$Type_C85 JPEG
Set File$Type_FAF HTML
|
Set Alias$URLOpen_HTTP   /STBWeb:!Run -URL %%*0
Set Alias$URLOpen_GOPHER /STBWeb:!Run -URL %%*0
Set Alias$URLOpen_FILE   /STBWeb:!Run -URL %%*0
Set Alias$URLOpen_FTP    /STBWeb:!Run -URL %%*0
|
| ----------------------------------------------------------------------------
| Fiddle wimpslot stuff before loading modules
|
Wimpslot -min 64k -max 64k
If "<STB$2>" <> "" AND "<Navigator$Dir>" <> "" Then Run <Navigator$Dir>.Library.Setlims
|
| -----------------------------------------------------------------------
| Network modules
|
SetEval STBWeb$NeedResolver 0
RMEnsure Resolver 0 SetEval STBWeb$NeedResolver 1
|
RMEnsure Resolver       0.39 RMLoad <STBWeb$Dir>.resolver
RMEnsure Resolver       0.39 Error You need Resolver 0.39 or later
|
RMEnsure HTTP 		0.52 RMLoad <STBWeb$Dir>.httpmod
RMEnsure HTTP 		0.52 Error You need HTTPMod 0.52 or later
RMEnsure WebGopher 	0.08 RMLoad <STBWeb$Dir>.WebGopher
RMEnsure WebGopher 	0.08 Error You need WebGopher 0.08 or later
RMEnsure WebFTP 	0.13 RMLoad <STBWeb$Dir>.WebFTP
RMEnsure WebFTP 	0.13 Error You need WebFTP 0.13 or later
|
| -----------------------------------------------------------------------
|
| If NHWidget is around then kill it to avoid conflicts with NHTwitter
SetEval STBWeb$HaveWidget 1
RMEnsure NHWidget 0.00 SetEval STBWeb$HaveWidget 0
If STBWeb$HaveWidget Then RMKill NHWidget
|
| Then load NHTwitter
RMEnsure NHTwitter 0.07 RMLoad Boot:RM.NHTwitter
|
| Load sound click
RMEnsure click 0 RMLoad <STBWeb$Dir>.click
|
| -----------------------------------------------------------------------
| Toolbox modules
|
If STBWeb$UseTB Then Set STBWeb$TBOptions -t Else Set STBWeb$TBOptions ""
|
If STBWeb$UseTB Then RMEnsure Toolbox 1.36 RMLoad System:Modules.Toolbox.Toolbox
If STBWeb$UseTB Then RMEnsure Toolbox 1.36 Error You need Toolbox 1.36 or later
If STBWeb$UseTB Then RMEnsure Window  1.31 RMLoad System:Modules.Toolbox.Window
If STBWeb$UseTB Then RMEnsure Window  1.31 Error You need Window 1.30 or later
If STBWeb$UseTB Then RMEnsure Menu    0.24 RMLoad System:Modules.Toolbox.Menu
If STBWeb$UseTB Then RMEnsure Menu    0.24 Error You need Menu 0.23 or later
If STBWeb$UseTB Then RMEnsure ToolAction 0.12 RMLoad <STBWeb$Dir>.ToolAction
If STBWeb$UseTB Then RMEnsure ToolAction 0.12 Error You need ToolAction 0.12 or later
|
| -----------------------------------------------------------------------
| Ensure we have a scrap directory in cachefs
|
SetEval STBWeb$NeedScrap 0
If "<Wimp$ScrapDir>" = "" Then SetEval STBWeb$NeedScrap 1
|
If STBWeb$NeedScrap Then RMEnsure CacheFSRes 0 RMLoad Boot:RM.CacheFSRes
If STBWeb$NeedScrap Then RMEnsure CacheFS 0 RMLoad Boot:RM.CacheFS
If STBWeb$NeedScrap AND "<STBWeb$RamSize>"="" Then Set STBWeb$RamSize 128k
If STBWeb$NeedScrap Then Set Alias$STBWebCmd Cache_SetMax <STBWeb$RamSize>
If STBWeb$NeedScrap Then STBWebCmd
If STBWeb$NeedScrap Then CDir <Wimp$ScrapDir>
|
If "<Wimp$Scrap>" = "" Then SetMacro Wimp$Scrap <Wimp$ScrapDir>.ScrapFile
|
RMEnsure FileWatch 0.02 RMLoad <STBWeb$Dir>.FileWatch
|
| ----------------------------------------------------------------------------
| Trial specific setup
|
If "<STB$2>" = "yes" Then IRreset
If "<STB$2>" = "no" Then IR1to2
If "<STB$2>" = "no" Then fx 11 64
If "<STB$2>" <> "" Then WimpMode 50
|
| -----------------------------------------------------------------------
|
WimpSlot -min 512k -max 512K
|
| -----------------------------------------------------------------------
| Choose which RunImage to use (StrongARM or standard)
|
If "<STBWeb$RunImage>" = "" Then /<STBWeb$Dir>.CheckProc
|
| -----------------------------------------------------------------------
|
Set Alias$STBWebCmd Run <STBWeb$Dir>.<STBWeb$RunImage> <STBWeb$Errors> <STBWeb$TBOptions> %*0 <STBWeb$Welcome>
STBWebCmd
|
If "<STB$2>" <> "" AND "<Navigator$Dir>" <> "" Then Run <Navigator$Dir>.Library.Setlims
|
| ----------------------------------------------------------------------------
| Kill off any other modules we loaded if we are on the trial
|
IF STBWeb$KillModules Then RmKill HTTP
IF STBWeb$KillModules Then RmKill WebGopher
IF STBWeb$KillModules Then RmKill WebFTP
|
If STBWeb$NeedResolver Then RMKill Resolver
|
| ----------------------------------------------------------------------------
| Reset state so the trial will carry on working
|
If "<STB$2>" <> "" Then pointer 0
If "<STB$2>" = "yes" Then IRdefault
If "<STB$2>" = "no" Then IRdefault
If "<STB$2>" = "no" Then fx 11 0
|
| ----------------------------------------------------------------------------
| Reload the NHWidget module if we killed it
| If we didn't have NHWidget we leave NHTwitter around
|
If STBWeb$HaveWidget Then RMKill NHTwitter
If STBWeb$HaveWidget Then RMLoad Boot:RM.NHWidget
|
| ----------------------------------------------------------------------------
| Shrink RMA back down to minimum
|
ChangeDA 1 0
| ----------------------------------------------------------------------------
