| > !NCFresco.!Run
|
| World Wide Web browsing software
| (c) ANT Limited, 1995
|
IconSprites <Obey$Dir>.!Sprites
|
Set NCFresco$Dir Resources:$.Resources.NCFresco
Set NCFresco$Path <Obey$Dir>.,<NCFresco$Dir>.
|
| -----------------------------------------------------------------------
|
If "<Inet$MimeMappings>" = "" Then SetMacro Inet$MimeMappings <NCFresco$Dir>.MimeMap
|
| -----------------------------------------------------------------------
| Operating system variables
|
If "<NCFresco$Hotlist>" = "" AND "<Choices$Write>" <> "" Then SetMacro NCFresco$Hotlist <Choices$Write>.NCFresco.Hotlist
If "<NCFresco$Hotlist>" = "" Then SetMacro NCFresco$Hotlist <Wimp$ScrapDir>.NCHotlist
|
If "<NCFresco$Cookies>" = "" AND "<Choices$Write>" <> "" Then SetMacro NCFresco$Cookies <Choices$Write>.NCFresco.Cookies
If "<NCFresco$Users>" = "" AND "<Choices$Write>" <> "" Then SetMacro NCFresco$Users <Choices$Write>.NCFresco.Users
If "<NCFresco$PlugIns>" = "" AND "<Choices$Write>" <> "" Then SetMacro NCFresco$PlugIns <Choices$Write>.NCFresco.PlugIns
|
If "<NCFresco$Errors>" = "" Then Set NCFresco$Errors 2> null:
If "<NCFresco$Home>" = "" Then SetMacro NCFresco$Home <NCFresco$Dir>.Welcome
If "<NCFresco$Welcome>" = "" Then SetMacro NCFresco$Welcome <NCFresco$Dir>.Welcome
|
If "<NCFresco$Help>" = "" AND "<NCHelp$Path>" <> "" Then Set NCFresco$Help NCHelp:NCFresco.index/html
If "<NCFresco$Help>" = "" Then SetMacro NCFresco$Help <NCFresco$Dir>.Help
|
| -----------------------------------------------------------------------
| Set UseTB to 0 or 1 here or set to something before calling
|
If "<NCFresco$UseTB>" = "" Then SetEval NCFresco$UseTB 1
|
| -----------------------------------------------------------------------
| Boot file stuff
|
Set File$Type_695 GIF
Set File$Type_B28 URL
Set File$Type_B60 PNG
Set File$Type_B61 XBitMap
Set File$Type_B62 Gopher
Set File$Type_C85 JPEG
Set File$Type_FAF HTML
|
Set Alias$URLOpen_HTTP   /NCFresco:!Run -URL %%*0
Set Alias$URLOpen_GOPHER /NCFresco:!Run -URL %%*0
Set Alias$URLOpen_FILE   /NCFresco:!Run -URL %%*0
Set Alias$URLOpen_FTP    /NCFresco:!Run -URL %%*0
|
| -----------------------------------------------------------------------
| Toolbox modules
|
If NCFresco$UseTB Then Set NCFresco$TBOptions -t Else Set NCFresco$TBOptions ""
|
| -----------------------------------------------------------------------
|
If "<Wimp$ScrapDir>" = "" Then /NCFresco:SetScrap
If "<Wimp$Scrap>" = "" Then SetMacro Wimp$Scrap <Wimp$ScrapDir>.ScrapFile
CDir <Wimp$ScrapDir>
|
| -----------------------------------------------------------------------
| Set Config Cache variables depending on cache location
|
SetEval NCFresco$ScrapTemp ((Wimp$ScrapDir LEFT 1) = "c") OR ((Wimp$ScrapDir LEFT 1) = "C")
|
| Default is now to keep cache always in case we have to kill and restart the browser
| We only need to keep the list up to date off adfs
|
If NOT NCFresco$ScrapTemp Then Set NCFresco$ConfigCacheKeepUpToDate Yes
|
| -----------------------------------------------------------------------
| Set Config Display variables depending on TV$Type
|
SetEval NCFresco$TV ("<TV$Type>" = "PAL" OR "<TV$Type>" = "NTSC")
|
If "<NCFresco$ConfigDisplayScale>" = "" AND NCFresco$TV Then Set NCFresco$ConfigDisplayScale 125
If "<NCFresco$ConfigAnimationName>" = "" AND NCFresco$TV Then Set NCFresco$ConfigAnimationName nc_nlogo
|
If "<NCFresco$ConfigDisplayScale1>" = "" AND NCFresco$TV Then Set NCFresco$ConfigDisplayScale1 100
If "<NCFresco$ConfigDisplayScale2>" = "" AND NCFresco$TV Then Set NCFresco$ConfigDisplayScale2 125
If "<NCFresco$ConfigDisplayScale3>" = "" AND NCFresco$TV Then Set NCFresco$ConfigDisplayScale3 150
If "<NCFresco$ConfigDisplayScale4>" = "" AND NCFresco$TV Then Set NCFresco$ConfigDisplayScale4 200
|
| -----------------------------------------------------------------------
|
WimpSlot -min 128k -max 128K
|
Set Alias$NCFrescoRun Desktop_NCFresco <NCFresco$Errors> <NCFresco$TBOptions> %*0 <NCFresco$Welcome>
NCFrescoRun
