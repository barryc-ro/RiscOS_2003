|
| !Run file for !WebServe (11-Feb-1998)
|
Set WebServe$Dir <Obey$Dir>
Set WebServe$Path <Obey$Dir>.
IconSprites <WebServe$Dir>.!Sprites
|
| Can't do much if we haven't got a System or Internet resource...

If "<System$Path>"       = "" Then Set System$Path_Message System resources not found.
If "<InetDBase$Path>"    = "" Then Error Internet resources not found.
If "<Inet$MimeMappings>" = "" Then Set Inet$MimeMappings InetDBase:Mimemap
|
| If !WebCache has not been seen, assume it lives in our parent directory
If "<WebServe$CacheRoot>"="" Then Set WebServe$CacheRoot <WebServe$Dir>.^.!WebCache.Cache
If "<WebServe$ServeRoot>"="" Then Set WebServe$ServeRoot <WebServe$Dir>.^.!WebCache.Serve
If "<WebServe$ErrorRoot>"="" Then Set WebServe$ErrorRoot <WebServe$Dir>.^.!WebCache.Error
|
|The following Network URL modules are required
RMEnsure URL_Fetcher   0.31 RMLoad System:Modules.Network.URL.URL
RMEnsure URL_Fetcher   0.31 Error WebServe requires URL_Fetcher 0.31 or later
RMEnsure AcornHTTP     0.73 RMLoad System:Modules.Network.URL.HTTP
RMEnsure AcornHTTP     0.73 Error WebServe requires AcornHTTP 0.73 or later
RMEnsure FileFetcher   0.37 RMLoad System:Modules.Network.URL.File
RMEnsure FileFetcher   0.37 Error WebServe requires FileFetcher 0.37 or later
RMEnsure FTPFetcher    0.26 RMLoad System:Modules.Network.URL.FTP
RMEnsure FTPFetcher    0.26 Error WebServe requires FTPFetcher 0.26 or later
RMEnsure GopherFetcher 0.09 RMLoad System:Modules.Network.URL.Gopher
RMEnsure GopherFetcher 0.09 Error WebServe requires GopherFetcher 0.09 or later

RMEnsure MimeMap       0.05 RMLoad System:Modules.Network.MimeMap
RMEnsure MimeMap       0.05 Error WebServe requires MimeMap 0.05 or later
|
|The following Toolbox modules are required

RMEnsure Toolbox  1.36  RMLoad System:Modules.Toolbox.Toolbox
RMEnsure Toolbox  1.36  Error WebServe requires Toolbox 1.36 or later
RMEnsure Window   1.46  RMLoad System:Modules.Toolbox.Window
RMEnsure Window   1.46  Error WebServe requires Window 1.46 or later
RMEnsure IconBar  1.17  RMLoad System:Modules.Toolbox.IconBar
RMEnsure IconBar  1.17  Error WebServe requires IconBar 1.17 or later
RMEnsure Menu     0.31  RMLoad System:Modules.Toolbox.Menu
RMEnsure Menu     0.31  Error WebServe requires Menu 0.31 or later
RMEnsure ProgInfo 0.13  RMLoad System:Modules.Toolbox.ProgInfo
RMEnsure ProgInfo 0.13  Error WebServe requires ProgInfo 0.13 or later
|
| Configuration set by last run.
IfThere <WebServe$Dir>.!Config Then Obey <WebServe$Dir>.!Config
IfThere Choices:WWW.WebServe Then Obey Choices:WWW.WebServe
|
Wimpslot -max 96K -min 96K
Run <WebServe$Dir>.!RunImage %*0