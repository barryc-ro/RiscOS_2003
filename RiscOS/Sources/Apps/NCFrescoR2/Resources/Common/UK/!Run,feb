| > !NCFresco.!Run
|
| World Wide Web browsing software
| (c) ANT Limited, 1995-2001
|     Pace Micro Technology plc, 1995-2001
|
Set NCFresco$Dir <Obey$Dir>
SetMacro NCFresco$Path <NCFresco$Dir>.
|
IconSprites <NCFresco$Dir>.!Sprites
|
If "<TV$Type>" = "NTSC"  Then Set NCFresco$Path <NCFresco$Dir>.Resources.USA.,<NCFresco$Path>
If "<TV$Type>" <> "NTSC" Then Set NCFresco$Path <NCFresco$Dir>.Resources.UK.,<NCFresco$Path>
IfThere Cache:$.NCFresco Then Set NCFresco$Path Cache:$.NCFresco.,<NCFresco$Path>
|
| -----------------------------------------------------------------------
|
If "<Inet$MimeMappings>" = "" Then SetMacro Inet$MimeMappings NCFresco:MimeMap
|
| -----------------------------------------------------------------------
| Operating system variables
|
If "<NCFresco$Hotlist>" = "" AND "<Choices$Write>" <> "" Then SetMacro NCFresco$Hotlist <Choices$Write>.NCFresco.Hotlist
If "<NCFresco$Cookies>" = "" AND "<Choices$Write>" <> "" Then SetMacro NCFresco$Cookies <Choices$Write>.NCFresco.Cookies
If "<NCFresco$Users>" = "" AND "<Choices$Write>" <> "" Then SetMacro NCFresco$Users <Choices$Write>.NCFresco.Users
If "<NCFresco$Allow>" = "" AND "<Choices$Write>" <> "" Then SetMacro NCFresco$Allow <Choices$Write>.NCFresco.Allow
If "<NCFresco$Deny>" = "" AND "<Choices$Write>" <> "" Then SetMacro NCFresco$Deny <Choices$Write>.NCFresco.Deny
|
If "<NCFresco$Errors>" = "" Then Set NCFresco$Errors 2> null:
If "<NCFresco$Home>" = "" Then SetMacro NCFresco$Home NCFresco:Welcome
If "<NCFresco$Welcome>" = "" Then SetMacro NCFresco$Welcome NCFresco:Welcome
|
| -----------------------------------------------------------------------
| These 4 variables are unset after use to prevent accidental reuse
| They are to aid running different versions during debugging
|
If "<NCFresco$WimpSlot>" = "" Then Set NCFresco$WimpSlot 1200k
If "<NCFresco$RunImage>" = "" Then Set NCFresco$RunImage !RunImage
|
If "<NCFresco$HTTP>" = "" Then Set NCFresco$HTTP HTTPMod
If "<NCFresco$WebFTP>" = "" Then Set NCFresco$WebFTP WebFTP
|
| -----------------------------------------------------------------------
| Set up the command to use to redial
|
If "<Alias$NCFrescoRedial>" = "" Then Set Alias$NCFrescoRedial NCDialUI_Start
|
| -----------------------------------------------------------------------
| Boot file stuff
|
Set File$Type_695 GIF
Set File$Type_B28 URL
Set File$Type_B60 PNG
Set File$Type_B61 XBitMap
Set File$Type_C85 JPEG
Set File$Type_FAF HTML
|
Set Alias$URLOpen_HTTP   /NCFresco:!Run -URL %%*0
Set Alias$URLOpen_FILE   /NCFresco:!Run -URL %%*0
Set Alias$URLOpen_FTP    /NCFresco:!Run -URL %%*0
|
| -----------------------------------------------------------------------
| File types the browser may come across
|
If "<File$Type_FF0>" = "" Then Set File$Type_FF0 TIFF
If "<File$Type_BE8>" = "" Then Set File$Type_BE8 PhotoCD
If "<File$Type_69C>" = "" Then Set File$Type_69C Windows Bitmap
If "<File$Type_BF8>" = "" Then Set File$Type_BF8 MPEG
If "<File$Type_B29>" = "" Then Set File$Type_B29 VRML
If "<File$Type_AE3>" = "" Then Set File$Type_AE3 Shockwave
If "<File$Type_AE4>" = "" Then Set File$Type_AE4 Java
If "<File$Type_AE6>" = "" Then Set File$Type_AE6 Microsoft Word
If "<File$Type_C46>" = "" Then Set File$Type_C46 Tar
If "<File$Type_F89>" = "" Then Set File$Type_F89 GZip
If "<File$Type_DDC>" = "" Then Set File$Type_DDC Archive
If "<File$Type_FD4>" = "" Then Set File$Type_FD4 MIDI
|
| -----------------------------------------------------------------------
| System modules
|
RMEnsure UtilityModule 3.10 Error This application requires RISC OS 3.10 or later
RMEnsure UtilityModule 3.60 RMEnsure CallASWI 0.02 RMLoad System:Modules.CallASWI
RMEnsure UtilityModule 3.60 RMEnsure CallASWI 0.02 Error This application requires CallASWI 0.02 or later
RMEnsure FPEmulator 4.03 RMLoad System:Modules.FPEmulator
RMEnsure FPEmulator 4.03 Error This application requires FPEmulator 4.03 or later
RMEnsure SharedCLibrary 5.17 RMLoad System:Modules.CLib
RMEnsure SharedCLibrary 5.17 Error This application requires SharedCLibrary 5.17 or later

RMEnsure WindowManager  0.00 RMLoad System:Modules.Wimp
RMEnsure WindowManager  3.98 Error You need WindowManager 3.98 or later
|
| -----------------------------------------------------------------------
| Network modules
|
RMEnsure Resolver       0.64 RMLoad System:Modules.Network.Resolver
RMEnsure Resolver       0.64 Error You need Resolver 0.64 or later
RMEnsure MimeMap        0.12 RMLoad System:Modules.Network.MimeMap
RMEnsure MimeMap        0.12 Error You need MimeMap 0.12 or later
|
RMEnsure HTTP           2.04 IfThere System:Modules.Network.<NCFresco$HTTP> Then RMLoad System:Modules.Network.<NCFresco$HTTP>
RMEnsure HTTP           2.04 IfThere NCFresco:<NCFresco$HTTP> Then RMLoad NCFresco:<NCFresco$HTTP>
RMEnsure HTTP           2.04 Error You need <NCFresco$HTTP> 2.04 or later
Unset NCFresco$HTTP
|
RMEnsure WebFTP         2.02 IfThere System:Modules.Network.<NCFresco$WebFTP> Then RMLoad System:Modules.Network.<NCFresco$WebFTP>
RMEnsure WebFTP         2.02 IfThere NCFresco:<NCFresco$WebFTP> Then RMLoad NCFresco:<NCFresco$WebFTP>
RMEnsure WebFTP         2.02 Error You need <NCFresco$WebFTP> 2.02 or later
Unset NCFresco$WebFTP
|
| -----------------------------------------------------------------------
| Toolbox modules
|
RMEnsure Toolbox        1.47 RMLoad System:Modules.Toolbox.Toolbox
RMEnsure Toolbox        1.47 Error You need Toolbox 1.47 or later
RMEnsure Window         1.65 RMLoad System:Modules.Toolbox.Window
RMEnsure Window         1.65 Error You need Window 1.65 or later
RMEnsure ToolAction     0.18 RMLoad System:Modules.Toolbox.ToolAction
RMEnsure ToolAction     0.18 Error You need ToolAction 0.18 or later
|
| -----------------------------------------------------------------------
|
If "<Choices$Write>" <> "" Then IfThere <Choices$Write> Then CDir <Choices$Write>.NCFresco
|
| -----------------------------------------------------------------------
| Set Config Cache variables depending on cache location
|
SetEval NCFresco$ScrapTemp ((Wimp$ScrapDir LEFT 1) = "c") OR ((Wimp$ScrapDir LEFT 1) = "C")
If NCFresco$ScrapTemp Then RMEnsure FileWatch 0.07 RMLoad System:Modules.FileWatch
|
| -----------------------------------------------------------------------
|
Set Alias$NCFrescoWimpSlot WimpSlot -min <NCFresco$WimpSlot> -max <NCFresco$WimpSlot>
Unset NCFresco$WimpSlot
NCFrescoWimpSlot
|
| -----------------------------------------------------------------------
|
Set Alias$NCFrescoRun /NCFresco:<NCFresco$RunImage> <NCFresco$Errors> %*0 <NCFresco$Welcome>
Unset NCFresco$RunImage
NCFrescoRun
