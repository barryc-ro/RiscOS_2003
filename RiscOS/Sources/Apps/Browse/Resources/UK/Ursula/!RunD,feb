Set Browse$Dir <Obey$Dir>
Set Browse$Path <Browse$Dir>.

| Kick DDT

Set DDT$Present Yes
RMEnsure DDT 0.00 Set DDT$Present No
If "<DDT$Present>" = "Yes" Then RMKill DDT
UnSet DDT$Present

| Can't do much if we haven't got a System or Internet resource...

If "<System$Path>"       = "" Then Set System$Path_Message System resources not found.
If "<InetDBase$Path>"    = "" Then Error Internet resources not found.
If "<Inet$MimeMappings>" = "" Then Set Inet$MimeMappings InetDBase:Mimemap

| For font choices, if someone else hasn't already set them up

If "<Font$Bold>"   = "" Then Set Font$Bold   "Bold Demi ExtraBold Ultra Heavy"
If "<Font$Italic>" = "" Then Set Font$Italic "Italic Oblique Slant"

| HTTP fetcher controls

Set Browes$AcceptAllCookies on
Set Browse$CookieFile       Browse:User.Cookies
Set Browse$CookieFileFormat 2

| Ensure specific versions of Toolbox modules (or later ones)
| are present

RMEnsure Toolbox        1.36 RMLoad System:Modules.Toolbox.Toolbox
RMEnsure Toolbox        1.36 Error The browser requires version 1.36 or later of the Toolbox module
RMEnsure IconBar        1.14 RMLoad System:Modules.Toolbox.IconBar
RMEnsure IconBar        1.14 Error The browser requires version 1.14 or later of the IconBar module
RMEnsure ProgInfo       0.09 RMLoad System:Modules.Toolbox.ProgInfo
RMEnsure ProgInfo       0.09 Error The browser requires version 0.09 or later of the ProgInfo module
RMEnsure Window         1.41 RMLoad System:Modules.Toolbox.Window
RMEnsure Window         1.41 Error The browser requires version 1.41 or later of the Window module
RMEnsure Menu           0.28 RMLoad System:Modules.Toolbox.Menu
RMEnsure Menu           0.28 Error The browser requires version 0.28 or later of the Menu module
RMEnsure ToolAction     0.14 RMLoad System:Modules.Toolbox.ToolAction
RMEnsure ToolAction     0.14 Error The browser requires version 0.14 or later of the ToolAction module
RMEnsure PrintDBox      0.10 RMLoad System:Modules.Toolbox.PrintDBox
RMEnsure PrintDBox      0.10 Error The browser requires version 0.10 or later of the PrintDBox module
RMEnsure FontMenu       0.17 RMLoad System:Modules.Toolbox.FontMenu
RMEnsure FontMenu       0.17 Error The browser requires version 0.17 or later of the FontMenu module 

| Important networking modules

RMEnsure URL_Fetcher    0.16 RMLoad System:Modules.Network.URL.URL
RMEnsure URL_Fetcher    0.16 Error The browser requires version 0.16 or later of the URL module
RMEnsure AcornHTTP      0.43 RMLoad System:Modules.Network.URL.HTTP
RMEnsure AcornHTTP      0.43 Error The browser requires version 0.43 or later of the HTTP module
RMEnsure FileFetcher    0.23 RMLoad System:Modules.Network.URL.File
RMEnsure FileFetcher    0.23 Error The browser requires version 0.23 or later of the File module
RMEnsure FTPFetcher     0.15 RMLoad System:Modules.Network.URL.FTP
RMEnsure FTPFetcher     0.15 Error The browser requires version 0.15 or later of the FTP module
RMEnsure GopherFetcher  0.03 RMLoad SysteM:Modules.Network.URL.Gopher
RMEnsure GopherFetcher  0.03 Error The browser requires version 0.03 or later of the Gopher module

RMEnsure MimeMap        0.04 RMLoad System:Modules.Network.MimeMap
RMEnsure MimeMap        0.04 Error The browser requires version 0.04 or later of the MimeMap module

| The URI handler for mailto:

RMEnsure AcornURI       0.05 RMLoad System:Modules.Network.URI

| The URI handler has a Wimp task side, so want to do a
| '*Desktop' when inside the application after Wimp_Initialise
| to start it. Set a system variable if this is NOT needed.

Set Browse$IssueDesktopCommand "yes"
RMEnsure AcornURI 0.05 Set Browse$IssueDesktopCommand "no"

| JPEG support for OS versions without this in SpriteExtend;
| again, not technically essential so let this fail too.

RMEnsure SpriteExtend 0.99 RMEnsure JPEGMsgs   0.00 RMLoad System:Modules.JPEGMsgs
RMEnsure SpriteExtend 0.99 RMEnsure JPEGRender 0.00 RMLoad System:Modules.JPEGRender

| Get round the 'problematic' last stage of the printer protocol

Set Alias$@PrintType_FF4 "Copy %%0 Printer: ~A ~C ~D F ~L ~N ~P ~Q ~R ~S ~T ~V"

| Routine actions...

IconSprites <Browse$Dir>.!Sprites

Set File$Type_FAF HTML
Set File$Type_F91 URI
Set File$Type_695 GIF
Set File$Type_C85 JPEG
Set File$Type_B60 PNG

Set Alias$@RunType_FAF /<Browse$Dir>.!Run -html %%*0
Set Alias$@RunType_F91 /<Browse$Dir>.!Run -uri %%*0

| And finally, run the application.

WimpSlot -Min 2880K -Max 2880K

%UnSet ChangeFSI$Dir
|Run <Browse$Dir>.!RunImage -debug CMal,CFle %*0 2> Pipe:$.StdErr
Run <Browse$Dir>.!RunImage %*0
