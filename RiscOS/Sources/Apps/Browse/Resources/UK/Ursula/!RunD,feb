| !Run file for Browse
| (C) Acorn Computers Ltd. 1994
|
| Last modified: 09/07/1998

Set      Browse$Dir          <Obey$Dir>

SetMacro Browse$Path         Choices:WWW.Browse.,<Browse$Dir>.,<Wimp$ScrapDir>.WWW.Browse.,<Browse$Dir>.User.
SetMacro Browse$ChoicesFile  Browse:Choices
SetMacro Browse$ControlsFile Browse:Controls
SetMacro Browse$ChoicesSave  <Choices$Write>.WWW.Browse.Choices

| URI handler support

Set Alias$Open_URI_http   <Browse$Dir>.!Run
Set Alias$Open_URI_ftp    <Browse$Dir>.!Run
Set Alias$Open_URI_gopher <Browse$Dir>.!Run

| Support the ANT URL protocol
|
| SetMacro Alias$URLOpen_HTTP   Run <Phoenix$Dir>.!Run -url %%*0
| SetMacro Alias$URLOpen_Gopher Run <Phoenix$Dir>.!Run -url %%*0
| SetMacro Alias$URLOpen_File   Run <Phoenix$Dir>.!Run -url %%*0
| SetMacro Alias$URLOpen_FTP    Run <Phoenix$Dir>.!Run -url %%*0

| Kick DDT

Set DDT$Present Yes
RMEnsure DDT 0.00 Set DDT$Present No
If "<DDT$Present>" = "Yes" Then RMKill DDT
UnSet DDT$Present

| Can't do much if we haven't got a System or Internet resource...

If "<System$Path>"       = "" Then Set System$Path_Message System resources not found.
If "<InetDBase$Path>"    = "" Then Error Internet resources not found.
If "<Unicode$Path>"      = "" Then Error Unicode resources not found.
If "<Inet$MimeMappings>" = "" Then Set Inet$MimeMappings InetDBase:Mimemap

| For font choices, if someone else hasn't already set them up

If "<Font$Bold>"   = "" Then Set Font$Bold   "Bold Demi ExtraBold Ultra Heavy"
If "<Font$Italic>" = "" Then Set Font$Italic "Italic Oblique Slant"

| HTTP fetcher controls

Set Browse$AcceptAllCookies off
Set Browse$CookieFile       Browse:User.Cookies
Set Browse$CookieFileFormat 2

| undamental modules

RMEnsure WindowManager  3.98 Error Browse requires version 3.98 or later of the WindowManager ('nested WIMP') module

| Ensure specific versions of Toolbox modules (or later ones)
| are present

RMEnsure Toolbox        1.43 RMLoad System:Modules.Toolbox.Toolbox
RMEnsure Toolbox        1.43 Error Browse requires version 1.43 or later of the Toolbox module
RMEnsure IconBar        1.18 RMLoad System:Modules.Toolbox.IconBar
RMEnsure IconBar        1.18 Error Browse requires version 1.18 or later of the IconBar module
RMEnsure ProgInfo       0.14 RMLoad System:Modules.Toolbox.ProgInfo
RMEnsure ProgInfo       0.14 Error Browse requires version 0.14 or later of the ProgInfo module
RMEnsure Window         1.54 RMLoad System:Modules.Toolbox.Window
RMEnsure Window         1.54 Error Browse requires version 1.54 or later of the Window module
RMEnsure Menu           0.33 RMLoad System:Modules.Toolbox.Menu
RMEnsure Menu           0.33 Error Browse requires version 0.33 or later of the Menu module
RMEnsure ToolAction     0.15 RMLoad System:Modules.Toolbox.ToolAction
RMEnsure ToolAction     0.15 Error Browse requires version 0.15 or later of the ToolAction module
RMEnsure PrintDBox      0.14 RMLoad System:Modules.Toolbox.PrintDBox
RMEnsure PrintDBox      0.14 Error Browse requires version 0.14 or later of the PrintDBox module
RMEnsure FontMenu       0.21 RMLoad System:Modules.Toolbox.FontMenu
RMEnsure FontMenu       0.21 Error Browse requires version 0.21 or later of the FontMenu module

| Important networking modules

RMEnsure URL_Fetcher    0.39 RMLoad System:Modules.Network.URL.URL
RMEnsure URL_Fetcher    0.39 Error Browse requires version 0.39 or later of the URL module
RMEnsure AcornHTTP      0.82 RMLoad System:Modules.Network.URL.HTTP
RMEnsure AcornHTTP      0.82 Error Browse requires version 0.82 or later of the HTTP module
RMEnsure FileFetcher    0.38 RMLoad System:Modules.Network.URL.File
RMEnsure FileFetcher    0.38 Error Browse requires version 0.38 or later of the File module
RMEnsure FTPFetcher     0.29 RMLoad System:Modules.Network.URL.FTP
RMEnsure FTPFetcher     0.29 Error Browse requires version 0.29 or later of the FTP module
RMEnsure GopherFetcher  0.12 RMLoad SysteM:Modules.Network.URL.Gopher
RMEnsure GopherFetcher  0.12 Error Browse requires version 0.12 or later of the Gopher module

RMEnsure MimeMap        0.05 RMLoad System:Modules.Network.MimeMap
RMEnsure MimeMap        0.05 Error Browse requires version 0.05 or later of the MimeMap module

| The URI handler for mailto:

RMEnsure AcornURI       0.12 RMLoad System:Modules.Network.URI

| The URI handler has a Wimp task side, so want to do a
| '*Desktop' when inside the application after Wimp_Initialise
| to start it. Set a system variable if this is NOT needed.

Set Browse$IssueDesktopCommand "yes"
RMEnsure AcornURI 0.12 Set Browse$IssueDesktopCommand "no"

| JPEG support for OS versions without this in SpriteExtend;
| again, not technically essential so let this fail too.

RMEnsure SpriteExtend 0.99 RMEnsure JPEGMsgs   0.00 RMLoad System:Modules.JPEGMsgs
RMEnsure SpriteExtend 0.99 RMEnsure JPEGRender 0.00 RMLoad System:Modules.JPEGRender

| Routine actions...

IconSprites <Browse$Dir>.!Sprites

Set File$Type_FAF HTML
Set File$Type_F91 URI
Set File$Type_F81 JSScript
Set File$Type_695 GIF
Set File$Type_C85 JPEG
Set File$Type_B60 PNG

Set Alias$@RunType_FAF /<Browse$Dir>.!Run -html %%*0
Set Alias$@RunType_F91 /<Browse$Dir>.!Run -uri %%*0
Set Alias$@RunType_F81 /<Browse$Dir>.!Run -jss %%*0

| And finally, run the application.

WimpSlot -Min 5120K -Max 5120K

%UnSet ChangeFSI$Dir
|Run <Browse$Dir>.!RunImage -debug CMal,CFle %*0 2> Pipe:$.StdErr
Run <Browse$Dir>.!RunImage %*0
