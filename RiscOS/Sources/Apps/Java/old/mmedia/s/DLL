; DLL module wrapper

        GET	hdr:SWINames
        GET	hdr.DLL
        
        GET     DLLName.s
        
        AREA    |!!!Module$$Header|,CODE,READONLY

Module_BaseAddr
        DCD     0
        [ :DEF:REMOVE_RAMLOAD_CHECK:LAND::LNOT::DEF:INCLUDES_INDEX
        DCD	0
        |
        DCD     DLL_Init - Module_BaseAddr
        ]
	DCD     DLL_Die - Module_BaseAddr
        DCD     DLL_Service - Module_BaseAddr
        DCD     DLL_TitleString - Module_BaseAddr
        DCD     DLL_HelpString - Module_BaseAddr
        DCD     0
        DCD     0
        DCD     0
        DCD     0
        DCD     0

DLL_HelpString
        DCB	DLLName, 9
  [ :LEN: DLLName < 7
        DCB     9
  ]
        DCB     DLLVersion, 0

DLL_TitleString DCB DLLName, 0

        ALIGN


; Initialise module
DLL_Init
        [ :LNOT::DEF:REMOVE_RAMLOAD_CHECK
        ADRL    r0, Module_BaseAddr
        LDR     r1, module_linkaddr
        CMP     r0, r1
        MOVEQ	pc, lr
        ADR	r0, module_ramload_error
        STMDB	r13!, {r7, r14}
        MOV     r1, #0
        MOV     r2, #0
        ADRL    r4, DLL_TitleString
        MOV     r5, #0
        MOV     r6, #0
        MOV     r7, #0
        SWI     XMessageTrans_ErrorLookup
        LDMIA	r13!, {r7, r15}
        
module_ramload_error
        DCD     &800e0a
        DCB     "RAMLoadC", 0
        ALIGN     
           
        |
        
        [ :DEF:INCLUDES_INDEX
        IMPORT	|__RelocCode|
        IMPORT	index_file
               
        STMDB	r13!, {r14}
        BL	|__RelocCode|
        LDR	r0, =index_file
        SWI	XResourceFS_RegisterFiles
        LDMIA	r13!, {r15}
	]
	]
    
module_linkaddr
        DCD     Module_BaseAddr
       

; Finalise module
DLL_Die
        STMDB	r13!, {r14}
        ADRL	r0, DLL_TitleString
        SWI	XJava_DeregisterDLL
        LDMVCIA	r13!, {r15}
	LDR	r1, [r0]
	LDR	r2, =NoSuchSWI		;SWI will not be recognised if Java
					;not loaded
	TEQ	r1, r2
        LDMNEIA	r13!, {r15}
        LDMEQIA	r13!, {r15}^		;Clear error

      
; Service call handler
DLL_Service
	CMPS	r1, #Service_JavaStarting
	MOVNES	r15, r14
	
	STMDB	r13!, {r1, r14}
	ADRL	r0, DLL_TitleString
	[ :DEF:INCLUDES_INDEX
	LDR	r1, module_linkaddr
	|
	MOV	r1, #0
	]
	SWI	XJava_RegisterDLL
	LDMIA	r13!, {r1, r15}
	
        END
