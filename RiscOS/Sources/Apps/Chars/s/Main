; > Sources.Main

; *********************************
; ***    C h a n g e   L i s t  ***
; *********************************

; Date       Description
; ----       -----------
; 19-Feb-90     1.01    File created
; 08-Jan-91     1.02    Font Menu constructed using new Font_ListFonts in Font Manager 2.78
; 06-Nov-94     AMcC    &.Hdr. changed to Hdr:

Module_BaseAddr

        GET     s.AppName         ; read ApplicationName

        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:ModHand
        GET     Hdr:Services
        GET     Hdr:Proc
        GET     Hdr:ResourceFS
        GET     Version

        LEADR   Module_LoadAddr


; **************** Module code starts here **********************

        ASSERT  (.-Module_BaseAddr) = 0

        DCD     0 ; App_Start    - Module_BaseAddr
        DCD     App_Init     - Module_BaseAddr
        DCD     App_Die      - Module_BaseAddr
        DCD     App_Service  - Module_BaseAddr
        DCD     App_Title    - Module_BaseAddr
        DCD     App_HelpStr  - Module_BaseAddr
        DCD     0
        DCD     0
        DCD     0
        DCD     0
        DCD     0

        MACRO
        TabTo   $n
        LCLA    fred
fred    SETA    $n + &100000
        WHILE   fred > &100000
        DCB     9
fred    SETA    fred-8
        WEND
        MEND

App_Title       DCB     "!$ApplicationName", 0
App_HelpStr     DCB     "!$ApplicationName"
                TabTo   16-(.-App_HelpStr)
                DCB     "$VString ($Date)", 0
                ALIGN

; *****************************************************************************
;
;       App_Init - Initialisation entry
;

App_Init ENTRY "r0"

        ADR     R0, resourcefsfiles
        SWI     XResourceFS_RegisterFiles   ; ignore errors (starts on Service_ResourceFSStarting)

        EXITS

; *****************************************************************************
;
;       App_Die - Die entry
;

App_Die ENTRY "r0"

        ADR     R0, resourcefsfiles
        SWI     XResourceFS_DeregisterFiles

        EXITS                   ; ignore errors (ResourceFS might be dead)

; *****************************************************************************
;
;       App_Service - Main entry point for services
;

App_Service ROUT

        TEQ     R1, #Service_ResourceFSStarting
        MOVNE   PC, LR

; In    R2 -> address inside ResourceFS module to call
;       R3 = workspace pointer for module
; Out   R2 called with R0 -> files, R3 -> workspace

svc_resourcefsstarting ENTRY "R0-R3"

        ADR     R0, resourcefsfiles
        MOV     LR, PC                          ; get return address
        MOV     PC, R2                          ; R2 = address to call

        EXITS

resourcefsfiles
        GET     s.ResFiles
        DCD     0

        END
