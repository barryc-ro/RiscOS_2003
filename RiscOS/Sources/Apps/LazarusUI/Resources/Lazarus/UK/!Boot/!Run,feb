| Master Boot file.
|
Set Boot$Dir <Obey$Dir>
FreePool

| ** Run TestMenu if the TestStart$Flag is set.
| NOTE: This has got to be before ZipFS is used otherwise the parallel test fails.
if "<TestStart$Flag>" <> "" then Run Resources:$.Resources.Test.!TestMenu

| ** Check for Zip drive booting.
Run <Boot$Dir>.Utils.BootZip
Set Alias$Filer_Boot IfThere %%0.!Boot Then Run %%0.!Boot

| ** Configure Resolver.
Set Inet$ResolverDelay 5
Set Inet$ResolverRetries 3

| ** Setup Wimp Scrap directory.
Set Wimp$ScrapDir Cache:$.ScrapDir
Set Wimp$Scrap <Wimp$ScrapDir>.ScrapFile

| ** Setup UI paths.
Set OptScr$Path <Boot$Dir>.HTML.Setup.
Set MAScr$Path <Boot$Dir>.HTML.MA.
Set DialScr$Path <Boot$Dir>.HTML.Dialling.
Set ISPCScr$Path <Boot$Dir>.HTML.ISPCapture.
Set OtherScr$Path <Boot$Dir>.HTML.Other.

| ** Desktop Font.
Set Wimp$Font \FCaius.Book
Set Wimp$FontSize 224

| ** NCFresco configuration.
|
| NCOptions.
Set NCFresco$EventURL00 NCOptions_OpenPage:?file_template=OptScr:Options&quit_page=ncint:current
| Email button - superseded by NCMail (if present).
SetMacro NCFresco$EventURL01 <NCFresco$EmailURL>
| mailto: support
SetMacro NCFresco$ProxyMailTo <NCFresco$EmailURL>
| Hangup/Logout Button
Set NCFresco$EventURL02 ncma:logout
| Help button.
SetMacro NCFresco$EventURL03 <NCFresco$HelpURL>
| Smartcard setup.
Set NCFresco$EventURL04 Program_Smartcard:
| Startup URL
Set NCFresco$CurrentURL file:/OptScr:StartUp
| Welcome URL
SetMacro NCFresco$Welcome -url <NCFresco$CurrentURL>
| Home URL
SetMacro NCFresco$Home -url ncma:userhome
| Redial Command (deprecated in builds that have Logout).
Set Alias$NCFrescoRedial ncma_urc:DialUI_Start

| Added support for CTRL-SHIFT F12 to open supervisor.
Set Alias$URLOpen_gos echo |<22>|<0>|mHourglass Off|mGos %%*1
Set Alias$URLOpen_basic basic|M
Set Alias$URLOpen_command gos|m
Set NCFresco$ConfigModePlatform 0
Set NCFresco$EventURLFF gos:

Run <Boot$Dir>.Utils.CheckTest

| ** Set IconHigh colour.
IconHigh_SetHighlightColour 0x3333ff00

| ** Shrinkwrap configuration.
RMReInit ShrinkWrap
ShrinkWrapAddZone Resources:$.Apps

| Filer_Boot all apps in Resources:$.Apps
Repeat Filer_Boot Resources:$.Apps -Applications

| ** Execute Zip drive auto boot
IfThere IZipFS:Auto         Then IZipFS:Auto

| ** Java.
Ifthere Resources:$.Resources.Java.!Java then Run Resources:$.Resources.Java.!Java.!Boot

| Crude fix to get around Boot$Dir being reset by boot server's boot structure.
IfThere <Boot$Dir>.Palette Then WimpPalette <Boot$Dir>.Palette Else WimpPalette Resources:$.!Boot.Palette
Desktop <WatchDog$Dir>.!Run
