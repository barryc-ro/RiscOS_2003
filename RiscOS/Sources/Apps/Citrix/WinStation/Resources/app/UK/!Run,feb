| <ICAClient$Dir>.!Run
| application version
|
| -----------------------------------------------------------------------------
| Copy of boot file
|
Set ICAClient$Dir <Obey$Dir>
SetMacro ICAClient$Path <ICAClient$Dir>.
|
IconSprites ICAClient:!Sprites
|
Set File$Type_AD0 ICA
Set Alias$@RunType_AD0 /ICAClient:!Run -file %%*0
|
Set Alias$URLOpen_ICA  /ICAClient:!Run -url %%*0
Set Alias$Open_URI_ICA /ICAClient:!Run %%*0
|
| -----------------------------------------------------------------------------
|
| Work out what kind of NC it is, if any
|
Set ICAClient$NC ""
RMEnsure TaskManager 0 Set ICAClient$NC "modem"
RMEnsure PPP 0 Set ICAClient$NC "net"
|
| NCs with NCMALite 0.45 or greater specify the interface type.
If "<Inet$NICType>" = "Ether" Then Set ICAClient$NC "net"
If "<Inet$NICType>" = "Modem" Then Set ICAClient$NC "modem"
|
| -----------------------------------------------------------------------------
| Set default variables
|
If "<ICAClientINI$Path>" = "" AND "<Choices$Path>" <> "" Then Set ICAClientINI$Path Choices:ICAClient.INI.,ICAClient:ini.
If "<ICAClientINI$Path>" = "" AND "<Choices$Path>"  = "" Then Set ICAClientINI$Path ICAClient:ini.
|
If "<ICAClient$Errors>" = "" Then Set ICAClient$Errors 2> null:
If "<ICAClient$Run>" = ""    Then Set ICAClient$Run Run
|
If "<ICAClient$Options>" = "" AND "<ICAClient$NC>" = "modem" Then Set ICAClient$Options -c
If "<ICAClient$Options>" = "" Then Set ICAClient$Options "-"
|
If "<ICAClient$FileOptions>" = "" AND "<ICAClient$NC>" <> "" Then Set ICAClient$FileOptions -m
If "<ICAClient$FileOptions>" = "" Then Set ICAClient$FileOptions -i
|
If "<ICAClient$NoFileOptions>" = "" Then Set ICAClient$NoFileOptions -i
|
| -----------------------------------------------------------------------------
| Ensure modules
|
RMEnsure KeyWatch 0.09 RMLoad ICAClient:KeyWatch
RMEnsure KeyWatch 0.09 Error 0 You need KeyWatch 0.09 or later to run ICAClient
|
RMEnsure Toolbox 0 RMLoad System:Modules.Toolbox.Toolbox
RMEnsure Toolbox 0 Error You need the Toolbox module to run ICAClient
|
| -----------------------------------------------------------------------------
|
WimpSlot -min 400K -max 400K
|
Set Alias$ICAClientRun <ICAClient$Run> ICAClient:!RunImage %%*0 <ICAClient$Errors>
ICAClientRun %*0
