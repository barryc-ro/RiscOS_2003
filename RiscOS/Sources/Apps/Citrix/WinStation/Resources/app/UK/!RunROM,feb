| <WinFrame$Dir>.!Run
| ROM version
|
| -----------------------------------------------------------------------------
| Copy of boot file
|
Set WinFrame$Dir Resources:$.Resources.WinFrame
Set WinFrame$Path <Obey$Dir>.,<WinFrame$Dir>.
|
IconSprites WinFrame:!Sprites
|
Set File$Type_AD0 ICA
Set Alias$@RunType_AD0 /WinFrame:!Run %%*0
|
Set Alias$URLOpen_ICA  /WinFrame:!Run %%*0 -url
Set Alias$Open_URI_ICA /WinFrame:!Run %%*0
|
| -----------------------------------------------------------------------------
| Set default variables
|
If "<WinFrameINI$Path>" = ""       Then Set WinFrameINI$Path WinFrame:ini.
If "<WinFrame$Errors>" = ""        Then Set WinFrame$Errors 2> null:
If "<WinFrame$Options>" = ""	   Then Set WinFrame$Options ""
If "<WinFrame$FileOptions>" = ""   Then Set WinFrame$FileOptions ""
If "<WinFrame$NoFileOptions>" = "" Then Set WinFrame$NoFileOptions -i
|
| -----------------------------------------------------------------------------
| Build up option string
|
Set WinFrame$_Arg "%0"
If "<WinFrame$_Arg>" <> "" AND (("<WinFrame$_Arg>" LEFT 1) <> "-") Then Set WinFrame$_Opt <WinFrame$FileOptions> -f <WinFrame$_Arg> Else Set WinFrame$_Opt <WinFrame$NoFileOptions>
|
| See if we require toolbox modules
If ("<WinFrame$_Opt>" LEFT 2) = "-i" OR ("<WinFrame$Options>" LEFT 2 = "-i") Then SetEval WinFrame$_TB 1 Else SetEval WinFrame$_TB 0
|
| -----------------------------------------------------------------------------
| Ensure modules
|
RMEnsure KeyWatch 0.04 RMLoad System:Modules.KeyWatch
RMEnsure KeyWatch 0.04 Error 0 You need KeyWatch 0.04 or later to run WinFrame
|
RMEnsure Toolbox 0 RMLoad System:Modules.Toolbox.Toolbox
RMEnsure Toolbox 0 Error You need the Toolbox module to run WinFrame
If WinFrame$_TB Then RMEnsure Window 0 RMLoad System:Modules.Toolbox.Window
If WinFrame$_TB Then RMEnsure Window 0 Error You need the Window module to run WinFrame
If WinFrame$_TB Then RMEnsure Iconbar 0 RMLoad System:Modules.Toolbox.Iconbar
If WinFrame$_TB Then RMEnsure Iconbar 0 Error You need the Iconbar module to run WinFrame
If WinFrame$_TB Then RMEnsure Menu 0 RMLoad System:Modules.Toolbox.Menu
If WinFrame$_TB Then RMEnsure Menu 0 Error You need the Menu module to run WinFrame
|
| -----------------------------------------------------------------------------------------
|
RMEnsure !WinFrame 0 RMReinit !WinFrame
RMEnsure !WinFrame 0 Error You need the WinFrame module loaded
|
| -----------------------------------------------------------------------------
|
WimpSlot -min 128K -max 128K
|
Set Alias$WinFrameRun /WinFrame:!RunImage <WinFrame$_Opt> <WinFrame$Options> %*1 <WinFrame$Errors>
WinFrameRun
