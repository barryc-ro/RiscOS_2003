> MetSrc
        
\\\ SrceD
 
osword          *       &FFF1
osbyte          *       &FFF4
osget           *       &FFE0
oswrch          *       &FFEE
oscli           *       &FFF7
eventvec        *       &220
 
screen          *       &30
linelen         *       &280
`cutoff         *       &F0
`base           *       screen * 256 + linelen * (256-`cutoff) DIV 8
`depth          *       &8000 - `base
`col0           *       0
`col1           *       &0F
`col2           *       &F0
`col3           *       &FF
looptime        *       2
`dieprob        *       40
`capslock       *       &BF
`ctrl           *       &FE
`shift          *       &FF
`space          *       &9D
`return         *       &B6
`esc            *       &1B
`copy           *       &87
`delete         *       &7F
`spcprob        *       10
`spcvel         *       480
`spccnt         *       12
`timelo         *       &5A
`timehi         *       &46
accfreq         *       8
 
                ^       0
tempx           #       1
colour          #       1
colmask         #       1
addr            #       2
mask            #       1
limit           #       1
temp            #       2
raster          #       2
seed            #       3
stack           #       1
newshcnt        #       1
numcyc          #       1
score           #       2
gremno          #       1
shipshape       #       1
acccntr         #       1
collision       #       1
dead            #       1
hyperspace      #       1
time            #       1
oldtime         #       5
numroids        #       1
supp            #       1
sheetno         #       1
numships        #       1
soundon         #       1
oldscore        #       1
beepcount       #       1
beepdelay       #       1
messX           #       1
messY           #       1
posnno          #       1
spacecounter    #       1
killed          #       1
retflag         #       1
shipssofar      #       1
demo            #       1
beating         #       1
cycletime       #       1
cyclecount      #       1
beattime        #       1
beatcount       #       1
beatsnd         #       1
rocktime        #       1
beatdelay       #       1
letgoyet        #       1
sptype          #       1
lastcheck       #       1
bulletX         #       1
rightrocks      #       1
old224          #       1
V               #       1
K               #       1
dx              #       1
dy              #       1
vx              #       1
vy              #       1
_x              #       1
_y              #       2
_z              #       2
_a              #       4
_b              #       2
nextexp         #       1
currtime        #       1
endZ%           #       0
 
H%              *       &2B00

                ^       H%
hientlen        *       23
maxhient        *       9*hientlen
hiscore         #       0
hiscoretab      #       10*hientlen
endH%           #       0

posnxtab        *       5
scorextab       *       9
namextab        *       17
nameytab        *       5
 
rocklen         *       26
nb              *       5
len             *       rocklen+nb

T%              *       &2C00
ntyp            *       48
ntyp1           *       ntyp-32

                ^       T%
size            #       ntyp
shapelo         #       ntyp
shapehi         #       ntyp

                ^       @-32
xlength         #       ntyp1
xoffset         #       ntyp1
ylength         #       ntyp1
yoffset         #       ntyp1

                ^       @+32-rocklen
exvlo           #       nb
exvhi           #       nb
eyvlo           #       nb
eyvhi           #       nb
egflags         #       nb
                ^       @+rocklen

nx              *       6
extime          #       nx
exshapes        #       16
limtime         #       8
exaddlo         #       8*nx
exaddhi         #       8*nx
exmask          #       8*nx
endT%           #       0
 
U%              *       &400

                ^       U%
type            #       len
oldtype         #       len
xlo             #       len
xhi             #       len
ylo             #       len
yhi             #       len
xvlo            #       len
xvhi            #       len
yvlo            #       len
yvhi            #       len
oldlo           #       len
oldhi           #       len
oldmask         #       len
newlo           #       len
newhi           #       len
newmask         #       len
gflags          #       len
endU%           #       0
 
S%              *       &2400
 
data%           *       endT%

                ^       data%
nsh             *       32
accn            *       7
xacclo          #       nsh
xacchi          #       nsh
yacclo          #       nsh
yacchi          #       nsh
bxvlo           #       nsh
bxvhi           #       nsh
byvlo           #       nsh
byvhi           #       nsh
 
nsnds           *       12
sndbuff         #       8
snd0            #       nsnds
snd1            #       nsnds
snd2            #       nsnds
snd3            #       nsnds
snd4            #       nsnds
S_type          #       2
S_palette       #       2
S_scorelo       #       2
S_scorehi       #       2
S_sound         #       2
my224           #       1
oswordblk       #       5
bits            #       4
rockscore       #       3
M%              #       0

nmess           *       7
messptr         *       M%
messlen         *       messptr + 2*nmess + 2
currmess%       *       messlen + nmess

 
\\\ Srce1
         
        JMP     entry
         
vsyncevent
        PHP
        PHA
        CMP     #4
        BNE     endevent
        INC     my224
        JSR     start6522
endevent
        PLA
        PLP
        RTS

vsync
        JMP     P%
newvsync
        LDA     my224
        CMP     old224
        BEQ     newvsync
        STA     old224
        RTS

oldvsync
        LDA     &224
        CMP     old224
        BEQ     oldvsync
        STA     old224
\$      JSR     start6522

start6522
        LDA     #&C0
        STA     &FE6B
        LDA     #`timelo
        STA     &FE64
        LDA     #`timehi
        STA     &FE65
        RTS

timeout6522
        BIT     &FE60
        RTS

setcaps
        JMP     P%

newcaps
        LDA     #202
        LDX     #&20
        LDY     #&CF
        JMP     osbyte

oldcaps
        LDA     #&20
        STA     &D8
        RTS

\ ***************************
         
crtcreg
        PHA
        LDA     #23
        JSR     oswrch
        LDA     #0
        JSR     oswrch
        TXA
        JSR     oswrch
        PLA
        JSR     oswrch
        JSR     three0s
        JMP     three0s
         
ospalette
        PHA
        LDA     #19
        JSR     oswrch
        PLA
        PHA
        LSR     A
        LSR     A
        LSR     A
        LSR     A
        JSR     oswrch
        PLA
        AND     #15
        JSR     oswrch
three0s
        LDA     #0
        JSR     oswrch
        JSR     oswrch
        JMP     oswrch
         
inkey0
        LDY     #0
        LDX     #0
        LDA     #&81
        JSR     osbyte
        TXA
        CPY     #`esc
        BNE     doneinkey
        LDA     #126
        JSR     osbyte
        LDA     #`esc
doneinkey
        RTS

inkey
        LDY     #&FF
        LDA     #&81
        JSR     osbyte
        TXA
noupdate
        RTS
         
sound
        STA     temp
        LDA     demo
        BNE     nosound
        LDA     soundon
        BEQ     nosound
        TXA
        PHA
        TYA
        PHA
        LDX     temp
        LDA     snd0,X
        STA     temp
sndlp
        LDA     snd0,X
        STA     sndbuff+1
        LDA     snd1,X
        STA     sndbuff
        LDY     #2
        LDA     snd2,X
        JSR     putsnd
        LDA     snd3,X
        JSR     putsnd
        LDA     snd4,X
        JSR     putsnd
        TXA
        PHA
        LDX     #sndbuff AND &FF
        LDY     #sndbuff DIV 256
        LDA     #7
        JSR     osword
        PLA
        TAX
        INX
        DEC     temp
        BPL     sndlp
        PLA
        TAY
        PLA
        TAX
nosound
        RTS
         
putsnd
        STA     sndbuff,Y
        INY
        ASL     A
        LDA     #0
        BCC     P%+4
        LDA     #&FF
        STA     sndbuff,Y
        INY
        RTS
         
addvels
        LDA     type,X
        ASL     A
        BMI     noupdate
        LSR     A
        STA     type,X
        CLC
        LDA     xlo,X
        ADC     xvlo,X
        STA     xlo,X
        LDA     xhi,X
        ADC     xvhi,X
        STA     xhi,X
        CMP     #160
        BCC     okup
        CMP     #210
        BCS     offleft
        SEC
        SBC     #160
        STA     xhi,X
        SEC
        LDA     yhi,X
        SBC     #8
        STA     yhi,X
        JMP     okup

offleft
        CLC
        ADC     #160
        STA     xhi,X
        CLC
        LDA     yhi,X
        ADC     #8
        STA     yhi,X
okup
        CLC
        LDA     ylo,X
        ADC     yvlo,X
        STA     ylo,X
        LDA     yhi,X
        ADC     yvhi,X
        CMP     #(256+`cutoff) DIV 2
        BCC     L1
        ADC     #`cutoff-1
L1
        CMP     #`cutoff
        BCC     L2
        SBC     #`cutoff
L2
        STA     yhi,X
        RTS
         
update
        JSR     addvels
\$      JSR     getaddr
         
getaddr
        LDA     #screen DIV 4
        STA     addr+1
        LDA     xhi,X
        AND     #&FE
        ASL     A
        ROL     addr+1
        ASL     A
        ROL     addr+1
        STA     addr
        LDA     yhi,X
        AND     #7
        EOR     #7
        ORA     addr
        STA     newlo,X
        LDA     #0
        STA     addr
        LDA     yhi,X
        EOR     #&FF
        AND     #&F8
        LSR     A
        LSR     A
        STA     temp
        LSR     A
        LSR     A
        ROR     addr
        ADC     temp
        STA     temp
        CLC
        LDA     addr
        ADC     newlo,X
        STA     newlo,X
        LDA     addr+1
        ADC     temp
        STA     newhi,X
        STX     temp
        LDA     xlo,X
        ASL     A
        LDA     xhi,X
        ROL     A
        AND     #3
        TAX
        LDA     bits,X
        LDX     temp
        STA     newmask,X
        RTS
         
setcol
        LDA     #`col2
        CPX     #32
        BCC     L10
        CPX     #35
        BCC     L11
L10
        LDA     #`col3
        BCC     L11
        LDA     #`col1
        CPX     #35
        BEQ     L11
        CPX     #36
        BEQ     L11
        CPX     #42
        BEQ     L11
        LDA     #`col3
L11
        STA     colour
        AND     mask
        STA     colmask
        RTS
         
getexplos
        LDY     #nx-1
findexplp
        LDA     extime,Y
        BEQ     gotanexp
        DEY
        BPL     findexplp
        RTS
gotanexp
        LDA     #10
        STA     extime,Y
        TYA
        PHA
        CPX     #0
        BEQ     nooffset
        LDA     type,X
        AND     #&7F
        TAY
        LDA     xlength,Y
        LSR     A
        STA     temp
        LDA     ylength,Y
        LSR     A
        STA     temp+1
        SEC
        LDA     xhi,X
        PHA
        SBC     xoffset,Y
        CLC
        ADC     temp
        STA     xhi,X
        SEC
        LDA     yhi,X
        PHA
        SBC     yoffset,Y
        CLC
        ADC     temp+1
        CMP     #`cutoff
        BCC     L7
        SBC     #`cutoff
L7
        STA     yhi,X
        JSR     getaddr
        PLA
        STA     yhi,X
        PLA
        STA     xhi,X
nooffset
        PLA
        ASL     A
        ASL     A
        ASL     A
        TAY
startexplp
        LDA     newlo,X
        STA     exaddlo,Y
        LDA     newhi,X
        STA     exaddhi,Y
        LDA     newmask,X
        STA     exmask,Y
        INY
        TYA
        AND     #7
        BNE     startexplp
        RTS
         
doexplosion
        LDX     nextexp
        INX
        CPX     #nx
        BNE     nowrap1
        LDX     #0
nowrap1
        STX     nextexp
        LDA     extime,X
        BEQ     retn
        SEC
        SBC     #1
        STA     extime,X
        STA     currtime
        BEQ     remexp
        TXA
        ASL     A
        ASL     A
        ASL     A
        TAX
plotexplp
        TXA
        AND     #7
        TAY
        LDA     limtime,Y
        CMP     currtime
        BCC     notyet
        JSR     plotexpbit
notyet
        INX
        TXA
        AND     #7
        BNE     plotexplp
retn
        RTS

remexp
        TXA
        ASL     A
        ASL     A
        ASL     A
        TAX
        LDY     #0
remexplp
        LDA     exaddlo,X
        STA     addr
        LDA     exaddhi,X
        STA     addr+1
        LDA     exmask,X
        AND     #`col3
        EOR     (addr),Y
        STA     (addr),Y
        INX
        TXA
        AND     #7
        BNE     remexplp
        RTS
         
plotexpbit
        LDA     exaddlo,X
        AND     #7
        TAY
        LDA     exaddlo,X
        AND     #&F8
        STA     addr
        LDA     exaddhi,X
        STA     addr+1
        LDA     #2
        STA     limit
        LDA     exmask,X
        STA     mask
        LDA     #`col3
        STA     colour
        AND     mask
        STA     colmask
        TXA
        PHA
        AND     #7
        ASL     A
        ADC     #exshapes AND &FF
        STA     P1+1
        STA     P2+1
        STA     P3+1
        LDA     #0
        ADC     #exshapes DIV 256
        STA     P1+2
        STA     P2+2
        STA     P3+2
        LDX     #0
        JSR     plot4
        PLA
        TAX
        TYA
        ORA     addr
        STA     exaddlo,X
        LDA     addr+1
        STA     exaddhi,X
        LDA     mask
        STA     exmask,X
        RTS
         
plotroid
        JSR     setcol
plotshape
        LDA     addr
        AND     #7
        TAY
        LDA     addr
        AND     #&F8
        STA     addr
        TXA
        PHA
        LDA     size,X
        STA     limit
        LDA     shapelo,X
        STA     P1+1
        STA     P2+1
        STA     P3+1
        LDA     shapehi,X
        STA     P1+2
        STA     P2+2
        STA     P3+2
        LDA     #0
        STA     collision
        LDX     #0
        JSR     plot4
        PLA
        TAX
        RTS
         
qinc1
        INC     addr+1
        BNE     qplot2
qplot1
        ASL     mask
        BCC     qplot2
        LDA     #&11
        STA     mask
        SEC
        LDA     addr
        SBC     #8
        STA     addr
        BCS     qplot2
        DEC     addr+1
        BNE     qplot2
qplotdone
        RTS
         
quickplot
P4
        LDA     P%,X
        AND     #&20
        BNE     noqplot
        LDA     colour
        AND     mask
        EOR     (addr),Y
        STA     (addr),Y
noqplot
        INX
        CPX     limit
        BEQ     qplotdone
P5
        LDA     P%,X
        BMI     qplot1
        ASL     A
        BPL     qplot2
        LSR     mask
        BCC     qplot2
        LDA     #&88
        STA     mask
        CLC
        LDA     addr
        ADC     #8
        STA     addr
        BCS     qinc1
qplot2
P6
        LDA     P%,X
        LSR     A
        BCS     qplot3
        LSR     A
        BCC     quickplot
        DEY
        BPL     quickplot
        LDY     #7
        SEC
        LDA     addr
        SBC     #linelen AND &FF
        STA     addr
        LDA     addr+1
        SBC     #linelen DIV 256
        STA     addr+1
        BNE     quickplot
qplot3
        INY
        CPY     #8
        BNE     quickplot
        LDY     #0
        CLC
        LDA     addr
        ADC     #linelen AND &FF
        STA     addr
        LDA     addr+1
        ADC     #linelen DIV 256
        STA     addr+1
        BNE     quickplot
         
plotdone
        RTS
         
plot4
P3
        LDA     P%,X
        AND     #&20
        BNE     noplot
        LDA     colmask
        EOR     (addr),Y
        STA     (addr),Y
        AND     mask
        EOR     colmask
        BEQ     noplot
        STA     temp
        LDA     #`col3
        AND     mask
        EOR     temp
        STA     collision
noplot
        INX
        CPX     limit
        BEQ     plotdone
P1
        LDA     P%,X
        BMI     plot1
        ASL     A
        BPL     plot2
        LSR     colmask
        LSR     mask
        BCC     plot2
        LDA     #&88
        STA     mask
        AND     colour
        STA     colmask
        CLC
        LDA     addr
        ADC     #8
        STA     addr
        BCC     plot2
        INC     addr+1
        BPL     plot2
        LDA     addr
        SEC
        SBC     #`depth AND &FF
        STA     addr
        LDA     addr+1
        SBC     #`depth DIV 256
        STA     addr+1
        JMP     plot2

plot1
        ASL     colmask
        ASL     mask
        BCC     plot2
        LDA     #&11
        STA     mask
        AND     colour
        STA     colmask
        SEC
        LDA     addr
        SBC     #8
        STA     addr
        BCS     L6
        DEC     addr+1
L6
        LDA     addr+1
        CMP     #`base DIV 256
        BCC     L4
        BNE     plot2
        LDA     addr
        CMP     #`base AND &FF
        BCS     plot2
L4
        CLC
        LDA     addr
        ADC     #`depth AND &FF
        STA     addr
        LDA     addr+1
        ADC     #`depth DIV 256
        STA     addr+1

plot2
P2
        LDA     P%,X
        LSR     A
        BCS     plot3
        LSR     A
        BCC     plot4b
        DEY
        BPL     plot4b
        LDY     #7
        SEC
        LDA     addr
        SBC     #linelen AND &FF
        STA     addr
        LDA     addr+1
        SBC     #linelen DIV 256
        STA     addr+1
        CMP     #`base DIV 256
        BCC     L5
        BNE     plot4b
        LDA     addr
        CMP     #`base AND &FF
        BCS     plot4b
L5
        CLC
        LDA     addr
        ADC     #`depth AND &FF
        STA     addr
        LDA     addr+1
        ADC     #`depth DIV 256
        STA     addr+1
plot4b
        JMP     plot4

plot3
        INY
        CPY     #8
        BNE     plot4a
        LDY     #0
        CLC
        LDA     addr
        ADC     #linelen AND &FF
        STA     addr
        LDA     addr+1
        ADC     #linelen DIV 256
        STA     addr+1
        BPL     plot4a
        SEC
        LDA     addr
        SBC     #`depth AND &FF
        STA     addr
        LDA     addr+1
        SBC     #`depth DIV 256
        STA     addr+1
plot4a
        JMP     plot4
         
execute
        LDX     #0
exlp
        LDA     type,X
        BMI     noex
        CMP     #32
        BCS     ex1
exwait
        LDY     yhi,X
        JSR     testraster
        CMP     #24
        BCC     exwait
ex1
        JSR     unplot
        LDA     newlo,X
        STA     oldlo,X
        STA     addr
        LDA     newhi,X
        STA     oldhi,X
        STA     addr+1
        BEQ     ex2
        LDA     newmask,X
        STA     oldmask,X
        STA     mask
        TXA
        PHA
        LDA     type,X
        STA     oldtype,X
        TAX
        JSR     plotroid
        PLA
        TAX
        CPX     #0
        BNE     ex2a
        LDA     collision
        ORA     dead
        STA     dead
        JMP     ex2
ex2a
        CPX     #rocklen
        BCC     ex2
        LDA     killed
        BNE     ex2
        LDA     collision
        BEQ     ex2
        JSR     directhit
ex2
        LDA     type,X
        ORA     #&80
        STA     type,X
noex
        INX
        CPX     #len
        BNE     exlp
endex
        RTS
         
unplot
        LDA     oldlo,X
        STA     addr
        LDA     oldhi,X
        STA     addr+1
        BEQ     ex1a
        LDA     oldmask,X
        STA     mask
        TXA
        PHA
        LDA     oldtype,X
        TAX
        JSR     plotroid
        PLA
        TAX
ex1a
        RTS
         
kill
        LDA     rocktime
        STA     beatcount
        LDA     #2
        JSR     sound
        JSR     getexplos
        CPX     #1
        BNE     itsarock
        JSR     killspc
        LDX     sptype
        LDY     S_scorehi,X
        LDA     S_scorelo,X
        TAX
        JMP     addscXY

itsarock
        DEC     numroids
gorock
        LDA     type,X
        ASL     A
        BMI     nokill
        LSR     A
        SEC
        SBC     #32
        CMP     #3
        BCS     nokill
        PHA
        STX     temp
        TAY
        LDA     bulletX
        CMP     #rocklen
        BEQ     noscore
        LDX     rockscore,Y
        LDY     #0
        JSR     addscXY
noscore
        LDX     temp
        LDY     temp
        PLA
        CMP     #0
        BNE     notbig
        INY
        INY
        BNE     kill1
notbig
        CMP     #1
        BNE     notmedium
        INY
kill1
        JMP     getrocks
notmedium
        JMP     die
nokill
        RTS
         
getrocks
        TXA
        PHA
        JSR     veladd
        ASL     temp
        ROL     temp+1
        CLC
        LDA     xvlo,X
        ADC     temp
        STA     xvlo,Y
        LDA     xvhi,X
        ADC     temp+1
        STA     xvhi,Y
        SEC
        LDA     xvlo,X
        SBC     temp
        STA     xvlo,X
        LDA     xvhi,X
        SBC     temp+1
        STA     xvhi,X
        JSR     veladd
        CLC
        LDA     yvlo,X
        ADC     temp
        STA     yvlo,Y
        LDA     yvhi,X
        ADC     temp+1
        STA     yvhi,Y
        SEC
        LDA     yvlo,X
        SBC     temp
        STA     yvlo,X
        LDA     yvhi,X
        SBC     temp+1
        STA     yvhi,X
        JSR     copycoords
        CLC
        LDA     yhi,Y
        ADC     #8
        STA     yhi,Y
        LDA     type,X
        ORA     #&80
        CLC
        ADC     #1
        STA     type,Y
        STA     type,X
        TYA
        TAX
        JSR     zeroaddrs
        PLA
        TAX
        RTS
         
veladd
        JSR     random
        AND     #31
        STA     _x
        LDA     V
        STA     _y
        JSR     multiply
        LDA     _a+1
        STA     temp+1
        LDA     _a
        STA     temp
        RTS
         
die
        LDA     type,X
        ASL     A
        BMI     nodie
        JSR     unplot
        LDA     #&FF
        STA     type,X
nodie
        RTS
         
directhit
        CPX     #rocklen
        BNE     notspbullet
        LDA     #2
        BNE     eitherb
notspbullet
  \ JSR  die
        LDA     #1
eitherb
        STA     lastcheck
        TXA
        PHA
        LDA     xhi,X
        STA     tempx
        LDA     yhi,X
        STA     tempx+1
        LDX     #rocklen-1
hitlp
        LDA     type,X
        ASL     A
        BMI     endhit
        LSR     A
        TAY
        LDA     xlength,Y
        STA     temp+1
        SEC
        LDA     xhi,X
        SBC     xoffset,Y
        STA     temp
        SEC
        LDA     #160
        SBC     temp
        CMP     temp+1
        BCS     nolrwrap
        CLC
        LDA     temp+1
        ADC     #256-160
        STA     temp+1
nolrwrap
        SEC
        LDA     tempx
        SBC     temp
        CMP     temp+1
        BCS     endhit
        LDA     ylength,Y
        STA     temp+1
        SEC
        LDA     yhi,X
        SBC     yoffset,Y
        STA     temp
        SEC
        LDA     #`cutoff
        SBC     temp
        CMP     temp+1
        BCS     noudwrap
        CLC
        LDA     temp+1
        ADC     #256-`cutoff
        STA     temp+1
noudwrap
        SEC
        LDA     tempx+1
        SBC     temp
        CMP     temp+1
        BCC     gothit
endhit
        DEX
        CPX     lastcheck
        BCS     hitlp
        PLA
        TAX
        RTS
gothit
        JSR     kill
        PLA
        TAX
        JMP     die
         
waitspace
        JSR     cycle
        LDA     type+1
        ASL     A
        BPL     waitspace
        LDX     #rocklen-1
wsplp
        LDA     type,X
        ASL     A
        BMI     nowsp
        LDA     xhi,X
        CMP     #50
        BCC     nowsp
        CMP     #90
        BCS     nowsp
        LDA     yhi,X
        CMP     #85
        BCC     nowsp
        CMP     #148
        BCS     nowsp
        JMP     waitspace
nowsp
        DEX
        BNE     wsplp
        RTS
         

\\\ Srce2
        
getspaceship
        LDA     type+1
        ASL     A
        BPL     nospc
        LDA     demo
        BNE     getspc
        LDA     type
        ASL     A
        BMI     nospc
        LDA     beating
        BEQ     nospc
        LDA     beatcount
        BEQ     getspc
nospc
        RTS
getspc
        LDA     #0
        STA     rightrocks
        LDA     rocktime
        STA     beatcount
        LDX     #1
        JSR     zeroaddrs
        INC     shipssofar
        LDA     shipssofar
        CMP     #7
        LDA     #0
        BCC     gotsptype
        LDA     #1
gotsptype
        STA     sptype
        TAX
        LDA     S_type,X
        STA     type+1
        LDA     S_palette,X
        JSR     ospalette
        LDA     S_sound,X
        JSR     sound
        LDA     #0
        STA     xhi+1
        JSR     random
        STA     yhi+1
        LDA     #`spcvel AND &FF
        STA     xvlo+1
        LDA     #`spcvel DIV 256
        STA     xvhi+1
        JSR     random
        BPL     posvel
        LDA     #159
        STA     xhi+1
        LDA     #-`spcvel AND &FF
        STA     xvlo+1
        LDA     #(-`spcvel AND &FFFF) DIV 256
        STA     xvhi+1
posvel
        LDA     #1
        STA     spacecounter
\$      JSR     rndspcvel
         
rndspcvel
        JSR     rndvel
        STA     yvlo+1
        LDA     temp+1
        STA     yvhi+1
        RTS
         
spaceship
        TYA
        PHA
        LDA     xvhi+1
        ASL     A
        LDA     xhi+1
        BCC     tryr
        CMP     #10
        BCC     spcoff
        BCS     okspc
tryr
        CMP     #150
        BCC     okspc
spcoff
        JSR     killspc
        LDA     #8
        JSR     sound
        JMP     spcdone
okspc
        DEC     spacecounter
        BNE     tryb
        LDA     #`spccnt
        STA     spacecounter
        JSR     rndspcvel
tryb
        LDA     type+rocklen
        ASL     A
        BPL     spcdone
        LDA     killed
        BNE     spcdone
        LDA     #&80+35
        LDX     #rocklen
        JSR     initobj
        LDX     #1
        LDY     #rocklen
        JSR     copycoords
        LDX     #rocklen
        JSR     firevels
        LDA     #40
        STA     gflags,Y
        LDA     #6
        JSR     sound
spcdone
        PLA
        TAY
        LDX     #1
        RTS
         
killspc
        LDA     #0
        STA     rightrocks
        LDX     #1
        JSR     die
        LDA     #8
        JMP     sound
         
scale8
        PHA
        PHP
        LDA     #0
        PLP
        BPL     L8
        LDA     #&FF
L8
        STA     temp
        PLA
        JMP     mult8
         
gremlins
        LDX     gremno
        LDY     numcyc
grlp
        LDA     type,X
        ASL     A
        BMI     gr2
        CPX     #1
        BNE     gr1
        JSR     spaceship
gr1
        JSR     update
gr2
        INX
        CPX     #rocklen
        BNE     P%+4
        LDX     #1
        DEY
        BNE     grlp
        STX     gremno
        LDX     #rocklen
grlp1
        LDA     type,X
        ASL     A
        BMI     grend
        DEC     gflags,X
        BNE     grok
        JSR     die
grok
        JSR     update
grend
        INX
        CPX     #len
        BNE     grlp1
        RTS
         
random
        TXA
        PHA
        LDX     #8
rndlp
        LDA     seed
        AND     #&48
        ADC     #&38
        ASL     A
        ASL     A
        ROL     seed+2
        ROL     seed+1
        ROL     seed
        DEX
        BNE     rndlp
        PLA
        TAX
        LDA     seed
        RTS
         
addscXY
        SED
        CLC
        TXA
        ADC     score
        STA     score
        TYA
        ADC     score+1
        STA     score+1
        CLD
        AND     #&F0
        CMP     oldscore
        STA     oldscore
        BEQ     P%+5
        JSR     extraship
\$      JSR     pscore
         
pscore
        LDX     #2
        LDY     #0
        JSR     tabxy
        LDA     score+1
        JSR     pfbyte
        LDA     score
\$      JSR     pbyte
         
pbyte
        PHA
        LSR     A
        LSR     A
        LSR     A
        LSR     A
        JSR     pdig
        PLA
        AND     #15
\$      JSR     pdig
         
pdig
        STX     mask
        TAX
        ORA     supp
        STA     supp
        BNE     pdig1
        LDX     #(ASC" "-ASC"0") AND &FF
pdig1
        TXA
        CLC
        ADC     #ASC"0"
        LDX     mask
        JMP     oswrch
         
pfbyte
        PHA
        LDA     #0
        STA     supp
        PLA
        JMP     pbyte
         
phiscore
        LDX     #17
        LDY     #1
        JSR     tabxy
        LDA     hiscore+1
        JSR     pfbyte
        LDA     hiscore
        JMP     pbyte
         
psheetno
        LDX     #37
        LDY     #0
        JSR     tabxy
        LDA     sheetno
        JMP     pfbyte
         
tabxy
        LDA     #31
        JSR     oswrch
        TXA
        JSR     oswrch
        TYA
        JMP     oswrch
         
cycles
        TXA
        PHA
        JSR     cycle
        PLA
        TAX
        DEX
        BNE     cycles
        RTS
         
cycle
        LDX     #`space
        JSR     inkey
        AND     letgoyet
        STA     letgoyet
        JSR     mvship
        JSR     setcaps
        JSR     gremlins
        JSR     doexplosion
        JSR     doexplosion
        JSR     vsync
        JSR     execute
        JSR     getspaceship
        JSR     dobeeps
        JSR     inkey0
        PHA
        JSR     checkoptions
        PLA
        RTS
         
checkoptions
        LDX     demo
        BNE     soundoptions
        CMP     #`esc
        BNE     notesc
isesc
        LDA     #0
        STA     score
        STA     score+1
        STA     numships
        LDX     #0
        JSR     die
        LDA     #&FF
        STA     killed
        LDX     #rocklen
bulloff
        JSR     die
        INX
        CPX     #len
        BNE     bulloff
        LDX     stack
        TXS
        JMP     allover
notesc
        CMP     #`copy
        BNE     notcopy
pauselp
        JSR     inkey0
        CMP     #`esc
        BEQ     isesc
        PHA
        JSR     soundoptions
        PLA
        CMP     #`delete
        BNE     pauselp
        RTS
notcopy
\$      JSR     soundoptions
         
soundoptions
        AND     #&DF
        CMP     #ASC"S"
        BNE     notS
        LDA     #&FF
        STA     soundon
        RTS
notS
        CMP     #ASC"Q"
        BNE     notQ
        LDA     #0
        STA     soundon
        LDA     #15
        LDX     #0
        JSR     osbyte
notQ
        RTS
         
dobeeps
        DEC     cyclecount
        BNE     nodec
        LDA     cycletime
        STA     cyclecount
        LDA     beattime
        CMP     #5
        BCC     nodec
        DEC     beattime
nodec
        LDA     beepcount
        BEQ     nobeep
        DEC     beepdelay
        BNE     nobeat
        LDA     #8
        STA     beepdelay
        DEC     beepcount
        LDA     #1
        JMP     sound
nobeep
        LDA     type+1
        ASL     A
        BPL     nobeat
        LDA     beating
        BEQ     nobeat
        DEC     beatdelay
        BNE     nobeat
        LDA     beattime
        STA     beatdelay
        LDA     beatcount
        BEQ     dobeat
        DEC     beatcount
dobeat
        LDA     beatsnd
        EOR     #(9EOR10)
        STA     beatsnd
        JMP     sound
nobeat
        RTS
         
entry
        LDA     #200
        LDX     #0
        LDY     #&FE
        JSR     osbyte    \ re-enable escape
        LDA     #&FF
        STA     soundon
        LDA     #4
        LDX     #1
        JSR     osbyte
keylp1
        JSR     inkey0
        CMP     #ASC" "
        BEQ     letsgo
        JSR     soundoptions
        JMP     keylp1

letsgo
        JSR     init
        TSX
        STX     stack
gameloop
        JSR     gameinit
restart
        LDA     #0
        STA     killed
mainloop
        JSR     cycle
        LDA     killed
        BNE     needanother
        LDA     numroids
        BNE     mainloop
        JSR     newsheet
        JMP     mainloop
needanother
        JSR     getship
        BNE     restart

gameover
        LDA     #2
        JSR     message
        LDX     #100
        JSR     cycles
allover
        LDA     #8
        JSR     sound
        JSR     highscores
        LDA     #&FF
        STA     demo
        LDA     #6
        JSR     message
        JSR     zaddrs

        LDA     #15
        LDX     #1
        JSR     osbyte
keylp
        JSR     cycle
        CMP     #ASC" "
        BNE     keylp
        JMP     gameloop
         
testraster
        LDA     #0
        STA     raster
        TYA
        SEC
        SBC     #40
        LSR     A
        ROR     raster
        LSR     A
        ROR     raster
        ADC     #4
        STA     raster+1
        SEC
        LDA     &FE64
        SBC     raster
        STA     raster
        LDA     &FE65
        SBC     raster+1
        STA     raster+1
        BMI     P%+3
        RTS
        CLC
        LDA     raster
        ADC     #`timelo
        LDA     raster+1
        ADC     #`timehi
        RTS
         
message
        STX     messX
        STY     messY
        ASL     A
        TAX
        LDA     messptr,X
        STA     addr
        LDA     messptr+1,X
        STA     addr+1
        LDY     #0
        TXA
        LSR     A
        TAX
messlp
        LDA     (addr),Y
        JSR     oswrch
        INY
        TYA
        CMP     messlen,X
        BNE     messlp
        LDX     messX
        LDY     messY
        RTS
         
init
        LDA     #0
        JSR     message
        LDX     #8
        LDA     #0
        JSR     crtcreg
        LDX     #10
        LDA     #32
        JSR     crtcreg
        LDA     #0
        STA     nextexp
        LDA     #1
        STA     seed
        LDX     #24
init1
        JSR     random
        DEX
        BNE     init1
        LDA     #oldvsync AND &FF
        STA     vsync+1
        LDA     #oldvsync DIV 256
        STA     vsync+2
        LDA     #oldcaps AND &FF
        STA     setcaps+1
        LDA     #oldcaps DIV 256
        STA     setcaps+2
        LDX     #0
        JSR     inkey
        BEQ     gotvsync
        LDA     #newcaps AND &FF
        STA     setcaps+1
        LDA     #newcaps DIV 256
        STA     setcaps+2
        SEI
        LDA     #vsyncevent AND &FF
        STA     eventvec
        LDA     #vsyncevent DIV 256
        STA     eventvec+1
        CLI
        LDA     #14
        LDX     #4
        JSR     osbyte
        LDA     #newvsync AND &FF
        STA     vsync+1
        LDA     #newvsync DIV 256
        STA     vsync+2
gotvsync
        RTS
         
gameinit
        LDA     #&11
        JSR     ospalette
        LDA     #&26
        JSR     ospalette
        LDA     #&33
        JSR     ospalette
        LDA     #&FF
        STA     letgoyet
        LDA     #1
        JSR     message
        LDA     #3
        STA     V
        LDA     #20
        STA     rocktime
        STA     beatcount
        LDA     #100
        STA     cycletime
        STA     cyclecount
        LDA     #1
        STA     gremno
        LDA     #8
        STA     numcyc
        LDA     #0
        STA     shipshape
        STA     shipssofar
        STA     demo
        STA     oldscore
        STA     score
        STA     score+1
        STA     beating
        STA     beepcount
        STA     rightrocks
        JSR     pscore
        JSR     phiscore
        LDA     #1
        STA     acccntr
        LDX     #len-1
initlp
        LDA     #&FF
        STA     type,X
        DEX
        BPL     initlp
        LDX     #nx-1
initlp1
        LDA     #0
        STA     extime,X
        DEX
        BPL     initlp1
        LDA     #0
        STA     sheetno
        LDA     #3
        STA     numships
        JSR     zaddrs
        JSR     getship
        JSR     newsheet
        RTS
         
zaddrs
        LDX     #len-1
zaddlp
        JSR     zeroaddrs
        DEX
        BPL     zaddlp
        LDX     #nx-1
zaddlp1
        LDA     #0
        STA     extime,X
        DEX
        BPL     zaddlp1
        RTS
         
getship
        LDA     numships
        BNE     P%+3
        RTS
        LDA     #&FF
        STA     type
        JSR     waitspace
        DEC     numships
        JSR     pships
        LDA     #&80
        STA     type
        LDA     #0
        STA     dead
        STA     hyperspace
        LDA     #80
        STA     xhi
        LDA     #128
        STA     yhi
        LDX     #0
        JSR     zerovels
        JSR     zeroaddrs
        LDA     #1
        STA     beating
        RTS
         
killship
        LDA     #0
        STA     beating
        LDA     #8
        JSR     sound
        LDA     #&FF
        STA     killed
        LDA     #4
        JSR     sound
        LDX     #0
        JSR     die
        LDX     #rocklen
getexplp
        JSR     die
        TXA
        TAY
        LDX     #0
        JSR     copycoords
        TYA
        TAX
        LDA     exvlo,X
        STA     xvlo,X
        LDA     exvhi,X
        STA     xvhi,X
        LDA     eyvlo,X
        STA     yvlo,X
        LDA     eyvhi,X
        STA     yvhi,X
        CLC
        TXA
        ADC     #&A5-rocklen
        STA     type,X
        LDA     egflags,X
        STA     gflags,X
        JSR     zeroaddrs
        INX
        CPX     #len
        BNE     getexplp
        LDX     #100
        JMP     cycles
         
extraship
        LDA     #5
        STA     beepcount
        LDA     #1
        STA     beepdelay
        INC     numships
\$      JSR     pships
pships
        LDX     #4
        LDY     #1
        JSR     tabxy
        LDX     numships
        BEQ     noprint
wrshlp
        LDA     #224
        JSR     oswrch
        DEX
        BNE     wrshlp
noprint
        LDA     #ASC" "
        JMP     oswrch
         
nonewsh
        RTS
         
newsheet
        LDA     #0
        STA     beating
        LDA     sheetno
        BEQ     nowait
        LDX     #70
        STX     newshcnt
newshwlp
        JSR     cycle
        LDA     killed
        BNE     nonewsh
        DEC     newshcnt
        BNE     newshwlp
canthavespc
        JSR     cycle
        LDA     type+1
        ASL     A
        BPL     canthavespc
nowait
        SED
        CLC
        LDA     sheetno
        ADC     #1
        STA     sheetno
        CLD
        JSR     psheetno
        INC     V
        LDA     V
        CMP     #10
        BCC     okv
        LDA     #9
        STA     V
okv
        DEC     rocktime
        LDA     rocktime
        STA     beatcount
        SEC
        LDA     cycletime
        SBC     #3
        CMP     #60
        BCS     notlimit
        LDA     #60
notlimit
        STA     cycletime
        LDA     #26
        STA     beattime
        STA     beatdelay
        LDA     #9
        STA     beatsnd
        LDA     #&FF
        STA     beating
        LDA     sheetno
        LDX     #4*7
        LDY     #4
        CMP     #1
        BEQ     gotnorocks
        LDX     #5*7
        LDY     #5
        CMP     #4
        BCC     gotnorocks
        LDX     #6*7
        LDY     #6
gotnorocks
        STX     numroids
        LDX     #2
sinitlp
        TYA
        PHA
        JSR     rndvel
        STA     xvlo,X
        LDA     temp+1
        STA     xvhi,X
        JSR     rndvel
        ASL     A
        ROL     temp+1
        STA     yvlo,X
        LDA     temp+1
        STA     yvhi,X
        LDA     #0
        STA     xhi,X
        STA     yhi,X
        JSR     random
        PHP
        JSR     random
        PLP
        BPL     topbot
        STA     yhi,X
        JMP     either

topbot
        STA     xhi,X
        CMP     #160
        BCC     either
        JSR     random
        JMP     topbot

either
        JSR     zeroaddrs
        LDA     #&A0
        STA     type,X
        INX
        INX
        INX
        INX
        PLA
        TAY
        DEY
        BNE     sinitlp
        RTS
         
rndvel
        JSR     random
        AND     #63
        CMP     #15
        BCC     rndvel
\$      JSR     getrndadd
         
getrndadd
        STA     _x
        LDA     V
        STA     _y
        TYA
        PHA
        JSR     multiply
        JSR     random
        BPL     gotrndv
        SEC
        LDA     #0
        SBC     _a
        STA     _a
        LDA     #0
        SBC     _a+1
        STA     _a+1
gotrndv
        PLA
        TAY
        LDA     _a+1
        STA     temp+1
        LDA     _a
        STA     temp
        RTS
         

\\\ Srce3
         
highscores
        LDX     #0
tryhighlp
        LDA     hiscoretab,X
        CMP     score
        LDA     hiscoretab+1,X
        SBC     score+1
        BCC     gotscore
        TXA
        CLC
        ADC     #hientlen
        TAX
        CPX     #maxhient+1
        BCC     tryhighlp
        BCS     printscores
gotscore
        STX     temp
        CPX     #maxhient
        BEQ     noshuffle
        LDX     #maxhient
shufflp
        DEX
        LDA     hiscoretab,X
        STA     hiscoretab+hientlen,X
        CPX     temp
        BNE     shufflp
noshuffle
        LDA     #13
        STA     hiscoretab+2,X
        LDA     score
        STA     hiscoretab,X
        LDA     score+1
        STA     hiscoretab+1,X
        JSR     printscores
        JSR     askforname
         
printscores
        LDA     #4
        JSR     message
        LDX     #0
        STX     posnno
        LDY     #nameytab
printhilp
        TXA
        PHA
        PHA
        SED
        CLC
        LDA     posnno
        ADC     #1
        STA     posnno
        CLD
        LDX     #posnxtab
        JSR     tabxy
        LDA     posnno
        JSR     pfbyte
        LDA     #ASC"."
        JSR     oswrch
        LDX     #scorextab
        JSR     tabxy
        PLA
        TAX
        LDA     hiscoretab+1,X
        JSR     pfbyte
        LDA     hiscoretab,X
        JSR     pbyte
        CPX     temp
        BNE     notthishi
        STY     temp+1
notthishi
        LDA     #5
        JSR     message
        INX
        INX
pnamelp
        LDA     hiscoretab,X
        JSR     oswrch
        INX
        CMP     #13
        BNE     pnamelp
        INY
        INY
        PLA
        CLC
        ADC     #hientlen
        TAX
        CPX     #maxhient+hientlen
        BNE     printhilp
        RTS
         
askforname
        LDA     #4
        LDX     #0
        JSR     osbyte
        LDA     #3
        JSR     message
        LDA     #15
        LDX     #1
        JSR     osbyte
        LDX     #namextab
        LDY     temp+1
        JSR     tabxy
        LDX     #10
        LDA     #&67
        JSR     crtcreg
        CLC
        LDA     temp
        ADC     #(hiscoretab+2) AND &FF
        STA     oswordblk+0
        LDA     #0
        ADC     #(hiscoretab+2) DIV 256
        STA     oswordblk+1
        LDA     #hientlen-3
        STA     oswordblk+2
        LDA     #32
        STA     oswordblk+3
        LDA     #126
        STA     oswordblk+4
        LDX     #oswordblk AND &FF
        LDY     #oswordblk DIV 256
        LDA     #0
        JSR     osword
        BCC     noescline
        LDA     #13
        LDX     temp
        STA     hiscoretab+2,X
noescline
        LDA     #4
        LDX     #1
        JSR     osbyte
        LDX     #10
        LDA     #32
        JMP     crtcreg
         
zerovels
        LDA     #0
        STA     xvlo,X
        STA     xvhi,X
        STA     yvlo,X
        STA     yvhi,X
        RTS
initobj
        STA     type,X
\$      JSR     zeroaddrs

zeroaddrs
        LDA     #0
        STA     newhi,X
        STA     oldhi,X
        RTS
copycoords
        LDA     xlo,X
        STA     xlo,Y
        LDA     xhi,X
        STA     xhi,Y
        LDA     ylo,X
        STA     ylo,Y
        LDA     yhi,X
        STA     yhi,Y
        RTS
         
fire
        LDX     #rocklen+1
findlp
        LDA     type,X
        ASL     A
        BMI     gotfire
        INX
        CPX     #len
        BNE     findlp
        RTS
gotfire
        LDA     #0
        JSR     sound
        LDA     shipshape
        LSR     A
        TAY
        LDA     bxvlo,Y
        STA     xvlo,X
        LDA     bxvhi,Y
        STA     xvhi,X
        LDA     byvlo,Y
        STA     yvlo,X
        LDA     byvhi,Y
        STA     yvhi,X
        LDA     #&80+35
        STA     type,X
        LDA     #50
        STA     gflags,X
        TXA
        TAY
        LDX     #0
        JSR     copycoords
        TYA
        TAX
        LDY     #4
offlp
        JSR     addvels
        DEY
        BNE     offlp
        CLC
        LDA     xvlo,X
        ADC     xvlo
        STA     xvlo,X
        LDA     xvhi,X
        ADC     xvhi
        STA     xvhi,X
        CLC
        LDA     yvlo,X
        ADC     yvlo
        STA     yvlo,X
        LDA     yvhi,X
        ADC     yvhi
        STA     yvhi,X
        JMP     zeroaddrs
         
mult16
        ASL     A
        ROL     temp
mult8
        ASL     A
        ROL     temp
mult4
        ASL     A
        ROL     temp
mult2
        ASL     A
        ROL     temp
        RTS
         
mvship
        LDA     type
        ASL     A
        BMI     nomvship
        LDA     dead
        BEQ     shipok
        LDA     killed
        BNE     P%+5
        JSR     killship
nomvship
        RTS

shipok
        LDA     hyperspace
        BEQ     mvsh1a
        DEC     hyperspace
        BEQ     P%+3
        RTS
        JSR     random
        STA     xhi
        JSR     random
        STA     yhi
        LDX     #0
        JSR     zerovels
        JSR     random
        CMP     #`dieprob
        BCS     mvsh1b
        LDA     #1
        STA     dead
        RTS

mvsh1a
        LDX     #`space
        JSR     inkey
        BEQ     mvsh1b
        LDA     letgoyet
        BNE     mvsh1b
        LDX     #0
        JSR     update
        LDA     #0
        STA     newhi
        LDA     #30
        STA     hyperspace
        RTS

mvsh1b
        LDX     #`return
        JSR     inkey
        BEQ     L3
        EOR     retflag
L3
        STX     retflag
        BEQ     P%+5
        JSR     fire
        LDX     #`capslock
        JSR     inkey
        BEQ     mvsh1
        INC     shipshape
mvsh1
        LDX     #`ctrl
        JSR     inkey
        BEQ     mvsh2
        DEC     shipshape
mvsh2
        LDA     shipshape
        AND     #63
        STA     shipshape
        LSR     A
        ORA     #&80
        STA     type
        LDX     #`shift
        JSR     inkey
        BEQ     mvsh3
        LDA     shipshape
        LSR     A
        TAX
        CLC
        LDA     xvlo
        ADC     xacclo,X
        STA     xvlo
        LDA     xvhi
        ADC     xacchi,X
        STA     xvhi
        CLC
        LDA     yvlo
        ADC     yacclo,X
        STA     yvlo
        LDA     yvhi
        ADC     yacchi,X
        STA     yvhi
mvsh3
        DEC     acccntr
        BNE     mvsh4
        LDA     #accfreq
        STA     acccntr
        LDA     xvlo
        STA     temp
        LDA     xvhi
        JSR     scale
        SEC
        LDA     xvlo
        SBC     temp
        STA     xvlo
        LDA     xvhi
        SBC     addr
        STA     xvhi
        LDA     yvlo
        STA     temp
        LDA     yvhi
        JSR     scale
        SEC
        LDA     yvlo
        SBC     temp
        STA     yvlo
        LDA     yvhi
        SBC     addr
        STA     yvhi
mvsh4
        LDX     #0
        JMP     update
         
scale
        PHP
        LSR     A
        ROR     temp
        LSR     A
        ROR     temp
        LSR     A
        ROR     temp
        LSR     A
        ROR     temp
        PLP
        BPL     P%+4
        ORA     #&F0
        STA     addr
        RTS
         
firevels
        LDA     sptype
        BNE     itsalittlun
sillyco
        JSR     random
        AND     #&7F
        STA     dx
        JSR     random
        AND     #&7F
        STA     dy
        CLC
        ADC     dx
        CMP     #20
        BCC     sillyco
        JSR     random
        ASL     A
        PHP
        ASL     A
        PHP
        JMP     nohalve
itsalittlun
        SEC
        LDA     xhi
        SBC     xhi+1
        PHP
        JSR     abs
        STA     dx
        SEC
        LDA     yhi
        SBC     yhi+1
        PHP
        JSR     abs
        STA     dy
        LDA     dx
        BMI     halveit
        LDA     dy
        BPL     nohalve
halveit
        LSR     dx
        LSR     dy
nohalve
        LSR     dy
        LDA     #55
        STA     K
        JSR     calcvels
        LDA     #0
        STA     temp
        LDA     vy
        JSR     mult16
        PLP
        JSR     plusminus
        STA     yvlo,X
        LDA     temp
        STA     yvhi,X
        LDA     #0
        STA     temp
        LDA     vx
        JSR     mult8
        PLP
        JSR     plusminus
        STA     xvlo,X
        LDA     temp
        STA     xvhi,X
        LDA     sptype
        BEQ     itsabigun
        CLC
        LDA     xvlo,X
        ADC     xvlo
        STA     xvlo,X
        LDA     xvhi,X
        ADC     xvhi
        STA     xvhi,X
        CLC
        LDA     yvlo,X
        ADC     yvlo
        STA     yvlo,X
        LDA     yvhi,X
        ADC     yvhi
        STA     yvhi,X
        JSR     rndadd
        CLC
        ADC     xvlo,X
        STA     xvlo,X
        LDA     temp
        ADC     xvhi,X
        STA     xvhi,X
        JSR     rndadd
        ASL     A
        ROL     temp
        CLC
        ADC     yvlo,X
        STA     yvlo,X
        LDA     temp
        ADC     yvhi,X
        STA     yvhi,X
itsabigun
        RTS
         
rndadd
        LDA     #0
        STA     temp
        JSR     random
        SEC
        SBC     #128
        BCS     rndadone
        DEC     temp
rndadone
        RTS
         
abs
        BCS     okabs
        EOR     #&FF
        CLC
        ADC     #1
okabs
        RTS
         
plusminus
        BCS     okpm
        STA     temp+1
        SEC
        LDA     #0
        SBC     temp+1
        PHA
        LDA     #0
        SBC     temp
        STA     temp
        PLA
okpm
        RTS
         
calcvels
        TXA
        PHA
        TYA
        PHA
        LDA     dx
        STA     _x
        STA     _y
        JSR     multiply
        LDA     _a
        STA     _b
        LDA     _a+1
        STA     _b+1
        LDA     dy
        STA     _x
        STA     _y
        JSR     multiply
        CLC
        LDA     _a
        ADC     _b
        STA     _a
        LDA     _a+1
        ADC     _b+1
        STA     _a+1
        JSR     squareroot
        LDA     dx
        STA     _x
        LDA     K
        STA     _y
        JSR     multiply
        JSR     divide
        LDA     _x
        STA     vx
        LDA     dy
        STA     _x
        LDA     K
        STA     _y
        JSR     multiply
        JSR     divide
        LDA     _x
        STA     vy
        PLA
        TAY
        PLA
        TAX
        RTS
         
multiply
        TYA
        PHA
        LDA     #0
        STA     _y+1
        STA     _a
        STA     _a+1
        LDY     #8
mloop
        LSR     _x
        BCC     noadd
        CLC
        LDA     _a
        ADC     _y
        STA     _a
        LDA     _a+1
        ADC     _y+1
        STA     _a+1
noadd
        ASL     _y
        ROL     _y+1
        DEY
        BNE     mloop
        PLA
        TAY
        RTS
         
divide
        LDA     #0
        STA     _y
        LDY     #16
dloop
        ASL     _a
        ROL     _a+1
        ROL     _y
        SEC
        LDA     _y
        SBC     _z
        STA     temp
        BCC     nosub
        STA     _y
nosub
        ROL     _x
        DEY
        BNE     dloop
        RTS
         
squareroot
        LDA     #0
        STA     _z
        STA     _z+1
        STA     temp
        STA     _a+2
        STA     _a+3
        LDA     #&80
        STA     temp+1
sqloop
        ASL     _a
        ROL     _a+1
        ROL     _a+2
        ROL     _a+3
        SEC
        LDA     _a+2
        SBC     temp
        TAY
        LDA     _a+3
        SBC     temp+1
        TAX
        BCC     nosq
        TYA
        SBC     _z
        TAY
        TXA
        SBC     _z+1
        TAX
        BCC     nosq
        TYA
        SBC     _z
        TAY
        TXA
        SBC     _z+1
        BCC     nosq
        STA     _a+3
        STY     _a+2
        LDA     _z
        ORA     temp
        STA     _z
        LDA     _z+1
        ORA     temp+1
        STA     _z+1
nosq
        LSR     temp+1
        ROR     temp
        BCC     sqloop
        RTS

