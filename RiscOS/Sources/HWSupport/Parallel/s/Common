; > Common

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; call: common_open
;
; in:   r0  = device interrupt number to claim
;       r1  = address of device interrupt handler
;       r2  = external handle (used for calling DeviceFS)
;       r3  = flags for opening the file
;       r6 -> special field that has been decoded
;
; out:  r1  = internal handle (used in communication from DeviceFS)
;
; This call is made to allow the parallel device driver to initialise itself, ie.
; claiming any vectors it may need.
;

common_open
                ENTRY   "r2-r4"

                Debug   open,"common_open",r2

                LDRB    r4, Flags
                TST     r4, #f_OpenForRead :OR: f_OpenForWrite
                ADRNEL   r0, ErrorBlock_Parallel_InUse
                PullEnv NE

              [ international
                BNE     MakeErrorWithDeviceName
              |
                ORRNES  pc, lr, #V_bit
              ]

                STR     r2, StreamHandle                        ; stash the stream handle

                TST     r4, #f_VectorOwned                      ; do I own the vectors?
                BNE     %FT00

                MOV     r2, wp                                  ; -> code and workspace
                SWI     XOS_ClaimDeviceVector
                EXIT    VS                                      ; return any error from ClaimDeviceVector

                ORR     r4, r4, #f_VectorOwned
00
                TST     r3, #&01
                ORREQ   r4, r4, #f_OpenForRead
                ORRNE   r4, r4, #f_OpenForWrite
                STRB    r4, Flags                               ; update to indicate that the vectors are owned

                EXITS



; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; call: common_close
;
; in:   r0  = device interrupt number to release
;       r1  = address of device interrupt handler
;       r2  = address of busy check callback handler (or 0)
;
; out:  -
;
; This call is made to allow the device to close down the routines associated
; with printing characters.
;

common_close
                ENTRY   "r2,r3"

                Debug   close,"common_close"

                ORR     lr, lr, #I_bit
                TEQP    lr, #0                                  ; disable IRQs

                TEQ     r2, #0
                LDRNEB  r3, IRQFlags
                TSTNE   r3, #if_PendingCallAfter
                BEQ     %05

                Push    "r0, r1"
                MOV     r0, r2
                MOV     r1, wp
                SWI     XOS_RemoveTickerEvent
                Pull    "r0, r1"

                BIC     r3, r3, #if_PendingCallAfter
                STRB    r3, IRQFlags

05
                LDRB    r3, Flags
                TST     r3, #f_VectorOwned                      ; is the vector owned at the moment
                BEQ     %10

                Debug   close," removing IRQ routine..."

                MOV     r2, wp                                  ; -> routines
                SWI     XOS_ReleaseDeviceVector
                PullEnv VS                                      ; return any error from ReleaseDeviceVector
                ORRVSS  pc, lr, #V_bit                          ; but ensure we set the I bit back

                BIC     r3, r3, #f_VectorOwned                  ; mark as vector no longer owned

                Debug   close," removed"
10
                BIC     r3, r3, #f_OpenForRead :OR: f_OpenForWrite
                STRB    r3, Flags

                MOV     r3, #0
                STR     r3, StreamHandle                        ; clear the output handle
                STR     r3, BufferHandle                        ; and buffer handle

                Debug   close,"finished closing stream"

                EXITS                                           ; exit restoring all flags (including I)



; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; call: common_createTX
;
; in:   r0 = reason code (6)
;       r2 = internal stream handle
;       r3 = flags for buffer
;       r4 = size for buffer
;       r5 = buffer handle (usually -1)
;       r6 = threshold for buffer
;
; out:  above registers modified
;
; This routine is called just before the output stream is created.
;

common_createTX
                Debug   open,"common_createTX"

                CMP     r5, #-1
                MOVEQ   r5, #3          ; set the buffer handle
                MOV     pc, lr



; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; call: common_createRX
;
; in:   r0 = reason code (7)
;       r2 = internal stream handle
;       r3 = flags for buffer
;       r4 = size for buffer
;       r5 = buffer handle (usually -1)
;       r6 = threshold for buffer
;
; out:  above registers modified
;
; This entry point is called just before the input stream buffer is going to
; be created.  The flags are modified so that the Halt and Resume entry points
; are called and the threshold is set.
;

common_createRX ENTRY   "r0-r2"

                Debug   open,"common_createRX"

                MOV     r0, #3                  ; change flags for parallel buffer (DeviceFS won't do this)
                ORR     r1, r3, #BufferFlags_SendThresholdUpCalls
                MOV     r2, #0
                SWI     XBuffer_ModifyFlags

                CMP     r5, #-1
                MOVEQ   r5, #3                  ; use parallel buffer
                MOV     r6, #Parallel_Threshold

                EXITS


                END
