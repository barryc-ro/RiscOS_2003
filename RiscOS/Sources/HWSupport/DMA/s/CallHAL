; CallHAL
;
; Attempt to insulate HAL caller from the differences between the HAL's
; device-based and non-device-based calling standards
;
; On entry:
;   r0-r3 hold first 4 arguments
;   up to 4 more arguments are on the stack
;
; On exit:
;   r4-r13 are always preserved
;   optionally r0-r3 and r14 may be preserved
;
; Macro parameters:
;   $routine = name of routine to call
;   $savelist = *comma-separated* list of additional registers to save
;               across the call (from the set r0,r1,r2,r3,r14);
;               note that if there is more than one register in the list,
;               this means that the list must be enclosed by quotes
;   $stackedargs = number of arguments that are on the stack (0-4)

 [ {TRUE}

        MACRO
$label  CallHAL $routine, $savelist, $stackedargs
        LCLS    arglist
arglist SETS    ""
      [ "$stackedargs"<>""
        LCLA    tempa
tempa   SETA    3+$stackedargs
        WHILE   tempa>3
      [ :LEN:arglist=0
arglist SETS    "r"
      |
arglist SETS    "$arglist,r"
      ]
arglist SETS    "$arglist":CC::CHR:(48+tempa)
tempa   SETA    tempa-1
        WEND
      ]
        LCLS    reglist
reglist SETS    "r8,r9"
      [ "$savelist"<>""
reglist SETS    "$reglist,$savelist"
      ]
      [ :LEN:arglist>0
reglist SETS    "$reglist,$arglist"
      ]
        Push    "$reglist"
      [ "$stackedargs"<>""
        LCLA    listlen
listlen SETA    1
        LCLS    temps
temps   SETS    reglist
        WHILE   :LEN:temps>0
      [ temps:LEFT:1 = ","
listlen SETA    listlen+1
      ]
temps   SETS    temps:RIGHT:(:LEN:temps-1)
        WEND
      [ $stackedargs=1
        LDR     r4,[sp,#4*$listlen]
      |
        ADD     r8,sp,#4*$listlen
        LDMIA   r8,{$arglist}
      ]
      ]
        MOV     r8,#0   ; OS_Hardware 0 calls routine immediately
        MOV     r9,#EntryNo_HAL_$routine
        SWI     XOS_Hardware
        Pull    "$reglist"
        MEND

 |

        MACRO
$label  CallHAL $routine, $savelist, $stackedargs
        LCLS    reglist
reglist SETS    "r12"
      [ "$savelist"<>""
reglist SETS    "$reglist,$savelist"
      ]
        Push    "$reglist"
        MOV     r14,pc
        LDR     pc,[r0,#DMADev_$routine]
        Pull    "$reglist"
        MEND

 ]

        END
