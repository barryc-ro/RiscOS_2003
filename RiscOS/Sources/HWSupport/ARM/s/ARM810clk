
;ARM810 stuff

  [ ARM810support

;entry: r5 bits 31:30 set clock state
;         00 = do nothing
;         01 = clock is bus clock (no dynamic switching)
;         10 = clock is PLL
;         11 = clock is Refclk pin
;
ARM810clockcontrol ROUT
        Push    "lr"
        TST     r5,#&c0000000
        BEQ     %FT09
;first, turn off dynamic switching, before any adjustments
        MRC     CP15,0,lr,C15,C0,0
        BIC     lr,lr,#&1                  ;turn off dynamic bus switching (bit0)
        MCR     CP15,0,lr,C15,C0,0
        TST     r5,#&80000000
        BEQ     %FT09
;turn on fast clock of required type
        BIC     lr,lr,#&2                  ;select asynchronous mode (default) (bit1)
        TST     r5,#&40000000
        BIC     lr,lr,#&C                  ;zap current clock select bits
        ORRNE   lr,lr,#&4                  ;select REFCLK as the FCLK source (bits3:2)
        ORREQ   lr,lr,#&C                  ;select PLLClkOut as the FCLK source (bits3:2)
        BIC     lr,lr,#&10                 ;ensure L=0 when writing (PLL locked) (bit4)
        MCR     CP15,0,lr,C15,C0,0
        NOP
        NOP
        NOP
        NOP
        ORR lr,lr,#&1                      ;select dynamic clock switching (bit0)
        MCR CP15,0,lr,C15,C0,0
09
        Pull    "pc"
  ]

        END
