; > adfs::TimSource.!IIC.IICMod

; *********************************
; ***    C h a n g e   L i s t  ***
; *********************************

; Date          Description
; ----          -----------
; 11-Mar-88     Started
; 15-Mar-88     Disable IRQs around SetC1C0 (uses IOCControlSoftCopy)
; 15-Mar-88     Added code to check for clock syncronisation
; 23-Nov-89     Added 'GET <Hdr>.File' so it assembles again
;               - no change to object
; 08-Jul-91     Look up messages in messages file
; 23-Apr-92 0.11 LRust,SWI errors no longer corrupt R1
;                 * Fixes to IIC transations for BMU
; 28-Apr-92 0.12 LRust, RxByte disables interupts for BMU
; 18-May-92 0.13 TMD - put bodges in for BMU, so interrupt latency isn't awful

        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:ModHand
        GET     Hdr:Services
        GET     Hdr:Proc
        GET     Hdr:FSNumbers
        GET     Hdr:NewErrors
        GET     Hdr:MsgTrans
        GET     Hdr:IIC
	GET	VersionASM

                GBLL    ReentrancyCheck
ReentrancyCheck SETL    {TRUE}                          ; Harmless if you don't need it

                GBLL    KernelDoesIIC
KernelDoesIIC   SETL    {TRUE} :LAND: ReentrancyCheck   ; Until the kernel's IIC support is farmed out

 ^ ErrorNumber_IIC_NoAcknowledge      ; The nearest thing we have to a defined error base
 # 1  ; AddError2 IIC_NoAcknowledge,    "No acknowledge from IIC device"
        AddError2 IIC_InUse,            "IIC operation in progress"

        LEADR   Module_LoadAddr

TAB     *       9
LF      *       10
FF      *       12
CR      *       13

IOCControlSoftCopy * &106       ; Address of soft copy of IOC control register
 [ KernelDoesIIC
IRQsema                         * &108  ; Address of IRQ semaphore
KernelVersion_STB22             * &45200
KernelVersion_STB3              * &47100
KernelVersion_STB400            * &53000
KernelVersion_32bitIRQStack     * &48100 ; trunk versions: beware desktop OS builders!
 ]

; Module workspace allocation

        ^ 0, R12

MessageFile_Block # 16
MessageFile_Open  # 4
 [ ReentrancyCheck
IICSWIThreaded  # 4
   [ KernelDoesIIC
KernelIICStart  # 4
KernelIICEnd    # 4
KernelIRQStackFlag # 4          ; negative if ARM2/3 style IRQ stack
   ]
 ]

IIC_WorkspaceSize * :INDEX: @

; **************** Module code starts here **********************

Module_BaseAddr

        DCD     0
        DCD     IIC_Init    -Module_BaseAddr
        DCD     IIC_Die     -Module_BaseAddr
        DCD     IIC_Service -Module_BaseAddr
        DCD     IIC_Title   -Module_BaseAddr
        DCD     IIC_HelpStr -Module_BaseAddr
        DCD     IIC_HC_Table-Module_BaseAddr
        DCD     IICSWI * Module_SWIChunkSize
        DCD     IIC_SWIHandler-Module_BaseAddr
        DCD     IIC_SWINameTable-Module_BaseAddr
        DCD     0 ; Code to manually decode swi name (not needed)
        DCD     0 ; No messages file
        DCD     IIC_ModFlags-Module_BaseAddr

IIC_SWINameTable
IIC_Title
        =       "IIC", 0
        =       "Control", 0
        =       0

IIC_HelpStr =       "IIC",TAB, TAB,"$Module_MajorVersion ($Module_Date)", 0
        ALIGN

IIC_ModFlags
      [ No26bitCode
        &       ModuleFlag_32bit
      |
        &       0
      ]

; *****************************************************************************

IIC_HC_Table * Module_BaseAddr

IIC_Service * Module_BaseAddr

IIC_Init
        STMDB   sp!, {lr}

        LDR     r2, [r12]               ; Hard or soft init ?
        TEQ     r2, #0
        BNE     %FT00

; Hard init

        LDR     r3, =IIC_WorkspaceSize
        MOV     r0, #ModHandReason_Claim
        SWI     XOS_Module
        LDMVSIA sp!, {pc}

        STR     r2, [r12]

00      MOV     r12, r2

        MOV     r14, #0
        STR     r14, MessageFile_Open

    [ ReentrancyCheck
        STR     r14, IICSWIThreaded
      [ KernelDoesIIC
        MOV     r0, #ModHandReason_LookupName
        ADR     r1, UtilityModuleString
        SWI     XOS_Module
        LDR     r0, [r3, #Module_HelpStr]
        ADD     r0, r0, r3
        BL      GetVerNoFromHelpString
        MOV     r2, #0          ; emergency defaults
        MOV     r3, #0          ; (lr=0 shouldn't appear on the IRQ stack)
        LDR     r0, =KernelVersion_STB22
        TEQ     r1, r0
        ADREQ   lr, KernelAddresses_STB22
        LDMEQIA lr, {r2, r3}
        LDR     r0, =KernelVersion_STB3
        TEQ     r1, r0
        ADREQ   lr, KernelAddresses_STB3
        LDMEQIA lr, {r2, r3}
        LDR     r0, =KernelVersion_STB400
        TEQ     r1, r0
        ADREQ   lr, KernelAddresses_STB400
        LDMEQIA lr, {r2, r3}
        ADR     lr, KernelIICStart
        ASSERT  :INDEX: KernelIICEnd = :INDEX: KernelIICStart + 4
        STMIA   lr, {r2, r3}
        LDR     r0, =KernelVersion_32bitIRQStack
        SUB     r0, r1, r0
        STR     r0, KernelIRQStackFlag
        CLRV
      ]
    ]
        LDMIA   sp!, {pc}

IIC_Die STMDB   sp!, {lr}
        LDR     r12, [r12]
        LDR     r0, MessageFile_Open
        CMP     r0, #0
        ADRNE   r0, MessageFile_Block
        SWINE   XMessageTrans_CloseFile
        CLRV
        LDMIA   sp!, {pc}

 [ KernelDoesIIC

UtilityModuleString
        =       "UtilityModule", 0
        ALIGN

KernelAddresses_STB22
        &       &03828A4C, &0382900C ; first (incl) and last (excl) addresses to trap
KernelAddresses_STB3
        &       &0382A808, &0382B334 ; first (incl) and last (excl) addresses to trap
KernelAddresses_STB400
        &       &0382C2FC, &0382CE68 ; first (incl) and last (excl) addresses to trap

        LTORG

; *************************************************************************
;
;       RMEGetVerNo - Read version number from a string
;
; in:   R0 -> string
;
; out:  R0, R4, R5, R12 corrupted
;       R1 = version number in BCD with the decimal point between bits 15 and 16
;            eg "2.34" => &00023400, "5.6789" => &00056789, "17" => &00170000
;            only the last 4 digits of the integer part, and the first 4 decimal places are stored
;

RMEGetVerNo Entry
        MOV     r1, #0
10
        LDRB    r12, [r0], #1
        CMP     r12, #" "
        BEQ     %BT10
11
        SUB     r12, r12, #"0"
        CMP     r12, #9
        ORRLS   r1, r12, r1, LSL #4             ; just keep nibbles - we only need the
        LDRLSB  r12, [r0], #1                   ; result to be ordered, not continous
        BLS     %BT11
        MOV     r5, #0
        CMP     r12, #"."-"0"
        BNE     %FT13
        MOV     r4, #16
12
        SUBS    r4, r4, #4
        BMI     %FT13
        LDRB    r12, [r0], #1
        SUB     r12, r12, #"0"
        CMP     r12, #9
        ORRLS   r5, r5, r12, LSL r4
        BLS     %BT12
13
        ORR     r1, r5, r1, LSL #16
        EXIT

; *************************************************************************
;
;       GetVerNoFromHelpString - Read version number from a module help string
;
; in:   R0 -> module help string
;
; out:  R1 = version number in BCD with the decimal point between bits 15 and 16
;            eg "2.34" => &00023400, "5.6789" => &00056789, "17" => &00170000
;            only the last 4 digits of the integer part, and the first 4 decimal places are stored
;       All other registers preserved
;

GetVerNoFromHelpString Entry "r0, r4, r5, r12"
        MOV     r5, #0                          ; char count
10
        LDRB    r1, [r0], #1
        CMP     r1, #0                          ; check character
        EXIT    EQ                              ; if end of string then no version number so return zero
        ADD     r5, r5, #1
        CMP     r1, #TAB
        ADDEQ   r5, r5, #7
        BICEQ   r5, r5, #7
        CMP     r5, #16                         ; hit verno col yet?
        BLT     %BT10
20
        LDRB    r1, [r0], #1
        CMP     r1, #TAB
        CMPNE   r1, #31                         ; if a control character (except TAB)
        MOVLT   r1, #0                          ; then no version number so return zero
        EXIT    LT
        SUB     r1, r1, #"0"
        CMP     r1, #9                          ; if not a digit
        BHI     %BT20                           ; then try next character
        SUB     r0, r0, #1                      ; was a digit so go back to it
        BL      RMEGetVerNo                     ; read version number from here
        EXIT

 ]

; *****************************************************************************
;
;       IIC_SWIHandler - Handler for my SWIs
;
; in:   R11 = SWINumber MOD 64 (only 0 is recognised)
;       R0 = device address (bit 0 set => read, clear => write)
;       R1 -> data block
;       R2 = length of data block
;
; out:  R10-R12 may be corrupted

IIC_SWIHandler ROUT
        LDR     r12, [r12]
        MOV     R10, LR
        TEQ     R11, #0                 ; we only have one SWI
        BNE     ReturnNoSuchSWIError

        Push    "R0-R1"
        Push    "R0-R2"                 ; parameters for OS_IICControl
        MOV     R0, R13
        MOV     R1, #1
        SWI     XOS_IICOp
        ADD     R13, R13, #12           ; balance stack
        BVS     %FT10
        Pull    "R0-R1"
        TEQ     PC, PC
        MOVNE   PC, R10                 ; 32-bit exit: NZC corrupted
        MOVEQS  PC, R10                 ; 26-bit exit: NZC preserved
10
        LDR     R14, [R0]
        SUB     R14, R14, #ErrorNumber_NoSuchSWI :AND: &FF00
        TEQ     R14, #ErrorNumber_NoSuchSWI :AND: &FF
        Pull    "R0-R1",EQ
        BEQ     %FT20

        ADD     R13, R13, #4
        Pull    "R1"
        TEQ     PC, PC
        MOVNE   PC, R10                 ; 32-bit exit: NZC corrupted
        MOVEQS  PC, R10                 ; 26-bit exit: NZC preserved

20
    [ ReentrancyCheck
      [ KernelDoesIIC
        Push    "R0-R2,R14"
        ADR     R14, KernelIICStart
        ASSERT  :INDEX: KernelIICEnd = :INDEX: KernelIICStart + 4
        LDMIA   R14, {R0, R1}
        SCPSR   I_bit, 0, R14,, R2      ; disable IRQs while we navigate the IRQ stack
        MOV     R11, #IRQsema
inspect_IRQ_stack
        LDR     R11, [R11]
        CMP     R11, #0
        BEQ     iis_end
        LDR     R14, KernelIRQStackFlag
        TEQ     R14, #0
        LDRMI   R14, [R11, #4*7]        ; get return lr from stack (pushed on ARM2/3, else 26-bit PSR
        BICMI   R14, R14, #ARM_CC_Mask  ; is mangled in by the kernel, and which we have to strip away)
        LDRPL   R14, [R11, #4*8]        ; get pushed lr from stack (no flag bits to worry about)
        CMP     R14, R0
        CMPGE   R1, R14
        BLT     inspect_IRQ_stack       ; try the next IRQ out, in case of IRQ re-entrancy

        ; Kernel IIC code is threaded!
        ; Can't easily do a OS_Heap type trick, so just report an error instead
        RestPSR R2,, c
        Pull    "R0-R2,R14"
        B       ReturnIICInUseError

iis_end
        RestPSR R2,, c
        Pull    "R0-R2,R14"
      ]
        LDR     R11, IICSWIThreaded
        TEQ     R11, #0
        BNE     ReturnIICInUseError
        MOV     R11, #1
        STR     R11, IICSWIThreaded
    ]

        Push    R14

        BL      Start
        BL      TXCheckAck              ; transmit device address

        MOV     R11, R1
        MOVS    R10, R2
        BEQ     %FT30

        TST     R0, #1                  ; Z => write, NZ => read
        BNE     %FT20
10
        LDRB    R0, [R11], #1           ; read byte from data block
        BL      TXCheckAck              ; transmit, checking for ack
        SUBS    R10, R10, #1            ; decrement byte count
        BNE     %BT10                   ; loop until finished
        BEQ     %FT30                   ; then stop

20
        BL      RXByte                  ; read byte from bus
        STRB    R0, [R11], #1           ; store in data block
        SUBS    R10, R10, #1            ; is it last byte ?
        MOVNE   R0, #0                  ; no, then acknowledge with 0 bit
        MOVEQ   R0, #1                  ; yes, then don't acknowledge
        BL      ClockData               ; but always send ack clock pulse
        BNE     %BT20
30
        BL      Stop
      [ ReentrancyCheck
        MOV     R11, #0
        STR     R11, IICSWIThreaded
      ]
        Pull    R14
        CMP     PC, PC                  ; also clears V in 32-bit modes
        MOVEQ   PC, R14                 ; 32-bit exit: NZC corrupted
        BICS    PC, R14, #V_bit         ; 26-bit exit: NZC preserved

TXCheckAck ROUT
        Push    R14
        BL      TXByte
        BL      Acknowledge
        Pull    R14
        MOVVC   PC, R14                 ; acknowledged ok, so return

        BL      Stop                    ; error, so tidy up
        ADR     R0, ErrorBlock_IIC_NoAcknowledge
        MOV     R11, #0
        BL      CopyError               ; sets V
      [ ReentrancyCheck
;       MOV     R11, #0
        STR     R11, IICSWIThreaded
      ]
        Pull    R14                     ; pull real return address
        TEQ     PC, PC
        MOVEQ   PC, R14                 ; 32-bit exit: NZC corrupted
        ORRS    PC, R14, #V_bit         ; 26-bit exit: NZC preserved

ReturnNoSuchSWIError
        ADR     R0, ErrorBlock_NoSuchSWI
        ADR     R11, IIC_Title
        BL      CopyError               ; sets V
        TEQ     PC, PC
        MOVEQ   PC, R10                 ; 32-bit exit: NZC corrupted
        ORRS    PC, R10, #V_bit         ; 26-bit exit: NZC preserved

      [ ReentrancyCheck
ReturnIICInUseError
        ADR     R0, ErrorBlock_IIC_InUse
        MOV     R11, #0
        BL      CopyError               ; sets V
        TEQ     PC, PC
        MOVEQ   PC, R10                 ; 32-bit exit: NZC corrupted
        ORRS    PC, R10, #V_bit         ; 26-bit exit: NZC preserved
      ]

        MakeInternatErrorBlock NoSuchSWI,,BadSWI
        MakeInternatErrorBlock IIC_NoAcknowledge,,NoAck
      [ ReentrancyCheck
        MakeInternatErrorBlock IIC_InUse,,InUse
      ]

; *****************************************************************************
;
;       SetC1C0 - Set clock and data lines to values in R1 and R0 respectively
;
; out:  All registers preserved, including PSR
;

SetC1C0 ROUT
 [ No26bitCode
        Push    "R0-R3,R14"
        mrs    ,R3, CPSR
        TEQ     PC, #0                          ; set NE
 |
        Push    "R0-R2,R14"
        BIC     R14, R14, #Z_bit                ; indicate not checking clock
 ]
SetOrCheck
 [ No26bitCode
        ORR     R14, R3, #I32_bit               ; disable interrupts round access to IOCControlSoftCopy
        msr    ,CPSR_c, R14                     ; preserves Z flag
 |
        ORR     R14, R14, #I_bit                ; disable interrupts round access to IOCControlSoftCopy
        TEQP    R14, #0
 ]

        ADD     R0, R0, R1, LSL #1              ; R0 := C0 + C1*2

        MOV     R2, #0                          ; prepare to index soft copy
        LDRB    R1, [R2, #IOCControlSoftCopy]   ; read soft copy
        BIC     R1, R1, #&03                    ; clear clock and data
        ORR     R1, R1, R0                      ; put in new clock and data
        ORR     R1, R1, #&C0                    ; make sure two test bits are
                                                ; always set to 1 !
        STRB    R1, [R2, #IOCControlSoftCopy]   ; store back to soft copy

        MOV     R2, #IOC
        STRB    R1, [R2, #IOCControl]
10
        LDREQB  R1, [R2, #IOCControl]           ; if EQ, then wait for clock
        TSTEQ   R1, #i2c_clock_bit              ; to read high
        BNE     %FT30                           ; if not checking, or it went high straight away, skip

20
        LDRB    R1, [R2, #IOCControl]           ; now wait for clock to go high
        TST     R1, #i2c_clock_bit
        BEQ     %BT20

30
        MOV     R0, #10                         ; delay for >= 10/2 microsecs
        BL      DoMicroDelay

 [ No26bitCode
        msr    ,CPSR_cf, R3
        Pull    "R0-R3,PC"
 |
        Pull    "R0-R2,PC",,^
 ]

; Set clock and data lines to R1 and R0 and then wait for clock to be high

SetC1C0CheckClock ROUT
 [ No26bitCode
        Push    "R0-R3,R14"
        mrs    ,R3, CPSR
        TEQ     R0, R0                          ; set EQ
 |
        Push    "R0-R2,R14"
        ORR     R14, R14, #Z_bit                ; indicate checking clock
 ]
        B       SetOrCheck


; *****************************************************************************
;
;       DoMicroDelay - Delay for >= R0/2 microseconds
;
; in:   R0 = time delay in 1/2 microsecond units
;       R2 -> IOC
;
; out:  R0,R1 corrupted
;

DoMicroDelay ROUT
        Push    R14
        STRB    R0, [R2, #Timer0LR]     ; copy counter into output latch
        LDRB    R1, [R2, #Timer0CL]     ; R1 := low output latch
10
        STRB    R0, [R2, #Timer0LR]     ; copy counter into output latch
        LDRB    R14, [R2, #Timer0CL]    ; R14 := low output latch
        TEQ     R14, R1                 ; unchanged ?
        MOVNE   R1, R14                 ; copy anyway
        BEQ     %BT10                   ; then loop
        SUBS    R0, R0, #1              ; decrement count
        BNE     %BT10                   ; loop if not finished
        Pull    PC

; *****************************************************************************
;
;       ClockData - Clock a bit of data down the IIC bus
;
; in:   R0 = data bit
;
; out:  All registers preserved, including PSR
;

ClockData ROUT
 [ No26bitCode
        Push    "R1,R2,R14"
        mrs    ,R2,CPSR
 |
        Push    "R1, R14"
 ]

        MOV     R1, #0                  ; clock LO
        BL      SetC1C0

; Disable interrupts if not talking to BMU to ensure clock hi with data hi is only transient
; This allows BMU to detect idle condition by polling

 [ No26bitCode
        ORR     r1, r2, #I32_bit
        msr    ,CPSR_c,r1
 |
        ORR     r1, pc, #I_bit
        TEQP    r1, #0
 ]

        MOV     R1, #1                  ; clock HI
        BL      SetC1C0CheckClock

; Delay here must be >= 4.0 microsecs

        MOV     R1, #0                  ; clock LO
        BL      SetC1C0

 [ No26bitCode
        msr    ,CPSR_cf,R2
        Pull    "R1,R2,PC"
 |
        Pull    "R1, PC",,^
 ]

; *****************************************************************************
;
;       Start - Send the Start signal
;
; out:  All registers preserved, including PSR
;

Start   ROUT
 [ No26bitCode
        Push    "R0-R3,R14"
        mrs    ,R3, CPSR
 |
        Push    "R0-R2,R14"
 ]

        MOV     R0, #1                  ; clock HI, data HI
        MOV     R1, #1
        BL      SetC1C0

; Delay here must be >= 4.0 microsecs

        MOV     R0, #0                  ; clock HI, data LO
        MOV     R1, #1
        BL      SetC1C0

; Hold start condition for BMU

        MOV     R2, #IOC
        MOV     R0,#10
        BL      DoMicroDelay

; Delay here must be >= 4.7 microsecs

        MOV     R0, #0                  ; clock LO, data LO
        MOV     R1, #0
        BL      SetC1C0

 [ No26bitCode
        msr    ,CPSR_f, R3
        Pull    "R0-R3,PC"
 |
        Pull    "R0-R2,PC",,^
 ]

; *****************************************************************************
;
;       Acknowledge - Check acknowledge after transmitting a byte
;
; out:  All registers preserved
;       V=0 => acknowledge received
;       V=1 => no acknowledge received
;

Acknowledge ROUT
        Push    "R0-R2,R14"

        MOV     R0, #1                  ; clock LO, data HI
        MOV     R1, #0
        BL      SetC1C0

; Disable interrupts if not talking to BMU to ensure clock hi with data hi is only transient
; This allows BMU to detect idle condition by polling

 [ No26bitCode
        mrs    ,R1,CPSR
        Push    "R1"
        ORR     r1, r1, #I32_bit
        msr    ,CPSR_c,r1
 |
        MOV     R1,PC
        Push    "R1"
        ORR     r1, r1, #I_bit
        TEQP    r1, #0
 ]

        MOV     R0, #1                  ; clock HI, data HI
        MOV     R1, #1
        BL      SetC1C0CheckClock

; Delay here must be >= 4.0 microsecs

        MOV     R2, #IOC
        LDRB    R2, [R2, #IOCControl]   ; get the data from IOC

        MOV     R0, #0                  ; clock LO, data LO
        MOV     R1, #0
        BL      SetC1C0

        Pull    "R1"
 [ No26bitCode
        msr    ,CPSR_c,R1
 |
        TEQP    PC,R1
 ]

        TST     R2, #1                  ; should be LO for correct acknowledge
        MOV     R2, PC
        BICEQ   R2, R2, #V_bit          ; clear V if correct acknowledge
        ORRNE   R2, R2, #V_bit          ; set V if no acknowledge
        TEQP    R2, #0

        Pull    "R0-R2,PC"

; *****************************************************************************
;
;       Stop - Send the Stop signal
;
; out:  All registers preserved, including PSR
;

Stop    ROUT
 [ No26bitCode
        Push    "R0-R2,R14"
        mrs    ,R2, CPSR
 |
        Push    "R0,R1,R14"
 ]

        MOV     R0, #0                  ; clock HI, data LO
        MOV     R1, #1
        BL      SetC1C0

; Delay here must be >= 4.0 microsecs

        MOV     R0, #1                  ; clock HI, data HI
        MOV     R1, #1
        BL      SetC1C0

 [ No26bitCode
        msr    ,CPSR_f, R2
        Pull    "R0-R2,PC"
 |
        Pull    "R0,R1,PC",,^
 ]

; *****************************************************************************
;
;       TXByte - Transmit a byte
;
; in:   R0 = byte to be transmitted
;
; out:  All registers preserved, including PSR
;

TXByte  ROUT
 [ No26bitCode
        Push    "R0-R3,R14"
        mrs    ,R3, CPSR
 |
        Push    "R0-R2,R14"
 ]
        MOV     R2, R0                  ; byte goes into R2
        MOV     R1, #&80                ; 2^7   the bit mask
10
        ANDS    R0, R2, R1              ; zero if bit is zero
        MOVNE   R0, #1
        BL      ClockData               ; send the bit
        MOVS    R1, R1, LSR #1
        BNE     %BT10
 [ No26bitCode
        msr    ,CPSR_f, R3
        Pull    "R0-R3,PC"
 |
        Pull    "R0-R2,PC",,^
 ]

; *****************************************************************************
;
;       RXByte - Receive a byte
;
; out:  R0 = byte received
;       All other registers preserved, including PSR
;

RXByte  ROUT
 [ No26bitCode
        Push    "R1-R5, R14"
        mrs    ,R5, CPSR
 |
        Push    "R1-R4, R14"
 ]
        MOV     R3, #0                  ; byte:=0
        MOV     R2, #IOC
        MOV     R4, #7

        MOV     R0, #1                  ; clock LO, data HI
        MOV     R1, #0
        BL      SetC1C0
10
 [ No26bitCode
        mrs    ,R1,CPSR
        Push    "R1"
        ORR     R1,R1,#I32_bit
        msr    ,CPSR_c,R1               ; Disable ints if not talking to BMU
 |
        MOV     LR, PC
        Push    "LR"
        ORR     LR, LR, #I_bit
        TEQP    LR, #0                  ; Disable ints if not talking to BMU
 ]

        MOV     R0, #1                  ; pulse clock HI
        MOV     R1, #1
        BL      SetC1C0CheckClock

        LDRB    R1, [R2, #IOCControl]   ; get the data from IOC
        AND     R1, R1, #1
        ADD     R3, R1, R3, LSL #1      ; byte:=byte*2+(IOC?0)AND1

        MOV     R0, #1                  ; return clock LO
        MOV     R1, #0
        BL      SetC1C0

 [ No26bitCode
        Pull    "R1"
        msr    ,CPSR_c,R1               ; restore interrupt state
 |
        Pull    "LR"
        TEQP    PC, LR                  ; restore interrupt state
 ]

        SUBS    R4, R4, #1
        BCS     %BT10

        MOV     R0, R3                  ; return the result in R0
 [ No26bitCode
        msr    ,CPSR_f, R5
        Pull    "R1-R5, PC"
 |
        Pull    "R1-R4, PC",,^
 ]

CopyError ENTRY r1-r7
        BL      open_messagefile
        EXIT    VS
        MOV     R4,R11                  ; (parameter) -> R4
CopyError0
        ADR     R1, MessageFile_Block
        MOV     R2, #0
        MOV     R5, #0
        MOV     R6, #0
        MOV     R7, #0
        SWI     XMessageTrans_ErrorLookup
        EXIT

message_filename
        DCB     "Resources:$.Resources.IIC.Messages", 0

        ALIGN

open_messagefile ENTRY r0-r2
        LDR     r0, MessageFile_Open
        CMP     r0, #0
        EXIT    NE
        ADR     R0, MessageFile_Block
        ADR     R1, message_filename
        MOV     r2, #0
        SWI     XMessageTrans_OpenFile
        STRVS   r0, [sp]
        EXIT    VS
        MOV     r0, #1
        STR     r0, MessageFile_Open
        EXIT

        END
