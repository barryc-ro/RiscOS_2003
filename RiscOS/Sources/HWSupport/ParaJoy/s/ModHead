; > Sources.ModHead

        ASSERT  (.=Module_BaseAddr)

        DCD     0                               	; Start
        DCD     Init           - Module_BaseAddr
        DCD     Die            - Module_BaseAddr
        DCD     0                                       ; Service
        DCD     Title          - Module_BaseAddr
        DCD     Helpstr        - Module_BaseAddr
        DCD     0                               	; Helptable
        DCD     &43f40                          	; SWIbase
        DCD     SWIhandler     - Module_BaseAddr
        DCD     SWInames       - Module_BaseAddr
        DCD     0                               	; SWIdecode
    [ :LNOT: No32bitCode
        DCD     0
        DCD     Flags          - Module_BaseAddr
    ]

; ---------------------------------------------------------------------------------------------------
Title   DCB     "ParallelJoystick",0
Helpstr DCB     "ParallelJoystick",9,"$Module_HelpVersion", 0
                ALIGN

    [ :LNOT: No32bitCode
Flags   DCD     ModuleFlag_32bit
    ]

; ---------------------------------------------------------------------------------------------------
;       Module initialisation point
Init
        Push    "lr"
        Debug   mod, "Initialising"

        LDR     r2, [r12]               		; Have we already got a workspace ?
        CMP     r2, #0                  		; clears V
        BNE     %FT01

        MOV     r0, #6
        LDR     r3, =max_running_work
        SWI     XOS_Module              		; Claim workspace
        Pull    "pc",VS

        STR     r2, [r12]               		; Store workspace pointer.
01
        MOV     r12, r2

        MOV     r0, #0
        STR     r0, message_file_open

        Pull    "pc"


; ---------------------------------------------------------------------------------------------------
;       RMKill'ing the module
Die
        Push    "lr"
        LDR     r12, [r12]
        BL      Shut_Down
        CLRV
        Pull    "pc"


Shut_Down
        Push    "r0,lr"

        Debug   mod, "Module dying"

        ; Close the message file if it's open
        LDR     r0, message_file_open
        TEQ     r0, #0
        ADRNE   r0, message_file_block
        SWINE   XMessageTrans_CloseFile

        Pull    "r0,pc"


        LNK     SWI.s
