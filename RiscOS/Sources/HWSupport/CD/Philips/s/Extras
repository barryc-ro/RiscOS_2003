; -> Extras.s














; This contains extra routines for the hitachi driver


; Routines in here:
;                  GetDrawerStatus
;                  GetPauseStatus
;                  GetStereoStatus
;                  HasDiscChanged
;                  SetDiscChanged
;                  SetDrawerStatus
;                  SetPauseStatus
;                  SetStereoStatus










;By MEW of Eesox.  8th May 1993

;--------------------------------------------------------------------------
HasDiscChanged ROUT
; on entry:
;          r7  -> control block
;          r12 -> workspace
; on exit:
;          returns r0 = 0 or 1
;--------------------------------------------------------------------------

; This gets the current state, then clears it in the list


 STASH     "r1 - r5, r14"

 ADR       r4, disc_changed_list


;--------------------------------
; Look thru list for disc changes
;--------------------------------
 LDMIA     r7, { r1, r2, r3 }

 ORR       r1, r1, r2, LSL #3

 LDRB      r2, [ r4, r1 ]!            ; ie/ device + card = byte number

 MOV       r5, #1
 ANDS      r0, r2, r5, LSL r3         ; ie/ LUN = bit number

;----------------------
; If set, then clear it
;----------------------
 BICNE     r2, r2, r5, LSL r3
 STRNEB    r2, [ r4 ]

 MOVNE     r0, #1

;----------------------


 GRABS     "r1 - r5, pc"


;--------------------------------------------------------------------------
SetPauseStatus ROUT
; on entry:
;          r7  -> control block
;          r12 -> workspace
; on exit:
;          returns r0 = 0 or 1
;--------------------------------------------------------------------------
; This sets the current state


 STASH     "r1 - r5, r14"

 ADR       r4, pause_list

 B         set_list

;--------------------------------------------------------------------------
SetDiscChanged ROUT
; on entry:
;          r7  -> control block
;          r12 -> workspace
; on exit:
;          r0 corrupted
;          returns void
;--------------------------------------------------------------------------
; This sets the current state


 STASH     "r1 - r5, r14"

 MOV       r0, #1

 ADR       r4, disc_changed_list

 B         set_list


;--------------------------------------------------------------------------
GetPauseStatus ROUT
; on entry:
;          r7  -> control block
;          r12 -> workspace
; on exit:
;          returns r0 = 0 or 1
;
; This gets the current state
;--------------------------------------------------------------------------

 STASH     "r1 - r5, r14"

 ADR       r4, pause_list

 B         get_list

;--------------------------------------------------------------------------
GetDrawerStatus ROUT
; on entry:
;          r7  -> control block
;          r12 -> workspace
; on exit:
;          returns r0 = 0 or 1
;
; This gets the current state
;--------------------------------------------------------------------------

 STASH     "r1 - r5, r14"

 ADR       r4, drawer_list

; B         get_list      ALLOW TO RUN ON

;--------------------------------------------------------------------------
get_list ROUT
; on entry:
;          r4 -> list to get from
;          r7  -> control block
;          r12 -> workspace
; on exit:
;          returns r0 = 0 or 1
;
; This gets the current state
;--------------------------------------------------------------------------

;--------------------------------
; Look thru list for disc changes
;--------------------------------
 LDMIA     r7, { r1, r2, r3 }

 ORR       r1, r1, r2, LSL #3

 LDRB      r2, [ r4, r1 ]!            ; ie/ device + card = byte number

 MOV       r5, #1
 ANDS      r0, r2, r5, LSL r3         ; ie/ LUN = bit number

 MOVNE     r0, #1

;----------------------

 GRABS     "r1 - r5, pc"


;--------------------------------------------------------------------------
SetDrawerStatus ROUT
; on entry:
;          r0 = 0 or 1
;          r7  -> control block
;          r12 -> workspace
; on exit:
;          all regs preserved
;--------------------------------------------------------------------------
; This sets the current state


 STASH     "r1 - r5, r14"

 ADR       r4, drawer_list

; B         set_list      ALLOW TO RUN ON

;--------------------------------------------------------------------------
set_list ROUT
; on entry:
;          r0 = 0 or 1
;          r4 -> list to update
;          r7  -> control block
;          r12 -> workspace
; on exit:
;          all regs preserved
;--------------------------------------------------------------------------

;--------------------------------
; Look thru list for disc changes
;--------------------------------
 LDMIA     r7, { r1, r2, r3 }

 ORR       r1, r1, r2, LSL #3

 LDRB      r2, [ r4, r1 ]!            ; ie/ device + card = byte number

 MOV       r5, #1
 BIC       r2, r2, r5, LSL r3         ; ie/ LUN = bit number
 ORR       r2, r2, r0, LSL r3
 STRB      r2, [ r4 ]

;----------------------

 GRABS     "r1 - r5, pc"




;--------------------------------------------------------------------------
SetStereoStatus ROUT
; on entry:
;          r0 = 0, 1, 2, or 3
;          r7  -> control block
;          r12 -> workspace
; on exit:
;          all regs preserved
;--------------------------------------------------------------------------
; This sets the current state


 STASH     "r1 - r5, r14"

 ADR       r4, left_list

;--------------------------------
; Look thru list for disc changes
;--------------------------------
 LDMIA     r7, { r1, r2, r3 }

 ORR       r1, r1, r2, LSL #3

 LDRB      r2, [ r4, r1 ]!            ; ie/ device + card = byte number

; left channel
 MOV       r5, #1
 TST       r0, #2
 BICEQ     r2, r2, r5, LSL r3         ; ie/ LUN = bit number
 ORRNE     r2, r2, r5, LSL r3
 STRB      r2, [ r4 ]

; right channel
 LDRB      r2, [ r4, #32 ]!
 TST       r0, #1
 BICEQ     r2, r2, r5, LSL r3         ; ie/ LUN = bit number
 ORRNE     r2, r2, r5, LSL r3
 STRB      r2, [ r4 ]

;----------------------

 GRABS     "r1 - r5, pc"



;--------------------------------------------------------------------------
GetStereoStatus ROUT
; on entry:
;          r7  -> control block
;          r12 -> workspace
; on exit:
;          returns r0 = 0, 1, 2 or 3
;
; This gets the current state
;--------------------------------------------------------------------------

 STASH     "r1 - r5, r14"

 ADR       r4, left_list


;--------------------------------
; Look thru list for disc changes
;--------------------------------
 LDMIA     r7, { r1, r2, r3 }

 ORR       r1, r1, r2, LSL #3

; left channel
 LDRB      r2, [ r4, r1 ]!            ; ie/ device + card = byte number

 MOV       r5, #1
 ANDS      r0, r2, r5, LSL r3         ; ie/ LUN = bit number

 MOVNE     r0, #2

; right channel
 LDRB      r2, [ r4, #32 ]            ; ie/ device + card = byte number

 TST       r2, r5, LSL r3         ; ie/ LUN = bit number

 ORRNE     r0, r0, #1

;----------------------

 GRABS     "r1 - r5, pc"



;--------------------------------------------------------------------------
;--------------------------------------------------------------------------
;--------------------------------------------------------------------------


 END
