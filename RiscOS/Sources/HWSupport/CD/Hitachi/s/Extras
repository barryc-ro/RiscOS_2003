; -> Extras.s














; This contains extra routines for the hitachi driver


; Routines in here:
;                  GetDrawerStatus
;                  GetPauseStatus
;                  GetStereoStatus
;                  get_list
;                  HasDiscChanged
;                  SetDiscChanged
;                  SetDrawerStatus
;                  SetStereoStatus
;                  SetPauseStatus
;                  set_list










;By MEW of Eesox.  8th May 1993

;--------------------------------------------------------------------------
HasDiscChanged ROUT
; on entry:
;          r7  -> control block
;          r12 -> workspace
; on exit:
;          returns r0 = 0 or 1
;--------------------------------------------------------------------------

; This gets the current state, then clears it in the list


 STASH     "r1 - r5, r14"

 ADR       r4, disc_changed_list


;--------------------------------
; Look thru list for disc changes
;--------------------------------
 LDMIA     r7, { r1, r2, r3 }

 ORR       r1, r1, r2, LSL #3

 LDRB      r2, [ r4, r1 ]!            ; ie/ device + card = byte number

 MOV       r5, #1
 ANDS      r0, r2, r5, LSL r3         ; ie/ LUN = bit number

;----------------------
; If set, then clear it
;----------------------
 BICNE     r2, r2, r5, LSL r3
 STRNEB    r2, [ r4 ]

 MOVNE     r0, #1

;----------------------


 GRABS     "r1 - r5, pc"

;--------------------------------------------------------------------------
SetDiscChanged ROUT
; on entry:
;          r7  -> control block
;          r12 -> workspace
; on exit:
;          all regs preserved
;
; This sets the current state to 1
;
;--------------------------------------------------------------------------

 STASH     "r0 - r5, r14"

 ADR       r4, disc_changed_list

;--------------------------------
; Look thru list for disc changes
;--------------------------------
 LDMIA     r7, { r1, r2, r3 }

 ORR       r1, r1, r2, LSL #3

 LDRB      r2, [ r4, r1 ]!            ; ie/ device + card = byte number

 MOV       r5, #1
 ORR       r2, r2, r5, LSL r3         ; ie/ LUN = bit number
 STRB      r2, [ r4 ]

;----------------------

 GRABS     "r0 - r5, pc"


;--------------------------------------------------------------------------
GetPauseStatus ROUT
; on entry:
;          r7  -> control block
;          r12 -> workspace
; on exit:
;          returns r0 = 0 or 1
;
; This gets the current state
;--------------------------------------------------------------------------

 STASH     "r1 - r5, r14"

 ADR       r4, pause_list

 B         get_list

;--------------------------------------------------------------------------
GetDrawerStatus ROUT
; on entry:
;          r7  -> control block
;          r12 -> workspace
; on exit:
;          returns r0 = 0 or 1
;
; This gets the current state
;--------------------------------------------------------------------------

 STASH     "r1 - r5, r14"

 ADR       r4, drawer_list

; B         get_list   ALLOW TO RUN ON


;--------------------------------------------------------------------------
get_list
; on entry:
;          r4  -> list to get from
;          r7  -> control block
;          r12 -> workspace
;
;          r1-r5 and r14 are on the stack
;
; on exit:
;          returns r0 = 0 or 1
;
; This gets the current state
;--------------------------------------------------------------------------
;--------------------------------
; Look thru list for disc changes
;--------------------------------
 LDMIA     r7, { r1, r2, r3 }

 ORR       r1, r1, r2, LSL #3

 LDRB      r2, [ r4, r1 ]!            ; ie/ device + card = byte number

 MOV       r5, #1
 ANDS      r0, r2, r5, LSL r3         ; ie/ LUN = bit number

 MOVNE     r0, #1

;----------------------

 GRABS     "r1 - r5, pc"


;--------------------------------------------------------------------------
SetPauseStatus ROUT
; on entry:
;          r0 = 0 or 1
;          r7  -> control block
;          r12 -> workspace
; on exit:
;          all regs preserved
;
; This sets the current state
;--------------------------------------------------------------------------

 STASH     "r1 - r5, r14"

 ADR       r4, pause_list

 B         set_list

;--------------------------------------------------------------------------
SetDrawerStatus ROUT
; on entry:
;          r0 = 0 or 1
;          r7  -> control block
;          r12 -> workspace
; on exit:
;          all regs preserved
;
; This sets the current state
;--------------------------------------------------------------------------

 STASH     "r1 - r5, r14"

 ADR       r4, drawer_list

; B         set_list     ALLOW TO RUN ON

;--------------------------------------------------------------------------
set_list
; on entry:
;          r0  =  0 or 1
;          r4  -> list to update
;          r7  -> control block
;          r12 -> workspace
;
;          r1-r5 and r14 are on the stack
;
; on exit:
;          all regs preserved
; This sets the current state
;--------------------------------------------------------------------------
;--------------------------------
; Look thru list for disc changes
;--------------------------------
 LDMIA     r7, { r1, r2, r3 }

 ORR       r1, r1, r2, LSL #3

 LDRB      r2, [ r4, r1 ]!            ; ie/ device + card = byte number

 MOV       r5, #1
 BIC       r2, r2, r5, LSL r3         ; ie/ LUN = bit number
 ORR       r2, r2, r0, LSL r3
 STRB      r2, [ r4 ]

;----------------------

 GRABS     "r1 - r5, pc"


 [ 1=0               ; NOT USED
;--------------------------------------------------------------------------
SetStereoStatus ROUT
; on entry:
;          r0 = 0, 1, 2 or 3
;          r7  -> control block
;          r12 -> workspace
; on exit:
;          all regs preserved
;
; This sets the current state
;--------------------------------------------------------------------------

 STASH     "r1 - r6, r14"

 ADR       r4, left_channel_list

 MOV       r6, r0, LSR #1
 AND       r0, r0, #1

;--------------------------------
; Look thru list for disc changes
;--------------------------------
 LDMIA     r7, { r1, r2, r3 }

 ORR       r1, r1, r2, LSL #3

; Left channel
 LDRB      r2, [ r4, r1 ]!            ; ie/ device + card = byte number

 MOV       r5, #1
 BIC       r2, r2, r5, LSL r3         ; ie/ LUN = bit number
 ORR       r2, r2, r0, LSL r3
 STRB      r2, [ r4 ]

; Right channel
 LDRB      r2, [ r4, #32 ]!           ; ie/ device + card = byte number

 BIC       r2, r2, r5, LSL r3         ; ie/ LUN = bit number
 ORR       r2, r2, r6, LSL r3
 STRB      r2, [ r4 ]

;----------------------

 GRABS     "r1 - r6, pc"

;--------------------------------------------------------------------------
GetStereoStatus ROUT
; on entry:
;          r7  -> control block
;          r12 -> workspace
; on exit:
;          r0 = 0, 1, 2 or 3
;          all regs preserved
;
; This sets the current state
;--------------------------------------------------------------------------

 STASH     "r1 - r6, r14"

 ADR       r4, left_channel_list

;--------------------------------
; Look thru list for disc changes
;--------------------------------
 LDMIA     r7, { r1, r2, r3 }

 ORR       r1, r1, r2, LSL #3

; Left channel
 LDRB      r2, [ r4, r1 ]!            ; ie/ device + card = byte number
 MOV       r5, #1
 ANDS      r0, r2, r5, LSL r3         ; ie/ LUN = bit number
 MOVNE     r0, #1

; Right channel
 LDRB      r2, [ r4, r1 ]!            ; ie/ device + card = byte number
 MOV       r5, #1
 ANDS      r6, r2, r5, LSL r3         ; ie/ LUN = bit number
 MOVNE     r6, #1

;----------------------
 ORR       r0, r0, r6, LSL #1

 GRABS     "r1 - r6, pc"



 ]

;--------------------------------------------------------------------------


 END
