;******************************************************************************/
;**  File:    IRQEnable.s                                                    **/
;**  	      								     **/
;**  Purpose: Allows FIQ/IRQ disabled access to the IOMD2 interrupt regs     **/
;**  	      								     **/
;**  Author:  W.Turner                                                       **/
;**  	      								     **/
;**  History: 24-Aug-98 WT  Created                                          **/
;******************************************************************************/

	EXPORT	HW_Asm_ClaimLSERR
	EXPORT	HW_Asm_ReleaseLSERR
	
	AREA	|C$$Code|, CODE, READONLY


IOMD2_Base	* &03200000
PLX_Base	* &A0000000
IOMD2_INTENBL	* &318
iomd2_plx_bit	* 1:SHL:7
Device_PLX	* 32
MTAborts	* 3:SHL:28
XOS_ClaimDeviceVector * &0002004B
XOS_ReleaseDeviceVector * &0002004C
	

;-----------------------------------------------------------------------------
;
;
HW_Asm_ClaimLSERR
	STMFD	sp!, {r0-r2,lr}
	;Claim device vector
	MOV	r0, #Device_PLX
	ADR	r1, HW_Asm_HandleLSERR
	MOV	r2, r12
	SWI	XOS_ClaimDeviceVector
;	MOVVS	r0, #-1
;	LDMVSFD	sp!, {r0-r2,pc}		;Exit if couldn't claim
	
	;Enable LSERR in IOMD2 with IRQ/FIQ off
	MOV	r0, pc
	ORR	r0, r0, #3:SHL:26
	TEQP	r0, #0		;Off
	NOP
	MOV	r0, #IOMD2_Base
	LDR	r1, [r0, #IOMD2_INTENBL]
	ORR	r1, r1, #iomd2_plx_bit
	STR	r1, [r0, #IOMD2_INTENBL]	
	TEQP	lr, #0		;Back
	NOP

	;Enable LSERR generation in PLX
	MOV	r1, #PLX_Base
	ORR	r1, r1, #&E8
	LDR	r0, [r1]
;	ORR	r0, r0, #&400	;Enable MAbort to PCI IRQ
	ORR	r0, r0, #1	;Enable LSERR
	STR	r0, [r1]
	LDMFD	sp!, {r0-r2,pc}


;-----------------------------------------------------------------------------
;
;
HW_Asm_HandleLSERR
	STMFD	sp!, {r0-r1,lr}
	MOV	r1, #PLX_Base
	ORR	r1, r1, #&04
	LDR	r0, [r1]
	ORR	r0, r0, #MTAborts
	STR	r0, [r1]
	LDMFD	sp!, {r0-r1,pc}


;-----------------------------------------------------------------------------
;
;
HW_Asm_ReleaseLSERR
	STMFD	sp!, {r0-r2,lr}

	;Disable LSERR generation in PLX
	MOV	r1, #PLX_Base
	ORR	r1, r1, #&E8
	LDR	r0, [r1]
	BIC	r0, r0, #1
	STR	r0, [r1]

	;Disable LSERR in IOMD2 with IRQ/FIQ off
	MOV	r0, pc
	ORR	r0, r0, #3:SHL:26
	TEQP	r0, #0		;Off
	NOP
	MOV	r0, #IOMD2_Base
	LDR	r1, [r0, #IOMD2_INTENBL]
	BIC	r1, r1, #iomd2_plx_bit
	STR	r1, [r0, #IOMD2_INTENBL]	
	TEQP	lr, #0		;Back
	NOP

	;Release device vector
	MOV	r0, #Device_PLX
	ADR	r1, HW_Asm_HandleLSERR
	MOV	r2, r12
	SWI	XOS_ReleaseDeviceVector

	LDMFD	sp!, {r0-r2,pc}


	END
