;
; 		Copyright 1996 Acorn Network Computing
;
;  This material is the confidential trade secret and proprietary
;  information of Acorn Network Computing. It may not be reproduced,
;  used, sold, or transferred to any third party without the prior
;  written consent of Acorn Network Computing. All rights reserved.
;
; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; Description of the structure of the two port implemementation.
;
;   The device probe routine is called with a possible base address given in
;   r10. If present, a port specific workspace area is claimed and the this base
;   address stored there. The pointer to this workspace is stored either of two
;   words in main module workspace and usually referenced as r11. r11 is used
;   during devicefs registration so that it is passed into the devicefs handler
;   each time it is called, automatically establishing that port session
;   depending on which devicefs object was referenced.
;

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; module workspace
                        ^ 0, wp
Flags			# 4
BuffManWkSpace          # 4	; buffer manager workspace
BuffManService          # 4     ; address of buffer manager routine
Port1			# 4	; address of port 1 workspace
Port2			# 4	; address of port 2 workspace
MessagesWorkspace       # 16    ; needed for messages routines
workspace               * :INDEX: @

;
; bit definitions for Flags
;
flag_Port1Present	* 1:SHL:0  ; set => hardware detected for port1
flag_Port2Present	* 1:SHL:1  ; set => hardware detected for port2
flag_OwnIRQ             * 1:SHL:2  ; set => IRQ owned for device
flag_HaveMessages	* 1:SHL:3  ; set => opened messages file

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; port specific workspace

                        ^ 0, r11
DeviceFSBlock		# 4	; address of devicefs registration block
DeviceHandle		# 4	; handle returned from DeviceFS_Register
InputStream		# 4	; devicefs stream handle
OutputStream		# 4	; devicefs stream handle
InputFSHandle		# 4     ; file switch handle of input stream
OutputFSHandle		# 4     ; file switch handle of output stream
 [ debug
InputBuffer		# 4	; buffer manager handle
OutputBuffer		# 4	; buffer manager handle
 ]
InputBufferPrivId      	# 4	; buffer managers private buffer id
OutputBufferPrivId	# 4	; buffer managers private buffer id
InputBufferSize		# 4	; buffer size in bytes
OutputBufferSize	# 4	;
InputBufferThreshold	# 4	; buffer threshold point in bytes
OutputBufferThreshold	# 4	;
BaseAddress		# 4	; base address of register set
BaudRate		# 4	; ports programmed baud rate
DataFormat		# 4	; ports programmed data format
PortFlags		# 4	; port specific flags
ControlLines		# 1
TxByteCount		# 1
FIFOTrigger		# 1
Dummy1			# 1
 [ counting
ByteCount		# 4
ErrorCount		# 4
OverrunCount		# 4
ParityCount		# 4
FramingCount		# 4
InterruptCount		# 4
 ]
port_workspace		* :INDEX: @

;
; bit definitions for PortFlags
;
flag_FIFOsPresent	* 1:SHL:0	; we have FIFOs
flag_UseRTS		* 1:SHL:1	; rts/cts handshaking
flag_UseXon		* 1:SHL:2	; xon/off handshaking
flag_UseDTR		* 1:SHL:3	; dtr/dsr handshaking
flag_TxDormant		* 1:SHL:4	; no more characters tx
flag_RxThresholded	* 1:SHL:5	; rx buffer thresholded
flag_TxXon		* 1:SHL:6	; need to send xon char
flag_TxXoff		* 1:SHL:7	; need to send xoff char
flag_TxXoffed		* 1:SHL:8	; we have been xoffed
flag_Registered		* 1:SHL:9	; registered with devicefs

;
; bit definitions for ControlLines
;
ctrl_line_dtr			* 1:SHL:0
ctrl_line_rts			* 1:SHL:1
ctrl_line_cts			* 1:SHL:2
ctrl_line_dsr			* 1:SHL:3
ctrl_line_ri			* 1:SHL:4
ctrl_line_dcd			* 1:SHL:5

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; standard module declarations

module          & 0
                & init    -module               ; => initalise routine
                & final   -module               ; => finalise routine
                & service -module               ; => service trap

                & title -module                 ; => title string
                & help  -module                 ; => help string
                & command_table-module		; => command table

                & 0

title           = "Serial", 0

 [ debug
help            = "Serial", 9, 9, "$VString ($Date) (Debug on)", 0
 |
help            = "Serial", 9, 9, "$VString ($Date)", 0
 ]

command_table   = "SerialInfo", 0
		ALIGN
		DCD	serial_command -module
		DCD	0
		DCD	serial_syntax -module
		DCD	serial_help   -module
		&	0

serial_help    = "*SerialInfo will display internal statistics", 13
serial_syntax  = "Syntax: *SerialInfo", 0
		ALIGN

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; debug options
 [ debug
Host_Debug  	SETL 	False
HostDebugViaTML	SETL 	False
Debug_Module	SETL	False
;Debug_File      SETS	"$.Debug"
 ]
; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; DeviceFS declarations
device_name1    = "serial1", 0
                ALIGN
device_name2	= "serial2", 0
		ALIGN

device_block1   & device_name1 - device_block1
                & DeviceFlag_BufferedDevice + DeviceFlag_DefinePathVariable
                & 8                             ; default RX buffer flags
                & 1024                          ; default RX buffer size
                & 8                             ; default TX buffer flags
                & 512                           ; default TX buffer size
                & 0                             ; reserved field (must be zero)

                & 0                             ; end of table

                ALIGN

device_block2   & device_name2 - device_block2
                & DeviceFlag_BufferedDevice + DeviceFlag_DefinePathVariable
                & 8                             ; default RX buffer flags
                & 1024                          ; default RX buffer size
                & 8                             ; default TX buffer flags
                & 512                           ; default TX buffer size
                & 0                             ; reserved field (must be zero)

                & 0                             ; end of table

                ALIGN

device_validation = "baud/ndata/nstop/nnoparity,even,odd/snohandshake,rts,xon,dtr/ssize/nthres/n",0
; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; Globals
serial_baud_rate	= "  Baud (bps)  : ",0
serial_data_format	= "  Data format : ",0
serial_handshake	= "  Handshake   : ",0
serial_handshake_rts	= "rts/cts",0
serial_handshake_dtr    = "rtr/dsr",0
serial_handshake_xon    = "xon/xoff",0
serial_handshake_none   = "none",0
 [ debug
serial_flags		= "  Port flags      : 0x",0
serial_control_line	= "  Control lines   : 0x",0
serial_base_address	= "  Base address    : 0x",0
serial_workspace	= "  Workspace       : 0x",0
serial_input_buffer	= "  IP buffer       : ",0
serial_ip_buffer_size   = "  IP buffer size  : ",0
serial_ip_buffer_thres  = "  IP buffer thres : ",0
serial_output_buffer	= "  OP buffer       : ",0
serial_op_buffer_size   = "  OP buffer size  : ",0
serial_op_buffer_thres  = "  OP buffer thres : ",0
serial_fifo_present     = "  Fifos present",0
serial_fifo_not_present = "  Fifos not present",0
 [ counting
serial_byte_count	= "  Byte count      : ",0
serial_error_count      = "  Error count     : ",0
serial_overrrun_count	= "  Overrun count   : ",0
serial_framing_count    = "  Framing count   : ",0
serial_interrupt_count  = "  Interrupt count : ",0
serial_parity_count	= "  Parity count    : ",0
 ]
 ]
resource_file = "Resources:$.Resources.Serial.Messages", 0

default_port		* 0
default_baud		* 9600
default_data   		* 8
default_stop   		* 1
default_parity 		* 0
default_threshold 	* 32
default_fifo_trigger	* 4

base_address1		* C710 + &3f8*4
base_address2		* C710 + &2f8*4

xonchar  * &11
xoffchar * &13

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; Error declarations

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; This code handles the startup of the module, the routine must claim the
; required workspace and then initialise the driver.
;
init            ENTRY	"r0-r11"

                LDR     r2, [r12]		; r2 = &wp
                TEQ     r2, #0                  ; any workspace / warm start?
                BNE     %10

                MOV     r0, #ModHandReason_Claim
                LDR     r3, =workspace          ; r3  = amount of workspace

                SWI     XOS_Module
                EXIT    VS                      ; return if didn't work

                STR     r2, [wp]		; wp = r2
10
                MOV     wp, r2                  ; wp -> workspace

 [ standalonemessages
                ADRL    r0, resource_file_block
                SWI     XResourceFS_RegisterFiles
 ]

                MOV     r0, #0
                STR     r0, Flags
                STR     r0, BuffManService
                STR     r0, BuffManWkSpace
                STR     r0, Port1
                STR     r0, Port2
 [ debug
		SetColour off
 ]

; check to see if the devices are present
		LDR	r0, Flags
		LDR 	r10, =base_address1
		BL	hardware_probe
		CMP	r10, #0
		ORRNE	r0, r0, #flag_Port1Present

		LDR 	r10, =base_address2
		BL	hardware_probe
		CMP	r10, #0
		ORRNE	r0, r0, #flag_Port2Present
		STR	r0, Flags

; now set these devices up
		TST	r0, #flag_Port1Present
		BEQ	%20
		LDR 	r10, =base_address1
		ADRL	r9, device_block1
		BL	establish_device
                STRVS   r0, [sp]
		STR	r11, Port1
		BVS	%40
20
		TST	r0, #flag_Port2Present
		LDR 	r10, =base_address2
		BEQ	%30
		ADRL	r9, device_block2
		BL	establish_device
                STRVS   r0, [sp]
		STR	r11, Port2
		BVS	%40
30
; claim the device vector
		BL 	hardware_claim
                EXIT

; free workspace up before exit
40
                LDR     r11, Port1
		BL	free_workspace
                LDR     r11, Port2
		BL	free_workspace
		BL	hardware_release
 [ standalonemessages
                ADRL    r0, resource_file_block
                SWI     XResourceFS_DeregisterFiles
 ]
                PullEnv
                ORRS  	pc, lr, #VFlag

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
		LTORG
; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; Handle module close down.
;
	MakeErrorBlock Serial_StreamInUse

final           ENTRY	"r0-r1,r11"

                LDR     wp, [r12]               ; wp -> workspace
; see if any streams are in use
		LDR	r11, Port1
		LDR	r0, InputStream
		CMP	r0, #0
		LDREQ	r0, OutputStream
		CMPEQ	r0, #0

		LDREQ	r11, Port2
		LDREQ	r0, InputStream
		CMPEQ	r0, #0
		LDREQ	r0, OutputStream
		CMPEQ	r0, #0

		BEQ	%10

; generate an error
		PullEnv
		ADR	r0, ErrorBlock_Serial_StreamInUse
		B	make_error
10
		BL	hardware_release

; sort the hardware out
                LDR     r11, Port1
		BL	hardware_remove		; release any owned IRQs
		BL	unregister_device
		BL	free_workspace

                LDR     r11, Port2
		BL	hardware_remove		; release any owned IRQs
		BL	unregister_device
		BL	free_workspace

 [ standalonemessages
                ADRL    R0, resource_file_block
                SWI     XResourceFS_DeregisterFiles
 ]
                EXITS

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; Handle service calls received by the module.
;
; Quickly test to see if they are of interest to use and pass on.

service         ROUT
                TEQ     r1, #Service_DeviceFSStarting
                TEQNE   r1, #Service_DeviceFSDying
 [ standalonemessages
                TEQNE   r1, #Service_ResourceFSStarting
 ]
                MOVNES  pc, lr

                LDR     wp, [r12]

; handle devicefs starting up
                TEQ     r1, #Service_DeviceFSStarting
		BNE	%10

		Push	"r1,r11,lr"
		LDR	r1, Flags
		TST	r1, #flag_Port1Present
                LDRNE	r11, Port1
                BLNE	register_device

		TST	r1, #flag_Port2Present
                LDRNE	r11, Port2
                BLNE	register_device
		Pull	"r1,r11,pc",,^
10
; handle devicefs going down
                TEQ     r1, #Service_DeviceFSDying
		BNE	%20

		Push	"r1,r11"
		LDR	r1, Flags
		TST	r1, #flag_Port1Present
                LDRNE	r11, Port1
                STRNE   r0, DeviceHandle

		TST	r1, #flag_Port2Present
                LDRNE	r11, Port2
                STRNE   r0, DeviceHandle
		Pull	"r1,r11"
		MOVS	pc, lr
20
 [ standalonemessages
                TEQ     r1, #Service_ResourceFSStarting
                BNE     %FT30					; no so continue
                Push    "r0-r3,lr"
                ADRL    r0, resource_file_block
                MOV     lr, pc
                MOV     pc, r2
                Pull    "r0-r3,pc",,^				; and exit
30
 ]
                MOVS  	pc, lr

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; free workspace
;
; in  : r11 port workspace pointer
;
free_workspace	ENTRY	"r0-r2"
		CMP	r11, #0
		EXITS	EQ
		MOV	r0, #ModHandReason_Free
		MOV	r2, r11
		SWI	XOS_Module
		EXITS
; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; establish device
;
; in  : r9 address of devicefs registration block
;       r10 hardware base address
;
; out : r10 address if port is present, 0 otherwise
;       r11 port workspace pointer
;
establish_device ENTRY "r0-r3"

; we need some workspace
                MOV     r0, #ModHandReason_Claim
                LDR     r3, =port_workspace	; amount of workspace
                SWI     XOS_Module
                STRVS   r0, [sp]
                EXIT    VS                      ; return if didn't work

		MOV	r11, r2			; establish port workspace

		MOV	r0, #0
		STR	r0, DeviceHandle
		STR	r0, InputStream
		STR	r0, OutputStream
		STR	r0, InputFSHandle
		STR	r0, OutputFSHandle
 [ debug
		STR	r0, InputBuffer
		STR	r0, OutputBuffer
 [ counting
		STR	r0, ByteCount
		STR	r0, ErrorCount
		STR	r0, OverrunCount
		STR	r0, ParityCount
		STR	r0, FramingCount
		STR	r0, InterruptCount
 ]
 ]
		STR	r0, BaudRate
		STR	r0, DataFormat
		STR	r0, InputBufferSize
		STR	r0, OutputBufferSize
		STR	r0, OutputBufferThreshold
		STR	r0, PortFlags
		STRB	r0, ControlLines
		STRB	r0, TxByteCount
		STRB	r0, FIFOTrigger

; set private buffer handles to invalid values
		MOV	r0, #-1
		STR	r0, InputBufferPrivId
		STR	r0, OutputBufferPrivId

		MOV	r0, #default_threshold
		STR	r0, InputBufferThreshold

		STR	r10, BaseAddress	; store base address
		STR	r9, DeviceFSBlock	; and registration info

		BL	register_device
                STRVS   r0, [sp]
		EXIT	VS

		BL	hardware_initialise
                STRVS   r0, [sp]
		EXIT	VS

		EXITS
; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; register device
;
; in  : r11 port workspace pointer
;
register_device ENTRY "r0-r7"
		CMP	r11, #0
		EXITS	EQ

; have we already been registered
		LDR	r0, PortFlags
		TST	r0, #flag_Registered
		EXITS	NE

                MOV     r0, #ParentFlag_FullDuplex :OR: ParentFlag_DeviceUpcalls
		LDR	r1, DeviceFSBlock
                ADDR    r2, handle_device_call    	; -> handler
                MOV     r3, r11				; passed in as r8
                MOV     r4, wp                         	; -> workspace
                ADDR    r5, device_validation           ; vaidation
                MOV     r6, #1				; max RX stream
                MOV     r7, #1                          ; max TX stream
                SWI     XDeviceFS_Register
                STRVS   r0, [sp]
                EXIT    VS
		STR	r0, DeviceHandle

; show we are registered
		LDR	r0, PortFlags
		ORR	r0, r0, #flag_Registered
		STR	r0, PortFlags

		EXITS

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; unregister device
;
; in  : r11 port workspace pointer
;
unregister_device ENTRY "r0"
		CMP	r11, #0
		EXITS	EQ

; have we been registered
		LDR	r0, PortFlags
		TST	r0, #flag_Registered
		EXITS	EQ

                LDR     r0, DeviceHandle
                CMP     r0, #0
                EXITS	EQ
                SWI     XDeviceFS_Deregister
                MOV     r0, #0
                STR     r0, DeviceHandle

; show we are unregistered
		LDR	r0, PortFlags
		BIC	r0, r0, #flag_Registered
		STR	r0, PortFlags

		EXITS
; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; handle the command *SerialInfo
;
serial_command ENTRY "r0-r3"

                LDR     wp, [r12]   		; wp -> workspace

		ADRL	r0, device_name1
		LDR	r11, Port1
		BL	serial_command_info

		ADRL	r0, device_name2
		LDR	r11, Port2
		BL	serial_command_info

		EXITS

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; handle the command *SerialInfo
;
; in  : r0 port name
;       r11 port workspace
;
serial_command_info ENTRY "r1-r3"

		CMP	r11, #0
		EXITS	EQ
; write the port name
		SWI	XOS_Write0
		SWI	XOS_NewLine
; baud rate
		ADRL	r0, serial_baud_rate
		SWI	XOS_Write0
		LDR	r0, BaudRate
		BL	show_integer
		SWI	XOS_NewLine
; data format
		ADRL	r0, serial_data_format
		SWI	XOS_Write0
;   data length
		LDR	r1, DataFormat
		AND	r0, r1, #&ff
		BL	show_integer
		MOV	r0, #"."
		SWI	XOS_WriteC
;   start bits
		MOV	r0, r1, LSR #8
		AND	r0, r0, #&ff
		BL	show_integer
		MOV	r0, #"."
		SWI	XOS_WriteC
;   parity
		MOV	r1, r1, LSR #16
		AND	r1, r1, #&ff
		CMP	r1, #0
		MOVEQ	r0, #"n"
		CMP	r1, #1
		MOVEQ	r0, #"e"
		CMP	r1, #2
		MOVEQ	r0, #"o"
		SWI	XOS_WriteC
		SWI	XOS_NewLine

; handshaking
		ADRL	r0, serial_handshake
		SWI	XOS_Write0
		LDR	r1, PortFlags
		ADRL	r0, serial_handshake_none	; default
		TST	r1, #flag_UseRTS
		ADRNEL	r0, serial_handshake_rts
		TST	r1, #flag_UseXon
		ADRNEL	r0, serial_handshake_xon
		TST	r1, #flag_UseDTR
		ADRNEL	r0, serial_handshake_dtr
		SWI	XOS_Write0
		SWI	XOS_NewLine
 [ debug
; base address
		ADRL	r0, serial_base_address
		SWI	XOS_Write0
		LDR	r0, BaseAddress
		BL	show_hex8
		SWI	XOS_NewLine
; workspace
		ADRL	r0, serial_workspace
		SWI	XOS_Write0
		MOV	r0, r11
		BL	show_hex8
		SWI	XOS_NewLine
; flags
		ADRL	r0, serial_flags
		SWI	XOS_Write0
		LDR	r0, PortFlags
		BL	show_hex4
		SWI	XOS_NewLine
; control line
		BL	hardware_ctrl_lines
		ADRL	r0, serial_control_line
		SWI	XOS_Write0
		LDRB	r0, ControlLines
		BL	show_hex4
		SWI	XOS_NewLine
; input buffer
		ADRL	r0, serial_input_buffer
		SWI	XOS_Write0
		LDR	r0, InputBuffer
		BL	show_integer
		SWI	XOS_NewLine
; buffer size
		ADRL	r0, serial_ip_buffer_size
		SWI	XOS_Write0
		LDR	r0, InputBufferSize
		BL	show_integer
		SWI	XOS_NewLine
; buffer threshold
		ADRL	r0, serial_ip_buffer_thres
		SWI	XOS_Write0
		LDR	r0, InputBufferThreshold
		BL	show_integer
		SWI	XOS_NewLine
; output buffer
		ADRL	r0, serial_output_buffer
		SWI	XOS_Write0
		LDR	r0, OutputBuffer
		BL	show_integer
		SWI	XOS_NewLine
; buffer size
		ADRL	r0, serial_op_buffer_size
		SWI	XOS_Write0
		LDR	r0, OutputBufferSize
		BL	show_integer
		SWI	XOS_NewLine
; buffer threshold
		ADRL	r0, serial_op_buffer_thres
		SWI	XOS_Write0
		LDR	r0, OutputBufferThreshold
		BL	show_integer
		SWI	XOS_NewLine
; FIFOs present
		LDRB	r0, PortFlags
		TST	r0, #flag_FIFOsPresent
		ADRNEL	r0, serial_fifo_present
		ADREQL	r0, serial_fifo_not_present
		SWI	XOS_Write0
		SWI	XOS_NewLine
 [ counting
; error count
		ADRL	r0, serial_error_count
		SWI	XOS_Write0
		LDR	r0, ErrorCount
		BL	show_integer
		SWI	XOS_NewLine
 ; byte count
		ADRL	r0, serial_byte_count
		SWI	XOS_Write0
		LDR	r0, ByteCount
		BL	show_integer
		SWI	XOS_NewLine
; overrun count
		ADRL	r0, serial_overrrun_count
		SWI	XOS_Write0
		LDR	r0, OverrunCount
		BL	show_integer
		SWI	XOS_NewLine
; framing count
		ADRL	r0, serial_framing_count
		SWI	XOS_Write0
		LDR	r0, FramingCount
		BL	show_integer
		SWI	XOS_NewLine
; parity count
		ADRL	r0, serial_parity_count
		SWI	XOS_Write0
		LDR	r0, ParityCount
		BL	show_integer
		SWI	XOS_NewLine
; interrupt count
		ADRL	r0, serial_interrupt_count
		SWI	XOS_Write0
		LDR	r0, InterruptCount
		BL	show_integer
		SWI	XOS_NewLine
 ]
 ]
		EXITS

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; Writes an integer to the screen.
;
; in:
;    r0 - value to be displayed

show_integer	ENTRY "r1-r2"
        	SUB     r13, r13, #16
        	MOV     r1, r13
        	MOV     r2, #16
        	SWI     XOS_ConvertInteger4
        	SWIVC	OS_Write0
        	ADD     r13, r13, #16
        	EXITS

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; Routine to write a hex number to the screen
; in:
;    r0 - value to be displayed
 [ debug
show_hex8
        Push    "r1-r2, lr"
        SUB     r13, r13, #16
        MOV     r1, r13
        MOV     r2, #16
        SWI     XOS_ConvertHex8
        SWIVC	OS_Write0
        ADD     r13, r13, #16
        Pull    "r1-r2, pc",,^
show_hex4
        Push    "r1-r2, lr"
        SUB     r13, r13, #16
        MOV     r1, r13
        MOV     r2, #16
        SWI     XOS_ConvertHex4
        SWIVC	OS_Write0
        ADD     r13, r13, #16
        Pull    "r1-r2, pc",,^
 ]

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; Generalised internationalisation routines, these ensure that messages files
; are correctly opened and then return the relevant data.
;
; Attempt to open the messages file.

open_messages   ENTRY   "r0-r3"
                LDRB    r3, Flags
                TST     r3, #flag_HaveMessages
                EXITS   NE

                ADR     r0, MessagesWorkspace
                ADRL    r1, resource_file     ; -> path to be opened
                MOV     r2, #0                ; allocate some wacky space in RMA
                SWI     XMessageTrans_OpenFile
                LDRVCB  r3, Flags
                ORRVC   r3, r3, #flag_HaveMessages
                STRVCB  r3, Flags
                EXITS

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; Attempt to close the messages file.

close_messages  ENTRY   "r0"
                LDRB    r0, Flags
                TST     r0, #flag_HaveMessages
                EXITS   EQ

                ADR     r0, MessagesWorkspace
                SWI     XMessageTrans_CloseFile
                LDRVCB  r0, Flags
                BICVC   r0, r0, #flag_HaveMessages
                STRVCB  r0, Flags
                EXITS

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; Generate an error based on the error token given.  Does not assume that
; the messages file is open.  Will attempt to open it, then look it up.
; if make_error_with_name is called, the module name is substituted in r4

make_error_with_name ENTRY "r1-r7"
		ADRL	r4, title
		B	make_error_entry

make_error      ENTRY "r1-r7"
                MOV     r4, #0
make_error_entry
                LDRB    r1, Flags
                TST     r1, #flag_HaveMessages
                BLEQ    open_messages

                LDRB    r1, Flags
                TST     r1, #flag_HaveMessages
                PullEnv EQ
                ORREQS  pc, lr, #VFlag

                ADR     r1, MessagesWorkspace
                MOV     r2, #0
                MOV     r3, #0
                MOV     r5, #0
                MOV     r6, #0
                MOV     r7, #0
                SWI     XMessageTrans_ErrorLookup
                BL      close_messages

                EXIT                           ; return, r0 -> block, V set

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; This bit of appartently harmless code will bind a messages file into the code
; in the case of a standalone module. The macro ResourceFile will create the
; stuff and the label resource_file is used to point to the block required by
; ResourceFS

 [ standalonemessages
resource_file_block
	ResourceFile Resources.<Locale>.Messages, Resources.Serial.Messages
	DCD     0
 ]

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

		END
