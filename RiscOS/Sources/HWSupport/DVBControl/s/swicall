; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
;    Filename     : swicall
;    Author	  : R.W.Buckley
;    Company	  : Online Media
;    Date         : Dec 1995
;    Description  : This code should handle all the swis provided by this module.
;
; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
;
; Modification history
;
; 13-Feb-96  Rich  Reset FPGA copy of packet counter on DVBControl_OpenStream
;
;
; call: swi_match_mask
;
; in:	r0 - index value
;	r1 - match value
; 	r2 - mask value
; out:
;
swi_match_mask 	ENTRY "r0-r5"
		MOV	r0, r0, LSL #3		; multiply by 8
		LDR	r3, MatchMaskStart	; point to base of match masks
		STR	r1, [r3, r0]		; write match
		ADD	r3, r3, #4		; point to mask value
		STR	r2, [r3, r0]		; write mask

		EXIT

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; call: swi_match_mask_disable
;
; in: r0 - index value
;
; out:
;
swi_match_mask_disable 	ENTRY "r0-r2"
		MOV	r0, r0, LSL #3		; multiply by 8
		ADD	r0, r0, #4		; point to mask value
		LDR	r1, MatchMaskStart	; point to base of match masks
		MVN	r2, #0			; set mask to FFFF...
		STR	r2, [r1, r0]		; write mask
		SUB	r0, r0, #4		; point back to match value
		MOV	r2, #0			; set match to 0
		STR	r2, [r1, r0]		; write match
		EXIT

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; call: swi_start
;
; in: r0 : flags
;     r1 : MPEG control stream handle
;
; out:
;
swi_open_stream	ENTRY "r0-r1,r11"
		STR	r1, MPEGControlStream
                LDR     r11, FPGABaseAddress

		LDR	r0, PacketCounter	; set up the packet counters
		STR	r0, APacketCounter
		STR	r0, BPacketCounter
		SUB	r0, r0, #1
		STR	r0, DVB_a_counter
		STR	r0, DVB_b_counter

                MOV     r0, #1			; enable fpga processing
                STR     r0, DVB_processing
		MOV	r0, #0
		STR	r0, BankACount
		STR	r0, BankBCount
 [ dump_counting
		STR	r0, MPEGDescDump
		STR	r0, ReleaseCount
 ]

		EXIT

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; call: swi_stop
;
; in: r0 : flags
;     r1 : MPEG control stream handle
;
; out:
;
swi_close_stream ENTRY "r0-r1,r11"
                LDR     r11, FPGABaseAddress
                MOV     r0, #0			; disable fpga processing
                STR     r0, DVB_processing

		EXIT

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; call: swi_packet_counter
;
; in: r0 - Value to be used as the packet counter, -1 to read
;
; out: r0 - Actual packet counter
;
; Error returned if r0 too low.
;
swi_packet_counter ENTRY "r1"

		CMP	r0, #-1			; compare with -1
		LDREQ	r0, PacketCounter	; less than 0
		EXIT	EQ

		CMP	r0, #0			; compare with 0
		PullEnv	LE
		ADRLEL	r0, ErrorBlock_DVB_BadPacketCount
		DoError LE

		LDR	r1, =packet_counter	; cannot be greater than this
		CMP	r0, r1			; compare
		MOVGT	r0, r1			; if greater than, truncate

		STR	r0, PacketCounter	; stash away

		EXIT

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

		END
