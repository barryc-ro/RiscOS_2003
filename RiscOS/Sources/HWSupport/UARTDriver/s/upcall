; > upcall
;
; This code should handle all the requests from the upcall vector

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; call: handle_upcall
;
; in:  r0 - reason code
;      r1 - buffer handle
;      r2 - 0
;
; Handle trapping the UpCall vector as required.
;

handle_upcall   EntryS	"r0-r2, r11"

		LDR	lr, InputBufferHandle	    ; check input buffer
		TEQ	r1, lr
		TEQNE	r0, #UpCall_BufferFilling   ; 8, INSV thresholded
		TEQNE	r0, #UpCall_BufferEmptying  ; 9, REMV below threshold

                EXITS   NE

                LDR     r11, UARTBaseAddress        ; r11 -> UART base
                SETPSR  SVC_mode + I_bit,r2         ; disable IRQs
                LDR     r2, UARTDeviceFlags	    ; r2 = UART device flags

		TEQ	r0, #UpCall_BufferEmptying  ; moved below threshold
		BEQ	%10			    ;  deal with it below

;upcall threshold halt
                ORR     r2, r2, #1:SHL:SF_Thresholded

                TST     r2, #1:SHL:SF_XONXOFFOn      ; if doing XON/XOFF
                ORRNE   r2, r2, #1:SHL:SF_IXOFFedHim ; indicate XOFFing him
                STR     r2, UARTDeviceFlags	     ; store UART device flags

                MOVNE   lr, #XOFFChar
                STRNEB  lr, UARTXONXOFFChar          ; try to send an XOFF

		B	%20
10
;upcall threshold resume
                BIC     r2, r2, #1:SHL:SF_Thresholded

                TST     r2, #1:SHL:SF_XONXOFFOn
                BICNE   r2, r2, #1:SHL:SF_IXOFFedHim  ; clear my XOFF bit
                STR     r2, UARTDeviceFlags	      ; store UART device flags
                BEQ     %20

                TST     r2, #1:SHL:SF_UserXOFFedHim   ; if user not XOFFing him
                MOVEQ   lr, #XONChar                  ; then send an XON
                STREQB  lr, UARTXONXOFFChar	      ; store UART device flags

20
                BL      update_DTRRTSTXI

                PullEnv
                PullEnvS
                TEQ     pc, pc
                Pull    pc,NE,^                       ; 26-bit claim
                Pull    pc                            ; 32-bit claim

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

		END
