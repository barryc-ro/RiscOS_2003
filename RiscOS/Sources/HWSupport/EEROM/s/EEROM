; > s.EEROM

        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:Machine.<Machine>
        GET     Hdr:ImageSize.<ImageSize>
        $GetCPU
        $GetIO
        $GetMEMC
        $GetMEMM
        $GetVIDC

                    GBLL  med_00001_debug
med_00001_debug     SETL  {FALSE}

AssemblingArthur SETL  {TRUE}

                GBLL    Module
Module          SETL    {FALSE}

                GBLL   ChecksumCMOS
ChecksumCMOS    SETL  {TRUE}

                GBLL    CacheCMOSRAM                            ; Whether to keep a RAM copy of CMOS RAM for faster access
CacheCMOSRAM    SETL    MEMM_Type = "ARM600" :LAND: {TRUE}      ; (Space only allocated on ARM600 versions)

                GBLL    FullCMOSRAMCache        ; Whether to cache all CMOS RAM or only first 240 bytes.
FullCMOSRAMCache SETL   {FALSE}

		GBLL	ChecksumOnlyFirst256Bytes
ChecksumOnlyFirst256Bytes SETL {TRUE}		; since NCOS 1.06 only does that

		GBLL	InitialWritebackFromOSCache
InitialWritebackFromOSCache SETL {TRUE}		; on startup, read OS's copy of each location from 0 to &EF and write it properly

        GBLL    ProtectStationID        ; if TRUE, disallow OSBYTE &A2,0,n
ProtectStationID SETL {TRUE}

                GBLL    TestHarness
TestHarness     SETL    {TRUE}

        GET     Hdr:ModHand
        GET     Hdr:Proc
        GET     Hdr:CMOS
        GET     Hdr:PublicWS
;        GET     Hdr:KernelWS

;        ! 0, "NVRAMSize at ":CC::STR:(NVRamSize)

IOCControlSoftCopy * &106	; address of kernel's soft copy of IOC control register
				; (where IIC clock/data bits are)

        LEADR   Module_LoadAddr

TAB     *       9

; Module workspace allocation

        ^ 0, R12

 [ FullCMOSRAMCache
i2cWorkSpace    #       2032
 |
i2cWorkSpace    #       240
 ]
corruption_test #       4
NVSize          #       1
RTCFlag         #       1
NVBase          #       1
NVIniting	#	1	; NZ => inside InitCMOSCache, so don't bother updating checksum
MaxRetries	#	4	; maximum number of retries of IIC command due to latency after EEROM write

IIC_WorkspaceSize * :INDEX: @

; **************** Module code starts here **********************

Module_BaseAddr

        DCD     0
        DCD     IIC_Init    -Module_BaseAddr
        DCD     IIC_Die     -Module_BaseAddr
        DCD     IIC_Service -Module_BaseAddr
        DCD     IIC_Title   -Module_BaseAddr
        DCD     IIC_HelpStr -Module_BaseAddr
        DCD     IIC_HC_Table-Module_BaseAddr
        DCD     0
        DCD     0
        DCD     0
        DCD     0 ; Code to manually decode swi name (not needed)

IIC_Title
        DCB     "EEROM",0
IIC_HelpStr
        =       "EEROM"
        =       TAB, TAB
        =       "0.03 (02 May 1997)", 0
        ALIGN

IIC_HC_Table
	Command EEROMStats, 0, 0
	=	0

EEROMStats_Help
	=	"*EEROMStats prints out statistics about EEROM accesses.", 13
EEROMStats_Syntax
	=	"Syntax: *EEROMStats", 0
	ALIGN

; *****************************************************************************

IIC_Service * Module_BaseAddr

IIC_Init
        ENTRY

        LDR     r2, [r12]               ; Hard or soft init ?
        TEQ     r2, #0
        BNE     %FT00

; Hard init

        LDR     r3, =IIC_WorkspaceSize
        TEQ     r3, #0
        BEQ     %FT00
        MOV     r0, #ModHandReason_Claim
        SWI     XOS_Module
        LDMVSIA sp!, {pc}

        STR     r2, [r12]

00
        MOV     r12, r2

	ADR	r0, versiontestcli
	SWI	XOS_CLI
	BVS	%FT10
	ADR	r0, not106error
	SETV
	EXIT

10
        LDR     R0, =&5C5C5C5C
        STR     R0, corruption_test

	MOV	r0, #0
	STR	r0, MaxRetries		; zero count of maximum number of retries to EEROM

 [ CacheCMOSRAM
        BL      InitCMOSCache
 ]
        BL      ClaimByteV

        EXIT

versiontestcli
	=	"RMEnsure OSVersion 1.07", 0
	ALIGN
not106error
	&	1
	=	"EEROM module is only required on NC OS 1.06 or earlier", 0
	ALIGN

IIC_Die ENTRY
        TEQ     r12, #0
        LDRNE   r12, [r12]

        BL      ReleaseByteV

        EXITS

EEROMStats_Code Entry
	LDR	wp, [r12]
	SWI	XOS_WriteS
	=	"Maximum retries = ", 0
	ALIGN
	LDR	r0, MaxRetries
	BL	DecimalOutput
	SWI	XOS_NewLine
	EXIT

; **************************************************************************
;
;       DecimalOutput - Decimal output routine
;
; in:   r0 = value to be output
; out:  -

DecimalOutput Entry "r0-r2",16
        MOV     r1, sp
        MOV     r2, #16
        SWI     XOS_ConvertCardinal4
        SWI     XOS_Write0
        EXIT


; *****************************************************************************

; ByteV handling routines.

ClaimByteV
        ENTRY   "r1,r2"
        MOV     r0, #ByteV
        ADR     r1, ByteVHandler
        MOV     r2, r12
        SWI     XOS_Claim
        EXIT

ReleaseByteV
        ENTRY   "r1,r2"
        MOV     r0, #ByteV
        ADR     r1, ByteVHandler
        MOV     r2, r12
        SWI     XOS_Release
        EXIT

ByteVHandler
        Push    "r0,r1"
        TEQ     r0, #&A2
        BEQ     OsbyteA2
        TEQ     r0, #&A1
        Pull    "r0,r1",NE
        MOVNES  pc, lr
        ; If &A1 then drop through to...

; *****************************************************************************

; OS_Byte &A1 and &A2 handlers pulled from s.PMF.osbyte. We declare our own
; MyOsbyte macro to exit our handler by claiming the ByteV call.

        MACRO
        MyOsbyte $cond
        Pull    "r0,r1,pc",$cond,^
        MEND

; Read CMOS RAM
OsbyteA1    ; R1 = address , R2 = result
        CLRPSR  I_bit, R0       ; this may take some time
        MOV     R0, R1
        BL      Read            ; Read CMOS ram at address <R0>
        MOV     R2, R0          ; Result in R0, return in R2
        MyOsbyte

; Write CMOS RAM
OsbyteA2
        CLRPSR  I_bit, R0       ; this may take some time
        [ E2ROMSupport
        MOVS	R0, R1
        |
        ANDS    R0, R1, #&FF    ; only look at bottom byte
        ]
        [ ProtectStationID
        MyOsbyte EQ
        ]

        ; This bit is conditioned out to make life easier...
        ;
        [ {FALSE}
        ; Protect machine address CMOS (if not corrupt)
        ASSERT  EtherCheckCMOS = EtherAddrCMOS+6
        CMP     r0, #EtherAddrCMOS
        BLT     %FT10
        CMP     r0, #EtherCheckCMOS
        BGT     %FT10
        Push    "r0,r1"
        BL      GetMachineAddressCMOS
        Pull    "r0,r1"
        MyOsbyte EQ             ; don't allow write if address is valid
10
        ]

        MOV     R1, R2
        BL      Write
        MOV     R1, R0          ; R1 is supposed to be preserved
        MyOsbyte

; *****************************************************************************

; Include the i2cutils source file - similar to the Kernel s.PMF.i2cutils

        GET     s.i2cutils

        END
