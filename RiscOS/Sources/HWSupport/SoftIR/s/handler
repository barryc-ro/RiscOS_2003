; > handler.s
; interrupt handlers

	GET     hdr:ListOpts
	GET     hdr:Macros
	GET     hdr:System
	GET	hdr:targetsys



	EXPORT fiq_handler_code
	EXPORT fiq_installer

	AREA	|fiqhandler$$code|,CODE,READONLY

	;; fiq_installer copys the fiq_handler_code to zero page
	; void fiq_installer(unsigned int buffer address)
fiq_installer
	; need to disable interrupts
	mrs r3, CPSR
	orr r1, r3, #F32_bit+I32_bit
	msr CPSR_c, r1
	bic r1, r1, #M32_bits
	orr r1, r1, #FIQ32_mode
	msr CPSR_c, r1

	mov r2,#&1c

	; now copy data to fiq vector
	str r0,[r2, #data_adr - fiq_handler_code]
	mov r8,r0		; currently in FIQ mode so preset r8 to be data block

	adr r0,fiq_handler_code ;
	adr r1,fiq_handler_init_end

fiq_inst_loop
	ldr r14,[r0],#4		; get instruction from handler code
	str r14,[r2],#4		; store at fiq offset
	cmp r0,r1
	blo fiq_inst_loop

	; set up FIQ registers (for example)

					; r8=data block, initialised above
;	mov r11,#0			; r11=flag,  initialise to zero

	mov r12,#0			; initialise flag

	[ TVLinkLazarus
          mov r13,#&f9000000
          add r13,r13,#&009f0000
          add r13,r13,#&00008000	; APB_BASE
	] ; TVLinkLazarus

	[ Base26Morris4
	  mov r13,#&03200000		; IOMD base
	] ; Base26Morris4

	[ MarthaMartha
	  mov r13,#&xxxxxxxx		; a base point
	]


	; restore interrupt flags and mode to previous state
	msr CPSR_c, r3

	; note, this r12 isn't the above r12
	mov r12, r14


	mov r0, #1
	mov r1, #&1c
	; r2 is set up from above
	swi XOS_SynchroniseCodeAreas	; corrupts r14, remember


      	[ {CONFIG}=26

	movs pc,r12

      	|

        mov pc,r12

	]


; Actual FIQ handling code below

; ****************************************************************************
; *                               TViLINK Version                            *
; ****************************************************************************
	[ TVLinkLazarus
	GET tvilink.s
	] ; TVLinkLazarus
; ****************************************************************************
; *                               RPC Version                                *
; ****************************************************************************
	[ Base26Morris4
	GET rpc.s
	] ; Base26Morris4
; ****************************************************************************
; *                               Martha Version                             *
; ****************************************************************************
	[ MarthaMartha
	GET martha.s
	] ; MarthaMartha

; End of system dependant fiq handler, common stuff below

flag
	DCD 0
fiq_handler_init_end

data_adr
	DCD 0

fiq_handler_end


	END

