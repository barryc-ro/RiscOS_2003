; > Module
; Source for NHTwitter Module

; ***********************************
; ***    C h a n g e   L i s t    ***
; ***********************************

; Date       Who  Version       Description
; ----       ---  -------       -----------
; 31-Oct-95  TMD  0.01          Started work
; 01-Nov-95  TMD  0.02		Version that adjusts for different linelengths
; 03-Nov-95  TMD  0.03		Fix screen corruption bugs
; 14-Nov-95  TMD  0.04		Re-read screen vars on Service_SwitchingOutputToSprite, as well as mode change
; 14-Nov-95  TMD  0.05		Add SWI call as well as star command
; 14-Nov-95  TMD  0.06		Fix incorrect restoration of r0 on errors in star command.
; 19-Jan-96  TMD  0.07		Better sanity checking on coordinates.
; 05-Aug-99  KJB  0.08		Added service call table.
; 16-Feb-00  KJB  0.09		Cunning stunt for modes with "1MB+half a line" LineLength - stick to the first
;				two copies to save thrashing the MMU's TLB. Gives a 15% speed up.
; 24-Feb-00  KJB  0.10          Supports supremacy bit - transparent areas will not be touched, and solid
;                               areas will not blend with them.
;                               Version number wasn't reported correctly.
;                               Made 32-bit compatible.
;                               Added OS_SynchroniseCodeAreas for dynamic code.
;                               Issues OS_Remove/RestoreCursors around screen update.
;                               Updates OS_ChangedBox.
;                               Errors now have sensible numbers, rather than zero.

        AREA    |NHTwitter$$Code|, CODE, READONLY, PIC

TAB     *       9
LF      *       10
FF      *       12
CR      *       13

        	^       0, wp
TwitterOK	#	4	; 0 => can't anti-twitter in this mode
				; 1 => OK
OutVarTable	#	0
VScreenStart	#	4
VLineLen	#	4
VXEig		#	4
VYEig		#	4
VXWindLimit	#	4
VYWindLimit	#	4
VLog2BPC	#	4

ChangedBox      #       4       ; address of OS_ChangedBox block

VolatileCodeBlock #	0

Module_WorkspaceSize * (:INDEX: @) + (CodeBlockEnd - CodeBlockStart)

; **************** Module code starts here **********************

Module_BaseAddr

        &       0                               ; no app start entry
        &       Mod_Init    -Module_BaseAddr
        &       Mod_Die	    -Module_BaseAddr
        &       Mod_Service -Module_BaseAddr
        &       Mod_Title   -Module_BaseAddr
        &       Mod_HelpStr -Module_BaseAddr
        &       Mod_HC_Table-Module_BaseAddr
        &       NHSWI_Base
        &       Mod_SWIHandler-Module_BaseAddr
        &       Mod_SWITable-Module_BaseAddr
        &       0                               ; no swi name decode entry
        &       0                               ; no internationalised help
        &       Mod_Flags   -Module_BaseAddr

Mod_Title
        =       "NHTwitter", 0

Mod_HelpStr
        =       "NHTwitter", TAB, "$Module_HelpVersion"
 [ Supremacy
        =       " Supremacy"
 ]
	= 0
        ALIGN

Mod_SWITable
	=	"NH", 0
	=	"AntiTwitter", 0
	=	0
	ALIGN

Mod_Flags
        =       1       ; 32-bit compatible

Mod_HC_Table
        Command NHAntiTwitter, 4, 4
        =       0

NHAntiTwitter_Help
	=	"*NHAntiTwitter performs an anti-twitter filter on part of the screen.", 13
NHAntiTwitter_Syntax
        =       "Syntax: *NHAntiTwitter <x> <y> <w> <h>", 0
        ALIGN

	MACRO
	GetInt	$reg, $index, $t1, $t2
	LDR	$t1, [sp, $index]
	LDRB	$reg, [$t1, #1]!
	LDRB	$t2, [$t1, #1]!
	ORR	$reg, $reg, $t2, LSL #8
	LDRB	$t2, [$t1, #1]!
	ORR	$reg, $reg, $t2, LSL #16
	LDRB	$t2, [$t1, #1]!
	ORR	$reg, $reg, $t2, LSL #24
	MEND

Mod_Init Entry
        LDR     r2, [R12]               ; have we got workspace yet ?
        TEQ     r2, #0
        BNE     %FT05

        MOV     r0, #ModHandReason_Claim
        LDR     r3, =Module_WorkspaceSize
        SWI     XOS_Module
        EXIT    VS

; r2 -> workspace

        STR     r2, [r12]               ; save address in my workspace pointer,
                                        ; so Tutu can free it for me when I die
05
        MOV     r12, r2

	ADRL	r0, CodeBlockStart
	ADR	r1, VolatileCodeBlock
	LDR	r2, =CodeBlockEnd-CodeBlockStart
10
	LDR	r3, [r0], #4
	STR	r3, [r1], #4
	SUBS	r2, r2, #4
	BNE	%BT10

        MOV     r0, #-1
        SWI     XOS_ChangedBox
        EXIT    VS
        STR     r1, ChangedBox

	BL	GetScreenVars
	EXIT

Mod_Die Entry
	LDR	wp, [r12]
	CLRV
	EXIT				; for now

	ASSERT	Service_ModeChange < Service_SwitchingOutputToSprite
Mod_ServiceTable
	DCD	0
	DCD	Mod_ServiceEntry - Module_BaseAddr
	DCD	Service_ModeChange
	DCD	Service_SwitchingOutputToSprite
	DCD	0

	DCD	Mod_ServiceTable - Module_BaseAddr
Mod_Service ROUT
	MOV	r0, r0
	TEQ	r1, #Service_ModeChange
	TEQNE	r1, #Service_SwitchingOutputToSprite
	MOVNE	pc, lr
Mod_ServiceEntry
	LDR	wp, [r12]

	TEQ	r1, #Service_SwitchingOutputToSprite
	BEQ	MyCallBack		; read vars directly, not on callback

; On Service_ModeChange, we have to re-read vars on a callback from the service, not directly
; on the service, so that the Interlace module has finished tinkering with the vars by then.


	Push	"r0,r1, lr"
	ADR	r0, MyCallBack
	MOV	r1, wp
	SWI	XOS_AddCallBack
	Pull	"r0,r1, pc"

MyCallBack
	Push	"r0-r6, lr"
	BL	GetScreenVars
	Pull	"r0-r6, pc"

; **************************************************************************
;
;       Mod_SWIHandler - Main entry point for SWIs
;
; in:   r11 = SWI number
;

Mod_SWIHandler ROUT
        LDR     wp, [r12]
        CMP     r11, #1
        ADDCC   pc, pc, r11, LSL #2
        B       NoSuchSWI
        B       AntiTwitterSWI

NoSuchSWI
        ADRL    r0, NoSuchSWIError
        SETV
        MOV     pc, lr

NoSuchSWIError
        &       ErrorNumber_ModuleBadSWI
        =       "No such NHTwitter SWI", 0
        ALIGN

; **************************************************************************

GetScreenVars Entry
	ADR	r0, InVarTable
	ADR	r1, OutVarTable
	SWI	XOS_ReadVduVariables

	LDR	r0, VLog2BPC
	TEQ	r0, #4
	MOVNE	r0, #0
	BNE	%FT90			; not in 16bpp, so don't even try it

	LDR	r0, VLineLen
	BL	ConvertToImmediates	; out: r1, r2 = immediate fields

	TEQ	r0, #0			; if remainder after two immediates is non-zero
	MOVNE	r0, #0			; then we've failed
	BNE	%FT90

	ADR	r0, CodePatchTable
	ADR	r3, VolatileCodeBlock
10
	LDR	lr, [r0], #4
	CMP	lr, #-1
	BEQ	%FT80
	LDR	r4, [lr, r3]!
	MOV	r4, r4, LSR #12
	ORR	r4, r1, r4, LSL #12
	STR	r4, [lr]
	LDR	r4, [lr, #4]!
	MOV	r4, r4, LSR #12
	ORR	r4, r2, r4, LSL #12
	TEQ	r2, #&940		; is it &100000? if so, we're doing funny interlace
	BIC	r4, r4, #&01E00000	; clear opcode
	ORREQ	r4, r4, #&00200000	; make it EORcc Rx,Rx,#&100000 (saves MMU TLB thrash)
	ORRNE	r4, r4, #&00800000	; make it ADDcc Rx,Rx,#xx
	STR	r4, [lr]
	B	%BT10
80

        MOV     r0, #1
        ADR     r1, VolatileCodeBlock
        LDR     r2, =CodeBlockEnd-CodeBlockStart-4
        ADD     r2, r1, r2
        SWI     XOS_SynchroniseCodeAreas

	MOV	r0, #1			; indicate OK
90
	STR	r0, TwitterOK
	CLRV
	EXIT

CodePatchTable
	&	codepatch1a - CodeBlockStart
	&	codepatch1b - CodeBlockStart
	&	codepatch1c - CodeBlockStart
	&	codepatch2_0 - CodeBlockStart
	&	codepatch2_1 - CodeBlockStart
	&	codepatch2_2 - CodeBlockStart
	&	codepatch2_3 - CodeBlockStart
	&	codepatch2_4 - CodeBlockStart
	&	codepatch3_0 - CodeBlockStart
	&	codepatch3_1 - CodeBlockStart
	&	codepatch3_2 - CodeBlockStart
	&	codepatch3_3 - CodeBlockStart
	&	codepatch3_4 - CodeBlockStart
 [ Supremacy
	&	codepatch4_0 - CodeBlockStart
	&	codepatch4_1 - CodeBlockStart
	&	codepatch4_2 - CodeBlockStart
	&	codepatch5_0 - CodeBlockStart
	&	codepatch5_1 - CodeBlockStart
	&	codepatch5_2 - CodeBlockStart
	&	codepatch6_0 - CodeBlockStart
	&	codepatch6_1 - CodeBlockStart
	&	codepatch6_2 - CodeBlockStart
 ]
	&	-1

; in:	r0 = value to convert to two immediate values
; out:	r1, r2 are two 12-bit fields to substitute in bits 0..11
;	of instruction
;	r0 = remainder - should be zero

ConvertToImmediates Entry
	BL	GetImmediate		; out: r0 = remainder, r1 = bit field
	MOV	r2, r1
	BL	GetImmediate
	EXIT

GetImmediate Entry
	MOV	r1, #0			; number of rotates if zero
	MOVS	lr, r0			; if initial value zero
	EXIT	EQ			; there's nothing to do (imm field = 0)
	MOV	r1, #8
10
	TST	lr, #&C0000000		; if top two bits clear
	MOVEQ	lr, lr, LSL #2		; then shift up
	ADDEQ	r1, r1, #2		; and add two extra rotates
	BEQ	%BT10

	AND	r1, r1, #30		; ignore any wrap
	MOV	lr, lr, LSR #24		; move 8-bit field down to bottom
	BIC	r0, r0, lr, ROR r1	; knock out the field we just made
	ORR	r1, lr, r1, LSL #7	; merge 8-bit field with 4-bit rotate amount
	EXIT

NHAntiTwitter_Code Entry "r0-r3"
	LDR	wp, [r12]

; r0 -> command line

	MOV	r1, r0
	ADR	r0, Keys
	SUB	sp, sp, #128
	MOV	r2, sp
	MOV	r3, #128
	SWI	XOS_ReadArgs
	BVS	%FT90

	GetInt	r0, #0, r4,r5
	GetInt	r1, #4, r4,r5
	GetInt	r2, #8, r4,r5
	GetInt	r3, #12, r4,r5

	ADD	sp, sp, #128

	BL	AntiTwitterSWI
90
	STRVS	r0, [sp]
	EXIT

AntiTwitterSWI Entry "r0-r7"
	LDR	lr, TwitterOK
	TEQ	lr, #0
	BEQ	%FT99			; can't filter in this mode

	LDR	lr, VXWindLimit

	LDR	r1, VXEig
	MOVS	r4, r0, ASR r1		; r4 = XC >> XEIG
	MOVMI	r4, #0

	CMP	r4, lr
	BGT	%FT95			; left X off RHS of screen
;	BIC	r4, r4, #3		; r4 = X0

	ADD	r2, r2, r0		; r2 = WC + XC
	MOV	r5, r2, ASR r1
	SUBS	r5, r5, #1
	BMI	%FT95			; right X off LHS of screen

	CMP	r5, lr			; if right X off RHS of screen
	MOVGT	r5, lr			; move to RHS of screen
;	ORR	r5, r5, #3
	SUB	r5, r5, r4
	ADDS	r5, r5, #1		; r5 = W
	BLE	%FT95			; width <= 0

	LDR	r0, [sp, #1*4]		; r0 = YC
	LDR	r1, VYEig
	LDR	r2, [sp, #3*4]		; r2 = HC
	LDR	r3, VYWindLimit
	SUBS	r6, r3, r0, ASR r1
	BMI	%FT95			; if bot Y off top of screen
	CMP	r6, r3
	MOVGT	r6, r3			; r6 = Y0

	ADD	r0, r0, r2
	SUB	r7, r3, r0, ASR r1
	ADDS	r7, r7, #1		; top Y
	MOVMI	r7, #0			; r7 = Y1
	CMP	r7, r3			; if top Y off bot of screen
	BGT	%FT95
	SUBS	r6, r6, r7		; r6 = bottom-top (inclusive)
	BLT	%FT95			; top below bottom

        LDR     r0, ChangedBox
        LDR     r14, [r0]
        TST     r14, #1                 ; ChangedBox enabled?
        BLNE    UpdateChangedBox

20      SWI     XOS_RemoveCursors

	LDR	r0, VScreenStart
	ADD	r0, r0, r4, LSL #1
	LDR	r3, VLineLen
	MLA	r0, r3, r7, r0
	MOV	r1, r0
	ADD	r2, r6, #1		; r2 = H
	MOV	r3, r5

 [ {FALSE}
	DLINE	"About to call volatile code"
	DREG	r0, "Screen address = "
	DREG	r3, "Columns = "
	DREG	r2, "Rows = "
 |
	MOV	lr, pc
	ADR	pc, VolatileCodeBlock
 ]

        SWI     XOS_RestoreCursors

	CLRV
	EXIT

95
	PullEnv
	ADR	r0, BadCoordsError
	SETV
	MOV	pc, lr

99
	PullEnv
	ADR	r0, TwitterFailError
	SETV
	MOV	pc, lr

BadCoordsError
	&	ErrorNumber_BadParameters
	=	"Invalid coordinates", 0
	ALIGN

TwitterFailError
	&	ErrorNumber_BadPixelDepth
	=	"Can't perform anti-twitter in this mode", 0
	ALIGN

InVarTable
	&	VduExt_ScreenStart
	&	VduExt_LineLength
	&	VduExt_XEigFactor
	&	VduExt_YEigFactor
	&	VduExt_XWindLimit
	&	VduExt_YWindLimit
	&	VduExt_Log2BPC
	&	-1


Keys
	=	"/E,/E,/E,/E", 0
	ALIGN

lbpp		*	4
fl_mask1    	*	2_01111100000111110000001111100000
fl_mask2	*	2_00000011111000000111110000011111

	MACRO
	FNdo2words $w00,$w10,$w01,$w11,$w02,$w12,$w0d,$w1d,$temp,$gm,$gm2,$entry
fl_entry$entry
	LDMIA	r0, {$w02,$w12}
 [ Supremacy
	ORR	$temp, $w00, $w01		; check for any supremacy bits set
	ORR	$temp, $temp, $w02
	ORR	$temp, $temp, $w10
	ORR	$temp, $temp, $w11
	ORR	$temp, $temp, $w12
 ]
 [ TestWhite
        MOV     $w0d, #-1
        MOV     $w1d, #-1
 ]
	STMCSIA	r1, {$w0d,$w1d}

codepatch2_$entry
	ADDCS	r1, r1, #0			; addcs dstlen
	ADDCS	r1, r1, #0
 [ Supremacy
	ORRS	$temp, $temp, $temp, ROR #16
	BMI	transparency_$entry		; if supremacy bit set in any of 12 pixels
 ]
10	SUBS	r2, r2, #1
codepatch3_$entry
	ADDNE	r0, r0, #0			; addne srclen
	ADDNE	r0, r0, #0			; (advance src ptr except on last time but one - don't care on last time)

	AND	$w0d, $w00, $gm			; B_R_G_ word 0 line 0
	AND	$temp, $w02, $gm		; B_R_G_ word 0 line 2
	ADD	$w0d, $w0d, $temp		; B_R_G_ word 0 line 0+2
	AND	$temp, $w01, $gm		; B_R_G_ word 0 line 1
	ADD	$w0d, $temp, $w0d, LSR #1	; B_R_G_ word 0 line 0/2 + 1 + 2/2
	ORR	$w0d, $gm2, $w0d, LSR #1	; BFRFGF word 0 line 0/4 + 1/2 + 2/4

	BIC	$w00, $w00, $gm			; _G_B_R word 0 line 0
	BIC	$temp, $w02, $gm		; _G_B_R word 0 line 2
	ADD	$w00, $w00, $temp		; _G_B_R word 0 line 0+2
	BIC	$temp, $w01, $gm		; _G_B_R word 0 line 1
	ADD	$w00, $temp, $w00, LSR #1	; _G_B_R word 0 line 0/2 + 1 + 2/2
	ORR 	$w00, $gm, $w00, LSR #1		; FGFBFR word 0 line 0/4 + 1/2 + 2/4

	AND	$w0d, $w0d, $w00		; BGRBGR word 0 line 0/4 + 1/2 + 2/4

	AND	$w1d, $w10, $gm			; B_R_G_ word 1 line 0
	AND	$temp, $w12, $gm		; B_R_G_ word 1 line 2
	ADD	$w1d, $w1d, $temp		; B_R_G_ word 1 line 0+2
	AND	$temp, $w11, $gm		; B_R_G_ word 1 line 1
	ADD	$w1d, $temp, $w1d, LSR #1	; B_R_G_ word 1 line 0/2 + 1 + 2/2
	ORR	$w1d, $gm2, $w1d, LSR #1	; BFRFGF word 1 line 0/4 + 1/2 + 2/4

	BIC	$w10, $w10, $gm			; _G_B_R word 1 line 0
	BIC	$temp, $w12, $gm		; _G_B_R word 1 line 2
	ADD	$w10, $w10, $temp		; _G_B_R word 1 line 0+2
	BIC	$temp, $w11, $gm		; _G_B_R word 1 line 1
	ADD	$w10, $temp, $w10, LSR #1	; _G_B_R word 1 line 0/2 + 1 + 2/2
	ORR	$w10, $gm, $w10, LSR #1		; FGFBFR word 1 line 0/4 + 1/2 + 2/4

	AND	$w1d, $w1d, $w10		; BGRBGR word 1 line 0/4 + 1/2 + 2/4

 [ TestWhite
        MOV     $w0d, #-1
        MOV     $w1d, #-1
 ]
 [ Supremacy
	BCS	%FT99
	B	exitcolumn

transparency_$entry
; we know at least one pixel is transparent
	ORR	$temp, $w01, $w11
	TST	$temp, $temp, ROR #16
	BPL	%FT70
; fast case first - both words of line 1 totally transparent
	SUBS	r2, r2, #1
codepatch4_$entry
	ADDNE	r0, r0, #0
	ADDNE	r0, r0, #0
	BCC	exitcolumn2
codepatch5_$entry
	ADD	r1, r1, #0
	ADDS	r1, r1, #0	; clears carry, so no store (or increment) next time
	B	%FT99

; and the hard case
70
	FNdo1wordtrans $w00,$w01,$w02,$w0d,$temp,$gm,$gm2
	FNdo1wordtrans $w10,$w11,$w12,$w1d,$temp,$gm,$gm2
	SUBS	r2, r2, #1
codepatch6_$entry
	ADDNE	r0, r0, #0
	ADDNE	r0, r0, #0
 ]
 [ TestWhite
        MOV     $w0d, #-1
        MOV     $w1d, #-1
 ]
	BCC	exitcolumn
99
	MEND

 [ Supremacy
	MACRO
	FNdo1wordtrans $w00,$w01,$w02,$w0d,$temp,$gm,$gm2
	Push	"$w02"
	MOV	$temp, $w01, LSL #16
	MOV	$temp, $temp, LSR #16		; temp = centre left
	TST	$w00, #1:SHL:15			; top left clear?
	MOVNE	$w00, $w00, LSR #16		; make same as centre left
	ORRNE	$w00, $temp, $w00, LSL #16
	TST	$w02, #1:SHL:15			; bottom left clear?
	MOVNE	$w02, $w02, LSR #16		; make same as centre left
	ORRNE	$w02, $temp, $w02, LSL #16
	MOV	$temp, $w01, LSR #16
	MOV	$temp, $temp, LSL #16		; temp = centre right
	TST	$w00, #1:SHL:31			; top right clear?
	MOVNE	$w00, $w00, LSL #16		; make same as centre right
	ORRNE	$w00, $temp, $w00, LSR #16
	TST	$w02, #1:SHL:31			; bottom right clear?
	MOVNE	$w02, $w02, LSL #16		; make same as centre right
	ORRNE	$w02, $temp, $w02, LSR #16

	AND	$w0d, $w00, $gm			; B_R_G_ word 0 line 0
	AND	$temp, $w02, $gm		; B_R_G_ word 0 line 2
	ADD	$w0d, $w0d, $temp		; B_R_G_ word 0 line 0+2
	AND	$temp, $w01, $gm		; B_R_G_ word 0 line 1
	ADD	$w0d, $temp, $w0d, LSR #1	; B_R_G_ word 0 line 0/2 + 1 + 2/2
	ORR	$w0d, $gm2, $w0d, LSR #1	; BFRFGF word 0 line 0/4 + 1/2 + 2/4

	BIC	$w00, $w00, $gm			; _G_B_R word 0 line 0
	BIC	$temp, $w02, $gm		; _G_B_R word 0 line 2
	ADD	$w00, $w00, $temp		; _G_B_R word 0 line 0+2
	BIC	$temp, $w01, $gm		; _G_B_R word 0 line 1
	ADD	$w00, $temp, $w00, LSR #1	; _G_B_R word 0 line 0/2 + 1 + 2/2
	ORR 	$w00, $gm, $w00, LSR #1		; FGFBFR word 0 line 0/4 + 1/2 + 2/4

	AND	$w0d, $w0d, $w00		; BGRBGR word 0 line 0/4 + 1/2 + 2/4

	TST	$w01, #1:SHL:15			; centre left clear?
	MOVNE	$w0d, $w0d, LSR #16		; make destination left = centre left
	MOVNE	$temp, $w01, LSL #16
	MOVNE	$temp, $temp, LSR #16
	ORRNE	$w0d, $temp, $w0d, LSL #16
	TST	$w01, #1:SHL:31			; centre right clear?
	MOVNE	$w0d, $w0d, LSL #16		; make destination right = centre left
	MOVNE	$temp, $w01, LSR #16
	MOVNE	$temp, $temp, LSL #16
	ORRNE	$w0d, $temp, $w0d, LSR #16
	Pull	"$w02"
	MEND
 ]

	MACRO
	FNdo1word $w00,$w01,$w02,$w0d,$temp,$gm,$gm2,$entry
fl_entry$entry
	LDR	$w02, [r0]
 [ Supremacy
	ORR	$temp, $w00, $w01		; check for any supremacy bits set
	ORR	$temp, $temp, $w02
 ]
	STRCS	$w0d, [r1]
codepatch2_$entry
	ADDCS	r1, r1, #0			; addcs dstlen
	ADDCS	r1, r1, #0
 [ Supremacy
	ORRS	$temp, $temp, $temp, ROR #16
	BMI	transparency1_$entry		; if supremacy bit set in any of 12 pixels
 ]

	AND	$w0d, $w00, $gm			; B_R_G_ word 0 line 0
	AND	$temp, $w02, $gm		; B_R_G_ word 0 line 2
	ADD	$w0d, $w0d, $temp		; B_R_G_ word 0 line 0+2
	AND	$temp, $w01, $gm		; B_R_G_ word 0 line 1
	ADD	$w0d, $temp, $w0d, LSR #1	; B_R_G_ word 0 line 0/2 + 1 + 2/2
	ORR	$w0d, $gm2, $w0d, LSR #1	; BFRFGF word 0 line 0/4 + 1/2 + 2/4

	BIC	$w00, $w00, $gm			; _G_B_R word 0 line 0
	BIC	$temp, $w02, $gm		; _G_B_R word 0 line 2
	ADD	$w00, $w00, $temp		; _G_B_R word 0 line 0+2
	BIC	$temp, $w01, $gm		; _G_B_R word 0 line 1
	ADD	$w00, $temp, $w00, LSR #1	; _G_B_R word 0 line 0/2 + 1 + 2/2
	ORR 	$w00, $gm, $w00, LSR #1		; FGFBFR word 0 line 0/4 + 1/2 + 2/4

	AND	$w0d, $w0d, $w00		; BGRBGR word 0 line 0/4 + 1/2 + 2/4
 [ Supremacy
	B	%FT99

transparency1_$entry
; we know at least one pixel is transparent
	TST	$w01, $w01, ROR #16
; fast case first - both pixels of line 1 totally transparent
	MOVMI	$w0d, $w01
	BMI	%FT99

; and the hard case
	FNdo1wordtrans $w00,$w01,$w02,$w0d,$temp,$gm,$gm2
99
 ]
	SUBS	r2, r2, #1
codepatch3_$entry
	ADDNE	r0, r0, #0			; addne srclen
	ADDNE	r0, r0, #0			; (advance src ptr except on last time but one - don't care on last time)

 [ TestWhite
        MOV     $w0d, #-1
 ]
	MEND

	MACRO
	FNdohalfword $w00,$w01,$w02,$w0d,$temp,$gm,$gm2,$entry
fl_entry$entry
	LDR	$w02, [r0]			; NB may not be word-aligned
 [ Supremacy
	ORR	$temp, $w00, $w01		; check for any supremacy bits set
	ORR	$temp, $temp, $w02
 ]
	STRCSB	$w0d, [r1]
	MOVCS	$w0d, $w0d, LSR #8
	STRCSB	$w0d, [r1, #1]
codepatch2_$entry
	ADDCS	r1, r1, #0			; addcs dstlen
	ADDCS	r1, r1, #0
 [ Supremacy
	TST	$temp, #1:SHL:15
	BNE	transparencyh_$entry
 ]

	AND	$w0d, $w00, $gm			; B_R_G_ word 0 line 0
	AND	$temp, $w02, $gm		; B_R_G_ word 0 line 2
	ADD	$w0d, $w0d, $temp		; B_R_G_ word 0 line 0+2
	AND	$temp, $w01, $gm		; B_R_G_ word 0 line 1
	ADD	$w0d, $temp, $w0d, LSR #1	; B_R_G_ word 0 line 0/2 + 1 + 2/2
	ORR	$w0d, $gm2, $w0d, LSR #1	; BFRFGF word 0 line 0/4 + 1/2 + 2/4

	BIC	$w00, $w00, $gm			; _G_B_R word 0 line 0
	BIC	$temp, $w02, $gm		; _G_B_R word 0 line 2
	ADD	$w00, $w00, $temp		; _G_B_R word 0 line 0+2
	BIC	$temp, $w01, $gm		; _G_B_R word 0 line 1
	ADD	$w00, $temp, $w00, LSR #1	; _G_B_R word 0 line 0/2 + 1 + 2/2
	ORR 	$w00, $gm, $w00, LSR #1		; FGFBFR word 0 line 0/4 + 1/2 + 2/4

	AND	$w0d, $w0d, $w00		; BGRBGR word 0 line 0/4 + 1/2 + 2/4
 [ Supremacy
	B	%FT99

transparencyh_$entry
; we know at least one pixel is transparent
	TST	$w01, #1:SHL:16
; fast case first - line 1 transparent
	MOVNE	$w0d, $w01
	BNE	%FT99

; and the hard case
	FNdo1wordtrans $w00,$w01,$w02,$w0d,$temp,$gm,$gm2
99
 ]
	SUBS	r2, r2, #1
codepatch3_$entry
	ADDNE	r0, r0, #0			; addne srclen
	ADDNE	r0, r0, #0			; (advance src ptr except on last time but one - don't care on last time)
 [ TestWhite
        MOV     $w0d, #-1
 ]

	MEND

CodeBlockStart

; in -  r0 -> src bitmap
;       r1 -> dst bitmap
;       r2 = number of src lines
;	r3 = number of src columns

AntiTwitter Entry "r8-r12"
	LDR	r12, flmask			; inverted mask
	MOV	r5, r3, LSL #(lbpp-3)
	SUBS	r2, r2, #1
	EXIT	LS			; < 2 lines, so nothing to do

	TST	r0, #2
	BLNE	DoHalfWordColumn

	MOV	r14, r12, ROR #16		; get normal mask
fl_startofcolumn
	SUBS	r5, r5, #8
	BCC	DoOddWord
	STMFD	r13!, {r0-r2,r5}
	LDMIA	r0, {r3,r4}
	MOV	r5, r3
	MOV	r6, r4
codepatch1a
	ADD	r0, r0, #0		; add srclen
	ADDS	r0, r0, #0			; clear carry, so store doesn't happen 1st time thru
fl_columnloop
	FNdo2words r3,r4,r5,r6,r7,r8,r9,r10,r11,r14,r12,0
	FNdo2words r5,r6,r7,r8,r3,r4,r9,r10,r11,r14,r12,1
	FNdo2words r7,r8,r3,r4,r5,r6,r9,r10,r11,r14,r12,2
	B	fl_columnloop

exitcolumn
	STMIA	r1, {r9,r10}
exitcolumn2
	LDMFD	r13!, {r0-r2,r5}
	ADD	r0, r0, #8
	ADD	r1, r1, #8
	B	fl_startofcolumn

DoOddWord
	ADD	r5, r5, #8

	TST	r5, #4
	BLNE	DoOneWordColumn
	TST	r5, #2
	BLNE	DoHalfWordColumn

	EXIT

flmask	&	fl_mask2

DoHalfWordColumn
	STMFD	r13!, {r0-r2,r5}
	MOV	r4, r12, ROR #16
	LDR	r3, [r0]
	MOV	r5, r3
codepatch1b
	ADD	r0, r0, #0		; add srclen
	ADD	r0, r0, #0
	CMP	r0, #-1			; clear carry, so store doesn't happen 1st time thru
fl_halfcolumnloop
	FNdohalfword r3,r5,r7,r9,r11,r4,r12,3
	MOV	r3, r5			; shuffle manually
	MOV	r5, r7
	BCS	fl_halfcolumnloop
	STRB	r9, [r1]
	MOV	r9, r9, LSR #8
	STRB	r9, [r1, #1]
	LDMFD	r13!, {r0-r2,r5}
	ADD	r0, r0, #2
	ADD	r1, r1, #2
	SUB	r5, r5, #2
	MOV	pc, lr

DoOneWordColumn
	STMFD	r13!, {r0-r2,r5}
	MOV	r4, r12, ROR #16
	LDR	r3, [r0]
	MOV	r5, r3
codepatch1c
	ADD	r0, r0, #0		; add srclen
	ADD	r0, r0, #0
	CMP	r0, #-1			; clear carry, so store doesn't happen 1st time thru
fl_onecolumnloop
	FNdo1word r3,r5,r7,r9,r11,r4,r12,4
	MOV	r3, r5			; shuffle manually
	MOV	r5, r7
	BCS	fl_onecolumnloop
	STR	r9, [r1]
exitonewordcolumn2
	LDMFD	r13!, {r0-r2,r5}
	ADD	r0, r0, #4
	ADD	r1, r1, #4
	SUB	r5, r5, #4
	MOV	pc, lr


CodeBlockEnd

; On entry: R0 -> changed box block
;           R4 = x0 (pixels)
;           R5 = x1 - x0 (pixels)
;           R6 = y0 - y1 (pixels)
;           R7 = y1 (pixels from top)
; On exit:  R1-R3 corrupted
UpdateChangedBox
        Entry   "R4-R7"
        ADD     R5, R4, R5
        SUB     R5, R5, #1
        LDR     R1, VYWindLimit
        ADD     R6, R6, R7
        SUB     R6, R1, R6
        SUB     R7, R1, R7
        LDMIB   R0, {R1,R2,R3,R14}
        CMP     R4, R1
        MOVLT   R1, R4
        CMP     R6, R2
        MOVLT   R2, R6
        CMP     R5, R3
        MOVGT   R3, R5
        CMP     R7, R8
        MOVGT   R8, R7
        STMIB   R0, {R1,R2,R3,R14}
        EXIT

;	BIN	"o.filter"

;	InsertDebugRoutines

        END
