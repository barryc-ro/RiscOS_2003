	GET	Desk.sh.FnNames
	
	AREA	|C$code|, CODE, READONLY
	
	MACRO	
	Convert3BitCol	$reg, $offset, $tableoffset
	; Converts the 8-bit colour at bits { $offset, $offset+1, ..., $offset+7} within $reg.
	
	IF $offset=0
	AND	r14, $reg, #255
	ELSE
	MOV	r14, $reg, LSR #$offset
	AND	r14, r14, #255
	ENDIF
	
	ADD	r14, a3, r14, LSL #2
	LDR	r14, [r14, #$tableoffset]
	;LDR	r14, [a3, r14, LSL #2]
	
	
	ORR	r12, r12, r14, LSL #$offset
	MEND
	
	
	MACRO
	ConvertReg	$reg
	MOV	r12, #0
	Convert3BitCol	$reg,  0, 0*256*4
	Convert3BitCol	$reg,  8, 1*256*4
	Convert3BitCol	$reg, 16, 2*256*4
	MOV	$reg, r12
	MEND
	
	
	
	; On entry:
	;	r0=a1 is start address.
	;	r1=a2 is end address
	;	r2=a3 is convertions (3*256 entries, one set for each 8 bit part of word)
	;	r3=a4 is target colour.
	
	Desk_ASMacros_EXTERNALFUNCTION	DrawLine_32bpp
	
	STMFD	sp!, { v1-v6, r10, r11, lr}
	
	; 
loopstart
	LDMFD	r0, { r4-r11}
	
	; Now convert each of r4-r11
	ConvertReg	r4
	ConvertReg	r5
	ConvertReg	r6
	ConvertReg	r7
	ConvertReg	r8
	ConvertReg	r9
	ConvertReg	r10
	ConvertReg	r11
	
	STMEA	a1!, { r4-r11}
	
	CMP	a1, a2
	BLT	loopstart
	
	LDMFD	sp!, { v1-v6, r10, r11, pc}^
	
	END
