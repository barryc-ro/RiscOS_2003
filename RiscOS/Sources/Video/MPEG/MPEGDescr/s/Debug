; >s.Debug
; Copyright © Pace Micro Technology plc, 2003
; All rights reserved.
;
; Various useful debugging macros and routines.


; ******************************************************
; *
; * OVERVIEW OF MACROS:
; *
; *   DInit ["DA name string" [, <maximum size (KB)>]]   initialise the DA debug code
; *   DKill                                              remove the DA debug dynamic area
; *   DSave                                              save the DA debug area contents as a text file
; *
; *   DWriteC <char>                                     write the specified character
; *   DWriteS ["string"] [, <char>] [, <char>]           write the string following the call
; *   DWrite0 <register>                                 write the string pointed to by the register
; *   DNewLine                                           write a new-line (CR, LF)
; *
; *   DDec1 <reg>                                        write the register contents in hex (least significant byte)
; *   DDec2 <reg>                                        write the register contents in hex (least significant halfword)
; *   DDec3 <reg>                                        write the register contents in hex
; *
; *   DBin1 <reg>                                        write the register contents in hex (least significant byte)
; *   DBin2 <reg>                                        write the register contents in hex (least significant halfword)
; *   DBin3 <reg>                                        write the register contents in hex
; *
; *   DHex1 <reg>                                        write the register contents in hex (least significant byte)
; *   DHex2 <reg>                                        write the register contents in hex (least significant halfword)
; *   DHex3 <reg>                                        write the register contents in hex
; *
; * Each macro will expand to nothing if DEBUG is {FALSE}.


; ******************************************************************************
; *                                                                            *
; *                              INTERFACE                                     *
; *                                                                            *
; ******************************************************************************
;
; ******************************************************
; *
; * Create a dynamic area for debugging stuff to be written into. This area
; * requires two words of module workspace to store the area base address and
; * the area number, e.g.
; *
; *   WS_DAnum
; *   WS_DAbase
; *
; * The dynamic area contents are of the format:
; *
; *   0 ... pointer to current dump position (base+12 at start)
; *   4 ... pointer to end of mapped-in pages
; *   8 ... dynamic area number (for removal on Mod_Final)
; *  12 ... page size (bytes) of host machine
; *  16 ... strings written
; *
; * Thus, the contents of the dynamic area, from base+12 to base[4] could be
; * dumped to a file with a text filetype if you want to read its contents at a
; * later date.
; *
; * In...
; *   R12	= module workspace pointer
; *
; * Out...
; *   R0-R13	= preserved
; *   R14	= corrupted
; *
; * Syntax...
; *   DInit ["DA name string" [, <maximum size (KB)>]]
; *
; * Examples...
; *   DInit
; *   DInit "My debug area"
; *   DInit "DADebug", 2*1024    ; 2 MB maximum for DA
; *
; * Notes...
; *   Does nothing if DEBUG is FALSE.
; *
	MACRO
$lbl	DInit	$name, $max
$lbl
  [ DEBUG
	Push	"R5, R8"
	ADR	R8, %FT00
        B	%FT01
00
  [ "$name" = ""
	DCB	"Debug area", 0
  |
	DCB	"$name", 0
  ];"$name" = ""
	ALIGN
01
  [ "$max" = ""
	MOV	R5, #1*1024*1024
  |
	MOV	R5, #$max*1024
  ];"$max" = ""
	BL	DADB_Init
	Pull	"R5, R8"
  ];DEBUG
	MEND


; ******************************************************
; *
; * If we managed to create our debugging dynamic area, remove it and store
; * zero in our debug info words of the module workspace.
; *
; * In...
; *   R12	= module workspace pointer
; *
; * Out...
; *   R0-R13	= preserved
; *   R14	= corrupted
; *
; * Syntax...
; *   DKill
; *
; * Notes...
; *   Does nothing if DEBUG is FALSE.
; *
	MACRO
$lbl	DKill
$lbl
  [ DEBUG
  	BL	DADB_Kill
  ];DEBUG
	MEND


; ******************************************************
; *
; * Save our dynamic area to the specified file and set the filetype as
; * text. This only saves the debug contents of the dynamic area, excluding
; * the header structure.
; *
; * In...
; *   R1	= pointer to zero-terminated filename string
; *   R12	= module workspace pointer
; *
; * Out...
; *   R0	= corrupted
; *   R1-R13	= preserved
; *   R14	= corrupted
; *
; * Syntax...
; *   DSave
; *
; * Notes...
; *   Does nothing if DEBUG is FALSE.
; *
	MACRO
$lbl	DSave
$lbl
  [ DEBUG
	BL	DADB_Save
  ];DEBUG
	MEND


; ******************************************************
; *
; * Output the contents of a register to our dynamic area in decimal,
; * hexadecimal or binary.
; *
; * In...
; *   R12	= module workspace pointer
; *
; * Out...
; *   R0-R13	= preserved
; *   R14	= corrupted
; *
; * Syntax...
; *   DDec[1 | 2 | 4] <register>
; *   DBin[1 | 2 | 4] <register>
; *   DHex[1 | 2 | 4] <register>
; *
; * Notes...
; *   Does nothing if DEBUG is FALSE.
; *
	;
	; All of them use this primitive...
	;
	MACRO
$lbl	DARegVal $reg, $type
$lbl
  [ DEBUG
    [ ("$reg" = "R0") :LOR: ("$reg" = "r0")
	Push	"R10"
	MOV	R10, #$type
	BL	DADB_RegVal
	Pull	"R10"
    |
	Push	"R0, R10"
	MOV	R0, $reg
	MOV	R10, #$type
	BL	DADB_RegVal
	Pull	"R0, R10"
    ];("$reg" = "R0") :LOR: ("$reg" = "r0")
  ];DEBUG
  	MEND

	;
	; Decimal...
	;
	MACRO
$lbl	DDec1	$reg
$lbl	DARegVal $reg, 0xD9
	MEND
	MACRO
$lbl	DDec2	$reg
$lbl	DARegVal $reg, 0xDA
	MEND
	MACRO
$lbl	DDec3	$reg
$lbl	DARegVal $reg, 0xDC
	MEND
	;
	; ...and in binary...
	;
	MACRO
$lbl	DBin1	$reg
$lbl	DARegVal $reg, 0xDD
	MEND
	MACRO
$lbl	DBin2	$reg
$lbl	DARegVal $reg, 0xDE
	MEND
	MACRO
$lbl	DBin3	$reg
$lbl	DARegVal $reg, 0xE0
	MEND
	;
	; ...and in hexadecimal...
	;
	MACRO
$lbl	DHex1	$reg
$lbl	DARegVal $reg, 0xD1
	MEND
	MACRO
$lbl	DHex2	$reg
$lbl	DARegVal $reg, 0xD2
	MEND
	MACRO
$lbl	DHex3	$reg
$lbl	DARegVal $reg, 0xD4
	MEND


; ******************************************************
; *
; * Write the specified character into our debug area.
; *
; * In...
; *   R12	= module workspace pointer
; *
; * Out...
; *   R0-R13	= preserved
; *   R14	= corrupted
; *
; * Syntax...
; *   DWriteC <char>
; *
; * Examples...
; *   DWriteC 10
; *
; * Notes...
; *   Does nothing if DEBUG is FALSE.
; *
	MACRO
$lbl	DWriteC $chr
$lbl
  [ DEBUG
	Push	"R0"
	MOV	R0, #$chr
	BL	DADB_WrtC
	Pull	"R0"
  ];DEBUG
	MEND


; ******************************************************
; *
; * Embed a string in our code and branch to a debugging routine which will
; * write that string to the debugging dynamic area.
; *
; * In...
; *   R12	= module workspace pointer
; *
; * Out...
; *   R0-R13	= preserved
; *   R14	= corrupted
; *
; * Syntax...
; *   DWriteS ["string"] [, <char>] [, <char>]
; *
; * Examples...
; *   DWriteS "Hello world"
; *   DWriteS "Line feed", 10
; *   DWriteS "LF and CR", 10, 13
; *
; * Notes...
; *   Does nothing if DEBUG is FALSE.
; *
	MACRO
$lbl	DWriteS $str, $chr1, $chr2
$lbl
  [ DEBUG
	BL	DADB_WrtS
    [ "$str" <> ""
	DCB	"$str"
    ];"$str" <> ""
    [ "$chr1" <> ""
	DCB	$chr1
      [ "$chr2" <> ""
	DCB	$chr2
      ];"$chr2" <> ""
    ];"$chr1" <> ""
	DCB	0
	ALIGN
  ];DEBUG
	MEND


; ******************************************************
; *
; * Copy the zero-terminated string pointed to by the specified register into
; * the debugging dynamic area.
; *
; * In...
; *   R<n>	= pointer to zero-terminated string to copy
; *   R12	= module workspace pointer
; *
; * Out...
; *   R0-R13	= preserved
; *   R14	= corrupted
; *
; * Syntax...
; *   DWrite0 <register>
; *
; * Examples...
; *   DWrite0 R0
; *
; * Notes...
; *   Does nothing if DEBUG is FALSE.
; *
	MACRO
$lbl	DWrite0 $reg
$lbl
  [ DEBUG
	Push	"R0"
    [ ("$reg" = "R0") :LOR: ("$reg" = "r0")
    |
	MOV	R0, $reg
    ];("$reg" = "R0") :LOR: ("$reg" = "r0")
	BL	DADB_Wrt0
	Pull	"R0"
  ];DEBUG
	MEND


; ******************************************************
; *
; * Write a new-line (CR, LF) into our debug area.
; *
; * In...
; *   R12	= module workspace pointer
; *
; * Out...
; *   R0-R13	= preserved
; *   R14	= corrupted
; *
; * Syntax...
; *   DNewLine
; *
; * Examples...
; *   DNewLine
; *
; * Notes...
; *   Does nothing if DEBUG is FALSE.
; *
	MACRO
$lbl	DNewLine
$lbl
  [ DEBUG
	BL	DADB_NwLn
  ];DEBUG
	MEND


; ******************************************************************************
; *                                                                            *
; *                           IMPLEMENTATION                                   *
; *                                                                            *
; ******************************************************************************


  [ DEBUG
  ! 0, ""
  ! 0, "WARNING: This is a debug build!"
  ! 0, ""


		^	0
DADB_DumpPtr	#	4	; Pointer to current dump position
DADB_EndPtr	#	4	; Pointer to end of area
DADB_DAnum	#	4	; Number of area
DADB_PageSize	#	4	; Page size of host machine
DADB_HdrSize	*	@	; Size of debug area header


	AREA	|zDebug$$Code|, CODE, READONLY, PIC


; ******************************************************
; *
; * Create a dynamic area for debugging purposes.
; *
; * In...
; *   R5	= maximum size of dynamic area to create
; *   R8	= pointer to zero-terminated name string for the dynamic area
; *   R12	= pointer to module workspace
; *   R13	= full-descending stack pointer
; *   R14	= pointer to start of zero-terminated string
; * Out...
; *   R0-R13	= preserved
; *   R14	= corrupted
; *
DADB_Init Entry	"R0-R9"
	; Zero-initialise module workspace debug information...
	MOV	R0, #0
	STR	R0, [R12, #WS_DAnum]
	STR	R0, [R12, #WS_DAbase]
	; Read the machine's page size...
	SWI	XOS_ReadMemMapInfo
	MOVVS	R9, #4096
	MOVVC	R9, R0
	; Attempt to create a dynamic area for debugging output...
	MOV	R0, #0
	MOV	R1, #-1
	MOV	R2, R9
	MOV	R3, #-1
	MOV	R4, #1 :SHL: 7
	MOV	R6, #0
	MOV	R7, #0
	SWI	XOS_DynamicArea
	BVS	%FT00
	; Read the current size of the dynamic area...
	MOV	R0, #2
	SWI	XOS_DynamicArea
	BVS	%FT00
	ADD	R2, R3, R2
	ADD	R0, R3, #DADB_HdrSize
	; Initialise the module workspace info and dynamic area...
	STR	R1, [R12, #WS_DAnum]
	STR	R3, [R12, #WS_DAbase]
	STR	R0, [R3, #DADB_DumpPtr]
	STR	R2, [R3, #DADB_EndPtr]
	STR	R1, [R3, #DADB_DAnum]
	STR	R9, [R3, #DADB_PageSize]
00	;
	EXIT


; ******************************************************
; *
; * If we have created a debug dynamic area, remove it.
; *
; * In...
; *   R12	= pointer to module workspace
; *   R13	= full-descending stack pointer
; *   R14	= pointer to start of zero-terminated string
; * Out...
; *   R0-R13	= preserved
; *   R14	= corrupted
; *
DADB_Kill Entry	"R0-R1"
	LDR	R0, [R12, #WS_DAbase]
	LDR	R1, [R12, #WS_DAnum]
	TEQ	R0, #0
	MOVNE	R0, #1
	SWINE	XOS_DynamicArea
	MOVNE	R0, #0
	STRNE	R0, [R12, #WS_DAbase]
	EXIT


; ******************************************************
; *
; * If we have created a debug dynamic area, save it as a text file.
; *
; * In...
; *   R1	= pointer to zero-terminated filename
; *   R12	= pointer to module workspace
; *   R13	= full-descending stack pointer
; *   R14	= pointer to start of zero-terminated string
; * Out...
; *   R0	= corrupted
; *   R1-R13	= preserved
; *   R14	= corrupted
; *
DADB_Save Entry "R2, R4-R5"
	LDR	R4, [R12, #WS_DAbase]
	MOV	R0, #10
	MOV	R2, #0xFF0
	ORR	R2, R2, #0x00F
	TEQ	R4, #0
	BEQ	%FT00
	LDR	R5, [R4, #DADB_DumpPtr]
	ADD	R4, R4, #DADB_HdrSize
	CMP	R5, R4
	SWIHI	XOS_File
00	;
	EXIT


; ******************************************************
; *
; * Convert the value passed in R0 into a string using the OS conversion
; * SWI as passed in R10 and then copy that string into our debug area.
; *
; * In...
; *   R0	= value to write into our debugging area
; *   R10	= SWI number of OS conversion routine to use
; *   R12	= pointer to module workspace
; *   R13	= full-descending stack pointer
; *   R14	= pointer to start of zero-terminated string
; * Out...
; *   R0-R13	= preserved
; *   R14	= corrupted
; *
DADB_RegVal Entry "R0-R2"
	SUB	SP, SP, #36		; Use stack as temp buf
	MOV	R1, SP
	MOV	R2, #36
	ORR	R10, R10, #0x20000	; Ensure X bit
	SWI	XOS_CallASWI		; Convert value in R0 to string
	BLVC	DADB_Wrt0		; Write string into debug area
	ADD	SP, SP, #36		; Free temp buf off stack
	EXIT


; ******************************************************
; *
; * Write the specified character into our debugging dynamic area.
; *
; * In...
; *   R0	= character to output
; *   R12	= module workspace pointer
; *   R13	= full-descending stack pointer
; *   R14	= return address
; * Out...
; *   R0-R13	= preserved
; *   R14	= corrupted
; *
DADB_WrtC Entry "R10-R12"
	LDR	R11, [R12, #WS_DAbase]
	TEQ	R11, #0
	EXIT	EQ
	LDMIA	R11, {R10, R12}
	; R0  = character to output
	; R10 = pointer to dump point in DA
	; R11 = pointer to base of DA
	; R12 = pointer to end of DA mapped-in pages
	STRB	R0, [R10], #1
	; If need be, grow the dynamic area...
	CMP	R10, R12
	STR	R10, [R11, #DADB_DumpPtr]
	BLHS	DADB_Grow
	EXIT


; ******************************************************
; *
; * Similar to SWI OS_WriteS, except that the string pointed to gets written
; * into our debugging dynamic area. This area is grown every time the current
; * size is used-up.
; *
; * In...
; *   R12	= pointer to module workspace
; *   R13	= full-descending stack pointer
; *   R14	= pointer to start of zero-terminated string
; * Out...
; *   R0-R13	= preserved
; *   R14	= corrupted
; *
DADB_WrtS ROUT
	Push	"R0"
	RSB	R0, PC, PC	; R0 = current PSR bits if 26-bit, 0 if 32-bit
	SUB	R0, LR, R0
	BL	DADB_Wrt0
	BIC	LR, R0, #3
	Pull	"R0"
	ADD	PC, LR, #4


; ******************************************************
; *
; * Copy the zero-terminated string from the source pointer specified into our
; * debugging dynamic area and return a pointer to the end of the source
; * string.
; *
; * In...
; *   R0	= pointer to zero-terminated string to write
; *   R12	= module workspace pointer
; *   R13	= full-descending stack pointer
; *   R14	= return address
; * Out...
; *   R0	= pointer to zero-terminator of source string
; *   R1-R13	= preserved
; *   R14	= corrupted
; *
DADB_Wrt0 Entry	"R9-R12"
	LDR	R11, [R12, #WS_DAbase]
	TEQ	R11, #0
	EXIT	EQ
	LDMIA	R11, {R10, R12}
	; R0  = pointer to string to output
	; R10 = pointer to dump point in DA
	; R11 = pointer to base of DA
	; R12 = pointer to end of DA mapped-in pages
	LDRB	R9, [R0]
	TEQ	R9, #0
	EXIT	EQ
00	STRB	R9, [R10], #1
	LDRB	R9, [R0, #1]!
	; If need be, grow the dynamic area...
	CMP	R10, R12
	BLHS	DADB_Grow
	TEQ	R9, #0
	BNE	%BT00
	STR	R10, [R11, #DADB_DumpPtr]
	EXIT


; ******************************************************
; *
; * Output a CR,LF newline to the debugging area.
; *
; * In...
; *   R12	= module workspace pointer
; *   R13	= full-descending stack pointer
; *   R14	= return address
; * Out...
; *   R0-R13	= preserved
; *   R14	= corrupted
; *
DADB_NwLn Entry "R0"
	ADR	R0, DADB_NwLn_Str
	BL	DADB_Wrt0
	EXIT
DADB_NwLn_Str
	DCB	13, 10, 0
	ALIGN


; ******************************************************
; *
; * Attempt to grow the debugging dynamic area by a single page. If we cannot
; * manage to do this, throw whatever debug data is in there away and reset the
; * are to empty and just one page in extent.
; *
; * In...
; *   R11	= pointer to debug dynamic area
; *   R13	= full-descending stack pointer
; *   R14	= return address
; * Out...
; *   R0-R9	= preserved
; *   R10	= new dump pointer (if shrunk back)
; *   R11	= preserved
; *   R12	= new end pointer for dynamic area
; *   R13	= preserved
; *   R14	= corrupted
; *
DADB_Grow Entry	"R0-R8"
	LDR	R0, [R11, #DADB_DAnum]
	LDR	R1, [R11, #DADB_PageSize]
	SWI	XOS_ChangeDynamicArea
	BVS	%FT00
	MOV	R0, #2
	LDR	R1, [R11, #DADB_DAnum]
	SWI	XOS_DynamicArea
	ADD	R12, R3, R2
	STR	R12, [R11, #DADB_EndPtr]
	EXIT
00	; Area cannot grow any more, empty and shrink back to one page...
	MOV	R0, #2
	LDR	R1, [R11, #DADB_DAnum]
	SWI	XOS_DynamicArea
	MOV	R0, R1
	LDR	R1, [R11, #DADB_PageSize]
	SUB	R1, R1, R2
	SWI	XOS_ChangeDynamicArea
	LDR	R12, [R11, #DADB_PageSize]
	ADD	R10, R11, #DADB_HdrSize
	ADD	R12, R11, R12
	STR	R10, [R11, #DADB_DumpPtr]
	STR	R12, [R11, #DADB_EndPtr]
	EXIT


  ];DEBUG


	END
