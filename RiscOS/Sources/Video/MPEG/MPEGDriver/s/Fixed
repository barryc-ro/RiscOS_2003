; /****** Fixed.s ************************************************************
;
; Project:	STB-400
; Component:	MPEGDriver
; This file:	Fixed-point arithmetic (16.16 bits)
;
; Copyright 1999 Pace Micro Technology plc. All rights reserved.
;
; This material is the confidential trade secret and proprietary information
; of Pace Micro Technology plc. It may not be reproduced, used, sold, or
; transferred to any third party without the prior written consent of
; Pace Micro Technology plc.
;
; History:
; Date		Who	Change
; ----------------------------------------------------------------------------
; 11/04/2000	BJGA	Created
;
; ***************************************************************************/


; Included headers
; ----------------

        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:APCS.<APCS>


        AREA    |Asm$$Code|, CODE, READONLY


; Exported functions
; ------------------

; extern fixed Fixed_FixedMultiply (fixed multiplier, fixed multiplicand);

        EXPORT  Fixed_FixedMultiply
Fixed_FixedMultiply
        MOV     a4, a2, LSL #16
        MOV     a4, a4, LSR #16         ; a4 = lsw of multiplicand
        MOV     a3, a2, LSR #16         ; a3 = msw of multiplicand
        MOV     a2, a1, LSL #16
        MOV     a2, a2, LSR #16         ; a2 = lsw of multiplier
        MOV     a1, a1, LSR #16         ; a1 = msw of multiplier
        MUL     ip, a1, a3
        TST     ip, #&FF000000
        TSTNE   ip, #&00FF0000
        BNE     mult_overflow
        MUL     a1, a4, a1
        ADDS    a1, a1, ip, LSL #16
        MULCC   ip, a2, a3
        ADDCCS  a1, a1, ip              ; can't use MLA because we need C flag
        MULCC   ip, a2, a4
        ADDCCS  a1, a1, ip, LSR #16
        Return  , LinkNotStacked, CC
mult_overflow
        MOV     a1, #-1                 ; indicates not-a-number
        Return  , LinkNotStacked


; extern fixed Fixed_IntDivide (unsigned int dividend, unsigned int divisor);
; extern fixed Fixed_FixedDivide (fixed dividend, fixed divisor);

; In:   a1 = dividend, a2 = divisor
; Out:  a1 = quotient

        EXPORT  Fixed_IntDivide
        EXPORT  Fixed_FixedDivide
Fixed_IntDivide
Fixed_FixedDivide                       ; identical code, different prototype!
        TEQ     a2, #0
        BEQ     div_overflow
        MOV     a3, a1
        MOV     a4, #0
        CMP     a2, a3, LSR #1
01
        MOVLS   a2, a2, LSL #1
        ADDLS   a4, a4, #1
        CMPLS   a2, a3, LSR #1
        BLS     %BT01

        MOV     a1, #0
        ADD     a4, a4, #16             ; 16 fractional bits required
02
        CMP     a3, a2
        SUBCS   a3, a3, a2
        MOV     a3, a3, LSL #1
        ADCS    a1, a1, a1
        BCS     div_overflow
        SUBS    a4, a4, #1
        BPL     %BT02
        Return  , LinkNotStacked
div_overflow
        MOV     a1, #-1                 ; indicates not-a-number
        Return  , LinkNotStacked


        END
