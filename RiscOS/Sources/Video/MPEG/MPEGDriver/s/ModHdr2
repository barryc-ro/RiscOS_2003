; /****** ModHdr2.s **********************************************************
;
; Project:	STB-400
; Component:	MPEGDriver
; This file:	Secondary module header (for audio SWIs)
;
; Copyright 1999-2000 Pace Micro Technology plc. All rights reserved.
;
; This material is the confidential trade secret and proprietary information
; of Pace Micro Technology plc. It may not be reproduced, used, sold, or
; transferred to any third party without the prior written consent of
; Pace Micro Technology plc.
;
; History:
; Date		Who	Change
; ----------------------------------------------------------------------------
; 01/10/1999	BJGA	Created
; 02/12/1999	BJGA	Made Init2_Callback 32-bit safe
; 13/12/1999	BJGA	Exits gracefully if MPEGDriver initialisation fails
;			*after* MPEGDriver+ is up and running
; 03/05/2000	BJGA	Uses allocated error number
; 15/05/2000	BJGA	Now has module flags word to mark module as being 32-bit compatible
;
; ***************************************************************************/


        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:ModHand
        GET     Hdr:MsgTrans
        GET     VersionASM

        IMPORT  |Image$$RO$$Base|

        AREA    |Asm$$Code|, CODE, READONLY

        &       ModHdr2_End - .         ; module length in bytes

        EXPORT  ModHdr2
ModHdr2
        &       Start2 - ModHdr2
        &       Init2 - ModHdr2
        &       Final2 - ModHdr2
        &       Service2 - ModHdr2
        &       Title2 - ModHdr2
        &       Help2 - ModHdr2
        &       CommandTable2 - ModHdr2
        &       MPEGAudioSWI_Base
        &       SWIHandler2 - ModHdr2
        &       SWITable2 - ModHdr2
        &       SWIDecode2 - ModHdr2
        &       Messages2 - ModHdr2
        &       ModFlags - ModHdr2

        IMPORTVar Module_Dying
        IMPORTVar Module_MessageFD

Start2          *       ModHdr2

Init2
        Push    "lr"
        ADR     r0, Init2_Callback
        MOV     r1, r12
        SWI     XOS_AddCallBack         ; MPEGDriver isn't linked into the module chain yet!
        Pull    "pc"
Init2_Callback
        Push    "r0-r5, lr"
        MOV     r0, #ModHandReason_LookupName
        ADR     r1, MPEGDriver_Name
        SWI     XOS_Module
        STR     r4, [r12]               ; make our private word contents the same as MPEGDriver's
        Pull    "r0-r5, pc"
MPEGDriver_Name
        =       "MPEGDriver", 0
        ALIGN

Final2
        Push    "lr"
        ADR     r0, Init2_Callback
        MOV     r1, r12                 ; in case there was an error later in MPEGDriver init -
        SWI     XOS_RemoveCallBack      ; it does no harm if the callback has already gone off
        LDR     r12, [r12]
        CMP     r12, #0                 ; if there was an error later in MPEGDriver init, then
        Pull    "pc", EQ                ; r12 hasn't been initialised, so don't try to use it
        StaticBaseFromWP lr, r12
        LDRVar  r0, lr, Module_Dying
        CMP     r0, #0                  ; helpfully clears V
        Pull    "pc", NE                ; don't complain if MPEGDriver is dying too
        ADR     r0, CantDie2ErrBlk      ; otherwise refuse to die
        ADRVar  r1, lr, Module_MessageFD
        MOV     r2, #0
        SWI     XMessageTrans_ErrorLookup
        Pull    "pc"
CantDie2ErrBlk
        &       &818508
        =       "Err_CantDie2", 0
        ALIGN

Service2        *       ModHdr2

Title2  =       "MPEGDriver+", 0

Help2   =       "MPEGDriver+", 9, "$Module_MajorVersion ($Module_Date)"
        [ Module_MinorVersion <> ""
        =       " $Module_MinorVersion"
        ]
        =       0

        ALIGN

CommandTable2   *       ModHdr2

SWIHandler2
        Push    "r0"
        LDR     r10, =|Image$$RO$$Base|
        LDR     r0, [r10, #Module_SWIEntry]
        ADD     r10, r10, r0
        ADD     r11, r11, #MPEGAudioSWI_Base - MPEGVideoSWI_Base
        Pull    "r0"
        MOV     pc, r10

SWITable2
        =       "MPEGAudio", 0
        =       "OpenStream", 0
        =       "FullPackets", 0
        =       "CloseStream", 0
        =       "Play", 0
        =       "SetSpeed", 0
        =       "SkipFrame", 0
        =       "RepeatFrame", 0
        =       "ResetStream", 0
        =       "MuteSound", 0
        =       "SoundVolume", 0
        =       "Stats", 0
        =       "SetAudioParameters", 0
        =       0

        ALIGN

SWIDecode2      *       ModHdr2

Messages2       *       ModHdr2

ModFlags
      [ {CONFIG} <> 26
        &       ModuleFlag_32bit
      |
        &       0
      ]

ModHdr2_End

        END
