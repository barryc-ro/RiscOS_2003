;*****************************************************************************
; $Id$
; $Name$
;
; Author(s):  Ben Avison
; Project(s):
;
; ----------------------------------------------------------------------------
; Copyright [2002] Pace Micro Technology PLC.  All rights reserved.
;
; The copyright in this material is owned by Pace Micro Technology PLC
; ("Pace").  This material is regarded as a highly confidential trade secret
; of Pace.  It may not be reproduced, used, sold or in any other way exploited
; or transferred to any third party without the prior written permission of
; Pace.
;
; ----------------------------------------------------------------------------
; Purpose: 64-bit type
;
; ----------------------------------------------------------------------------
; History: See source control system log
;
;****************************************************************************/

        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:APCS.<APCS>

        AREA    |Asm$$Code|, CODE, READONLY

; extern int64_t int64_Neg(int64_t x);
        EXPORT  int64_Neg
int64_Neg
        RSBS    a1, a1, #0
        RSC     a2, a2, #0
        Return  , LinkNotStacked

; extern int64_t int64_Add(int64_t x, int64_t y);
        EXPORT  int64_Add
int64_Add
        ADDS    a1, a1, a3
        ADC     a2, a2, a4
        Return  , LinkNotStacked

; extern int64_t int64_Sub(int64_t x, int64_t y);
        EXPORT  int64_Sub
int64_Sub
        SUBS    a1, a1, a3
        SBC     a2, a2, a4
        Return  , LinkNotStacked

; extern bool int64_Equal(int64_t x, int64_t y);
        EXPORT  int64_Equal
int64_Equal
        TEQ     a1, a3
        TEQEQ   a2, a4
        MOVEQ   a1, #1
        MOVNE   a1, #0
        Return  , LinkNotStacked

; extern bool int64_NotEqual(int64_t x, int64_t y);
        EXPORT  int64_NotEqual
int64_NotEqual
        EORS    a1, a1, a3
        TEQEQ   a2, a4
        MOVNE   a1, #1
        Return  , LinkNotStacked

; extern bool int64_GreaterThan(int64_t x, int64_t y);
        EXPORT  int64_GreaterThan
int64_GreaterThan
        CMP     a1, a3
        SBCS    a1, a2, a4
        MOVGT   a1, #1
        MOVLE   a1, #0
        Return  , LinkNotStacked

; extern bool int64_GreaterThanOrEqual(int64_t x, int64_t y);
        EXPORT  int64_GreaterThanOrEqual
int64_GreaterThanOrEqual
        CMP     a1, a3
        SBCS    a1, a2, a4
        MOVGE   a1, #1
        MOVLT   a1, #0
        Return  , LinkNotStacked

; extern bool int64_LessThan(int64_t x, int64_t y);
        EXPORT  int64_LessThan
int64_LessThan
        CMP     a1, a3
        SBCS    a1, a2, a4
        MOVLT   a1, #1
        MOVGE   a1, #0
        Return  , LinkNotStacked

; extern bool int64_LessThanOrEqual(int64_t x, int64_t y);
        EXPORT  int64_LessThanOrEqual
int64_LessThanOrEqual
        CMP     a1, a3
        SBCS    a1, a2, a4
        MOVLE   a1, #1
        MOVGT   a1, #0
        Return  , LinkNotStacked

        END
