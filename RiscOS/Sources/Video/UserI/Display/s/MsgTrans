; > Sources.MsgTrans

MsgTrans_FileName
        DCB     "DisplayManager:Messages",0
                ALIGN

;---------------------------------------------------------------------------
; MsgTrans_OpenFile
;
;       Out:    r0 -> message file block
;
;       Open our messages file.
;
MsgTrans_OpenFile
        Entry   "r1-r2"

        Debug   msg,"MsgTrans_OpenFile"

        ADR     r0, message_file_block
        LDRB    lr, flags
        TST     lr, #f_messagesopen
        EXIT    NE

        Debug   msg," opening file"

        ADR     r1, MsgTrans_FileName
        MOV     r2, #0
        SWI     XMessageTrans_OpenFile
        LDRVCB  lr, flags
        ORRVC   lr, lr, #f_messagesopen
        STRVCB  lr, flags

        EXIT


;---------------------------------------------------------------------------
; MsgTrans_ErrorLookup
;
;       In:     r0 -> token error block
;               r4-r7 -> optional parameters
;       Out:    r0 -> translated error block
;
;       Lookup an error in our messages file.
;
MsgTrans_ErrorLookup
        Entry   "r0-r2"

        Debug   msg,"MsgTrans_ErrorLookup"

        CLRV
        BL      MsgTrans_OpenFile

        MOVVC   r1, r0
        LDRVC   r0, [sp]
        MOVVC   r2, #0
        SWIVC   XMessageTrans_ErrorLookup
        STR     r0, [sp]

        EXIT


;---------------------------------------------------------------------------
; MsgTrans_Lookup
;
;       In:     r1 -> token
;               r2 -> buffer (or 0)
;               r3 = size of buffer (if r2 non-zero)
;               r4-r7 = parameters
;       Out:    r2 -> translated string
;               r3 = size of result before terminator
;
;       Look up the given token in our messages file.
;
MsgTrans_Lookup
        Entry   "r0,r1"

        Debug   msg,"MsgTrans_Lookup"

        CLRV
        BL      MsgTrans_OpenFile

        SWIVC   XMessageTrans_Lookup
        STRVS   r0, [sp]

        EXIT


        END
