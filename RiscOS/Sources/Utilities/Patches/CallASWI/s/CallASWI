        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System

        GET     Version

                        ^       0,R12
Old_CallASWI            #       4
Old_CallASWIR12         #       4
Old_PlatformFeatures    #       4
Old_SynchroniseCodeAreas #      4
WorkSpaceSize           *       :INDEX: @

SLVK_SetV               *       &01F03388
SLVK_TestV              *       &01F0338C
SLVK                    *       &01F03390
CallASWIReEntryPoint    *       &01F033D4
SWIDispatcherTable      *       &01F033FC

        MACRO
        GrabSWI $Name
        LDR     R1,[R0,#OS_$Name * 4]
        STR     R1,Old_$Name
        ADR     R1,Our_$Name
        STR     R1,[R0,#OS_$Name * 4]
        MEND

        MACRO
        FreeSWI $Name
        LDR     R1,Old_$Name
        STR     R1,[R0,#OS_$Name * 4]
        MEND

ModuleBase

        &       0                       ; Start
        &       Initialise - ModuleBase
        &       Finalise - ModuleBase
        &       0                       ; Service
        &       Title - ModuleBase
        &       Help - ModuleBase
        &       0                       ; Help and commands

Title
        =       "CallASWI"
        =       0
Help
        =       "CallASWI", 9, "$VString ($Date)"
        =       0

        ALIGN

Initialise
        Push    "LR"
        MOV     R0,#129
        MOV     R1,#0
        MOV     R2,#255
        SWI     XOS_Byte
        Pull    "PC",VS
        CMP     R1,#&A4         ; RISC OS 3.10
        BLO     BadOSVersion
        CMP     R1,#&A6         ; RISC OS 3.60
        BHI     BadOSVersion
        MOV     R0,#6
        MOV     R3,#WorkSpaceSize
        SWI     XOS_Module
        Pull    "PC",VS
        STR     R2,[R12]
        MOV     R12,R2
        LDR     R0,=SWIDispatcherTable
        GrabSWI CallASWI
        GrabSWI CallASWIR12
        GrabSWI PlatformFeatures
        GrabSWI SynchroniseCodeAreas
        Pull    "PC",,^

BadOSVersion
        ADR     R0,BadOSVersionError
        Pull    "LR"
        ORRS    PC,LR,#V_bit

BadOSVersionError
        &       0
        =       "The CallASWI module will only work on RISC OS 3.1, "
        =       "3.5 or 3.6"
        =       0
        ALIGN

Finalise
        Push    "LR"
        LDR     R12,[R12]
        LDR     R0,=SWIDispatcherTable
        FreeSWI CallASWI
        FreeSWI CallASWIR12
        FreeSWI PlatformFeatures
        FreeSWI SynchroniseCodeAreas
        MOV     R0,#7
        MOV     R2,R12
        SWI     XOS_Module
        Pull    "PC",,^

Our_CallASWI
        LDR     R11,[SP,#4]             ; pick-up target SWI code (stored R10)
        BIC     R11,R11,#&FF000000      ; just in case
        STR     R11,[SP,#0]             ; CallASWI now incognito as target SWI
        LDR     PC,=CallASWIReEntryPoint

Our_CallASWIR12
        LDR     R11,[SP,#12]            ; pick-up target SWI code (stored R12)
        BIC     R11,R11,#&FF000000      ; just in case
        STR     R11,[SP,#0]             ; CallASWI now incognito as target SWI
        LDR     PC,=CallASWIReEntryPoint

Our_PlatformFeatures ROUT
        Push    "LR"
        TEQ     R0, #0                  ; Is it a known reason code?
        BNE     %FT50                   ; No, so send out a service call

        ; OK, it's the 'code_features' reason code.
        ; Always 0 for ARM2,3,6,7 hence always 0 for RISC OS 3.1-3.6
        Pull    "LR"
        LDR     PC,=SLVK
50
        ; Would issue Service_UnknownPlatformFeatures here, but the official
        ; OS team position is "bollocks to it"
        ADR     R0,platfeaterror
        LDR     PC,=SLVK_SetV

platfeaterror
        &       0
        =       "Bad OS_PlatformFeatures reason code",0
        ALIGN

Our_SynchroniseCodeAreas
        BIC     LR,LR,#V_bit
        LDR     PC,=SLVK

        END
