; > PDumper.Header

; ********************
; *** CHANGES LIST ***
; ********************

;  6-Feb-89 GJS Created PDumper
; 10-Apr-91 OSS Internationalised
; 25-Apr-91 OSS Fix bug: Finalisation corrupting module chain
;  5-May-91 DDV Sorted it all out finally
;  5-May-91 DDV Bug fix: Sorted out Owens attempt at improved service handling
;  5-May-91 DDV Bug fix: Passes on service codes to devices correctly
;  5-May-91 DDV Handles new scheme for registering and deregistering devices
;  5-May-91 DDV Various optimisations in handling of reasons etc
;  5-May-91 DDV Introduced bad strip type error message
;  6-May-91 DDV Now ignores Service_PDumperDying
;  6-May-91 DDV Intergrated new message handling stuff
;  6-May-91 DDV Added extra buffer checks for buffer overflow when copying strings
; 25-May-91 DDV Bug fix: pdumper word was not a word it was a byte
; 31-May-91 DDV Added handling of PDumper_MiscOp
;  6-Jun-91 DDV Bug fix: Finalise resolves the workspace pointer before using it
; 13-Jun-91 DDV Now use PDriver_MiscOpForDriver to register
; 20-Jun-91 DDV Changed the help string to be less verbose
; 20-Jun-91 DDV Expanded configuration block to contain left skip
; 05-Jul-91 DDV Updated to cope with PDumper_SetDriver not SetPrinter
; 21-Jul-91 DDV Assembled with the 3.16 code
; 15-Aug-91 DDV Added stuff about file debugging
;  5-Sep-91 DDV Added handling for non-fatal abort jobs
;  5-Sep-91 DDV Colour routines don't assume palette is first block in chain
; ------------------------> RISC OS 3.00 release <------------------------
; 18-Oct-91 DDV Modified to use the PDumper Support module
; 21-Oct-91 DDV Removed 24bit entries - bonus code
; 22-Oct-91 DDV Removed message handling, now part of the support module
; 22-Oct-91 DDV Simplified the handling of SetDriver, now calls support function
; 23-Oct-91 DDV Bug fix: init does not exit when workspace claimed, ie. before registering
; 24-Oct-91 DDV Bug fix: error despatching within the module
; 25-Oct-91 DDV PrintLengthString macro tweeked
; 28-Oct-91 DDV Bug fix: prepstrip no longer corrupts memory
; 25-Nov-91 OSS Bug fix: pd_data is now 244 bytes again instead of 4. Also colour dither called when halftone size = 1.
; 16-Dec-91 OSS Bug fix to DM/IW Workspace palette_file_end size. Fix
;               PDumper_Entry to store SP and not R0 in FramePointer, and
;               don't use ENTRY/EXIT so that they can be used in the code.
;               Fix PDumper_Error to not get clever (and fail).
; 30-Dec-91 OSS Added leading zero skip, page length and roll paper fields.
; 13-Jan-92 OSS Added form feed field.
; 20-Feb-92 JSR Preserve r8 where appropriate.
;  5-Mar-92 DDV Sorted out owens little mess up with version bytes.
; 25-Mar-92 DDV Sending $Extra variables resolved.
; 24-Apr-92 DDV Support for v3 data files with offsets, new data block
; ------------------------> RISC OS 3.10 release <------------------------
; 04-Aug-93 PAH Added support for colour LJ, bought from Ace Computing - version number increased to 4.00
; 17-Aug-93 PAH Added support for 24 bit colour output for all dumpers (that could support it)
; 24_Feb-94 PAH Added abortjob sequence to help set printer into a sane state when aborting a
;               printjob (used to get in a confused state when aborting graphics printing)
; 29 Apr 97 JRC Added GBPB error bit (bit 1 of pd_error_flag_set), reentrancy check (bit 2).
; 07 May 97 JRC Return an error EscDisabled if any PDumper is called with escape disabled.
;               Check the GBPB error flag before checking the error-suppress flag.
;               Copy the error block returned by OS_GBPB so FileSwitch doesn't overwrite it.

                GET     hdr:ListOpts
                GET     hdr:Macros
                GET     hdr:System
                GET     hdr:FSNumbers
                GET     hdr:HighFSI
                GET     hdr:Modhand
                GET     hdr:NewErrors
                GET     hdr:PDriver
                GET     hdr:PDriverReg
                GET     hdr:PDumper
                GET     hdr:PDumperReg
                GET     hdr:Services
                GET     hdr:Sprite
                GET     hdr:ColourTran
                GET     hdr:Proc
                GET     hdr:MsgTrans
                GET     hdr:HostFS
                GET     hdr:NDRDebug
                GET     hdr:DDVMacros
                GET     hdr:HostDebug

                GET     s.Version

                GBLL    debug
                GBLL    hostvdu
                GBLL    file
                GBLA    PDumperVersion

PDumperVersion  SETA    328

debug           SETL    false :LAND: PDumperDebug
hostvdu         SETL    false
debug_file	SETS	"<PDmpDebug>"

                GBLL    Medusa
        [ PDumperVersion <= 400
Medusa          SETL    {TRUE}
        |
Medusa          SETL    {FALSE}
        ]

AbortJob        SETD    false
Dump            SETD    false
Dump2           SETD    false
DumpMJS         SETD    false
Init            SETD    false
EndPage         SETD    false
Colour          SETD    false
Job             SETD    false
All             SETD    false
StartPage       SETD    false
xx              SETD    false
Error           SETD    false
Calls           SETD    true
Margin          SETD    false
misc		SETD	true

                GET     s.Macros


;..............................................................................

ofs             RN      R10
ptr             RN      R11
workspace       RN      R12

; Workspace as part of the job record

                        ^ 0
pd_dump_depth           # 1             ; depth of each dump run in dots
pd_interlace            # 1             ; vert. interlace passes (0 = none)
pd_x_interlace          # 1             ; horiz. interlace passes (0 = none)
pd_passes_per_line      # 1             ; number of colour ribbon passes
pd_strip_type           # 1             ; strip type (mono, grey etc.)
pd_output_bpp           # 1             ; 1=>1bpp, 8=>8bpp
pd_no_passes            # 1             ; ?
pd_pass                 # 1             ; ?
pd_pdumper_word         # 4             ; private word decdicated to the PDumper itself
pd_data                 # 244           ; PDumper specific information
                        ASSERT (@:AND:3)=0
pd_private_flags        # 3             ; PDumper configuration bytes (flags)
pd_data_version         # 1             ; Version of the PD data file
pd_pdumperpointer       # 4             ; Pointer to active pdumper module
pd_pdumpernumber        # 4             ; PDumper module number
pd_leftmargin           # 4             ; Left margin in pixels

                        ^ 300
pd_linelength           # 4             ; addition for colour LJ
                        ^ 424
pd_totalheight          # 4             ; addition for colour LJ
pd_currentline          # 4             ; addition for colour LJ
; Any of the text offsets may be 0, indicating no text.
; If the offset is non-zero then the text it points to is <length><data>[<data> ...]
; Everything is by bytes

                        ^ 0
pd_data_dlm             # 1             ; Data length multiplier
pd_data_dla             # 1             ; Data length added (line as printer
                                        ; sees it is dlm*width+dla
pd_data_dht             # 1             ; Bit rows high per dump
; Byte offsets start here
pd_data_page_start      # 1             ; Page start (offset from pd_data)
pd_data_page_end        # 1             ; Page start (offset from pd_data)

pd_data_line_return     # 1             ; Line return (for x interlace)
pd_data_line_skip       # 1             ; Line skip (for blank lines)

pd_data_line_end        # 1             ; Line end (offset from pd_data)
pd_data_line_end_i2     # 1             ; Line end 2nd vertical interlace
pd_data_line_end_i3     # 1             ; Line end 3rd vertical interlace
pd_data_zero_skip       # 1             ; Was pd_data_line_end_i4, but 4
                                        ; passes is never used and is stupid
pd_data_line_start      # 1             ; Line start (pre length output)
pd_data_line_start_2    # 1             ; Line start (post length output)
pd_data_pass1_start     # 1             ; Line start for colour1 pre length
pd_data_pass1_start_2   # 1             ; Line start for colour1 post length
pd_data_pass2_start     # 1             ; Line start for colour2 pre length
pd_data_pass2_start_2   # 1             ; Line start for colour2 post length
pd_data_pass3_start     # 1             ; Line start for colour3 pre length
pd_data_pass3_start_2   # 1             ; Line start for colour3 post length
pd_data_pass4_start     # 1             ; Line start for colour4 pre length
pd_data_pass4_start_2   # 1             ; Line start for colour4 post length

; 21 bytes to this point here, where the additions for version 1 start.

pd_data_set_lines       # 1             ; Set lines per page (27, "C").
; End of byte offsets
pd_data_num_lines       # 1             ; Number of lines per page.
pd_data_pixel_run_up    # 1             ; Number of leading zeros to leave.
pd_data_skip_multiplier # 4             ; Multiplier and divider used to
pd_data_skip_divider    # 4             ; convert from output to skip DPI.
pd_data_roll_advance    # 4             ; Short advance used for roll paper
pd_data_form_feed       # 1             ; Offset to form feed string (V>=2)

; Version 3 stuff from here onwards.

pd_data_unused_bytes    # 3             ; Any new byte size stuff will go here.
pd_data_left_offset     # 4             ; X pixels to subtract from margin.
pd_data_top_offset      # 4             ; Y pixels to subtract from margin.

;..............................................................................

                        ^ 0,WP

FramePointer            # 4     ;->Return frame used for quick get away
FileHandle              # 4     ;Current output stream for outputing data to
pd_error_flag_set       # 4	;divided into bits as follows JRC Mon 28th April 1997
				;bit 0 error this job (as before)
				;bit 1 GBPB error this job (don't do any more GBPB's)
				;bit 2 reentrancy flag ("PDumper in use")
error_buffer		# 256	;must avoid MessageTrans error blocks because they seem to be reused

                        GET     s.Workspace

workspace_required      * :INDEX: @

;..............................................................................

        LEADR   Module_LoadAddr

module_start
        & 0                             ;not an application
        & init -module_start
        & final -module_start
        & service -module_start

        & title -module_start
        & help -module_start
        & 0                             ;no commands
    [ :LNOT: No32bitCode
        & 0
        & 0
        & 0
        & 0
        & 0
        & moduleflags -module_start
    ]

title   = "$PDumperTitle",0
help    = "$PDumperHelp",9,"$Module_HelpVersion"
      [ debug
        = " Development version"
      ]
        = 0
        ALIGN

    [ :LNOT: No32bitCode
moduleflags DCD ModuleFlag_32bit
    ]
        PDumper_InitialiseMacros

;..............................................................................

; handle generate module startup, including claiming workspace

init    Entry                           ; note, pushes lr

        LDR     R2,[WP]                 ;Is this a cold start?
        TEQ     R2,#0
        BNE     %FT10                   ;Skip if we already have workspace

; we will be dormant in ROM, as long as PDriver is after us in chain
        SWI     XPDriver_CurrentJob     ;cheap check that PDriver module is present
        BVC     init_pdriver_active
; PDriver not present
        ADR     r0, no_PDriver_error
        MOV     r1, #0 ; use global messages
        MOV     r2, #0 ; use internal buffer
        ADR     r4, title
        ADR     r5, pdr_title
        MOV     r6, #0
        MOV     r7, #0
        SWI     XMessageTrans_ErrorLookup
        EXIT

no_PDriver_error
        DCD     ErrorNumber_NeedMod
        DCB     "NeedMod",0
        ALIGN
pdr_title
        DCB     "PDriver",0
        ALIGN

init_pdriver_active
        MOV     R0,#ModHandReason_Claim
        LDR     R3,=workspace_required
        SWI     XOS_Module              ;Attempt to claim some workspace
        EXIT    VS

        STR     R2,[WP]
10      MOV     WP,R2                   ;Setup a suitable private word

	ADR	r1, pd_error_flag_set
	MOV	r0, #0
	STR	r0, [r1]		;clear all flags

        BL      register_dumper

        Debug_Open "<PDumper$Debug>"
        CLRV
        EXIT                            ; never return an error

; -------------------------------------------------------------------------
;
; register_dumper
;
; In -
; Out r0 corrupt, error possible
;
; registers dumper
;
register_dumper Entry "r1-r8"

        Debug   Init,"Attempting to register dumper ID=$PDumperNumber"

        MOV     R0,#PDumperMiscOp_AddPDumper
        MOV     R1,#PDumperNumber       ;ID of PDumper to register
        LDR     R2,=PDumperVersion      ;PDriverDP version required
        MOV     R3,WP                   ;->My workspace block
        ADR     R4,BranchCode
        MOV     R5,#2_11111111
        [ Medusa
        Push    "R0,R1"
        LDR R0, magic_value
        MOV R1, #4
        SWI XOS_ReadModeVariable
        MOVCC R6, #2_110111             ;strip_5+strip_4+strip_2+strip_1+strip_0
        MOVCS R6, #2_1111               ;strip_3+strip_2+strip_1+strip_0
        ;MOV     R6, #2_111111
        Pull    "R0,R1"
        |
        MOV     R6,#2_111               ;Strips and branches supported
        ]
        MOV     R8,#PDriverType_DP
        SWI     XPDriver_MiscOpForDriver
        DebugE  Init,"Error returned from register"

        EXIT

magic_value
  DCD &301680B5

;..............................................................................

; Attempt to finalise the module, this involves releasing any workspace
; and decoupling the device.

final   Entry   "r8"

        LDR     WP,[WP]                 ;Resolve the workspace pointer

        MOV     R0,#PDumperMiscOp_RemovePDumper
        MOV     R1,#PDumperNumber
        MOV     R8,#PDriverType_DP
        SWI     XPDriver_MiscOpForDriver

        Debug_Close
        CLRV
        EXIT

;..............................................................................

; Trap service calls being broadcast throughout the system

service ROUT
        TEQ     R1,#Service_PDumperStarting
        MOVNE   PC,LR

        Entry   "r0"
        LDR     WP,[WP]                 ;Resolve workspace pointer
        BL      register_dumper
        EXIT                            ;Preserve R0 - other regs not corrupted

;..............................................................................

        LTORG

; Get the PDumper specific files for each of these features

        GET     s.BranchCode
        GET     s.SetDriver
        GET     s.OutputDump
        GET     s.ColourSet
        GET     s.StartPage
        GET     s.EndPage
        GET     s.AbortJob
        GET     s.StartJob
        GET     s.MiscOp

        PDumper_InsertRoutines

      [ debug
        InsertNDRDebugRoutines
      ]

        END

