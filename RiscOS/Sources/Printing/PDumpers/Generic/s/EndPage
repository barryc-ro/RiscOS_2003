; > EndPage

;..............................................................................
; 
; Handle the end of page condition, check to see if there is an end of
; page sequence.  If there is then send it to specified file handle.
;
; in    R1 file handle
;       R2 strip type
;       R3 ->private word for dumper
;       R4 job workspace
;
; out   V =1 => R0 -> error block

EndPage_Monochrome ROUT
EndPage_Grey       ROUT
EndPage_Colour256  ROUT

        PDumper_Entry "R0-R3,R4-R6"
             
        Debug   EndPage,"(endpage) handle, strip type, private word, jobws",R1,R2,R3,R4
                           
        STR     R1,FileHandle           ;Store the handle
        PDumper_Reset                   ;And then flush it away
                                                              
        ADD     R4,R4,#pd_data

        LDRB    R0,[R4,#pd_data_version - pd_data]
        CMP     R0,#2                   ; Is version >=2?
        BLT     %FT10

        LDRB    R0,[R4,#pd_private_flags -pd_data]
        TST     R0,#df_NoPageAdvance    ; Is full page advance allowed?
        BNE     %FT10                   ; No - don't form feed

        LDRB    r0, [r4, #pd_data_form_feed]    ; -> form feed sequence
        TEQ     r0, #0                  ; is one specified?
        BEQ     %FT10                   ; No - don't output it

        ADD     r0, r4, r0      
        PDumper_PrintCountedString r0, r1, r2
10
        LDRB    R0,[R4,#pd_data_page_end]
        TEQ     R0,#0                   ;Is there an end of page sequence?
        BEQ     %FT10

        ADD     R0,R4,R0                ;->Sequence to be sent
        PDumper_PrintCountedString R0,R1,R2
10
        PDumper_EmptyBuffer
        PDumper_Exit                    ;Flush the buffer and then exit

        END
