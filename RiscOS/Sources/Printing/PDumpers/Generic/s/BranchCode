; > BranchCode

;..............................................................................

; Handle the reason codes being sent from the PDriverDP module to the 
; dumper.   We must range check this value and then attempt to 
; despatch as required, the strip type is also checked to ensure that
; it is supported.

; in    R11 reason code

BranchCode ROUT


        TEQ     PC,#SVC_mode            ;Enable IRQs

;for OutputDump, check width is multiple of 8 (assumed by newer code)
;ie. prevent old PDriverDP causing crash
        TEQ     R11,#PDumperReason_OutputDump
        BNE     bc_checkw_done
        TST     R3,#7
        BEQ     bc_checkw_done
        ADR     R0,bc_old_pdriverdp
        SETV
        MOV     PC,LR

bc_checkw_done
        TEQ     R11,#PDumperReason_SetDriver
        MOVEQ   R9,#0
        ANDNE   R9,R2,#&FF              ;Get the strip type or make one up if required

        CMP     R9,#StripType_MAX
        BCS     BadStripType            ;Complain about the bad strip type
                     
        MOV     R10,#(%10-%00)/4
        CMP     R11,R10                 ;Is the reason code valid?
        MLACC   R11,R9,R10,R11          ;If it is then index via n*strip type, where n is the number of jumps
        ADDCC   PC,PC,R11,LSL #2     
        B       BadPDumperCall          ;Complain about an invalid operation
00
        B       SetDriver_Monochrome
        B       OutputStrip_Monochrome
        B       ColourSet_Monochrome
        B       StartPage_Monochrome
        B       EndPage_Monochrome
        B       StartJob_Monochrome
        B       AbortJob_Monochrome
        B       MiscOp_Monochrome
10
        B       SetDriver_Grey
        B       OutputStrip_Grey
        B       ColourSet_Grey
        B       StartPage_Grey
        B       EndPage_Grey
        B       StartJob_Grey
        B       AbortJob_Grey
        B       MiscOp_Grey
                
        B       SetDriver_Colour256
        B       OutputStrip_Colour256
        B       ColourSet_Colour256
        B       StartPage_Colour256
        B       EndPage_Colour256
        B       StartJob_Colour256
        B       AbortJob_Colour256
        B       MiscOp_Colour256

        GET     s.24Bit

   

;..............................................................................

;rare error, not internationalised
bc_old_pdriverdp
        DCD     0
        DCB     "$PDumperTitle needs newer PDriverDP (illegal strip width)",0
        ALIGN

BadStripType
        ADR     R0,ErrorBlock_PDumperBadStrip
        B       %FT95

BadPDumperCall
        ADR     R0,ErrorBlock_PDumperBadCall
95
        Push    "R1,LR"                 
        ADDR    R1,title                ;->Title string 
        SWI     XPDumper_LookupError     
        Pull    "R1,PC"

        MakeInternatErrorBlock PDumperBadCall,,BadCall
        MakeInternatErrorBlock PDumperBadStrip,,BadStrp

        END
