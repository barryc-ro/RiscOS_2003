; > StartPage



; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; call: StartPage_Code
;
; in:   r0   = copies to do
;       r1   = file handle
;       r2   = strip type
;       r3   = number of blank pixel rows before start of data
;       r4  -> our private pdumper word for job
;       r5   = job workspace
;       r6   = number of blank pixels before start of line
;       r7   = horizontal and vertical resolution packed into a single word.
;
; out:  V clear;
;         r0 = number of copies to be performed
;         r3 = number of blank pixel rows before start of of image
;
;       V set;
;         r0 -> error block.
;
; This routine is called at the start of the page.  The routine will allow
; the PDumper to setup the printer and skip to the correct print position,
; the driver is also passed the number of copies to be performed.  Both
; of these values can be modified.  The device is also passed the horizontal
; margin for its own reference, this cannot be modified and it is assumed
; that the dumping routine will pad the line start with null bytes.
;
; The number of copies when returned should be non-zero, but the vertical
; skip should just be be +VE.
;

StartPage_Monochrome ROUT
StartPage_Grey ROUT
StartPage_Colour256 ROUT

        PDumper_Entry "R0-R2,R4,R6-R8"

        Debug   StartPage,"Copies to do",R0
        Debug   StartPage,"Vertical skip of",R3
        Debug   StartPage,"Left margin is",R6
        Debug   StartPage,"Packed resolution is",R7

        PDumper_Reset

        STR     R1,FileHandle          ;Stash the stream handle
        ADD     R5,R5,#pd_data          ;And point R5 at the configuration data
                         
      [ Version >= 012
        BL      sendextra               ;Stream out the extra sequence
      ]

        PDumper_AdjustFeed R6,R3,R5,R14

        MOV     R0, #0
        STR     R0, pending_line_ends

        LDRB    R0,[R5,#pd_data_version - pd_data]
        TEQ     R0,#0                   ;Is version number zero?
        LDRNEB  R0,[R5,#pd_data_set_lines]
        TEQNE   R0,#0                   ;Is there a set lines string?
        LDRNEB  R8,[R5,#pd_data_num_lines]
        TEQNE   R8,#0                   ;Is the number of lines zero?
        BEQ     %FT12

        ADD     R0,R5,R0                ;Output the set lines string
        PDumper_PrintCountedString R0,R1,R2
        PDumper_OutputReg R8            ; Output the number of lines
12
        LDRB    R0,[R5,#pd_data_page_start]
        TEQ     R0,#0                   ;Is there any page start sequences?
        BEQ     %FT10

        ADD     R0,R5,R0
        PDumper_PrintCountedString R0,R1,R2
10
        LDRB    R8,[R5,#pd_private_flags -pd_data]
        TST     R8,#df_NoPageAdvance    ;Is full page advance allowed?
        MOVNE   r3, #0                  ; No - default skip to zero
        LDRNEB  r0, [r5, #pd_data_version - pd_data]
        TEQNE   r0, #0                  ; Is data version zero?
        LDRNE   r3, [r5, #pd_data_roll_advance] ; No - load small skip

;set left margin
;
;if there is a pd_data_zero_skip 'string' it is actually parameters for the Esc ( g
;command: byte 0 = page length value (1/10 inch units)
;              1 = max allowable left margin value (1/10 inch) (currently ignored)
;              2 = no. of other parameters to send (nx = 0,1 or 2)
;              3 = right margin value (1/10 inch) (if nx > 0)
;              4 = offset value (1/60 inch) (if nx > 1)
;the max left margin (if not ignored) could fix the problem of printer ignoring
;settings if right-left is less than 1 inch - luckily this typically only arises for
;printouts with left margin of 7 inches or more
;
;if there is no pd_data_zero_skip 'string', a default (2 parameter) Esc ( g is sent
;to set the left margin (this is as version 1.17 would always do
;
        Push    "R0-R4,R7,R8"
        LDRB    R3,[R5,#pd_data_zero_skip]  ;is there a zero-skip string?
        ADD     R4,R5,R3          ;point to string count byte, if any
        ADD     R4,R4,#1          ;point to first useful byte (page length)
        TEQ     R3,#0
        MOVEQ   R8,#2             ;no. of params, defaults to 2
        LDRNEB  R8,[R4,#2]
        ADDNE   R8,R8,#2          ;else, no. of params is nx+2
        ADR     R0,cx_left_margin
        PDumper_PrintCountedString R0,R1,R2
        PDumper_OutputReg R8      ; = no. of params
        MOV     R0,#0
        PDumper_OutputReg R0      ; = high byte of (no. of params)
        TEQ     R3,#0
        MOVEQ   R0,#0             ;default to page-length=0 - should mean no change
        LDRNEB  R0,[R4]
        PDumper_OutputReg R0      ; = page length
        BIC     R7,R7,#&FF000000
        BIC     R7,R7,#&00FF0000  ;select just the horizontal resolution
        MOV     R2,#20            ;we can set left margin in 1/10'ths inch - allow for rounding
        MUL     R6,R2,R6
        DivRem  R0,R6,R7,R1
        ADD     R0,R0,#1          ;round to nearest
        MOV     R0,R0,LSR #1
        PDumper_OutputReg R0      ; = left margin
        TEQ     R3,#0
        BEQ     %FT15
        ADD     R4,R4,#3          ;point to right margin value
        CMP     R8,#3
        BLT     %FT15
        LDRB    R0,[R4],#1
        PDumper_OutputReg R0      ; = right margin
        CMP     R8,#4
        BLT     %FT15
        LDRB    R0,[R4],#1
        PDumper_OutputReg R0      ; = offset
15
        Pull    "R0-R4,R7,R8"

;vertical skip to start
        CMP     R3,#0
        BLE     %FT20
        Push    "R0-R2"
        ADR     R0,cx_vertskip
        PDumper_PrintCountedString R0,R1,R2
        PDumper_PrintBinaryPair_HighFirst R3,R1
        MOV     R3,#0                   ;we've handled initial skip
        Pull    "R0-R2"

20
        PDumper_EmptyBuffer
        PDumper_Exit                    ;Flush the buffer and then return

cx_left_margin = 3,27,"(","g"
        ALIGN

cx_vertskip = 5,27,"(","e",2,0
        ALIGN

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

        GetIf   ^.Generic.s.SendExtra, Version >= 012
$GetConditionally

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

        END

