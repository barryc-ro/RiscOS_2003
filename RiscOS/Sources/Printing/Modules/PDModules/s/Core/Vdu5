; > Core.Vdu5

;----------------------------------------------------------------------------
;
; VDU 5 handling routines
;
;----------------------------------------------------------------------------

; Routine to initialise the VDU 5 information.

initvdu5info
        Push    "R0-R3,LR"
        BL      vdu5_flush
        MOVVC   R0,#16
        MOVVC   R1,#32
        MOVVC   R2,#16
        MOVVC   R3,#32
        ADRVC   LR,vdu5charsize
        STMVCIA LR,{R0-R3}
        ASSERT  vdu5charspace = vdu5charsize+8
        MOVVC   LR,#&40
        STRVCB  LR,cursorcontrol
        BLVC    adjustvdu5advances
        STRVS   R0,[R13]
        Pull    "R0-R3,PC"

; Routine to calculate the character and line advances from the information
; in 'vdu5charspace' and 'cursorcontrol'.

adjustvdu5advances
        Push    "R0-R5,LR"
        ADR     LR,vdu5charspace        ;Get defined character spacing
        LDMIA   LR,{R0,R1}
        LDRB    LR,cursorcontrol        ;And cursor control byte
        TST     LR,#&02                 ;Change signs of spacing to get
        RSBNE   R0,R0,#0                ;  the sum of the two advances
        TST     LR,#&04
        RSBEQ   R1,R1,#0
        MOV     R2,R0                   ;Copy results
        MOV     R3,R1
        TST     LR,#&08                 ;Make appropriate movement horizontal
        MOVEQ   R1,#0                   ;  and the other one vertical
        MOVEQ   R2,#0
        MOVNE   R0,#0
        MOVNE   R3,#0
        MOV     R4,R0                   ;Set automatic advance to character
        MOV     R5,R1                   ;  advance
        TST     LR,#&20                 ;Then disable automatic advance if
        MOVNE   R4,#0                   ;  required
        MOVNE   R5,#0
        ADR     LR,vdu5charadvance
        STMIA   LR,{R0-R5}
        ASSERT  vdu5lineadvance = vdu5charadvance + 8
        ASSERT  vdu5autoadvance = vdu5lineadvance + 8
        CLRV
        Pull    "R0-R5,PC"

        END
