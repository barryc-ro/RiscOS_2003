; Core.Colour

;----------------------------------------------------------------------------
;
; Colour handling subroutines
;
;----------------------------------------------------------------------------

; Routine to work out the RGB value that corresponds to a particular GCOL/
; tint combination.
;
; Entry: R1 = GCOL in bits 15-8, tint in bits 7-0
; Exit:  R1 = &BBGGRR00
;        NB does not return errors. V not significant on exit.

        [ Medusa
; 8bpp full palette: as for 8bpp non-full palette
; 16/32bpp: need to generate the &BBGGRR00 as for a
; standard palette from the GCOL and TINT
        ]

gcol_lookup
        Push    "R0,R2,R3,LR"
        LDR     R2,lgbpp

        [ Medusa
        TEQ     R2, #4                  ;16bpp?
        TEQNE   R2, #5                  ;  or 32bpp?
        BEQ     gcol_lookup_16or32      ;  then rip the bits out directly
        ]

        TEQ     R2,#3
        MOVNE   R0,R1,LSR #8
        MOVEQ   R1,R1,LSR #6            ;If 256 colour, convert to pixel
        ANDEQ   R0,R1,#&87              ;  value
        ANDEQ   R2,R1,#&38
        ORREQ   R0,R0,R2,LSL #1
        ANDEQ   R2,R1,#&40
        ORREQ   R0,R0,R2,LSR #3
gcol_lookup_common
        MOV     R1,#16
        BL      intcolour_readpalette
        MOV     R1,R2                   ;Return colour in R1
        Pull    "R0,R2,R3,PC"

        [ Medusa
gcol_lookup_16or32

        Debuga  Medusa, "gcol_lookup of ", R1

        MOV     R0, R1, LSR #8          ;GCol in R0
        MOV     R1, R1, LSR #6          ;Tint in R1 bits 0..1
        AND     R1, R1, #3              ;  with other bits clear

        AND     R2, R0, #4_0003         ;Extract red
        ORR     R2, R1, R2, LSL #2      ;  or the tint in
        MOV     R3, R2, LSL #15-4       ;  place it in destination bits

        AND     R2, R0, #4_0030         ;Extract green
        ORR     R2, R1, R2              ;  or the tint in
        ORR     R3, R3, R2, LSL #23-4   ;  place it in destination bits

        AND     R2, R0, #4_0300         ;Extract blue
        ORR     R2, R1, R2, LSR #2      ;  or the tint in
        ORR     R3, R3, R2, LSL #31-4   ;  place it in destination bits

        ORR     R1, R3, R3, LSR #4      ; duplicate nibbles

        Debug   Medusa, "Returns ", R1

        Pull    "R0,R2,R3,PC"
        ]
        

; A similar routine that looks up a pixel value, supplied in bits 0-7 of R1.

pixval_lookup
        Push    "R0,R2,R3,LR"
        MOV     R0,R1
        B       gcol_lookup_common

; Routine to read interesting variables about the current screen mode and
; do some preprocessing on them.

readcurvduvariables
        Push    "R0,R1,LR"
        ADR     R0,vduvariableswanted
        ADR     R1,fgmode
        SWI     XOS_ReadVduVariables
        ADRVC   R1,fggcol               ;Combine foreground GCOL and TINT
        LDMVCIA R1,{R0,R1}              ;  into a single word
        ORRVC   R1,R0,R1,LSL #8
        STRVC   R1,fggcol
        ADRVC   R1,bggcol               ;Combine background GCOL and TINT
        LDMVCIA R1,{R0,R1}              ;  into a single word
        ORRVC   R1,R0,R1,LSL #8
        STRVC   R1,bggcol
        STRVS   R0,[R13]
        Pull    "R0,R1,PC"

        ASSERT  fggcol = fgmode + 4
        ASSERT  fgrgb = fggcol + 4
        ASSERT  bgmode = fgrgb + 4
        ASSERT  bggcol = bgmode + 4
        ASSERT  bgrgb = bggcol + 4
        ASSERT  lgbpp = bgrgb + 4
        ASSERT  currxeig = lgbpp + 4
        ASSERT  curryeig = currxeig + 4

vduvariableswanted
        DCD     VduExt_GPLFMD
        DCD     VduExt_GFTint
        DCD     VduExt_GFCOL
        DCD     VduExt_GPLBMD
        DCD     VduExt_GBTint
        DCD     VduExt_GBCOL
        DCD     VduExt_Log2BPP
        DCD     VduExt_XEigFactor
        DCD     VduExt_YEigFactor
        DCD     -1

; Routine to determine the foreground or background colour.
; Entry: HI if background wanted, LS if foreground wanted
;        R1 contains set of 'disabled' flags to ignore.
; Exit:  disabled_colour bit of 'disabled' set or cleared as appropriate,
;        EQ set and R1 = &BBGGRR00 <=> all appropriate bits of disabled are
;        zero; R1 corrupt otherwise.

gcol_lookupfgorbg
        BHI     gcol_lookupbg

; Routine to determine the foreground colour.
; Entry: R1 contains set of 'disabled' flags to ignore.
; Exit:  disabled_colour bit of 'disabled' set or cleared as appropriate,
;        EQ set and R1 = &BBGGRR00 <=> all appropriate bits of disabled are
;        zero; R1 corrupt otherwise.
;        NB does not return errors. V insignificant on return

gcol_lookupfg
        Push    "R0,R2,R3,LR"
        ASSERT  fggcol = fgmode + 4
        ASSERT  fgrgb = fggcol + 4
        ADR     LR,fgmode
        B       gcol_lookupcommon

; Routine to determine the background colour.
; Entry: R1 contains set of 'disabled' flags to ignore.
; Exit:  disabled_colour bit of 'disabled' set or cleared as appropriate,
;        EQ set and R1 = &BBGGRR00 <=> all appropriate bits of disabled are
;        zero; R1 corrupt otherwise.
;        NB does not return errors. V insignificant on return

gcol_lookupbg
        Push    "R0,R2,R3,LR"
        ASSERT  bggcol = bgmode + 4
        ASSERT  bgrgb = bggcol + 4
        ADR     LR,bgmode

gcol_lookupcommon
        MOV     R3,R1
        LDMIA   LR,{R0,R1,R2}
        TST     R0,#7
        LDRB    LR,disabled
        ORRNE   LR,LR,#disabled_colour
        BICEQ   LR,LR,#disabled_colour
        STRB    LR,disabled
        BICS    LR,LR,R3
        Pull    "R0,R2,R3,PC",NE
        TEQ     R1,#0
        MOVMI   R1,R2
        BLPL    gcol_lookup
        TEQ     R0,R0                   ;Set EQ again
        Pull    "R0,R2,R3,PC"

        END
