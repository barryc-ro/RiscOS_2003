; > Sources.PDriverDP.Private



; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; the main dump routine!
; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

dump_current_buffer ROUT
        Push    "r0-r8, lr"

        BL      restore_output_state
        MOVVC   r0, #SpriteReason_RemoveLeftHandWastage
        ADRVC   r2, job_linebuffer
        BLVC    myspriteop

        BVS     %FT99

        LDRB    r4, job_no_passes
        CMP     r4, #1
        BLE     %FT00
        ADR     r0, job_DrawMatrix
        ADR     r3, job_linebuffer+12
01
        MOV     r2, r3
        BL      get_sprite_address
        BVS     %FT99

        LDR     r5, [r2, #spImage]
        ADD     r5, r5, r2
        STR     r5, [r0], #4
        ADD     r3, r3, #12
        SUBS    r4, r4, #1
        BGT     %BT01
        ADR     r0, job_DrawMatrix
        B       %FT02

00
        ADR     r2, job_linebuffer              ;; <== ADDED BY NRAINE, 2-May-89
        BL      get_sprite_address              ;; BVS moved to catch more cases
        BVS     %FT99

        LDR     r0, [r2, #spImage]
        ADD     r0, r0, r2
02
        LDR     r4, [r2, #spHeight]             ;; rows in buffer - 1
        ADD     r4, r4, #1                      ;; <== ADDED BY NRAINE, 20-Apr-89

        LDR     r5, job_currentline
        LDR     lr, job_totalheight
        SUB     r5, lr, r5                      ; rows left to do

        CMP     r5, r4                          ; r4 = number of rows to print this time
        MOVLT   r4, r5

        LDR     r5, [r2, #spWidth]              ; r5 = sprite width in words
        ADD     r5, r5, #1

        Push    "r9"
                                                ; r0 -> sprite pixel data for a single pass plot
        LDR     r1, jobhandle                   ; r1  = file handle for job
        [ Libra1
        LDR     r2, job_strip_type              ; r2  = strip type in byte 0, no_passes in byte 2
        |
        LDRB    r2, job_strip_type              ; r2  = strip type in byte 0, no_passes in byte 2
        ]
        LDR     r3, job_linelength              ; r3  = number of columns to output
        [ Libra1
        LDRB    LR, job_strip_type
        CMP     LR, #4
        MOVEQ   R5, R5, LSL #1
        CMPNE   LR, #5
        MOVNE   R5, R5, LSL #2
        |
        MOV     r5, r5, LSL #2                  ; r5  = no of bytes in one row
        ]
        LDR     r6, job_halftoneX               ; r6  = halftone width in byte 0, halftone height in byte 1
        ADR     r7, job_dump_depth              ; r7 -> job workspace
        ADR     r8, job_pdumper_word            ; r8  = pdumper private word
        MOV     r9, #PDumperReason_OutputDump
        BL      CallPDumperForJob
        Pull    "r9"                            ; preserving r9
        BLVC    redirect_output
                     
99      STRVS   r0, [stack]
        Pull    "r0-r8, pc"

; **********************************************************
; now get all the common subroutines

      [ debug
        InsertNDRDebugRoutines
      ]
        LNK     s.PDriverDP.Private2
