; > Sources.PDriverDP.Draw

; *****************************************************************
; thin stroke the specified path, closing all open subpaths.
;  Entry: R0 -> path
;         R2 -> Draw transformation matrix, or is zero
;         R3 = flatness, in user co-ordinates
;         R10 is the file handle for this print job
;  Exit:  R0,R9 corrupt (except R0 on an error)
;         All other registers preserved.
; *****************************************************************

draw_boundaryonly ROUT
          Push   "r1, r2, r8, lr"

          BL      SetDrawMatrixEtc
          Pull   "r1, r2, r8, pc",NE
          MOV     r1, #2_11000        ; set all boundary pixels
          SWI     XDraw_Fill
          STRB    r8, passthrough
          Pull   "r1, r2, r8, pc"

; *****************************************************************
; fill the interior of the specified path,
; excluding boundary pixels if possible.
; Entry: R0 -> path
;        Bit 1 of R1 is set if the even-odd winding rule is to be used
;              (otherwise, use the non-zero winding number rule).
;        R2 -> Draw transformation matrix, or is zero
;        R3 = flatness, in user co-ordinates
;        R10 is the file handle for this print job
; Exit:  R0,R9 corrupt (except R0 on an error)
;        All other registers preserved.
; *****************************************************************

draw_interiornobdry ROUT
          Push   "r1, r2, r8, lr"
          BL      SetDrawMatrixEtc
          Pull   "r1, r2, r8, pc",NE
          ORR     r1, r1, #2_100000     ; non-boundary interior pixels
          SWI     XDraw_Fill
          STRB    r8, passthrough
          Pull   "r1, r2, r8, pc"

; *****************************************************************
; fill the interior of the specified path.
;  Entry: R0 -> path
;         Bit 1 of R1 is set if the even-odd winding rule is to be used
;         (otherwise, use the non-zero winding number rule).
;         R2 -> Draw transformation matrix, or is zero
;         R3 = flatness, in user co-ordinates
;         R10 is the file handle for this print job
;  Exit:  R0,R9 corrupt (except R0 on an error)
;         All other registers preserved.
; *****************************************************************

draw_interior ROUT
          Push   "r1, r2, r8, lr"
          BL      SetDrawMatrixEtc
          Pull   "r1, r2, r8, pc",NE
          ORR     r1, r1, #2_110000     ; all interior pixels
          SWI     XDraw_Fill
          STRB    r8, passthrough
          Pull   "r1, r2, r8, pc"

; *****************************************************************
; fill the interior and boundary of the specified path.
;  Entry: R0 -> path
;         Bit 1 of R1 is set if the even-odd winding rule is to be used
;          (otherwise, use the non-zero winding number rule).
;         R2 -> Draw transformation matrix, or is zero
;         R3 = flatness, in user co-ordinates
;         R10 is the file handle for this print job
;  Exit:  R0,R9 corrupt (except R0 on an error)
;         All other registers preserved.
; *****************************************************************

draw_interiorwithbdry ROUT
          Push   "r1, r2, r8, lr"
          BL      SetDrawMatrixEtc
          Pull   "r1, r2, r8, pc",NE
          ORR     r1, r1, #2_111000     ; up to boundary edge pixels
          SWI     XDraw_Fill
          STRB    r8, passthrough
          Pull   "r1, r2, r8, pc"

; *****************************************************************
; stroke the specified path, using as close an approximation to
;  "plot interior, excluding interior boundary" as possible.
; Entry: R0 -> path
;        R2 -> Draw transformation matrix, or is zero
;        R3 = flatness, in user co-ordinates
;        R4 = thickness (non-zero)
;        R5 -> Draw cap and join specification
;        R6 -> Draw dash pattern, or is zero
;        R10 is the file handle for this print job
; Exit:  R0,R9 corrupt (except R0 on an error)
;        All other registers preserved.
; *****************************************************************

draw_strokenobdry ROUT
          Push   "r1, r2, r8, lr"
          BL      SetDrawMatrixEtc
          Pull   "r1, r2, r8, pc",NE
          MOV     r1, #2_100000
          SWI     XDraw_Stroke
          STRB    r8, passthrough
          Pull   "r1, r2, r8, pc"

; *****************************************************************
; stroke the specified path.
;  Entry: R0 -> path
;         R2 -> Draw transformation matrix, or is zero
;         R3 = flatness, in user co-ordinates
;         R4 = thickness (non-zero)
;         R5 -> Draw cap and join specification
;         R6 -> Draw dash pattern, or is zero
;         R10 is the file handle for this print job
;  Exit:  R0,R9 corrupt (except R0 on an error)
;         All other registers preserved.
; *****************************************************************

draw_stroke ROUT
          Push   "r1, r2, r8, lr"
          BL      SetDrawMatrixEtc
          Pull   "r1, r2, r8, pc",NE
          MOV     r1, #2_110000
          SWI     XDraw_Stroke
          STRB    r8, passthrough
          Pull   "r1, r2, r8, pc"

; *****************************************************************
; stroke the specified path, using as close an approximation to
;  "plot interior and all of boundary" as possible.
; Entry: R0 -> path
;        R2 -> Draw transformation matrix, or is zero
;        R3 = flatness, in user co-ordinates
;        R4 = thickness (non-zero)
;        R5 -> Draw cap and join specification
;        R6 -> Draw dash pattern, or is zero
;        R10 is the file handle for this print job
; Exit:  R0,R9 corrupt (except R0 on an error)
;        All other registers preserved.
; *****************************************************************

draw_strokewithbdry ROUT
          Push   "r1, r2, r8, lr"
          BL      SetDrawMatrixEtc
          Pull   "r1, r2, r8, pc",NE
          MOV     r1, #2_111000
          SWI     XDraw_Stroke
          STRB    r8, passthrough
          Pull   "r1, r2, r8, pc"

; *****************************************************************
; thin stroke the specified path.
;  Entry: R0 -> path
;         R2 -> Draw transformation matrix, or is zero
;         R3 = flatness, in user co-ordinates
;         R6 -> Draw dash pattern, or is zero
;         R10 is the file handle for this print job
;  Exit:  R0,R9 corrupt (except R0 on an error)
;         All other registers preserved.
; *****************************************************************

draw_thinstroke ROUT
          Push   "r1, r2, r4, r8, lr"
          BL      SetDrawMatrixEtc
          Pull   "r1, r2, r4, r8, pc",NE
          MOV     r1, #0             ; default
          MOV     r4, #0             ; for zero-thickness lines
          SWI     XDraw_Stroke
          STRB    r8, passthrough
          Pull   "r1, r2, r4, r8, pc"

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; Draw support routine: build a new transformation matrix from the
; one at r2, also set no interception of Draw calls.
; R9 corruptible
; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

SetDrawMatrixEtc ROUT
         Push   "r0, lr"
         LDRB    lr, disabled
         TST     lr, #disabled_nullclip
         Pull   "r0, pc", NE

   ; user flatness is specified in user coordinate space
   ; this will be scaled by job_currentX/Yscale / 180,
   ; so we should multiply by 180 / job_currentX/Yscale
   ; NOTE1: the default flatness is assumed to be 512 before scaling
   ; NOTE2: we assume that if the draw matrix is not the identity,
   ;        the caller will have modified his flatness accordingly.
   ;        We can therefore use the supplied flatness as a 'hint'

         CMP     r3, #0
         MOVEQ   r3, #512                ;; <== ADDED BY NRAINE, 26-Apr-89
      ;; BNE     sdme_flatOK

   ; user has asked for "default" flatness; we want to produce something
   ; that will give "adequate" results on the printed page.
   ; So take the default flatness (2), and scale it by the maximum
   ; scale factor.

         Push   "r0, r1"
         MOV     r0, #180                ;; <== ADDED BY NRAINE, 26-Apr-89
         MUL     r0, r3, r0              ;;
      ;; LDR     r0, =512*180            ; old default flatness + conversion
         LDR     r1, job_currentXscale
         LDR     lr, job_currentYscale
         CMP     r1, lr
         MOVLT   r1, lr
         BL      divide_and_scale
         MOVS    r3, r0, LSR #bufferpix_l2size   ; new flatness
         MOVEQ   r3, #1                          ; with underflow check

         Debug   Draw,"New flatness",R3

         Pull   "r0, r1"

sdme_flatOK
         LDRB    r8, passthrough
         ORR     lr, r8, #passthrough_draw
         STRB    lr, passthrough
         CMP     r2, #0
         ADREQ   r2, SDM_Identity

    ; first adjust the translation elements: there's the current offset from
    ; the bottom of the strip, then there's the adjustment from 0,0 to
    ; usersoffset-relative coordinates
    ; Translation entries refer to the output coordinate space, so need
    ; scaling.

         LDR     r0, [r2, #4*4]
         Debuga  Draw,"X translation is",R0

         LDR     lr, usersoffset
         SUB     r0, r0, lr, LSL #8        ; convert to usersoffset-relative
         Debuga  Draw,"->",R0

         BL      XScale                    ; scale to new output space
         Debuga  Draw,"->",R0

         LDR     lr, job_currentXoffset
         SUB     r0, r0, lr
         Debug   Draw,"->",R0
         STR     r0, job_DrawMatrix+4*4

         LDR     r0, [r2, #4*5]
         Debuga  Draw,"Y translation",R0

         LDR     lr, usersoffset+4
         SUB     r0, r0, lr, LSL #8
         Debuga  Draw,"->",R0

         BL      YScale
         Debuga  Draw,"->",R0

         LDR     lr, job_currentYoffset
         SUB     r0, r0, lr
         Debug   Draw,"->",R0
         STR     r0, job_DrawMatrix+4*5

     ; now add X scaling, Y scaling
         LDR     r0, [r2, #4*0]
         BL      XScale
         STR     r0, job_DrawMatrix+4*0

         LDR     r0, [r2, #4*2]
         BL      XScale
         STR     r0, job_DrawMatrix+4*2

         LDR     r0, [r2, #4*1]
         BL      YScale
         STR     r0, job_DrawMatrix+4*1

         LDR     r0, [r2, #4*3]
         BL      YScale
         STR     r0, job_DrawMatrix+4*3

       [ debugDraw
         LDR     r0, job_DrawMatrix
         Debuga  ,"New draw matrix",R0
         LDR     r0, job_DrawMatrix+4
         Debuga  ,",",R0
         LDR     r0, job_DrawMatrix+8
         Debuga  ,",",R0
         LDR     r0, job_DrawMatrix+12
         Debuga  ,",",R0
         LDR     r0, job_DrawMatrix+16
         Debuga  ,",",R0
         LDR     r0, job_DrawMatrix+20
         Debug   ,",",R0
       ]
         ADR     r2, job_DrawMatrix
         CMP     r0, r0               ; set EQ
         Pull   "r0, pc"

SDM_Identity & &10000, 0, 0, &10000, 0, 0

        END
