		GET	Hdr:ListOpts
		GET	Hdr:Macros
		GET	Hdr:System

		EXPORT	readw
		EXPORT	isram

		AREA	|C$$Code|, CODE, READONLY

readw		ROUT
		MOV	r1, lr
		SWI	OS_EnterOS
		LDR	r0, [r0]
		TEQP	r1, #0
		MOV	r0, r0
		MOVS	pc, r1

; Determine if the specified address is RAM.  Use the SWP instructions to
; bypass the cache.  The bus activity is as follows:
;   Address  Access  Data
;    r0      Read    d0
;    r0      Write   0
;    r1      Read    d1
;    r1      Write   -1
;    r0      Read    0 (?)
;    r0      Write   -1
;    r1      Read    -1 (?)
;    r1      Write   0
;    r0      Read    -1 (?)
;    r0      Write   d0
;    r1      Read    0 (?)
;    r1      Write   d1

isram		ROUT
		MOV	r3, lr
		SWI	OS_EnterOS
		STMDB	r13!, {r3, r4, r5}
		EOR	r1, r0, #4		; Alternate address

		MOV	r4, #0			; Values to store
		MOV	r5, #-1

		SWP	r2, r4, [r0]		; Store 0
		SWP	r3, r5, [r1]		; Put -1 on bus
		SWP	ip, r5, [r0]		; Store -1
		CMP	ip, r4			; Check value is 0
		SWPEQ	ip, r4, [r1]		; Put 0 on bus
		SWP	ip, r2, [r0]		; Store old value back
		CMP	ip, r5			; Check value is -1
		SWP	ip, r3, [r1]		; Put old value back

		MOVEQ	r0, #1
		MOVNE	r0, #0
		LDMIA	r13!, {r3, r4, r5}
		TEQP	r3, #0
		MOV	r0, r0
		MOVS	pc, r3

		END
