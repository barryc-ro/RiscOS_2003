#
# awkscript
#
# Copyright (C) Pace Micro Technologies 1999
#
# This awk-script replaces the GNU sed script which substituted in the
# version number of the bison tool - it reads the version number from
# the VersionNum file instead.
#
# Author: sbrodie

function findversion()
{
        while ((getline < "VersionNum") > 0) {
        	split($0, args);
                if (args[2] == "Module_MajorVersion_CMHG") {
                	VERSION = args[3]
                }
                if (args[2] == "Module_Date_CMHG") {
                	DATE = args[3] " " args[4] " " args[5]
                }
        }
}


BEGIN {
        findversion()
        marker = "@bison_version@"
        len = length(marker)
        marker2 = "@module_date@"
        len2 = length(marker2)
}

{
        pos = index($0, marker);
        if (pos > 0) {
	        printf("%s%s%s\n", substr($0, 1, pos - 1), VERSION,
       			substr($0, pos + len, length + 1 - (pos + len)));
	        next;
        }
        pos = index($0, marker2);
        if (pos > 0) {
	        printf("%s%s%s\n", substr($0, 1, pos - 1), DATE,
       			substr($0, pos + len2, length + 1 - (pos + len2)));
	        next;
        }

        print
}
