; assembler bits to do the decoding of the NVRAM area code
; couldn't be bothered doint it in C
;

	GET	s.registers

	AREA 	areacd,PIC,CODE,REL

	EXPORT	areacode

; on entry
; r0 is word containing the nvram data, shifted down to lsb */
; r1 is ptr to buffer for string (must be at least 8 bytes wide)

areacode
	STMDB	sp!, {r4-r11, lr}

	MOV	r4, #6
	MOV	r5, #&2f

	BIC	r0, r0, #&ff000000
	MOV	r3, #&f00000
area_loop
	ANDS	r2, r3, r0
	MOV	r0, r0, LSL#4
	BEQ	area_terminate
	ADD	r2, r5, r2, LSR#20
	STRB	r2, [r1], #1
	SUBS	r4, r4, #1
	BNE	area_loop
area_terminate
	MOV	r2, #0
	STRB	r2, [r1]
	LDMIA	sp!, {r4-r11, pc}

STACK_SIZE * 8192
OS_Module * &001e
OS_Exit * &0011


	IMPORT	|Image$$RO$$Base|
	IMPORT	|_Lib$Reloc$Off$DP|
	IMPORT	main
	IMPORT	task_stack

	EXPORT	|_blib_entermodule|

	AREA	|_blib_entermodulearea|,REL,CODE,READONLY

|_blib_entermodule|
	MOV	v1,r0		;Save command tail
	LDR	r12,[r12]	;Workspace ptr from private word
	LDMIB	r12,{v2,v3}	;Module relocations
	LDR	v4,taskstackptr ;A Ptr to taskstack variable
	ADD	v4,v4,v3	;gets relocated for user variable
	LDR	sl,[v4]		;check if a task is already running
	CMP	sl,#0
	MOVNE	r0,#0		;if so then set the return code to zero
	SWINE	OS_Exit		;and exit without doing anything
	MOV	r0,#6
	MOV	r3,#STACK_SIZE
	SWI	OS_Module
	MOV	sl,r2
	STR	sl,[v4]
	ADD	sp,sl,r3
	STMIA	sl,{v2,v3}

	; This is equivalent of 'ADD r10, r10, #0' + |_Lib$Reloc$Off$DP|
	DCD	|_Lib$Reloc$Off$DP| + &E28AA000

	MOV	fp,#0
	MOV	a1,v1
	BL	main

	; This is equivalent of 'SUB r10, r10, #0' + |_Lib$Reloc$Off$DP|
	DCD	|_Lib$Reloc$Off$DP| + &E24AA000

	MOV	v1,r0

	MOV	r2,sl
	MOV	r0,#7
	SWI	OS_Module
	MOV	r0,#0
	STR	r0,[v4]

	MOV	r0,v1
	SWI	OS_Exit

taskstackptr
	DCD	task_stack


	END
