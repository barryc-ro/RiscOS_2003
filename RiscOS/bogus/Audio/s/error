                ;           ;
                AREA        ErrorCode, CODE, READONLY

                GET     Hdr:ListOpts
                GET     Hdr:Macros
                GET     Hdr:System
                GET     Hdr:ModHand
                GET     Hdr:HostFS
                GET     Hdr:NdrDebug
;                GET     Hdr:DDVMacros
;                GET     Hdr:UpCall
                GET     Hdr:MsgTrans
                GET     Hdr:ResourceFS
                GET     Hdr:DeviceFS
                GET     Hdr:Buffer
                GET     Hdr:Proc

                GET     a.defs+ws
                GET     a.error

                EXPORT  MakeError

resource_file   = ResourceFile, 0

error_UKRateToken DCB "E00:",0

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; Generalised internationalisation routines, these ensure that messages files
; are correctly opened and then return the relevant data.
;


; Attempt to open the messages file.

OpenMessages    ROUT

                Push    "r0-r3, lr"

                LDR     r3, [r12, #Flags]
                TST     r3, #Flag_WeHaveMessages                   ; do we have an open messages block?
                Pull    "r0-r3, pc", NE, ^                      ; yes, so don't bother again

                ADD     r0, r12, #MessagesWorkspace
                ADRL    r1, resource_file                       ; -> path to be opened
                MOV     r2, #0                                  ; allocate some wacky space in RMA
                SWI     XMessageTrans_OpenFile
                LDRVC   r3, [r12, #Flags]
                ORRVC   r3, r3, #Flag_WeHaveMessages
                STRVC   r3, [r12, #Flags]                       ; assuming it worked mark as having messages

                Pull    "r0-r3, pc",,^                          ; returning VC, VS from XSWI!


; Attempt to close the messages file.

CloseMessages   ROUT

                Push    "r0, lr"

                LDR     r0, [r12, #Flags]
                TST     r0, #Flag_WeHaveMessages                   ; do we have any messages?
                Pull    "r0, pc", EQ, ^                         ; and return if not!

                ADD     r0, r12, #MessagesWorkspace
                SWI     XMessageTrans_CloseFile                 ; yes, so close the file
                LDRVC   r0, [r12, #Flags]
                BICVC   r0, r0, #Flag_WeHaveMessages
                STRVC   r0, [r12, #Flags]                       ; mark as we don't have them

                Pull    "r0, pc",,^


; Generate an error based on the error token given.  Does not assume that
; the messages file is open.  Will attempt to open it, then look it up.

;MakeErrorWithModuleName
;                ADRL    r4, title
;                B       MakeErrorEntry

MakeError       Entry   "r1-r7"
                MOV     r4, #0
MakeErrorEntry
                LDR     r1, [r12, #Flags]
                TST     r1, #Flag_WeHaveMessages                   ; has the messages file been closed? or not opened
                BLEQ    OpenMessages

                LDR     r1, [r12, #Flags]
                TST     r1, #Flag_WeHaveMessages
                PullEnv EQ
                ORREQS  pc, lr, #V_bit                          ; if still not open then return with V set

                ADD     r1, r12, #MessagesWorkspace                   ; -> message control block
                MOV     r2, #0
                MOV     r3, #0
                MOV     r5, #0
                MOV     r6, #0
                MOV     r7, #0                                  ; no substitution + use internal buffers
                SWI     XMessageTrans_ErrorLookup

                BL      CloseMessages                           ; attempt to close the doofer

                EXIT                                 ; return, r0 -> block, V set


;
                END
