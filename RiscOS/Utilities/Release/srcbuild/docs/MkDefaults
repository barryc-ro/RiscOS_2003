   10REM > GenerateTerritoryList
   20
   30REM This program will generate a source fragment that can be inserted into
   40REM srcbuild(riscos.c) to prime the territory environment when new built-in
   50REM territories are needed.  This should never be needed as long as new
   60REM Env files declare the territory numbers explicitly.
   70
   80DIM buf% 256
   90
  100swi$ = "OS_ServiceCall"
  110Service_International% = &43
  120Num_To_Name% = 2
  130
  140FOR T%=1 TO 255
  150  SYS swi$,,Service_International%, Num_To_Name%, T%, buf%, 256 TO ,ok%,,,name$,len%
  160  IF ok%=0 THEN
  170    PRINT FN_format(T%, LEFT$(name$, len%))
  180  ENDIF
  190NEXT
  200END
  210
  220DEF FN_format(territory%, name$)
  230="  (void) parse_territory_entry(strcpy(temporary, "+CHR$34+STR$territory%+"="+name$+CHR$34+"))"
